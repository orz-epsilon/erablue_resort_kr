;-------------------------------------------------
;ARG番のキャラが野外オナニーを実行するかを返す関数
;実行する場合フラグ立てまで行う
;開始時, 実行中には1を返す
;絶頂時(=終了時), 開始しない時, 途中終了時には0を返す
;-------------------------------------------------
@野外オナニー判定(ARG)
;既に実行中の場合は継続するか判定する
IF TCVAR:ARG:野外オナニー_残時間 > 0 || TCVAR:ARG:野外オナニー_絶頂
	CALL 野外オナニー継続判定(ARG)
	RETURN RESULT
ENDIF
;睡眠中に無意識に……みたいなのも面白そうだけど現在はなし
SIF CFLAG:ARG:睡眠
	RETURN 0
;ダウン時ダメ
SIF BASE:ARG:体力 < 1
	RETURN 0
;性知識が無い場合はオナニー出来ない
SIF 知識素質:ARG:性知識 < 0 && EXP:ARG:自慰経験 <= 0
	RETURN 0
;性感帯無い場合も駄目
SIF !HAS_EROGENOUS_ZONE(ARG)
	RETURN 0
;同行中はやんないよ
SIF CFLAG:ARG:同行
	RETURN 0
SIF CFLAG:ARG:一緒に遊ぶ
	RETURN 0
SIF CFLAG:ARG:デート
	RETURN 0
SIF CFLAG:ARG:うふふ
	RETURN 0
SIF MODE_存在判定_いずれか側("同行系", ARG)
	RETURN 0
SIF MODE_存在判定_いずれか側("キス系", ARG)
	RETURN 0
SIF MODE_存在判定_ターゲット側("全裸散歩", ARG)
	RETURN 0
SIF MODE_存在判定_いずれか側("ハーヴィンオナホ系", ARG)
	RETURN 0
;前回の野外オナニーの正常終了から時間経過してないとしない
;興味の場合は一日一回、愛好の場合は４時間置き（一日２回絶頂まで）、中毒の場合は２時間置き（一日３回絶頂まで）
SELECTCASE 性癖素質:ARG:野外オナニー
	CASE 1
		SIF TCVAR:ARG:野外オナニー_開始時刻
			RETURN 0
	CASE 2
		SIF TCVAR:ARG:野外オナニー_開始時刻 + 4 * 60 > NOW()
			RETURN 0
		SIF TCVAR:ARG:野外オナニー_実行回数 > 1
			RETURN 0
	CASE 3
		SIF TCVAR:ARG:野外オナニー_開始時刻 + 2 * 60 > NOW()
			RETURN 0
		SIF TCVAR:ARG:野外オナニー_実行回数 > 2
			RETURN 0
ENDSELECT
;性欲や自慰しやすいなどの条件を見る
IF BASE:ARG:性欲 >= (1000 - TALENT:ARG:自慰しやすい * 200)
	;通常オナニー条件
ELSEIF CFLAG:ARG:オナニー覚えたて
	;覚えたて補正
ELSEIF 性癖素質:ARG:野外オナニー == 3
	;中毒時は性欲関係なし
ELSE
	;上記の条件に当てはまらないなら0で戻す
	RETURN 0
ENDIF
;既に誰かが同じ場所で隠密してるなら出来ない
FOR LOCAL, 0, CHARANUM
	SIF LOCAL == ARG
		CONTINUE
	SIF IS_SAME_ROOM(ARG, LOCAL) && CFLAG:LOCAL:隠密
		RETURN 0
NEXT

;実行処理
TCVAR:ARG:野外オナニー_開始時刻 = NOW()
TCVAR:ARG:野外オナニー_残時間 = 15
TCVAR:ARG:野外オナニー_部位 = WEIGHTED_RANDOM_PART(ARG)
CFLAG:ARG:隠密 = 1
RETURN 1

;-------------------------------------------------
;ARG番のキャラが野外オナニーを継続するか返す関数
;継続する場合には1を返す
;被発見や絶頂時などで終了する場合には0を返す
;-------------------------------------------------
@野外オナニー継続判定(ARG)
#DIM 同行者未陥落
#DIM 同行者フラグ

;時間経過させる前に残時間が0になってる場合は異常事態なので途中終了させる
IF TCVAR:ARG:野外オナニー_残時間 <= 0 && TCVAR:ARG:野外オナニー_絶頂 == 0
	CALL 野外オナニー途中終了(ARG)
	CFLAG:ARG:隠密 = 0
	RETURN 0
ENDIF
;オナニー中にあなたが物陰に入ってきたパターン
IF CFLAG:PLAYER:隠密 && IS_SAME_ROOM(ARG)
	同行者未陥落 = 0
	同行者フラグ = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		IF CFLAG:(TARGET:LOCAL):同行
			同行者フラグ = 1
			SIF !陥落チェック_性的(TARGET:LOCAL)
				同行者未陥落 = 1
		ENDIF
	NEXT
	IF 陥落チェック_性的(ARG) && 同行者未陥落 == 0
		PRINTFORML %CALLNAME:PLAYER%이(가) 물그림자에 들어서자, 그곳에 있던%CALLNAME:ARG%은(는) 몹시 당황한 모습을 보였지만
		PRINTFORMW 나타난 것이%CALLNAME:PLAYER%인 것을 알자, 안심한 듯하다
		PRINTL
		PRINTFORML %CALLNAME:ARG%의 복장은 흐트러져 있었고, 뺨을 붉힌 모습은 매우 선정적이다……
		IF 同行者フラグ
			PRINTFORML %CALLNAME:ARG%들을 끌어안겠습니까?
		ELSE
			PRINTFORML %CALLNAME:ARG%을(를) 끌어안겠습니까?
		ENDIF
		PRINTBUTTONLC "[0] 예", 0
		PRINTBUTTONLC "[1] 아니요", 1
		PRINTL
		BINPUT
		IF RESULT == 0
			PRINTFORMW %CALLNAME:PLAYER%은(는) 그대로 물그림자에서%CALLNAME:ARG%을(를) 끌어안았다……。
			CALL 野外オナニー途中終了(ARG)
			CFLAG:PLAYER:うふふ = 1
			CFLAG:ARG:隠密 = 1
			PALAM:ARG:欲情 = MAX(30000, (PALAM:ARG:欲情 - 10000) * 3 / 2) ;見つかったけどPLAYERだったので安心
			PALAM:ARG:潤滑 = MAX(30000, PALAM:ARG:潤滑 * 11 / 10)
			CALL うふふ開始キャラ処理(ARG, "キャラの野外オナニー目撃")
			FOR LOCAL, 1, CHARANUM
				SIF TARGET:LOCAL < 1
					BREAK
				IF CFLAG:(TARGET:LOCAL):同行
					CFLAG:(TARGET:LOCAL):隠密 = 1
					CALL うふふ開始キャラ処理(TARGET:LOCAL, @"キャラの野外オナニー目撃_同行_{ARG}")
				ENDIF
			NEXT
			;処理の誤爆を防ぐためオートコマンド周りリセット
			TFLAG:オートコマンドフラグ = 0
			AUTOCOM_実行番号保存 = -1
			AUTOCOM_対象番号保存 = -1
			自慰ターゲット保存 = -1
			RETURN 0
		ELSE
			PRINTFORML 즐거움을 방해해서는 미안하다.
			PRINTFORMW %CALLNAME:PLAYER%은(는)%CALLNAME:ARG%에게 가볍게 사과하고, 그 자리를 떠났다.
			CFLAG:PLAYER:隠密 = 0
			FOR LOCAL, 1, CHARANUM
				SIF TARGET:LOCAL < 1
					BREAK
				SIF TARGET:LOCAL == ARG
					CONTINUE
				IF CFLAG:(TARGET:LOCAL):同行
					CFLAG:(TARGET:LOCAL):隠密 = 0
				ENDIF
			NEXT
			;下の処理へ進む
		ENDIF
	ELSE
		DRAWLINE
		PRINTFORML %CALLNAME:PLAYER%이(가) 물그림자에 들어서자, 그곳에 있던%CALLNAME:ARG%은(는) 몹시 당황한 모습으로 몸단장을 하고
		PRINTFORMW %CALLNAME:PLAYER%와 눈을 마주치지 않고 물그림자에서 떠나갔다…
		CALL 野外オナニー途中終了(ARG)
		CFLAG:ARG:隠密 = 0
		RETURN 0
	ENDIF
ENDIF

;経過時間で継続か絶頂か判定
;残時間が0になると絶頂して正常終了
;終了処理自体は@野外オナニー実処理で行う
;どちらでもこの時点では隠密を継続
TCVAR:ARG:野外オナニー_残時間 = MAX(0, TCVAR:ARG:野外オナニー_残時間 - TIME_PROGRESS(TFLAG:行動前時刻))
CFLAG:ARG:隠密 = 1
IF TCVAR:ARG:野外オナニー_残時間 > 0
	RETURN 1
ELSEIF TCVAR:ARG:野外オナニー_絶頂
	IF TCVAR:ARG:野外オナニー_絶頂 - TIME_PROGRESS(TFLAG:行動前時刻) < 1
		CALL 野外オナニー途中終了(ARG)
		CFLAG:ARG:隠密 = 0
		RETURN 0
	ENDIF
ELSE
	TCVAR:ARG:野外オナニー_絶頂 = 1
	TCVAR:ARG:野外オナニー_残時間 = 0
	RETURN 0
ENDIF

;-------------------------------------------------
;ARG番のキャラが野外オナニーを何らかの理由で途中終了した際に動かす関数
;フラグ周りを初期化する
;-------------------------------------------------
@野外オナニー途中終了(ARG)
TCVAR:ARG:野外オナニー_残時間 = 0
TCVAR:ARG:野外オナニー_場所ID = 0
TCVAR:ARG:野外オナニー_部位 = 0
TCVAR:ARG:野外オナニー_絶頂 = 0
RETURN 0

;-------------------------------------------------
;ARG番のキャラが野外オナニー中であるかを判定する関数
;野外オナニーの開始時から絶頂する瞬間までを野外オナニー中として判定する
;-------------------------------------------------
@IS_野外オナニー中(ARG)
#FUNCTION
RETURNF TCVAR:ARG:野外オナニー_残時間 || TCVAR:ARG:野外オナニー_絶頂

@一日終了時オナニー判定(ARG)
#DIM オナニー部位
#DIMS 使用道具
#DIMS 部位名称
#DIM 未知フラグ
;覚えたてでＣを覚えた時に0になってしまうので定数だけずらす
#DIM CONST 覚えたて定数 = 100
使用道具 = 

;酔い潰れや体力０の時はオナニーしない
IF CFLAG:ARG:酔いすぎ
	RETURN 0
ENDIF
IF CFLAG:ARG:体力限界
	RETURN 0
ENDIF
;夜這い対象時はオナニーしない
IF TCVAR:ARG:夜這い済み
	RETURN 0
ENDIF
;オプションによる制御
IF 一日終了時オナニーOPTION:(TALENT:ARG:性別) == 2
	IF BASE:ARG:性欲 >= (1000 - TALENT:ARG:自慰しやすい * 200)
		BASE:ARG:性欲 = MAX(BASE:ARG:性欲 - 600, 0)
	ENDIF
	RETURN 0
ENDIF
;性感帯無い場合も駄目
SIF !HAS_EROGENOUS_ZONE(ARG)
	RETURN 0


;母乳搾り
IF TALENT:ARG:母乳体質 && BASE:ARG:母乳 >= (MAXBASE:ARG:母乳 * 4 / 5)
	CSTR:ARG:オナニー報告_記録文字列 += @"%CALLNAME:ARG%은(는) 고여 있던 모유를 자기 방에서 짜고 있는 듯하다。<br>"
	IF ABL:ARG:Ｂ感覚 >= 3 || ABL:ARG:欲望 >= 4
		CSTR:ARG:オナニー報告_記録文字列 += @"짜는 동안 기분이 좋아진 것인지, %CALLNAME:ARG%의 손가락은 그대로 쾌락을 찾아 유두를 주무르고 있다…<br>"
	ELSE
		CSTR:ARG:オナニー報告_記録文字列 += @"유두에서 모유가 나오는 감각은, %CALLNAME:ARG%에게 적지 않은 쾌감을 주고 있다…<br>"
	ENDIF

	CALL 母乳搾りオナニー実処理(ARG)
	CSTR:ARG:オナニー報告_記録文字列 += "<br>"
	RETURN 2
ENDIF

;使用道具の設定
IF オナニー部位 == 部位_Ｃ && GETBIT(TALENT:ARG:性別, 0)
	IF プレゼント履歴:ARG:Ｅマッサージャ > 0
		使用道具 = Ｅマッサージャ
	ENDIF
ENDIF

;オナニー覚えたて
;１０回以上オナニーしたら覚えたてボーナスおしまい
SIF CFLAG:ARG:オナニー覚えたて && EXP:ARG:自慰経験 > 10
	CFLAG:ARG:オナニー覚えたて = 0
IF CFLAG:ARG:オナニー覚えたて
	オナニー部位 = CFLAG:ARG:オナニー覚えたて - 覚えたて定数

	CSTR:ARG:オナニー報告_記録文字列 += @"%CALLNAME:ARG%은(는) 오늘 밤도 막 배운 자위의 포로가 되어 있다<br>"
	CSTR:ARG:オナニー報告_記録文字列 += "<br>"
	CSTR:ARG:オナニー報告_記録文字列 += @"%CALLNAME:ARG%은(는)"
	SIF TALENT:ARG:羞恥心 || ABL:ARG:欲望 < 3
		CSTR:ARG:オナニー報告_記録文字列 += "살며시"
	IF 使用道具 == ""
		CSTR:ARG:オナニー報告_記録文字列 += @" 자신의%PART_DESCRIPTION(ARG, オナニー部位)%에 손을 뻗었다<br>"
	ELSE
		CSTR:ARG:オナニー報告_記録文字列 += @"%使用道具%을(를) 꺼내, %PART_DESCRIPTION(ARG, オナニー部位)%에 가까이 댔다<br>"
	ENDIF
	CSTR:ARG:オナニー報告_記録文字列 += "<br>"
	IF ABL:ARG:オナニー部位 >= 3 || ABL:ARG:欲望 >= 4
		CSTR:ARG:オナニー報告_記録文字列 += @"그대로 몰두하여, 자위가 가져다주는 쾌락에 자신의 몸을 맡기고 있다…<br>"
	ELSE
		CSTR:ARG:オナニー報告_記録文字列 += @"익숙지 않은 손놀림이지만, 자신을 절정으로 이끌려고 애쓰고 있다…<br>"
	ENDIF

	CALL オナニー実処理(ARG, オナニー部位, 使用道具)
	CSTR:ARG:オナニー報告_記録文字列 += "<br>"
	RETURN 3
ENDIF

;通常自慰
IF BASE:ARG:性欲 >= (1000 - TALENT:ARG:自慰しやすい * 200)
	オナニー部位 = WEIGHTED_RANDOM_PART(ARG)
	;候補部位がない場合は戻す
	SIF オナニー部位 == -1
		RETURN 0

	IF EXP:ARG:自慰経験 > 0
		未知フラグ = 0
	ELSEIF 知識素質:ARG:性知識 < 0
		未知フラグ = 1
	ELSE
		SELECTCASE TALENT:ARG:性別
			CASE 0
				未知フラグ = 1
			CASE 1
				SIF TALENT:ARG:妊娠不可 == 2
					未知フラグ = 1
			CASE 2
				SIF TALENT:ARG:射精不可 == 2
					未知フラグ = 1
			CASE 3
				SIF TALENT:ARG:妊娠不可 == 2 && TALENT:ARG:射精不可 == 2
					未知フラグ = 1
		ENDSELECT
	ENDIF

	CSTR:ARG:オナニー報告_記録文字列 += @"%CALLNAME:ARG%은(는) 성욕이 쌓여, 울렁거리는 기분이 되어 있다<br>"

	;性知識が無い場合はオナニー出来ない
	IF 未知フラグ
		CSTR:ARG:オナニー報告_記録文字列 += @"하지만 %CALLNAME:ARG%은(는) 그것이 무엇인지 몰라, 그저 답답해하고 있다…<br>"
		IF !RAND:10
			;10%の確率でオナニーに目覚める
			CSTR:ARG:オナニー報告_記録文字列 += "<br>"
			CSTR:ARG:オナニー報告_記録文字列 += "…하지만 문득, 자신의 육체를 만지면 기분 좋은 부위가 있다는 것을 깨달은 것 같다<br>"
			CFLAG:ARG:オナニー覚えたて = オナニー部位 + 覚えたて定数
		ELSE
			RETURN -1
		ENDIF
	ENDIF

	CSTR:ARG:オナニー報告_記録文字列 += @"%CALLNAME:ARG%은(는)"
	SIF TALENT:ARG:羞恥心 || ABL:ARG:欲望 < 3
		CSTR:ARG:オナニー報告_記録文字列 += "살며시"
	IF 使用道具 == ""
		CSTR:ARG:オナニー報告_記録文字列 += @"자신의%PART_DESCRIPTION(ARG, オナニー部位)%에 손을 뻗었다<br>"
	ELSE
		CSTR:ARG:オナニー報告_記録文字列 += @"%使用道具%을(를) 꺼내, %PART_DESCRIPTION(ARG, オナニー部位)%에 가까이 댔다<br>"
	ENDIF
	CSTR:ARG:オナニー報告_記録文字列 += "<br>"
	IF ABL:ARG:オナニー部位 >= 5
		CSTR:ARG:オナニー報告_記録文字列 += @"어떻게 하면 자신이 기분 좋은지 숙지한 듯 손가락을 움직여, 몇 번이고 몇 번이고 절정을 거듭하고 있다…<br>"
	ELSEIF ABL:ARG:オナニー部位 >= 3 || ABL:ARG:欲望 >= 4
		CSTR:ARG:オナニー報告_記録文字列 += @"그대로 몰두하여, 자위가 가져다주는 쾌락에 자신의 몸을 맡기고 있다…<br>"
	ELSE
		CSTR:ARG:オナニー報告_記録文字列 += @"단조로운 손놀림으로, 자신을 절정으로 이끌려고 애쓰고 있다…<br>"
	ENDIF

	CALL オナニー実処理(ARG, オナニー部位, 使用道具)
	CSTR:ARG:オナニー報告_記録文字列 += "<br>"
	RETURN 1
ENDIF


@オナニー実処理(ARG, オナニー部位, 使用道具 = "")
#DIM オナニー部位
#DIM オナニー回数
#DIM 絶頂回数
#DIM 射精回数
#DIM 噴乳回数
#DIM 性欲減少量
#DIMS 使用道具

IF EXP:ARG:自慰経験 > 500
	オナニー回数 = 8
ELSEIF ABL:ARG:欲望 >= 4 || EXP:ARG:自慰経験 > 100
	オナニー回数 = 3
ELSE
	オナニー回数 = 1
ENDIF
オナニー回数 += RAND(ABL:ARG:オナニー部位 + 1)

;道具によるオナニー回数増加
SIF 使用道具 == "Ｅマッサージャ"
	オナニー回数 += 5

EXP:ARG:自慰経験 += オナニー回数
CSTR:ARG:オナニー報告_記録文字列 += @"자위 경험 + {オナニー回数}<br>"
EXP:ARG:オナニー部位 += オナニー回数
CSTR:ARG:オナニー報告_記録文字列 += @"%GET_EXPNAME(オナニー部位, ARG)% + {オナニー回数}<br>"
性欲減少量 = MAX(200, オナニー回数 * (MAXBASE:ARG:性欲 / 20))
BASE:ARG:性欲 = MAX(BASE:ARG:性欲 - 性欲減少量, 0)

絶頂回数 = MIN(オナニー回数, MAX(ABL:ARG:オナニー部位, RAND:(オナニー回数 + 1)))
JUEL:ARG:欲情 += MIN(500, POWER(ABL:ARG:欲望 + 1, 3)) * 絶頂回数
JUEL:ARG:オナニー部位 += MIN(500, POWER(ABL:ARG:オナニー部位 + 1, 3)) * (絶頂回数 + TALENT:ARG:(オナニー部位 + 部位感度)>0)

IF 絶頂回数
	EXP:ARG:絶頂経験 += 絶頂回数
	CSTR:ARG:オナニー報告_記録文字列 += @"절정 경험 + {絶頂回数}<br>"
	EXP:ARG:(オナニー部位 + 部位絶頂経験) += 絶頂回数
	CSTR:ARG:オナニー報告_記録文字列 += @"%EXPNAME:(オナニー部位 + 部位絶頂経験)% + {絶頂回数}<br>"
	性欲減少量 = MAX(400, 絶頂回数 * (MAXBASE:ARG:性欲 / 10))
	BASE:ARG:性欲 = MAX(BASE:ARG:性欲 - 性欲減少量, 0)

	IF HAS_PENIS(ARG)
		IF オナニー部位 == 部位_Ｃ
			CALL コマンド外精力消費処理(ARG, 絶頂回数)
			射精回数 = RESULT
		ELSE
			;Ｃ以外のときは絶頂回数からかなり軽減する
			;前立腺が十分開発されている場合は前立腺を責める
			CALL コマンド外精力消費処理(ARG, RAND(絶頂回数/2+1), オナニー部位 == 部位_Ａ && EXP:ARG:前立腺開発 > 100)
			射精回数 = RESULT
		ENDIF
		IF 射精回数
			EXP:ARG:射精経験 += 射精回数
			CSTR:ARG:オナニー報告_記録文字列 += @"사정 경험 + {射精回数}<br>"
		ENDIF
	ENDIF
	IF TALENT:ARG:母乳体質
		噴乳回数 = 絶頂回数
		;Ｂ以外のときは絶頂回数からかなり軽減する
		SIF オナニー部位 != 部位_Ｂ
			噴乳回数 = RAND(絶頂回数/2+1)
		IF 噴乳回数
			EXP:ARG:噴乳経験 += 噴乳回数
			CSTR:ARG:オナニー報告_記録文字列 += @"분유 경험 + {噴乳回数}<br>"
		ENDIF
	ENDIF

	CSTR:ARG:オナニー報告_記録文字列 += "<br>"
	;一日終了時のオナニーは体力減少しない
	CALL ZP_取得処理(ARG, 絶頂回数)
	SIF RESULT > 0
		CSTR:ARG:オナニー報告_記録文字列 += @"{FLAG:ZP所持量 - RESULT} + {RESULT}（%CALLNAME:ARG%）= {FLAG:ZP所持量}<br>"
	CFLAG:ARG:体力成長値 = MIN(4000, CFLAG:ARG:体力成長値 + 絶頂回数 * 5)
ENDIF

IF CFLAG:ARG:リゾート内オナニー回数 < 1
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めてリゾート内で[オナニー]をした</font>","うふふ")
ELSEIF CFLAG:ARG:リゾート内オナニー回数 == 9
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>リゾート内で[オナニー]をした回数が１０回に到達した</font>","うふふ")
ELSEIF CFLAG:ARG:リゾート内オナニー回数 == 49
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>リゾート内で[オナニー]をした回数が５０回に到達した</font>","うふふ")
ELSEIF CFLAG:ARG:リゾート内オナニー回数 == 99
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>リゾート内で[オナニー]をした回数が１００回に到達した</font>","うふふ")
ENDIF
CFLAG:ARG:リゾート内オナニー回数 ++

RETURN オナニー回数

@母乳搾りオナニー実処理(ARG)
#DIM CONST オナニー部位 = 部位_Ｂ
#DIM オナニー回数
#DIM 絶頂回数
#DIM 噴乳回数
#DIM 性欲減少量

IF ABL:ARG:Ｂ感覚 >= 3 || ABL:ARG:欲望 >= 4
	オナニー回数 = 3
	噴乳回数 = 2
ELSE
	オナニー回数 = 1
	噴乳回数 = 1
ENDIF
オナニー回数 += RAND:(ABL:ARG:Ｂ感覚 + 1)

EXP:ARG:自慰経験 += オナニー回数
CSTR:ARG:オナニー報告_記録文字列 += @"자위 경험 + {オナニー回数}<br>"
EXP:ARG:オナニー部位 += オナニー回数
CSTR:ARG:オナニー報告_記録文字列 += @"%GET_EXPNAME(オナニー部位, ARG)% + {オナニー回数}<br>"
性欲減少量 = MAX(200, オナニー回数 * (MAXBASE:ARG:性欲 / 20))
BASE:ARG:性欲 = MAX(BASE:ARG:性欲 - 性欲減少量, 0)

絶頂回数 = MIN(オナニー回数, MAX(ABL:ARG:オナニー部位, RAND:(オナニー回数 + 1)))
JUEL:ARG:欲情 += MIN(500, POWER(ABL:ARG:欲望 + 1, 3)) * 絶頂回数
JUEL:ARG:オナニー部位 += MIN(500, POWER(ABL:ARG:オナニー部位 + 1, 3)) * (絶頂回数 + TALENT:ARG:(オナニー部位 + 部位感度)>0)

噴乳回数 += RAND:オナニー回数
IF 噴乳回数 > 0
	BASE:ARG:母乳 = MAX(BASE:ARG:母乳 - 噴乳回数 * 3000, 0)
	EXP:ARG:噴乳経験 += 噴乳回数
	CSTR:ARG:オナニー報告_記録文字列 += @"분유 경험 + {噴乳回数}<br>"
ENDIF

IF 絶頂回数
	EXP:ARG:絶頂経験 += 絶頂回数
	CSTR:ARG:オナニー報告_記録文字列 += @"절정 경험 + {絶頂回数}<br>"
	EXP:ARG:(オナニー部位 + 部位絶頂経験) += 絶頂回数
	CSTR:ARG:オナニー報告_記録文字列 += @"%EXPNAME:(オナニー部位 + 部位絶頂経験)% + {絶頂回数}<br>"
	性欲減少量 = MAX(400, 絶頂回数 * (MAXBASE:ARG:性欲 / 10))
	BASE:ARG:性欲 = MAX(BASE:ARG:性欲 - 性欲減少量, 0)
	CSTR:ARG:オナニー報告_記録文字列 += "<br>"
	;一日終了時のオナニーは体力減少しない
	CALL ZP_取得処理(ARG, 絶頂回数)
	SIF RESULT > 0
		CSTR:ARG:オナニー報告_記録文字列 += @"{FLAG:ZP所持量 - RESULT} + {RESULT}（%CALLNAME:ARG%）= {FLAG:ZP所持量}<br>"
	CFLAG:ARG:体力成長値 = MIN(4000, CFLAG:ARG:体力成長値 + 絶頂回数 * 5)
ENDIF

IF CFLAG:ARG:リゾート内オナニー回数 < 1
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めてリゾート内で[オナニー]をした</font>","うふふ")
ELSEIF CFLAG:ARG:リゾート内オナニー回数 == 9
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>リゾート内で[オナニー]をした回数が１０回に到達した</font>","うふふ")
ELSEIF CFLAG:ARG:リゾート内オナニー回数 == 49
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>リゾート内で[オナニー]をした回数が５０回に到達した</font>","うふふ")
ELSEIF CFLAG:ARG:リゾート内オナニー回数 == 99
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>リゾート内で[オナニー]をした回数が１００回に到達した</font>","うふふ")
ENDIF
CFLAG:ARG:リゾート内オナニー回数 ++



@野外オナニー実処理(ARG, オナニー部位)
#DIM オナニー部位
#DIM オナニー回数
#DIM 絶頂回数
#DIM 射精回数
#DIM 噴乳回数
#DIM 性欲減少量

IF EXP:ARG:自慰経験 > 500
	オナニー回数 = 8
ELSEIF ABL:ARG:欲望 >= 4 || EXP:ARG:自慰経験 > 100
	オナニー回数 = 3
ELSE
	オナニー回数 = 1
ENDIF
オナニー回数 += RAND(ABL:ARG:オナニー部位 + 1)

EXP:ARG:自慰経験 += オナニー回数
EXP:ARG:野外オナニー経験 += オナニー回数
EXP:ARG:オナニー部位 += オナニー回数
性欲減少量 = MAX(200, オナニー回数 * (MAXBASE:ARG:性欲 / 20))
BASE:ARG:性欲 = MAX(BASE:ARG:性欲 - 性欲減少量, 0)
EXP:ARG:露出快楽経験 += オナニー回数

CUP:ARG:欲情 += オナニー回数 * 1000
CUP:ARG:オナニー部位 += オナニー回数 * 1000
SIF TALENT:ARG:(オナニー部位 + 部位感度) > 0
	CUP:ARG:オナニー部位 += オナニー回数 * 1000
CUP:ARG:恥情 += オナニー回数 * 1000

;1回は必ず絶頂する
絶頂回数 = MIN(オナニー回数, MAX(1, ABL:ARG:オナニー部位, RAND:(オナニー回数 + 1)))
EXP:ARG:絶頂経験 += 絶頂回数
EXP:ARG:(オナニー部位 + 部位絶頂経験) += 絶頂回数
EXP:ARG:露出絶頂経験 += 絶頂回数
性欲減少量 = MAX(400, 絶頂回数 * (MAXBASE:ARG:性欲 / 10))
BASE:ARG:性欲 = MAX(BASE:ARG:性欲 - 性欲減少量, 0)
IF HAS_PENIS(ARG)
	IF オナニー部位 == 部位_Ｃ
		CALL コマンド外精力消費処理(ARG, 絶頂回数)
		射精回数 = RESULT
	ELSE
		;Ｃ以外のときは絶頂回数からかなり軽減する
		;前立腺が十分開発されている場合は前立腺を責める
		CALL コマンド外精力消費処理(ARG, RAND(絶頂回数/2+1), オナニー部位 == 部位_Ａ && EXP:ARG:前立腺開発 > 100)
		射精回数 = RESULT
	ENDIF
	IF 射精回数
		EXP:ARG:射精経験 += 射精回数
	ENDIF
ENDIF
IF TALENT:ARG:母乳体質
	噴乳回数 = 絶頂回数
	;Ｂ以外のときは絶頂回数からかなり軽減する
	SIF オナニー部位 != 部位_Ｂ
		噴乳回数 = RAND(絶頂回数/2+1)
	IF 噴乳回数
		EXP:ARG:噴乳経験 += 噴乳回数
		BASE:ARG:母乳 = MAX(BASE:ARG:母乳 - 噴乳回数 * 3000, 0)
	ENDIF
ENDIF

CUP:ARG:欲情 = MIN(CUP:ARG:欲情 + 絶頂回数 * 1000 + RAND:1000, 9999)
CUP:ARG:オナニー部位 = MIN(CUP:ARG:オナニー部位 + 絶頂回数 * 1000 + RAND:1000, 9999)
CUP:ARG:恥情 = MIN(CUP:ARG:恥情 + 絶頂回数 * 1000 + RAND:1000, 9999)

NOWEX:ARG:オナニー部位 = 絶頂回数
CALL 絶頂体力減少処理(ARG, NOWEX:ARG:Ｃ絶頂, NOWEX:ARG:Ｖ絶頂, NOWEX:ARG:Ａ絶頂, NOWEX:ARG:Ｂ絶頂, NOWEX:ARG:Ｓ絶頂)
IF IS_SAME_ROOM(ARG, PLAYER)
	CALL ZP_取得処理(ARG, 絶頂回数, 1)
ELSE
	CALL ZP_取得処理(ARG, 絶頂回数)
ENDIF
CFLAG:ARG:体力成長値 = MIN(4000, CFLAG:ARG:体力成長値 + 絶頂回数 * 5)
NOWEX:ARG:オナニー部位 = 0
TCVAR:ARG:野外オナニー_実行回数 += 1

IF CFLAG:ARG:リゾート内野外オナニー回数 < 1
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めてリゾート内で[野外オナニー]をした</font>","うふふ")
ELSEIF CFLAG:ARG:リゾート内野外オナニー回数 == 9
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>リゾート内で[野外オナニー]をした回数が１０回に到達した</font>","うふふ")
ELSEIF CFLAG:ARG:リゾート内野外オナニー回数 == 49
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>リゾート内で[野外オナニー]をした回数が５０回に到達した</font>","うふふ")
ELSEIF CFLAG:ARG:リゾート内野外オナニー回数 == 99
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>リゾート内で[野外オナニー]をした回数が１００回に到達した</font>","うふふ")
ENDIF
CFLAG:ARG:リゾート内野外オナニー回数 ++


@野外オナニー欲求不満処理(ARG)
#DIM 性欲上昇量

RESULTS = 
RESULTS += @"%CALLNAME:ARG%은(는) 만족스러운 야외 오나니를 하지 못하고, 답답해하고 있다……。"

性欲上昇量 = MIN(MAXBASE:ARG:性欲 - BASE:ARG:性欲, 500)

RESULTS += @"　　%CALLNAME:ARG%의 성욕이{性欲上昇量}상승했다."

