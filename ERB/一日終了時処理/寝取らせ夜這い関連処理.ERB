@寝取らせ夜這い判定()
#DIM ピックアップ配列, 5000
#DIM 島内配列, 5000
#DIM 主導キャラ配列, 5000
#DIM 主導キャラ
#DIM 候補カップリング配列, 5000, 2
#DIM 候補数
#DIM RAND値
#DIM ピックアップ数
#DIM カップリングキャラ１
#DIM カップリングキャラ２
#DIMS カップリング関係性
#DIM DT_ID
#DIM 選択結果
#DIM 対象キャラ
#DIM L_CNT

SIF OPTION変数:夜這い全部禁止
	RETURN 0

;気になってどういうものか見に来たあなたに向けて簡単な仕様説明
;
;・性癖開拓システムでリゾート客(仮にＡとする)にエロ本を押し付けてＡの脳を破壊し、性癖素質[寝取られ(興味)]を与えます。
;・縁結びシステムを使って、Ａと他のキャラ(仮にＢとする)をくっつけます。関係性は恋慕でもセフレでもＯＫ。
;・あなたとＡの「信頼度」を、一定以上にします。その際あなたが「遊び人」として極まってきているなら、このボーダーはちょっとだけ下がります。Ａが「従業員」であるなら、このボーダーは劇的に下がります。
;上記のみっつの条件を、すべて満たせば準備完了！(別にＡもＢも両方寝取られ性癖持ってても大丈夫だよ！)
;
;「ＡとＢがどちらも島にいる」状態で、あなたが「自室」で「就寝」する際、「他に同衾するキャラがいなければ(一人寝であるなら)」確率でイベントが発生。
;一日の終わりに、度し難い性癖に脳を破壊されたＡがあなたの部屋を訪ねてきて、「Ｂを抱いてくれ」とお願いされます。性癖名は「寝取られ」だけど、Ａがやる実際のプレイは「寝取らせ」が正しいわけですな。
;いやぁ、頼まれちゃったからなぁ～！しょうがないなぁ～～～！！ほな、いただきま～～～っす！！
;
;毎日来られても困るし特別感もないので、発生確率は低め。「こんなもんかな～」で適当に決めてるので、低すぎる！と思ったら本スレとかDiscordで教えてください。調整します。
;実際問題、発生に一人寝を要求すること自体がプレイに対する一つの縛りになっちゃってるから、もうちょい高くてもいいかもな～とは書いた本人も思ってます。
;抽出処理とタイミングの関係で、条件を満たしたカップルが増えるほどに発生確率は平均化されちゃうし、狙った組み合わせも出づらくなるし。
;あなたの「遊び人」が５とかだと確率がちょっと上がる。理由は確率処理んとこに書いた。
;
;さいごに
;エロ本ばら撒きには要注意。みんながみんな性癖持ちだと、「男あなたにグラン(♂)を差し出してくるジータ(寝取られ性癖持ち)」とかそういう事故が起きます。
;まぁ、意図して除外処理しなかったんですけども。だ、だってそういう需要もあるかもだし……。

;補正値の減衰
;・毎日減衰させたいので各種判定前に実行
FOR 対象キャラ, 1, CHARANUM
	; 1日ごとに補正が10％づつ増加(上限0％)
	SIF 寝取らせ_発生率補正:対象キャラ:0
		寝取らせ_発生率補正:対象キャラ:0 = MIN(寝取らせ_発生率補正:対象キャラ:0 + 10, 0)
NEXT

;あなたが自室に居ない場合はダメ
SIF CFLAG:PLAYER:現在位置 != CFLAG:PLAYER:自室位置
	RETURN 0
SIF CFLAG:PLAYER:現在マップ種別 != 0
	RETURN 0

;同衾キャラが居る場合はダメ
SIF TARGET:1 > 0
	RETURN 0
SIF FLAG:挿入睡眠相手記録 > 0
	RETURN 0

VARSET ピックアップ配列
;恋慕の組み合わせピックアップ処理
;まず恋慕orセフレで、組み合わせにあなたが入っていないカップリングを割り出す
ピックアップ数 = DT_SELECT("関係性データベース", @"対象キャラ１ <> {CFLAG:MASTER:人物番号} And 対象キャラ２ <> {CFLAG:MASTER:人物番号} And (関係性種別 = '恋慕' Or 関係性種別 = 'セフレ')", ,ピックアップ配列)
;この命令でピックアップ数には「該当するカップリングの数」が
;ピックアップ配列には「該当するデータベースID」が順番に入っている

;例外フォロー忘れずに
SIF ピックアップ数 < 1
	RETURN 0

;組み合わせが島内にいるかどうかを見る
VARSET 島内配列
VARSET 主導キャラ配列
候補数 = 0
FOR L_CNT, 0, ピックアップ数
	DT_ID = ピックアップ配列:L_CNT
	カップリングキャラ１ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", DT_ID, "対象キャラ１", 1))
	カップリングキャラ２ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", DT_ID, "対象キャラ２", 1))

	;どちらかが滞在していない場合はダメ
	SIF CFLAG:カップリングキャラ１:滞在期間 < 1
		CONTINUE
	SIF CFLAG:カップリングキャラ２:滞在期間 < 1
		CONTINUE

	;どちらかが寝取られ素質を持っているならOK
	SIF 性癖素質:カップリングキャラ１:寝取られ <= 0 && 性癖素質:カップリングキャラ２:寝取られ <= 0
		CONTINUE

	;- 主導/献上品が関係ない判定
	SIF !寝取らせ夜這い判定_キャラ状態判定(カップリングキャラ１) || !寝取らせ夜這い判定_キャラ状態判定(カップリングキャラ２)
		CONTINUE
	
	SIF !寝取らせ夜這い判定_身体条件判定(カップリングキャラ１) || !寝取らせ夜這い判定_身体条件判定(カップリングキャラ２)
		CONTINUE

	;1カップリングごとに主導キャラを入れ替えて2回判定する
	FOR 主導キャラ, 1, 2 + 1
		IF 主導キャラ == 1
			寝取らせ_主導キャラ   = カップリングキャラ１
			寝取らせ_献上品キャラ = カップリングキャラ２
		ELSE
			寝取らせ_主導キャラ   = カップリングキャラ２
			寝取らせ_献上品キャラ = カップリングキャラ１
		ENDIF
		;主導側が寝取られ素質を持っていない場合はダメ
		SIF 性癖素質:寝取らせ_主導キャラ:寝取られ <= 0
			CONTINUE
		;献上品側があなたと同性で同性愛を持っていない場合はダメ
		SIF 同性愛性癖判定(寝取らせ_献上品キャラ, PLAYER) && 性癖素質:寝取らせ_献上品キャラ:同性愛 < 1
			CONTINUE
		;主導側の信頼度が足りない場合はダメ
		SIF !寝取らせ夜這い判定_信頼度判定(寝取らせ_主導キャラ)
			CONTINUE
		;主導側の確率判定
		SIF !寝取らせ夜這い判定_確率判定(寝取らせ_主導キャラ)
			CONTINUE
		;候補に登録
		候補カップリング配列:候補数:0 = DT_ID
		候補カップリング配列:候補数:1 = 主導キャラ
		候補数 ++
	NEXT
NEXT

;例外フォロー忘れずに
SIF 候補数 < 1
	RETURN 0

;イベント本文
DRAWLINE
PRINTL
PRINTFORMW ──심야, %CALLNAME:PLAYER%의 방 문이 노크되었다.
PRINTL
PRINTFORML 이런 시간에 누구지……。
PRINTFORM 예기치 못한 손님에%CALLNAME:PLAYER%이 문을 열자,
IF 候補数 < 寝取らせ_対象選択化しきい値
	; 候補が少ないときは1組だけ選出
	RAND値 = RAND:候補数
	DT_ID      = 候補カップリング配列:RAND値:0
	主導キャラ = 候補カップリング配列:RAND値:1

	;該当のIDから対象キャラを持ってきてキャラ番号を取る
	カップリングキャラ１ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", DT_ID, "対象キャラ１", 1))
	カップリングキャラ２ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", DT_ID, "対象キャラ２", 1))
	カップリング関係性  '= DT_CELL_GETS("関係性データベース", DT_ID, "関係性種別", 1)

	;寝取らせの主導側と差し出される側を決める
	IF 主導キャラ == 1
		寝取らせ_主導キャラ   = カップリングキャラ１
		寝取らせ_献上品キャラ = カップリングキャラ２
	ELSE
		寝取らせ_主導キャラ   = カップリングキャラ２
		寝取らせ_献上品キャラ = カップリングキャラ１
	ENDIF
	PRINTFORML 그곳에는%CALLNAME:寝取らせ_主導キャラ%와 %CALLNAME:寝取らせ_献上品キャラ%의 모습이 있었다.
ELSE
	; 候補が多いときは複数選出してそこから選択
	PRINTFORML 그곳에는…
	; シャッフル
	FOR L_CNT, 0, 候補数
		RAND値 = RAND:候補数
		SWAP 候補カップリング配列:L_CNT:0, 候補カップリング配列:RAND値:0
		SWAP 候補カップリング配列:L_CNT:1, 候補カップリング配列:RAND値:1
	NEXT
	; シャッフルしたリストから表示人数分だけ表示
	FOR L_CNT, 0, MIN(候補数, 寝取らせ_選択表示人数)
		DT_ID      = 候補カップリング配列:L_CNT:0
		主導キャラ = 候補カップリング配列:L_CNT:1

		;該当のIDから対象キャラを持ってきてキャラ番号を取る
		カップリングキャラ１ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", DT_ID, "対象キャラ１", 1))
		カップリングキャラ２ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", DT_ID, "対象キャラ２", 1))
		カップリング関係性  '= DT_CELL_GETS("関係性データベース", DT_ID, "関係性種別", 1)
		IF 主導キャラ == 1
			寝取らせ_主導キャラ   = カップリングキャラ１
			寝取らせ_献上品キャラ = カップリングキャラ２
		ELSE
			寝取らせ_主導キャラ   = カップリングキャラ２
			寝取らせ_献上品キャラ = カップリングキャラ１
		ENDIF
		PRINTS "　"
		PRINTBUTTON @"[{L_CNT, 2}]%CALLNAME:寝取らせ_主導キャラ%와 %CALLNAME:寝取らせ_献上品キャラ%의 모습이 있었다", L_CNT
		PRINTL
	NEXT
	PRINTS "　"
	PRINTBUTTON "[-1]아무도 없었다", -1
	PRINTL
	BINPUT
	選択結果 = RESULT
	IF 選択結果 == -1
		; キャンセル
		PRINTL
		PRINTFORML …문을 연 곳에는 아무도 없었다. 아무래도 헛들린 소리였던 것 같다.
		PRINTFORMW %CALLNAME:PLAYER%은(는) 문을 닫고 방으로 돌아갔다.
		PRINTL
		RETURN 1
	ENDIF
	; 選択したカップリングの情報を取得
	DT_ID      = 候補カップリング配列:選択結果:0
	主導キャラ = 候補カップリング配列:選択結果:1
	カップリングキャラ１ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", DT_ID, "対象キャラ１", 1))
	カップリングキャラ２ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", DT_ID, "対象キャラ２", 1))
	カップリング関係性  '= DT_CELL_GETS("関係性データベース", DT_ID, "関係性種別", 1)
	IF 主導キャラ == 1
		寝取らせ_主導キャラ   = カップリングキャラ１
		寝取らせ_献上品キャラ = カップリングキャラ２
	ELSE
		寝取らせ_主導キャラ   = カップリングキャラ２
		寝取らせ_献上品キャラ = カップリングキャラ１
	ENDIF
	PRINTL
ENDIF
PRINTL 

RESULT = 0
TRYCALLFORM KOJO_EVENT_寝取らせ夜這い_訪問_{NO:寝取らせ_主導キャラ}(寝取らせ_主導キャラ, 寝取らせ_献上品キャラ)
SIF RESULT > 0
	GOTO 訪問ラベル

PRINTFORM 욕망이 깃든%CALLNAME:寝取らせ_主導キャラ%의 눈동자는 복도의 밤의 어둠 속에서 찬란하게 빛나는 듯하고,
;訪問キャラふたりの間では事前に話がついているので、これからの事に対する反応
IF EXP:寝取らせ_献上品キャラ:貸出経験 >= 3
	PRINTFORMW 함께 온%CALLNAME:寝取らせ_献上品キャラ%도 뺨을 붉히며, 이제부터 맛볼 불륜의 쾌락에 대한 기대로 가슴을 두근거리고 있는 모습이다.
ELSEIF TALENT:寝取らせ_献上品キャラ:処女 && GETBIT(TALENT:寝取らせ_献上品キャラ:性別, 0)
	PRINTFORMW 그와는 대조적으로, 함께 온%CALLNAME:寝取らせ_献上品キャラ%은(는) 불안과 공포로 가득 찬 모습이다.
ELSEIF TALENT:寝取らせ_献上品キャラ:羞恥心 == 1 || TALENT:寝取らせ_献上品キャラ:貞操 == 1
	PRINTFORMW 함께 온%CALLNAME:寝取らせ_献上品キャラ%은(는) 부끄러운 듯 고개를 숙이고 있다.
ELSEIF TALENT:寝取らせ_献上品キャラ:経験豊富
	PRINTFORMW 연인이었던%CALLNAME:寝取らせ_献上品キャラ%도 히죽히죽 윤기 나는 미소를 짓고 있다.
ELSE
	PRINTFORMW 그 반면, 연인이었던%CALLNAME:寝取らせ_献上品キャラ%은 고개를 숙인 채 어쩔 줄 몰라 하며 서 있다.
ENDIF
PRINTL
PRINTFORML 용건이 무엇이든 간에, 이런 곳에서 이야기하는 것도 좀 그렇다.
PRINTFORMW %CALLNAME:PLAYER%는 두 사람을 방 안으로 불러들였다……
PRINTL
PRINTFORML ────────
PRINTW
PRINTFORM ……흥분한%CALLNAME:寝取らせ_主導キャラ%의 용건이란 즉, %CALLNAME:PLAYER%에게
IF EXP:寝取らせ_主導キャラ:寝取られ経験 >= 5
	PRINTFORM , 평소처럼
ELSEIF EXP:寝取らせ_主導キャラ:寝取られ経験 >= 2
	PRINTFORM , 또 지난번처럼
ENDIF
PRINTFORMW %CALLNAME:寝取らせ_献上品キャラ%%TEXTR("를 안아주기를/와 섹스하기를")% 바라는 것 같았다.
PRINTL
PRINTFORML 이렇게 %CALLNAME:PLAYER%의 방으로 오기 전에, 두 사람 사이에서 이야기는 끝내놓은 듯했다.
;寝取られ中毒彼氏クンの手によって、セクシーランジェリーで飾りたてられているかもしれない？
IF 知識素質:寝取らせ_主導キャラ:性知識 > 1 && 性癖素質:寝取らせ_主導キャラ:寝取られ == 3
		SIF CSTR:寝取らせ_献上品キャラ:コスプレ前の服 == "" && CSTR:寝取らせ_献上品キャラ:表示グラフィックカテゴリ != ""
			CSTR:寝取らせ_献上品キャラ:コスプレ前の服 '= CSTR:寝取らせ_献上品キャラ:表示グラフィックカテゴリ
	LOCALS '= CSTR:ネ토리_헌상품캐릭터:벗기기 전의 옷
	CALL CLOTHES_CHANGE(寝取らせ_献上品キャラ, "セクシーランジェリー")
	CSTR:寝取らせ_献上品キャラ:脱ぐ前の服 '= LOCALS
	PRINTFORMW %CALLNAME:PLAYER%의 흥분을 더욱 부추기기 위해서인지, %CALLNAME:寝取らせ_主導キャラ%는 %CALLNAME:寝取らせ_献上品キャラ%에게 일부러 정욕을 부추기는 속옷까지 입혀서 오고 있다……。
ENDIF
PRINTL
IF EXP:寝取らせ_献上品キャラ:貸出経験 >= 1
	IF EXP:寝取らせ_献上品キャラ:貸出経験 <= 2
		;「今回が三回目」まで
		PRINTFORM %CALLNAME:PLAYER%이 「또 잘 부탁해」라고 속삭이자,
		IF TALENT:寝取らせ_献上品キャラ:経験豊富
			PRINTFORML %CALLNAME:寝取らせ_献上品キャラ%는 요염하게 미소 지어 보였다.
		ELSE
			PRINTFORML 지난번 일을 떠올렸는지, 뺨을 붉힌 %CALLNAME:寝取らせ_献上品キャラ%은 고개를 숙인 채 끄덕였다.
		ENDIF
	ELSEIF ABL:寝取らせ_献上品キャラ:マゾ性感 >= 2
		PRINTFORML %CALLNAME:PLAYER%이(가)「할 말은?」이라고%TEXTR("고하다/고압적으로 고하다/고하며, 유방을 난폭하게 움켜쥐다")%라고 하자, 자신의 입장을%TEXTR("알아차린/떠올린")%%CALLNAME:寝取らせ_献上品キャラ%은(는) 오싹오싹 몸을 떨며,
		IF ABL:寝取らせ_献上品キャラ:マゾ性感 >= 3
			IF 知識素質:寝取らせ_献上品キャラ:高貴
				;全裸土下座はこのあとの服の扱いが大変なのでパス。いや「全裸土下座フラグが1なら全裸スタート」とかできなくはないだろうけども……
				;手の甲へのキスは「敬愛」「忠誠」、足の甲へのキスは「服従」の意味を持つ。高貴だから詳しいんだ！
				PRINTFORM %TEXTR("그 자리에서 무릎을 꿇고 아름다운 도게자를 선보이며/그 자리에서 세 손가락을 짚고 이마를 바닥에 비비며/그 자리에서 무릎을 꿇고 당신의 손등에 입맞추며/그 자리에서 무릎을 꿇고 당신의 발등에 입맞추며")%,
			ELSE
				;「深々と頭を下げる」は微妙に状況にそぐわない気がしたので、とりあえず男だけで
				;土下座は……高貴ゆえに美しい土下座ができる方々と比べるとなんかこう、格が落ちるというか、下位互換的な被りかたっていうか……
				IF TEQUIP:スカートめくり可 == 1
					PRINTFORM 스커트를 걷어 올리면서,
				ELSEIF TALENT:寝取らせ_献上品キャラ:性別 == 1
					PRINTFORM %CALLNAME:PLAYER%의 손을 잡고 자신의 가슴을 주무르게 하자,
				ELSE
					PRINTFORM 깊이 고개를 숙이고,
				ENDIF
			ENDIF
			IF RISKY_DAY(寝取らせ_献上品キャラ) >= 1 && GETBIT(TALENT:PLAYER:性別, 1)
				PRINTFORM 「오늘은 위험한 날이니, 당신의%TEXTR("씨앗을 내려주세요/아이를 낳게 해주세요/아기를 임신시켜주세요/정액을 주세요")%
				CALL PRINT_STR("……_H_」")
				PRINTFORMW 라고 씨앗을 구걸했다.
			ELSE
				;できるだけ男女兼用できる文面に
				SELECTCASE RAND:4
				CASE 0
					PRINTFORM 「오늘 밤도, %CALLNAME:寝取らせ_主導キャラ%와는 할 수 없는 '진정한 섹스'를 가르쳐주세요
				CASEELSE
					PRINTFORM 「오늘 밤도%TEXTR("길들여주세요/많이 귀여워해주세요/, 잔뜩 '놀아주세요'/많이 기분 좋게 해주세요")%
				ENDSELECT
				CALL PRINT_STR("……_H_」")
				PRINTFORMW 라고 간청했다.
			ENDIF
		ELSE
			;「深々と頭を下げる」は微妙に状況にそぐわない気がしたので、とりあえず男だけで
			;土下座は……高貴ゆえに美しい土下座ができる方々と比べるとなんかこう、格が落ちるというか、下位互換的な被りかたっていうか……
			IF TEQUIP:スカートめくり可 == 1
				PRINTFORM 스커트를 걷어 올리면서,
			ELSEIF TALENT:寝取らせ_献上品キャラ:性別 == 1
				PRINTFORM %CALLNAME:PLAYER%의 손을 잡고 자신의 가슴을 주무르게 하자,
			ELSE
				PRINTFORM 깊이 고개를 숙이고,
			ENDIF
			IF TALENT:寝取らせ_献上品キャラ:プライド == 1
				PRINTFORM 굴욕에 떨리는 목소리로
			ELSEIF TALENT:寝取らせ_献上品キャラ:羞恥心 == 1 || TALENT:寝取らせ_献上品キャラ:貞操 == 1 || TALENT:寝取らせ_献上品キャラ:陽気／陰気 == -1
				PRINTFORM 망설이는 듯 떨리는 목소리로
			ENDIF
			;マゾ性感3の時よりも語彙と丁寧さを落とした感じ
			;できるだけ男女兼用できる文面に
			PRINTFORM 「또 오늘 밤도 %TEXTR("길들여 주세요/많이 귀여워해 주세요/많이 기분 좋게 해 주세요")%
			CALL PRINT_STR("……_H_」")
			PRINTFORMW 라고 간청했다.
		ENDIF
	ELSE
	;マゾ性感１以下かつ４回目以降。慣れで雑な扱いされてもよくなってしまう態度の変化
		PRINTFORM %CALLNAME:PLAYER%이 "또 잘 부탁해"라고 %TEXTR("인사 대신 가슴을 주무르다/인사 대신 엉덩이를 주무르다/인사 대신 젖꼭지를 꼬집다/인사 대신 젖꼭지를 잡아당기다/어깨를 안다/허리를 끌어안다/어깨에 손을 두르고 가슴을 주무르다/인사 대신 과시하듯이 입술을 빼앗다")%라고 하자,
		PRINTFORMW %CALLNAME:寝取らせ_献上品キャラ%은 달콤하게 한숨을 흘렸고, 기대했던 그 모습에 %CALLNAME:寝取らせ_主導キャラ%는 꿀꺽하고 침을 삼켰다.
	ENDIF
ELSE
	;はじめて「貸し出される」場合
	PRINTFORM %CALLNAME:PLAYER%이 "정말 괜찮겠어?"라고 묻자
	IF TALENT:寝取らせ_献上品キャラ:処女 || TALENT:寝取らせ_献上品キャラ:陽気／陰気 == -1 && GETBIT(TALENT:寝取らせ_献上品キャラ:性別, 0)
		PRINTFORMW , %CALLNAME:寝取らせ_献上品キャラ%는 목소리를 떨면서도, "%CALLNAME:寝取らせ_主導キャラ%를 위해서니까"라고 씩씩하게 고개를 끄덕여 보였다.
	ELSEIF TALENT:寝取らせ_献上品キャラ:羞恥心 == 1 || TALENT:寝取らせ_献上品キャラ:貞操 == 1
		PRINTFORMW , %CALLNAME:寝取らせ_献上品キャラ%는 고개를 숙이면서도, "%CALLNAME:寝取らせ_主導キャラ%를 위해서니까"라고 씩씩하게 고개를 끄덕여 보였다.
	ELSEIF TALENT:寝取らせ_献上品キャラ:経験豊富
		PRINTFORMW , %CALLNAME:寝取らせ_献上品キャラ%는 %CALLNAME:PLAYER%의 팔에 가슴을 밀어붙이듯이 안기며, "%CALLNAME:寝取らせ_主導キャラ%가 나쁜 거니까"라고 %CALLNAME:寝取らせ_主導キャラ%에게 흘겨보였다.
	ELSEIF TALENT:寝取らせ_献上品キャラ:プライド == 1
		PRINTFORMW %CALLNAME:寝取らせ_献上品キャラ%는, %CALLNAME:寝取らせ_主導キャラ%에 의해 %CALLNAME:PLAYER%에게 내밀어지는 굴욕에 얼굴을 일그러뜨리면서도 마지못해 고개를 끄덕여 보였다.
	ELSE
		PRINTFORMW , %CALLNAME:寝取らせ_献上品キャラ%는 고개를 숙이면서도, "%CALLNAME:寝取らせ_主導キャラ%를 위해서니까"라고 강한 척 어색하게 웃어 보였다.
	ENDIF
ENDIF
$訪問ラベル
PRINTL 
PRINTFORML ────자, 어떻게 할까?
DRAWLINE
PRINTBUTTON "[0] 받아들인다", 0
PRINTL
PRINTBUTTON "[1] 거절한다", 1
PRINTL
PRINTL
PRINTBUTTON "[100] 선택지에 대해 설명", 100

BINPUT
;口上用に選択肢を記録
RFLAG:コマンド結果受渡し変数２ = RESULT
選択結果 = RESULT
SELECTCASE 選択結果
	CASE 0
		RESULT = 0
		TRYCALLFORM KOJO_EVENT_寝取らせ夜這い_受諾_{NO:寝取らせ_主導キャラ}(寝取らせ_主導キャラ, 寝取らせ_献上品キャラ)
		SIF RESULT != 0
			GOTO 寝取らせ夜這い受諾_テキスト表示スキップ
		
		;貸出回数で分岐
		IF EXP:寝取らせ_献上品キャラ:貸出経験 >= 1
			PRINTFORML 모처럼 %CALLNAME:寝取らせ_主導キャラ%가 준비해 준 차려진 밥상이다. 거절할 이유도 없다.
			PRINTL 
			PRINTFORML 이전%TEXTR("충분히/마음껏")%%TEXTR("맛본/만끽한/즐긴")%%CALLNAME:寝取らせ_献上品キャラ%의 포옹감을 떠올리며, %CALLNAME:PLAYER%은(는) %CALLNAME:寝取らせ_献上品キャラ%에게 손을 뻗었다…….
		ELSE
			PRINTFORML %CALLNAME:寝取らせ_主導キャラ%의 간곡한 부탁이다. 합의하에 이루어진 일이라면, 무엇을 망설일 필요도 없다.
			PRINTL 
			PRINTFORML ……게다가, %CALLNAME:寝取らせ_献上品キャラ%와(과) %CALLNAME:寝取らせ_主導キャラ%가 지금의 관계가 될 수 있었던 것도, %CALLNAME:PLAYER%의 중재가 있었기에 가능한 일이다.
			PRINTFORMW %CALLNAME:PLAYER%에게는 이 「정당한 보수」를 받을 권리가 있을 것이다.
			PRINTL 
			PRINTFORML ──지금은 그저, 즐기면 된다. 마음껏, 욕망이 이끄는 대로.
			PRINTFORMW 도착적인 성벽에 파괴된 뇌로%CALLNAME:寝取らせ_献上品キャラ%를 내어주는%CALLNAME:寝取らせ_主導キャラ%의 모습에 속으로 비웃으며, %CALLNAME:PLAYER%은(는) %CALLNAME:寝取らせ_献上品キャラ%를 끌어안았다……
		ENDIF
		;関係性で分岐
		IF カップリング関係性 != "セフレ"
		;なんかあなたとの関係性も参照されちゃったっぽくて恋慕じゃ発生しなかったので
			PRINTL 
			;ふたりの関係が恋慕で、差し出される側が浮気を持っていない場合。必然、献上品にとってはこれが初めての貸出になるはず
			IF TALENT:寝取らせ_献上品キャラ:浮気 == 0
				IF TALENT:寝取らせ_献上品キャラ:処女 && GETBIT(TALENT:寝取らせ_献上品キャラ:性別, 0)
					PRINTFORM 사랑하는%CALLNAME:寝取らせ_主導キャラ%에게 아직 처녀도 바치지 못했는데, %CALLNAME:寝取らせ_主導キャラ%가 지켜보는 그 눈앞에서, %CALLNAME:寝取らせ_献上品キャラ%은(는) 원치 않는
				ELSE
					PRINTFORM 사랑하는%CALLNAME:寝取らせ_主導キャラ%가 지켜보는 그 눈앞에서, %CALLNAME:寝取らせ_献上品キャラ%은(는) 원치 않는
				ENDIF
				SETCOLOR カラーパレット("えっちな色")
				PRINT [외도]
				RESETCOLOR
				PRINTFORMW 로 발을 들여놓게 되어버렸다……
			ELSE
				PRINTL
				PRINTFORM 사랑하는%CALLNAME:寝取らせ_主導キャラ%가 지켜보는 그 눈앞에서 또, %CALLNAME:寝取らせ_献上品キャラ%의 원치 않는
				SETCOLOR カラーパレット("えっちな色")
				PRINT [외도]
				RESETCOLOR
				PRINTFORMW 의 막이 오른다……
			ENDIF
		ENDIF
		PRINTL 
		PRINTFORM %CALLNAME:寝取らせ_主導キャラ%의 소원을 이루어주기 위해, %CALLNAME:寝取らせ_献上品キャラ%은(는) 이 하룻밤%CALLNAME:PLAYER%의
		SETCOLOR カラーパレット("えっちな色")
		PRINT [말대로]
		RESETCOLOR
PRINTFORMW 이다. 원한다면 뭐든지 해줄 것이다……
		PRINTL 
		$寝取らせ夜這い受諾_テキスト表示スキップ
		
		RESULT = 0
		TRYCALLFORM KOJO_EVENT_寝取らせ夜這い_履歴登録_{NO:寝取らせ_主導キャラ}(寝取らせ_主導キャラ, 寝取らせ_献上品キャラ)
		SIF RESULT != 0
			GOTO 寝取らせ夜這い受諾_履歴登録スキップ
		IF カップリング関係性 != "セフレ"
			;ふたりの関係が恋慕で、差し出される側が浮気を持っていない場合。必然、献上品にとってはこれが初めての貸出になるはず
			IF TALENT:寝取らせ_献上品キャラ:浮気 == 0
				IF TALENT:寝取らせ_献上品キャラ:処女 && GETBIT(TALENT:寝取らせ_献上品キャラ:性別, 0)
					CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:寝取らせ_主導キャラ%からの願いで、まだ処女だった%CALLNAME:寝取らせ_献上品キャラ%を抱いてやった</font><img src='えっちハート'>","うふふ")
					CALL 履歴データベース登録(CFLAG:寝取らせ_献上品キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>まだ処女だったのに、恋人の%CALLNAME:寝取らせ_主導キャラ%のお願いで%CALLNAME:PLAYER%と[浮気]えっちさせられてしまった</font><img src='えっちハート'>","うふふ")
					CALL 履歴データベース登録(CFLAG:寝取らせ_主導キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:PLAYER%に%CALLNAME:寝取らせ_献上品キャラ%の処女を差し出し、寝取ってもらった</font><img src='えっちハート'>","うふふ")
				ELSE
					CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:寝取らせ_主導キャラ%の願いで%CALLNAME:寝取らせ_献上品キャラ%を抱いてやった</font><img src='えっちハート'>","うふふ")
					CALL 履歴データベース登録(CFLAG:寝取らせ_献上品キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>恋人の%CALLNAME:寝取らせ_主導キャラ%にお願いされて、%CALLNAME:PLAYER%と[浮気]えっちさせられてしまった</font><img src='えっちハート'>","うふふ")
					CALL 履歴データベース登録(CFLAG:寝取らせ_主導キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:PLAYER%に、恋人の%CALLNAME:寝取らせ_献上品キャラ%の自分ではまだ触れたこともない体を差し出し、寝取ってもらった</font><img src='えっちハート'>","うふふ")
				ENDIF
			ENDIF
		ELSEIF EXP:寝取らせ_献上品キャラ:貸出経験 == 0
			;はじめて貸し出される時
			IF TALENT:寝取らせ_献上品キャラ:処女 && GETBIT(TALENT:寝取らせ_献上品キャラ:性別, 0)
				CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:寝取らせ_主導キャラ%からの願いで、まだ処女だった%CALLNAME:寝取らせ_献上品キャラ%を抱いてやった</font><img src='えっちハート'>","うふふ")
				CALL 履歴データベース登録(CFLAG:寝取らせ_献上品キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>まだ処女だったのに、%CALLNAME:寝取らせ_主導キャラ%のお願いで、%CALLNAME:PLAYER%とセックスさせられてしまった</font><img src='えっちハート'>","うふふ")
				CALL 履歴データベース登録(CFLAG:寝取らせ_主導キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:PLAYER%に、%CALLNAME:寝取らせ_献上品キャラ%の処女を差し出し、寝取ってもらった</font><img src='えっちハート'>","うふふ")
			ELSE
				CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:寝取らせ_主導キャラ%の願いで%CALLNAME:寝取らせ_献上品キャラ%を抱いてやった</font><img src='えっちハート'>","うふふ")
				CALL 履歴データベース登録(CFLAG:寝取らせ_献上品キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:寝取らせ_主導キャラ%のお願いで、%CALLNAME:PLAYER%とセックスさせられてしまった</font><img src='えっちハート'>","うふふ")
				CALL 履歴データベース登録(CFLAG:寝取らせ_主導キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:PLAYER%に%CALLNAME:寝取らせ_献上品キャラ%を初めて差し出し、寝取ってもらった</font><img src='えっちハート'>","うふふ")
			ENDIF
		ENDIF
		$寝取らせ夜這い受諾_履歴登録スキップ
		
		SIF TALENT:寝取らせ_献上品キャラ:浮気 == 0
			TALENT:寝取らせ_献上品キャラ:浮気 = 1
		EXP:寝取らせ_主導キャラ:寝取られ経験 += 1
		EXP:寝取らせ_献上品キャラ:貸出経験 += 1
		TCVAR:寝取らせ_献上品キャラ:言いなり = 1
		;これが事実上の夜這いなので、あなたへの夜這いを抑制
		TCVAR:PLAYER:夜這い済み = 2
		;全員寝かせる
		;・日中に寝た時用処理。陥落済みのキャラが起きていると終了時に呼ぶ選択肢が出るため
		FOR 対象キャラ, 1, CHARANUM
			SIF !CFLAG:対象キャラ:睡眠
				CFLAG:対象キャラ:睡眠 = 1
		NEXT
		; 対象キャラを夜這い済みに設定
		TCVAR:寝取らせ_主導キャラ:夜這い済み = 2
		TCVAR:寝取らせ_献上品キャラ:夜這い済み = 2
		; 献上品キャラのうふふ用設定
		CFLAG:寝取らせ_献上品キャラ:睡眠 = 0
		CFLAG:寝取らせ_献上品キャラ:現在位置 = CFLAG:PLAYER:現在位置
		CFLAG:寝取らせ_献上品キャラ:現在マップ種別 = CFLAG:PLAYER:現在マップ種別
		CFLAG:寝取らせ_献上品キャラ:同室 = 1
		;あなたのうふふ用設定
		CFLAG:MASTER:うふふ = 1
		; 時間固定処理
		FLAG:イベントTIME記録 = TIME
		;処理の誤爆を防ぐためオートコマンド周りリセット
		TFLAG:オートコマンドフラグ = 0
		AUTOCOM_実行番号保存 = -1
		AUTOCOM_対象番号保存 = -1
		自慰ターゲット保存 = -1
		; 献上品キャラのうふふ用設定2
		CALL うふふ開始キャラ処理(寝取らせ_献上品キャラ, "寝取らせ")
		;TFLAG:寝取らせ夜這いうふふ済み
		;うふふ開始
		CALL TRAIN_イベントうふふ_LOOP()
		;うふふ終了後の処理
		CALL 寝取られオナニー処理(寝取らせ_主導キャラ, 寝取らせ_献上品キャラ)
	CASE 1
		RESULT = 0
		TRYCALLFORM KOJO_EVENT_寝取らせ夜這い_拒否_{NO:寝取らせ_主導キャラ}(寝取らせ_主導キャラ, 寝取らせ_献上品キャラ)
		IF RESULT == 0
			PRINTFORMW %CALLNAME:PLAYER%이(가) 오늘은 기분이 아니라고 전하자, %CALLNAME:寝取らせ_主導キャラ%은(는) 아쉬운 얼굴로 %CALLNAME:寝取らせ_献上品キャラ%와 함께 돌아갔다……
		ENDIF
		;断られるとしばらく対象キャラが主導の寝取らせ発生率が減少
		;・3日は発生しない値を設定
		;　補正値が残ってる状態で断られた場合は加算
		寝取らせ_発生率補正:寝取らせ_主導キャラ:0 += (寝取らせ夜這い判定_発生率取得(寝取らせ_主導キャラ) + (10 * 30)) * -1
	CASE 100
		;選択肢の説明
		DRAWLINE
		PRINTFORML 받아들인다: %CALLNAME:寝取らせ_主導キャラ%의 성벽을 이루기 위해, %CALLNAME:寝取らせ_献上品キャラ%와 우후후 모드에 돌입합니다.
		PRINTFORML 　　　　　　%CALLNAME:寝取らせ_主導キャラ%을(를) 위한 것이므로, 실행 불가를 제외한 모든 명령어에, %CALLNAME:寝取らせ_献上品キャラ%은(는) 저항할 수 없습니다.
		PRINTFORML 　　　　　　소스나 호감도 변화는 평소대로 이루어지는 것 외에, %CALLNAME:寝取らせ_主導キャラ%의 NTR 경험과 %CALLNAME:寝取らせ_献上品キャラ%의 대출 경험이 각각 +1됩니다.
		PRINTFORML 덧붙여, %CALLNAME:寝取らせ_主導キャラ%은(는) 없는 것으로 취급합니다(자실이나 은밀 상태에서 가혹한 자위행위를 하는 이미지)
		PRINTFORML ※이 우후후 모드 중에는 시간이 진행되지 않습니다
		PRINTFORML 
		PRINTFORML 거절한다: 거절합니다. %CALLNAME:寝取らせ_主導キャラ%은(는) 실망하고 돌아가, NTR 성벽이 충족되어 악화되는 일은 없습니다.
		PRINTFORML 　　　%CALLNAME:寝取らせ_献上品キャラ%은(는) 아마 안심하고 있을 겁니다.
		PRINTFORML 　　　당분간, %CALLNAME:寝取らせ_主導キャラ%이(가) NTR을 제안하지 않게 됩니다.
		WAIT
		GOTO 訪問ラベル
ENDSELECT
;寝取らせイベント用の変数をリセット
寝取らせ_主導キャラ = -1
寝取らせ_献上品キャラ = -1
寝取らせ_嘘告白特殊イベント発生済 = 0
RETURN 1


;寝取らせ_主導キャラがシコってる描写とかそこから生み出されるZPとか

@寝取られオナニー処理(オナニーキャラ, 寝取られキャラ)
#DIM オナニーキャラ
#DIM 寝取られキャラ
#DIM オナニー回数
#DIM オナニー絶頂回数
#DIM オナニー射精回数
#DIMS 射精語

PRINTL 

RESULT = 0
TRYCALLFORM KOJO_EVENT_寝取らせ夜這い_寝取られオナニー口上_{NO:オナニーキャラ}(オナニーキャラ, 寝取られキャラ)
SIF RESULT != 0
	GOTO 寝取らせ夜這い_寝取られオナニーテキスト表示スキップ

PRINTFORM %CALLNAME:PLAYER%에게 안겨 흐트러지고,
IF EXP:寝取られキャラ:貸出経験 >= 4
	SELECTCASE RAND:3
	CASE 0
		PRINTFORM 교성에 섞여 무의식적으로 %CALLNAME:PLAYER%에 대한 호의를 흘려버리고 있다
	CASE 1
		PRINTFORM 범해지면서 %CALLNAME:PLAYER%%TEXTR("에 대한 감사를 말하게 되고 있다/에게 사랑 고백을 강요당하고 있다")%
	CASE 2
		PRINTFORM %CALLNAME:オナニーキャラ%와 비교하라고 지시받자 별다른 갈등 없이 %CALLNAME:PLAYER%쪽을 선택한다
	ENDSELECT
ELSEIF ABL:PLAYER:技巧 >= 4
	PRINTFORM 자신과의 섹스에서는 들어본 적 없는 듯한%TEXTR("달콤하게 녹아내린/달콤하게 탁해진/짐승 같은/흐트러지게 녹아내린")%교성을 지른다
ELSE
	PRINTFORM 완전히%CALLNAME:PLAYER%의 성기의 노예가 되어버렸다
ENDIF
PRINTFORMW %CALLNAME:寝取られキャラ%의 모습을 숨어서 보고 있던%CALLNAME:オナニーキャラ%은(는), 네토라레 오나니가 매우 잘 진행된 것 같다……
PRINTL 
$寝取らせ夜這い_寝取られオナニーテキスト表示スキップ

RESULT = 0
TRYCALLFORM KOJO_EVENT_寝取らせ夜這い_寝取られオナニー実処理_{NO:オナニーキャラ}(オナニーキャラ, 寝取られキャラ)
SIF RESULT != 0
	GOTO 寝取らせ夜這い_寝取られオナニー処理スキップ

IF TALENT:オナニーキャラ:性別 == 2
;Ｖなしのオトコなら、絶頂は射精のみ
;経験加算値はなんとなくで決定したので、もっと説得力あるのがあればそっちに書き換えてください
	IF TALENT:オナニーキャラ:男性器サイズ <= -1
	;短小以下
		射精語 '= TEXTR("射精/ぴゅっぴゅ")
		SELECTCASE RAND:2
		CASE 0
			オナニー回数 = 5
			オナニー絶頂回数 = 2
			CALL コマンド外精力消費処理(オナニーキャラ, オナニー絶頂回数)
			オナニー射精回数 = RESULT
		CASE 1
			オナニー回数 = 6
			オナニー絶頂回数 = 3
			CALL コマンド外精力消費処理(オナニーキャラ, オナニー絶頂回数)
			オナニー射精回数 = RESULT
		ENDSELECT
	ELSEIF TALENT:オナニーキャラ:男性器サイズ == 0
	;普通
		射精語 '= "射精"
		SELECTCASE RAND:2
		CASE 0
			オナニー回数 = 6
			オナニー絶頂回数 = 3
			CALL コマンド外精力消費処理(オナニーキャラ, オナニー絶頂回数)
			オナニー射精回数 = RESULT
		CASE 1
			オナニー回数 = 7
			オナニー絶頂回数 = 4
			CALL コマンド外精力消費処理(オナニーキャラ, オナニー絶頂回数)
			オナニー射精回数 = RESULT
		ENDSELECT
	ELSEIF TALENT:オナニーキャラ:男性器サイズ >= 1
	;巨根以上
		射精語 '= "射精"
		SELECTCASE RAND:2
		CASE 0
			オナニー回数 = 8
			オナニー絶頂回数 = 4
			CALL コマンド外精力消費処理(オナニーキャラ, オナニー絶頂回数)
			オナニー射精回数 = RESULT
		CASE 1
			オナニー回数 = 9
			オナニー絶頂回数 = 5
			CALL コマンド外精力消費処理(オナニーキャラ, オナニー絶頂回数)
			オナニー射精回数 = RESULT
		ENDSELECT
	ENDIF
	;リザルト
	SELECTCASE オナニー射精回数
	CASE 0
		PRINTFORM %CALLNAME:オナニーキャラ%은(는)%TOFULL(TOSTR(オナニー回数))%회 오나니로%TOFULL(TOSTR(オナニー絶頂回数))%회 절정을 맞이했지만, 그
		IF TALENT:オナニーキャラ:男性器サイズ <= -2
			PRINT 볼품없는 고추
		ELSEIF TALENT:オナニーキャラ:男性器サイズ <= -1
			PRINT 작은 고추
		ELSEIF TALENT:オナニーキャラ:男性器サイズ == 0
			PRINT 육봉
		ELSE
			PRINT 겉만 번지르르한 큰 육봉
		ENDIF
		PRINTFORMW 에서 정액이 나오는 일은 한 번도 없었다……
	CASE 1
		PRINTFORMW 1회%射精語%할 수 있었던 것 같다! 더 노력하자!
	CASE 2
		PRINTFORMW 무려, 2회나%射精語%할 수 있었던 것 같다! 잘됐네!
	CASE 3
		PRINTFORMW 무려, 3회나%射精語%할 수 있었던 것 같다! 해냈네!
	CASE 4
		PRINTFORMW 무려! 4회나%射精語%할 수 있었던 것 같다! 대단하네!!
	CASE 5
		PRINTFORMW 무려! 5회나%射精語%할 수 있었던 것 같다!! 수고했어!!
	ENDSELECT
	EXP:オナニーキャラ:自慰経験 += オナニー回数
	PRINTFORML 자위 경험 + {オナニー回数}
	EXP:オナニーキャラ:絶頂経験 += オナニー絶頂回数
	PRINTFORML 절정 경험 + {オナニー絶頂回数}
	EXP:オナニーキャラ:Ｃ絶頂経験 += オナニー絶頂回数
	PRINTFORML C 절정 경험 + {オナニー絶頂回数}
	IF オナニー射精回数 > 0
		EXP:オナニーキャラ:射精経験 += オナニー射精回数
		PRINTFORML 사정 경험 + {オナニー射精回数}
	ENDIF
	FLAG:ZP所持量 += オナニー絶頂回数
	PRINTFORML ZP 소지량 + {オナニー絶頂回数}

ELSE
;Ｖありなら射精以外でも絶頂できるから回数いっぱい。でもあなたほどいっぱいイけるわけじゃない
	;Ｃ絶頂
	IF GETBIT(TALENT:オナニーキャラ:性別, 1)
		;ちんちんある場合
		SELECTCASE RAND:2
			CASE 0
				EXP:オナニーキャラ:Ｃ絶頂経験 += 3
				PRINTFORML C 절정 경험 + 3
				オナニー回数 = 5
				オナニー絶頂回数 = 3
				CALL コマンド外精力消費処理(オナニーキャラ, オナニー絶頂回数)
				オナニー射精回数 = RESULT
			CASE 1
				EXP:オナニーキャラ:Ｃ絶頂経験 += 2
				PRINTFORML C 절정 경험 + 2
				オナニー回数 = 5
				オナニー絶頂回数 = 2
				CALL コマンド外精力消費処理(オナニーキャラ, オナニー絶頂回数)
				オナニー射精回数 = RESULT
		ENDSELECT
	ELSE
		;クリの場合
		SELECTCASE RAND:3
			CASE 0
				EXP:オナニーキャラ:Ｃ絶頂経験 += 3
				PRINTFORML C 절정 경험 + 3
				オナニー回数 = 5
				オナニー絶頂回数 = 3
			CASE 1
				EXP:オナニーキャラ:Ｃ絶頂経験 += 4
				PRINTFORML C 절정 경험 + 4
				オナニー回数 = 6
				オナニー絶頂回数 = 4
			CASE 2
				EXP:オナニーキャラ:Ｃ絶頂経験 += 5
				PRINTFORML C 절정 경험 + 5
				オナニー回数 = 7
				オナニー絶頂回数 = 5
		ENDSELECT
	ENDIF
	;Ｂ絶頂
	SELECTCASE RAND:3
		CASE 0
			EXP:オナニーキャラ:Ｂ絶頂経験 += 3
			PRINTFORML B 절정 경험 + 3
			オナニー回数 += 5
			オナニー絶頂回数 += 3
		CASE 1
			EXP:オナニーキャラ:Ｂ絶頂経験 += 4
			PRINTFORML B 절정 경험 + 4
			オナニー回数 += 6
			オナニー絶頂回数 += 4
		CASE 2
			EXP:オナニーキャラ:Ｂ絶頂経験 += 5
			PRINTFORML B 절정 경험 + 5
			オナニー回数 += 7
			オナニー絶頂回数 += 5
	ENDSELECT
	IF TALENT:オナニーキャラ:処女 == 0
	;処女でないならＶでもオナニー
		SELECTCASE RAND:3
			CASE 0
				EXP:オナニーキャラ:Ｖ絶頂経験 += 3
				PRINTFORML V 절정 경험 + 3
				オナニー回数 += 5
				オナニー絶頂回数 += 3
			CASE 1
				EXP:オナニーキャラ:Ｖ絶頂経験 += 4
				PRINTFORML V 절정 경험 + 4
				オナニー回数 += 6
				オナニー絶頂回数 += 4
			CASE 2
				EXP:オナニーキャラ:Ｖ絶頂経験 += 5
				PRINTFORML V 절정 경험 + 5
				オナニー回数 += 7
				オナニー絶頂回数 += 5
		ENDSELECT
	ENDIF
	;リザルト
	PRINTFORM %CALLNAME:オナニーキャラ%은(는)
	SELECTCASE RAND:3
		CASE 0
			PRINTFORMW %CALLNAME:PLAYER% 덕분에, {オナニー絶頂回数}번이나 절정할 수 있었던 것 같다……
		CASE 1
			PRINTFORMW 많이 자위해서, {オナニー絶頂回数}ZP도 벌어준 것 같다……
		CASE 2
			PRINTFORMW {オナニー回数}번의 자위로 {オナニー絶頂回数}번이나 갈 수 있었던 것 같다……
	ENDSELECT
	EXP:オナニーキャラ:自慰経験 += オナニー回数
	PRINTFORML 자위 경험 + {オナニー回数}
	EXP:オナニーキャラ:絶頂経験 += オナニー絶頂回数
	PRINTFORML 절정 경험 + {オナニー絶頂回数}
	IF オナニー射精回数 > 0
		EXP:オナニーキャラ:射精経験 += オナニー射精回数
		PRINTFORML 사정 경험 + {オナニー射精回数}
	ENDIF
	FLAG:ZP所持量 += オナニー絶頂回数
	PRINTFORML ZP 소지량 + {オナニー絶頂回数}
ENDIF
$寝取らせ夜這い_寝取られオナニー処理スキップ
IF CFLAG:オナニーキャラ:リゾート内オナニー回数 < 1
	CALL 履歴データベース登録(CFLAG:オナニーキャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めてリゾート内で[オナニー]をした</font>","うふふ")
ELSEIF CFLAG:オナニーキャラ:リゾート内オナニー回数 == 9
	CALL 履歴データベース登録(CFLAG:オナニーキャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>リゾート内で[オナニー]をした回数が１０回に到達した</font>","うふふ")
ELSEIF CFLAG:オナニーキャラ:リゾート内オナニー回数 == 49
	CALL 履歴データベース登録(CFLAG:オナニーキャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>リゾート内で[オナニー]をした回数が５０回に到達した</font>","うふふ")
ELSEIF CFLAG:オナニーキャラ:リゾート内オナニー回数 == 99
	CALL 履歴データベース登録(CFLAG:オナニーキャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>リゾート内で[オナニー]をした回数が１００回に到達した</font>","うふふ")
ENDIF
CFLAG:オナニーキャラ:リゾート内オナニー回数 ++
PRINTL

@寝取らせ夜這い判定_キャラ状態判定(対象キャラ)
#FUNCTION
#DIM 対象キャラ
;酔い潰れや体力０の時は発生しない
;泥酔眠姦が実装されたら献上品の酔いすぎは条件から外してもいいと思う
SIF CFLAG:対象キャラ:酔いすぎ
	RETURNF 0
SIF CFLAG:対象キャラ:体力限界
	RETURNF 0
RETURNF 1

@寝取らせ夜這い判定_身体条件判定(対象キャラ)
#FUNCTION
#DIM 対象キャラ
;どちらかが「ＣもＶもないキャラ」だった場合発生しない
SIF TALENT:対象キャラ:Ｖ感度 == -2 && TALENT:対象キャラ:Ｃ感度 == -2
	RETURNF 0
RETURNF 1


@寝取らせ夜這い判定_信頼度判定(対象キャラ)
#FUNCTION
#DIM 対象キャラ
;とりあえず寝取らせ_主導キャラの信頼が親友以上から発生するようにしてみる。献上品の信頼は不問
;ただしあなたの遊び人が４以上の竿役適任つよつよオスである場合、ハードルがワンランク下がる。

IF TALENT:対象キャラ:従業員
	;寝取らせ_主導キャラが従業員である場合、「気にくわない上司に寝取られる」的なプレイを望むかも、みたいなフレーバーで信頼度要求を劇的に下げてみる。
	;これはこれで個人的に癖なので、思いついちゃったし仕方ないよね！縁結びでくっつける手間もあるわけだし……まぁヨシ！
	SIF CFLAG:対象キャラ:信頼度 < 経験値テーブル:(関係閾値:1)
		RETURNF 0
ELSEIF あなた特殊能力:遊び人 >= 4
	SIF CFLAG:対象キャラ:信頼度 < 経験値テーブル:(関係閾値:3)
		RETURNF 0
ELSE
	SIF CFLAG:対象キャラ:信頼度 < 経験値テーブル:(関係閾値:4)
		RETURNF 0
ENDIF
RETURNF 1

@寝取らせ夜這い判定_発生率取得(対象キャラ)
#FUNCTION
#DIM 対象キャラ
#DIM 発生率
;夜這いは性欲条件だけど、寝取られは何を条件にするのが適切なのかわからない。
;ので、普通に確率制御にしてみる。とりあえず性癖開拓のとこから処理持ってきていじってみる

;基礎確率は寝取られ性癖ごとに8%。きついのは初回だけでイベントが引ければ即2になるので、実質基礎16%
;ここで最大24%(Lv3×8)盛って、あとはおまけ程度の補正にする
SIF 性癖素質:対象キャラ:寝取られ > 0
	発生率 = 性癖素質:対象キャラ:寝取られ * 8

;あなたへの信頼。おまけ
SELECTCASE CFLAG:対象キャラ:信頼度
	CASE IS >= 経験値テーブル:(関係閾値:4)
		発生率 += 2
	CASEELSE
		;その他は補正なし
		発生率 += 0
ENDSELECT

;あなたの遊び人素質による加算。最大10%
;女の扱いがうまいと噂がたっていれば、性癖を満たすに丁度いい竿役と認識してもらえる可能性が高くなると思われるため
発生率 += あなた特殊能力:遊び人 * 2

;最低確率は5%。一応設定だけしとく。
発生率 = MAX(発生率, 5)

;これで最大確率36%。毎晩出てこられても困るだろうし、こんなもんじゃない？
RETURNF 発生率

@寝取らせ夜這い判定_確率判定(対象キャラ)
#FUNCTION
#DIM 対象キャラ
#DIM 発生率

;発生率取得
発生率 = 寝取らせ夜這い判定_発生率取得(対象キャラ)

;補正
;・過去に断られたことによる発生率減少
;　この補正による発生率の最低確率を超えて0％にする(断られてからしばらくは絶対に発生しない)
発生率 = MAX(発生率 + 寝取らせ_発生率補正:対象キャラ:0, 0)

;確率判定
SIF RAND:100 < 発生率
	RETURNF 1
RETURNF 0

