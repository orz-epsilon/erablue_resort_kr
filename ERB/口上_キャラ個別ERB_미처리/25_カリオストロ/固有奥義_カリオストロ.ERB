@固有奥義_25(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIM 効果数値
#DIMS 奥義名

;レベルなどの条件で奥義を強化する場合、奥義名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 34
	IF 陥落チェック(キャラ番号)
		奥義名 = オプス・マグナ
	ELSE
		奥義名 = アルス・マグナ＋＋
	ENDIF
	奥義威力 = 500
	効果数値 = 25
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 18
	奥義名 = アルス・マグナ＋
	奥義威力 = 500
	効果数値 = 20
ELSE
	奥義名 = アルス・マグナ
	奥義威力 = 500
ENDIF

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 = 오의 위력：{奥義威力}％<br>적 하나에게 자신의 속성［공격력✕{奥義威力}％］의 대미지를 준다.
		IF TALENT:キャラ番号:恋慕
			詳細文文字列受け渡し変数 += @"(진리 Lv을 모두 소비하여 위력 상승)"
		ENDIF
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 18
			詳細文文字列受け渡し変数 += @"<br>4턴 동안, 아군 전체의 공격력을［{効果数値}％/B칸］ 증가시킨다."
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 34
			詳細文文字列受け渡し変数 += @"<br>전투 동안, 자신의 회복력을[50％/삭제 불가] 상승시키고\@ 陥落チェック(キャラ番号)? 、[10％(최대100)]의 MP 회복을 부여한다# \@."
		SWAP TARGET, キャラ番号
		詳細文文字列受け渡し変数 += @"<br>자신에게［누적1 최대5］의%属性刻印名()%효과를 부여한다."
		SWAP TARGET, キャラ番号
		RETURN 奥義威力
	CASE "奥義追加効果"
		;ここに奥義効果を記述する。基本の奥義ダメージ部分は記載する必要はない。
		;直接記述しても良いが、基本的にアビテンプレートの関数を使用すると便利で良い
		;テンプレート使用の例は下部分を参照
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 18
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用変数:効果割合 = 効果数値
			アビ汎用文字列:バフ・デバフ枠 = 공격력B
			アビ汎用文字列:バフ・デバフ対象能力 = 공격력
			アビ汎用変数:持続ターン = 4
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 34
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:効果割合 = 50
			アビ汎用文字列:バフ・デバフ枠 = 회복력_%TOHALF(奥義名)%
			アビ汎用文字列:バフ・デバフ対象能力 = 회복력
			アビ汎用変数:持続ターン = -1
			アビ汎用変数:消去不可オプション = 1
			CALL アビテンプレート_有利効果_バフ効果セット
			IF 陥落チェック(キャラ番号)
				アビ汎用変数:効果割合 = 0
				アビ汎用変数:効果量 = MIN(BATTLE_STATE:戦闘行動キャラ:최대MP / 10, 99)
				CALL アビテンプレート_ＭＰ回復処理_MAXでも回復
			ENDIF
		ENDIF
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:持続ターン = -1
		アビ汎用文字列:バフ・デバフ対象能力 = %属性刻印名()%
		CALL アビテンプレート_刻印追加(1)
ENDSELECT
RETURN RESULT
