
;--------------------------------------------------
;戦闘能力の固有処理説明
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_25(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;基本的にWAIT系列は使わないことを推奨

IF ダンジョン表示モード != "" && INRANGE(キャラ隊列検索(対象キャラ), 0, 3)
	LOCAL = BATTLE_STATE:キャラ隊列検索(対象キャラ):属性
ELSEIF 装備ステータス補正保存:対象キャラ:属性
	LOCAL = 装備ステータス補正保存:対象キャラ:属性 - 10
ELSE
	LOCAL = 基礎BATTLE_STATE:対象キャラ:属性
ENDIF
LOCAL:1 = 有利属性変換(LOCAL)

IF TALENT:対象キャラ:恋慕
	KSTR:(K++) = □ 특성: 『진리 구현화』 □------------------------------------------------------------------
	KSTR:(K++) = 　아군이 오의를 발동할 때, 자신에게 ［누적 1 최대 8］의 진리 Lv 효과를 부여한다.
	KSTR:(K++) = 　자신이 오의를 발동할 때, 진리 Lv을 모두 소비하여 오의 위력을 ［진리 Lv✕30％］ 상승시킨다.
	KSTR:(K++) = 　
	KSTR:(K++) = □ 특성: 『칼리오스트로는 무적이라구☆』 □--------------------------------------------------
	KSTR:(K++) = 　아군이 디스펠 효과를 사용할 때, 그 대상의 적에게 ［공격력✕188％］의 무속성 대미지를 준다.
	KSTR:(K++) = 　［1］의 슬로우 효과를 부여한다.
	KSTR:(K++) = 　자신에게 ［누적 1 최대 8］의 진리 Lv 효과를 부여한다.
	KSTR:(K++) = 　
ENDIF
KSTR:(K++) = □ 특성: 『귀여움의 진수』 □---------------------------------------------------------------
KSTR:(K++) = 　아군에게 속성 각인이 부여되어 있을 경우, 그 값에 따라 크리티컬률 상승, 피회복 상한,_COLOR_{属性数値文字色変換(LOCAL:1)}_%属性数値文字列変換(LOCAL:1)%내성_상승.
KSTR:(K++) = 　%属性刻印名(LOCAL, , 2)%은(는) 효과가 높다.
KSTR:(K++) = 　또한, %属性刻印名(LOCAL, , 2)%의 값이 3 이상일 때, 턴 종료 시 HP가 ［50✕(%属性刻印名(LOCAL, , 2)%의 값-3)］ 회복한다.
KSTR:(K++) = 　(_COLOR_{属性数値文字色変換(LOCAL:1)}_%属性数値文字列変換(LOCAL:1)%내성_및%属性刻印名(LOCAL, , 2)%의 속성은, 칼리오스트로의 현재 속성에 따라 변화한다)
KSTR:(K++) = 　자신의 속성 각인 상승 시, 무작위 적에게［공격력✕100％］의 자속성 대미지를 {MIN(2+(基礎BATTLE_STATE:対象キャラ:Lv/39), 3)}회 부여한다.
KSTR:(K++) = 　

KSTR:(K++) = □ 특성: 『스페어 바디』 □-----------------------------------------------------------------
KSTR:(K++) = 　전투 시작 시, 자신에게 ［50％］의 자동 부활 효과를 부여한다.
KSTR:(K++) = 　

CALL 口上変数表示(対象キャラ, 1)

;------------------------------------------------------------------------
;　恋慕時（原作リミテッドVer）限定のサポアビ
;------------------------------------------------------------------------

@固有奥義後処理_バフ_真理具現化(バフ・デバフ行数, 全滅フラグ)
#DIM バフ・デバフ行数
#DIM 全滅フラグ
#DIM カリオストロ隊列

SIF 全滅フラグ
	RETURN 0

カリオストロ隊列 = キャラ隊列検索(キャラ検索("[개벽의 연금술사]칼리오스트로"))

IF INRANGE(カリオストロ隊列, 0, 3)
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_指定(カリオストロ隊列)
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = 진리Lv
	アビ汎用文字列:バフ・デバフ対象能力 = 데미지보정_오의데미지
	アビ汎用変数:持続回数 = 1
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:特殊表示オプション = 1
	アビ汎用変数:効果割合 = 30
	アビ汎用変数:累積上限_割合 = 240
	LOCAL = バフ番号取得_枠("真理Lv", カリオストロ隊列)
	IF LOCAL > -1
		LOCAL:1 = DT_CELL_GET("戦闘効果データベース", LOCAL, "効果量_割合")
	ELSE
		LOCAL:1 = 0
	ENDIF
	アビ汎用文字列:実行時メッセージ１ = 칼리오스트로의 진리Lv\@ LOCAL:1 < 아비汎用変数:累積上限_割合 ? 이 1 올랐다！ # 은(는) 이미 최대치다 \@
	CALL アビテンプレート_有利効果_累積バフ効果セット
ENDIF

@固有行動直後処理_バフ・デバフ_真理具現化(効果番号)
#DIM 効果番号
#DIM 隊列番号
#DIM 対象番号保存, アビ対象最大数
VARSET LOCAL
VARSET 対象番号保存, -1

;その行動でディスペルを使用していないなら戻す
SIF STRFIND(アビテンプレート用_付与デバフ保存, "ディスペル") < 0 
    RETURN 0

;カリオストロが前衛にいないなら戻す
隊列番号 = キャラ隊列検索(キャラ検索("[개벽의 연금술사]칼리오스트로"))
SIF 隊列番号 < 0 || 隊列番号 > 3
    RETURN 0

;一時的に戦闘行動キャラをカリオストロに渡す
SWAP 戦闘行動キャラ, 隊列番号

;ディスペルの対象に無属性ダメージ
アビテンプレート用_アビ名 = カリオストロは無敵なのっ☆

CALL アビ汎用変数文字列リセット
アビ汎用変数:効果割合 = 188
CALL アビテンプレート_ダメージ処理_無属性
アビ汎用変数:効果割合 = 0
アビ汎用変数:効果量 = 1
アビ汎用変数:基礎成功確率 = 100
CALL アビテンプレート_不利効果_スロウ

;元の行動の対象を保存
FOR LOCAL, 0, アビ対象最大数
	対象番号保存:LOCAL = アビテンプレート用_対象保存:LOCAL
NEXT

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_指定(戦闘行動キャラ)
アビ汎用変数:消去不可オプション = 1
アビ汎用文字列:バフ・デバフ枠 = 진리Lv
アビ汎用文字列:バフ・デバフ対象能力 = 데미지보정_오의데미지
アビ汎用変数:持続回数 = 1
アビ汎用変数:持続ターン = -1
アビ汎用変数:特殊表示オプション = 1
アビ汎用変数:効果割合 = 30
アビ汎用変数:累積上限_割合 = 240
LOCAL = バフ番号取得_枠("真理Lv", 戦闘行動キャラ)
IF LOCAL > -1
	LOCAL:1 = DT_CELL_GET("戦闘効果データベース", LOCAL, "効果量_割合")
ELSE
	LOCAL:1 = 0
ENDIF
アビ汎用文字列:実行時メッセージ１ = 칼리오스트로의 진리Lv\@ LOCAL:1 < 아비汎用変数:累積上限_割合 ? 이 1 올랐다！ # 은(는) 이미 최대치다 \@
CALL アビテンプレート_有利効果_累積バフ効果セット

;戦闘行動キャラと元の行動の対象を戻す
SWAP 戦闘行動キャラ, 隊列番号
FOR LOCAL, 0, アビ対象最大数
	アビテンプレート用_対象保存:LOCAL = 対象番号保存:LOCAL
NEXT

@バフ・デバフ特殊表示_真理Lv(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCAL
LOCAL = DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_割合")
PRINTFORML 진리Lv{LOCAL/30}
PRINTFORML 오의 위력을［진리Lv✕30（{LOCAL}）％］상승시킨다. 오의 발동 시 모두 소비된다.

;------------------------------------------------------------------------
;　通常（各属性・季節限定ごちゃまぜ）のサポアビ
;------------------------------------------------------------------------

@固有ターン終了時処理_バフ_カワイイの神髄(バフ・デバフ行数)
#DIM バフ・デバフ行数

LOCAL = 刻印現在数確認(属性刻印名(DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_割合")))
SIF LOCAL < 3
	RETURN 0

CALL アビ汎用変数文字列リセット
アビテンプレート用_アビ名表示済フラグ = 1
アビテンプレート用_アビ名 = カワイイの神髄
CALL アビ対象選択テンプレート_自己のみ
アビ汎用変数:効果量 = 50 * (LOCAL-2)
CALL アビテンプレート_回復処理_MAXでも回復


@固有行動直後処理_バフ・デバフ_カワイイの神髄(効果番号)
#DIM 効果番号
#DIM 隊列番号
#DIM 対象番号保存, アビ対象最大数
VARSET LOCAL
VARSET 対象番号保存, -1

[SKIPSTART]
;--- 刻印の増減による効果の増減に関する記述---
;その行動で属性刻印を増減していないなら戻す
FOR LOCAL, 0, 6
	SIF STRFIND(アビテンプレート用_付与バフ保存, 属性刻印:LOCAL) >= 0 
		LOCAL:1 ++
	SIF STRFIND(アビテンプレート用_付与デバフ保存, 属性刻印:LOCAL) >= 0 
		LOCAL:2 ++
NEXT
SIF !(LOCAL:1 + LOCAL:2)
	RETURN 0

;一時的に元の行動の対象を保存する
FOR LOCAL, 0, アビ対象最大数
	対象番号保存:LOCAL = アビテンプレート用_対象保存:LOCAL
NEXT

FOR 隊列番号, 0, 4
	;対象でないなら飛ばす
	SIF !MATCH(アビテンプレート用_対象保存, 隊列番号) || BATTLE_STATE:隊列番号:キャラ登録番号 < 1
		CONTINUE
	FOR LOCAL, 0, 6
		;属性刻印の最大値を算出する。カリオストロと同一属性刻印は2倍換算する。
		LOCAL:3 = MAX(LOCAL:3, 刻印現在数確認(属性刻印:LOCAL, 隊列番号))
		SIF LOCAL == BATTLE_STATE:キャラ隊列検索(キャラ検索("[개벽의 연금술사]칼리오스트로")):属性
			LOCAL:3 = MAX(LOCAL:3, 刻印現在数確認(属性刻印:LOCAL, 隊列番号)*2)
	NEXT
	LOCAL:4 = バフ番号取得_枠("刻印バフ_カリオストロ", 隊列番号)
	IF LOCAL:4 >= 0
	;効果値としては最大値の半分（端数切り上げ）他属性刻印でも多少は効果でるように。
		DT_CELL_SET "戦闘効果データベース", LOCAL:4, "効果量_固定値", (LOCAL:3 + 1) / 2
	ELSE
		;ないことだとは思うが、刻印バフが消えていた場合再設定
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_指定(隊列番号)
		アビテンプレート用_アビ名 = 刻印バフ_カリオストロ
		アビ汎用文字列:バフ・デバフ枠 = 각인버프_칼리오스트로
		アビ汎用文字列:バフ・デバフ対象能力 = 각인버프_칼리오스트로
		アビ汎用変数:効果量 = (LOCAL:3 + 1) / 2
		CALL アビテンプレート_有利効果_付随バフ効果セット("カワイイの神髄")
	ENDIF
	
NEXT
[SKIPEND]
;--- 刻印の増加による自動ダメアビに関する記述---
;カリオストロが前衛にいない、または元の行動の対象にカリオストロが含まれていないなら保存しておいた対象を戻して返す

FOR LOCAL, 0, 6
	SIF STRFIND(アビテンプレート用_付与バフ保存, 属性刻印:LOCAL) >= 0 
		LOCAL:1 ++
NEXT
;一時的に元の行動の対象を保存する
FOR LOCAL, 0, アビ対象最大数
	対象番号保存:LOCAL = アビテンプレート用_対象保存:LOCAL
NEXT

隊列番号 = キャラ隊列検索(キャラ検索("[개벽의 연금술사]칼리오스트로"))
IF INRANGE(隊列番号, 0, 3) && MATCH(対象番号保存, 隊列番号) && LOCAL:1 >= 0
ELSE
	FOR LOCAL, 0, アビ対象最大数
		アビテンプレート用_対象保存:LOCAL = 対象番号保存:LOCAL
	NEXT
	RETURN 0
ENDIF
;一時的に戦闘行動キャラをカリオストロに渡す
SWAP 戦闘行動キャラ, 隊列番号

;カリオストロの属性刻印が上昇した場合、追撃２～３回
CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_敵ランダムＸ体(MIN(2+(BATTLE_STATE:隊列番号:Lv/39), 3))
アビテンプレート用_アビ名 = カワイイの神髄
アビ汎用変数:効果割合 = 100
CALL アビテンプレート_ダメージ処理_自属性

;戦闘行動キャラと対象を戻す
SWAP 戦闘行動キャラ, 隊列番号
FOR LOCAL, 0, アビ対象最大数
	アビテンプレート用_対象保存:LOCAL = 対象番号保存:LOCAL
NEXT

@バフ・デバフ特殊表示_カワイイの神髄(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
#DIMS 刻印名
VARSET LOCAL

;LOCAL:0 = バフ番号取得_枠("刻印バフ_カリオストロ", 隊列番号)
LOCAL:1 = DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_割合")
刻印名 = %属性刻印名(LOCAL:1, , 1)%

LOCALS = 귀여움의 진수
LOCALS += @"<br>칼리오스트로가 파티 내에서 생존하고 있을 때, 자신에게 부여된 속성 각인의 값에 따라"
LOCALS += @"<br>크리티컬률・주는 대미지・받는 회복량・"
LOCALS += @"<font color='#%属性数値文字色変換_HTML(有利属性変換(LOCAL:1))%'>%属性数値文字列変換(有利属性変換(LOCAL:1))%내성</font>이 증가하는 상태."
LOCALS += @"<br>또한, %刻印名%의 값이 3 이상일 때, 턴 종료 시 HP가 [50✕(%刻印名%의 값-2)] 회복한다."
HTML_PRINT LOCALS
[IF_DEBUG]
LOCAL:2 = バフ番号取得_枠("刻印バフ_CT率_カリオストロ", 隊列番号)
LOCAL:3 = バフ番号取得_枠("刻印バフ_属性耐性_カリオストロ", 隊列番号)
LOCAL:4 = バフ番号取得_枠("刻印バフ_被回復_カリオストロ", 隊列番号)
LOCAL:5 = DT_CELL_GET("戦闘効果データベース", LOCAL:0, "効果量_固定値")
;PRINTFORML ＞［{LOCAL:5}］分の属性刻印の影響で
PRINTFORML ＞[{付随依存変動算出(LOCAL:2, 隊列番号, "固定値")}％]의 크리티컬률 상승
PRINTFORML ＞[{付随依存変動算出(LOCAL:4, 隊列番号, "固定値")}]의 받는 회복량 상승
PRINTFORML ＞[{付随依存変動算出(LOCAL:3, 隊列番号, "固定値")}％]의 %属性数値文字列変換(有利属性変換(LOCAL:1))%내성 상승
[ENDIF]
;@付随依存変動算出(判定行, 判定キャラ, 判定種別)

;--------------------------------------------------
;戦闘能力の開始時処理
;--------------------------------------------------
@固有戦闘開始時処理_キャラ_25
;戦闘開始時処理関数から呼び出し
#DIM バフ・デバフ行数
#DIM カリオストロ隊列

;バフ付け文章は出さないので1を入れておく
アビテンプレート用_アビ名表示済フラグ = 1

カリオストロ隊列 = キャラ隊列検索(キャラ検索("[개벽의 연금술사]칼리오스트로"))

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = 스페어 보디
アビ汎用文字列:バフ・デバフ対象能力 = 자동 부활
アビ汎用変数:効果割合 = 50
アビ汎用変数:持続回数 = 1
CALL アビテンプレート_有利効果_バフ効果セット

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_味方全体
アビテンプレート用_アビ名 = カワイイの神髄
アビ汎用文字列:適用範囲 = 아군 전체
アビ汎用文字列:バフ・デバフ枠 = 귀여움의 진수
アビ汎用文字列:バフ・デバフ対象能力 = 귀여움의 진수
アビ汎用変数:特殊表示オプション = 1
アビ汎用変数:効果量 = 1
アビ汎用変数:効果割合 = BATTLE_STATE:カリオストロ隊列:属性
CALL アビテンプレート_有利効果_パッシブバフ効果セット

[SKIPSTART]
CALL アビ汎用変数文字列リセット
アビテンプレート用_アビ名 = 刻印バフ_カリオストロ
アビ汎用文字列:適用範囲 = 아군 전체
アビ汎用文字列:バフ・デバフ枠 = 刻印バフ_カリオストロ
アビ汎用文字列:バフ・デバフ対象能力 = 刻印バフ_カリオストロ
アビ汎用変数:効果量 = 0
アビ汎用変数:効果割合 = BATTLE_STATE:カリオストロ隊列:属性
CALL アビテンプレート_有利効果_付随バフ効果セット("カワイイの神髄")
[SKIPEND]

CALL アビ汎用変数文字列リセット
アビ汎用文字列:適用範囲 = 아군 전체
アビ汎用文字列:バフ・デバフ枠 = 각인 버프_CT율_칼리오스트로
アビ汎用文字列:バフ・デバフ対象能力 = 크리티컬율
アビ汎用変数:付随量 = 5
アビ汎用文字列:参照先設定 = 부수 원 속성 각인
アビ汎用文字列:付随先_隊列設定 = {カリオストロ隊列}
CALL アビテンプレート_有利効果_付随バフ効果セット("カワイイの神髄")

CALL アビ汎用変数文字列リセット
アビ汎用文字列:適用範囲 = 아군 전체
アビ汎用文字列:バフ・デバフ枠 = 각인 버프_피회복_칼리오스트로
アビ汎用文字列:バフ・デバフ対象能力 = 피회복량
アビ汎用変数:付随量 = 30
アビ汎用文字列:参照先設定 = 부수 원 속성 각인
アビ汎用文字列:付随先_隊列設定 = {カリオストロ隊列}
CALL アビテンプレート_有利効果_付随バフ効果セット("カワイイの神髄")

CALL アビ汎用変数文字列リセット
アビ汎用文字列:適用範囲 = 아군 전체
アビ汎用文字列:バフ・デバフ枠 = 각인 버프_속성 내성_칼리오스트로
アビ汎用文字列:バフ・デバフ対象能力 = %属性数値文字列変換(有利属性変換(BATTLE_STATE:カリオストロ隊列:属性))%내성
アビ汎用変数:付随量 = 4
アビ汎用文字列:参照先設定 = 부수 원 속성 각인
アビ汎用文字列:付随先_隊列設定 = {カリオストロ隊列}
CALL アビテンプレート_有利効果_付随バフ効果セット("カワイイの神髄")

IF TALENT:キャラ検索("[개벽의 연금술사]칼리오스트로"):恋慕
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:適用範囲 = 아군 전체
	CALL アビ対象選択テンプレート_味方全体
	アビテンプレート用_アビ名 = 真理具現化
	アビ汎用文字列:バフ・デバフ枠 = 진리 구현화
	アビ汎用文字列:バフ・デバフ対象能力 = 진리 구현화
	アビ汎用変数:特殊表示オプション = -1
	CALL アビテンプレート_有利効果_パッシブバフ効果セット
ENDIF

;----------------------------------------------------------------------------------
;『真理Lv』戦闘画面上表示処理
@探索欄表示_キャラ_25(レイヤー番号, 隊列番号)
#DIM 隊列番号
#DIM レイヤー番号
#DIM バフ・デバフ行数
バフ・デバフ行数 = バフ番号取得_枠("真理Lv", 隊列番号)
SIF バフ・デバフ行数 < 0
	RETURN

GDRAWTEXT レイヤー番号, "真理Lv", 120, 90
GSETBRUSH レイヤー番号, 属性数値文字色変換_透明度込(BATTLE_STATE:隊列番号:属性)
GDRAWTEXT レイヤー番号, @"{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_割合")/30}", 200, 90

;サポアビ案
;「真理具現化」PTメンバーが奥義発動時、真理Lv+1
;「カリオストロは無敵なのっ☆」他キャラがディスペル使用時、敵に88万8888ダメージ、スロウ効果、真理Lv+1
;「ビーチスタイルだよっ☆」「世界で一番可愛いっ☆」「イ・タ・ズ・ラしちゃうぞ☆」
;		刻印の数に応じてクリティカル率UP（倍率30％/発動率5*刻印数）刻印上昇時に100％のアビリティダメージｘ3回、ダメージ上限UP（2*刻印数）
;「スペアボディ」戦闘開始時に自動復活効果（50％）
;「リポルションボディ」刻印の数に応じて被回復上限上昇、ターン終了時に回復
