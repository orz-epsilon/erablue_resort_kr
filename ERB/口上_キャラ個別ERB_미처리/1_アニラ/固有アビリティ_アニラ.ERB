
@固有アビ_1_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果ターン
#DIMS アビ名

IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 80
	アビ名 = 森羅万象＋＋
	消費ＭＰ = 100
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
	アビ名 = 森羅万象＋
	消費ＭＰ = 100
ELSE
	アビ名 = 森羅万象
	消費ＭＰ = 100
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 80
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵全体に［攻撃力✕100％］の各属性ダメージを与える。
			詳細文文字列受け渡し変数 += @"<br>［10(바람 공격만 20)％］（A칸・5턴）의 각 속성 공격 감소 효과를 부여한다."
			詳細文文字列受け渡し変数 += @"<br>［10(불 방어만 20)％］（A칸・5턴）의 각 속성 내성 감소 효과를 부여한다."
			詳細文文字列受け渡し変数 += @"<br>자신에게 ［40％］（일반칸・4턴）의 추격 효과를 부여한다."
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>ランダムな敵に［攻撃力✕100％］の各属性ダメージを与える。
			詳細文文字列受け渡し変数 += @"<br>자신에게 ［20％］（일반칸・4턴）의 추격 효과를 부여한다."
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>ランダムな敵に［攻撃力✕80％］の各属性ダメージを与える。
			詳細文文字列受け渡し変数 += @"<br>자신에게 ［20％］（일반칸・4턴）의 추격 효과를 부여한다."
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 80
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵全体に［攻撃力✕100％］の火属性ダメージを与える。
			詳細文文字列受け渡し変数 += "<br>［공격력✕100％］의 물 속성 데미지를 준다."
			詳細文文字列受け渡し変数 += "<br>［공격력✕100％］의 바람 속성 데미지를 준다."
			詳細文文字列受け渡し変数 += "<br>［공격력✕100％］의 흙 속성 데미지를 준다."
			詳細文文字列受け渡し変数 += "<br>［공격력✕100％］의 빛 속성 데미지를 준다."
			詳細文文字列受け渡し変数 += "<br>［공격력✕100％］의 어둠 속성 데미지를 준다."
			詳細文文字列受け渡し変数 += @"<br>［10］（일반칸・5턴）의 불 속성 공격 감소 효과를 부여한다. 성공률 85％."
			詳細文文字列受け渡し変数 += @"<br>［10］（일반칸・5턴）의 물 속성 공격 감소 효과를 부여한다. 성공률 85％."
			詳細文文字列受け渡し変数 += @"<br>［20］（일반칸・5턴）의 바람 속성 공격 감소 효과를 부여한다. 성공률 100％."
			詳細文文字列受け渡し変数 += @"<br>［10］（일반칸・5턴）의 흙 속성 공격 감소 효과를 부여한다. 성공률 85％."
			詳細文文字列受け渡し変数 += @"<br>［10］(일반 칸・5턴)의 빛 속성 공격 감소 효과를 부여한다. 성공률 85%."
			詳細文文字列受け渡し変数 += @"<br>［10］(일반 칸・5턴)의 어둠 속성 공격 감소 효과를 부여한다. 성공률 85%."
			詳細文文字列受け渡し変数 += @"<br>［20］(일반 칸・5턴)의 불 속성 내성 감소 효과를 부여한다. 성공률 100%."
			詳細文文字列受け渡し変数 += @"<br>［10］(일반 칸・5턴)의 물 속성 내성 감소 효과를 부여한다. 성공률 85%."
			詳細文文字列受け渡し変数 += @"<br>［10］(일반 칸・5턴)의 바람 속성 내성 감소 효과를 부여한다. 성공률 85%."
			詳細文文字列受け渡し変数 += @"<br>［10］(일반 칸・5턴)의 흙 속성 내성 감소 효과를 부여한다. 성공률 85%."
			詳細文文字列受け渡し変数 += @"<br>［10］(일반 칸・5턴)의 빛 속성 내성 감소 효과를 부여한다. 성공률 85%."
			詳細文文字列受け渡し変数 += @"<br>［10］(일반 칸・5턴)의 어둠 속성 내성 감소 효과를 부여한다. 성공률 85%."
			詳細文文字列受け渡し変数 += @"<br>자신에게 [40%] (A칸・4턴)의 추격 효과를 부여한다."
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>ランダムな敵に［攻撃力✕100％］の火属性ダメージを与える。
			詳細文文字列受け渡し変数 += "<br>[공격력✕100%]의 물 속성 대미지를 준다."
			詳細文文字列受け渡し変数 += "<br>[공격력✕100%]의 바람 속성 대미지를 준다."
			詳細文文字列受け渡し変数 += "<br>[공격력✕100%]의 흙 속성 대미지를 준다."
			詳細文文字列受け渡し変数 += "<br>[공격력✕100%]의 빛 속성 대미지를 준다."
			詳細文文字列受け渡し変数 += "<br>[공격력✕100%]의 어둠 속성 대미지를 준다."
			詳細文文字列受け渡し変数 += @"<br>자신에게 [20%] (A칸・4턴)의 추격 효과를 부여한다."
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>ランダムな敵に［攻撃力✕80％］の火属性ダメージを与える。
			詳細文文字列受け渡し変数 += "<br>［공격력✕80％］의 수속성 데미지를 준다."
			詳細文文字列受け渡し変数 += "<br>［공격력✕80％］의 풍속성 데미지를 준다."
			詳細文文字列受け渡し変数 += "<br>［공격력✕80％］의 토속성 데미지를 준다."
			詳細文文字列受け渡し変数 += "<br>［공격력✕80％］의 광속성 데미지를 준다."
			詳細文文字列受け渡し変数 += "<br>［공격력✕80％］의 암속성 데미지를 준다."
			詳細文文字列受け渡し変数 += @"<br>자신에게［20％］（Ａ범위・4턴）의 추격 효과를 부여한다."
		ENDIF
	CASE "アビ効果"
		IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 80
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 100
			CALL アビテンプレート_ダメージ処理_火属性
			CALL アビテンプレート_ダメージ処理_水属性
			CALL アビテンプレート_ダメージ処理_土属性
			CALL アビテンプレート_ダメージ処理_風属性
			CALL アビテンプレート_ダメージ処理_光属性
			CALL アビテンプレート_ダメージ処理_闇属性
			
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 10
			アビ汎用変数:持続ターン = 5
			アビ汎用変数:基礎成功確率 = 85
			アビ汎用文字列:バフ・デバフ枠 = 属性与ダメージ_火
			アビ汎用文字列:バフ・デバフ対象能力 = 属性与ダメージ_火
			CALL アビテンプレート_不利効果_デバフ効果セット
			アビ汎用文字列:バフ・デバフ枠 = 属性与ダメージ_水
			アビ汎用文字列:バフ・デバフ対象能力 = 属性与ダメージ_水
			CALL アビテンプレート_不利効果_デバフ効果セット
			アビ汎用文字列:バフ・デバフ枠 = 属性与ダメージ_土
			アビ汎用文字列:バフ・デバフ対象能力 = 属性与ダメージ_土
			CALL アビテンプレート_不利効果_デバフ効果セット
			アビ汎用変数:基礎成功確率 = 100
			アビ汎用文字列:バフ・デバフ枠 = 属性与ダメージ_風
			アビ汎用文字列:バフ・デバフ対象能力 = 属性与ダメージ_風
			CALL アビテンプレート_不利効果_デバフ効果セット
			アビ汎用変数:基礎成功確率 = 85
			アビ汎用文字列:バフ・デバフ枠 = 属性与ダメージ_光
			アビ汎用文字列:バフ・デバフ対象能力 = 属性与ダメージ_光
			CALL アビテンプレート_不利効果_デバフ効果セット
			アビ汎用文字列:バフ・デバフ枠 = 属性与ダメージ_闇
			アビ汎用文字列:バフ・デバフ対象能力 = 属性与ダメージ_闇
			CALL アビテンプレート_不利効果_デバフ効果セット
			アビ汎用変数:基礎成功確率 = 100
			アビ汎用文字列:バフ・デバフ枠 = 属性被ダメージ_火
			アビ汎用文字列:バフ・デバフ対象能力 = 属性被ダメージ_火
			CALL アビテンプレート_不利効果_デバフ効果セット
			アビ汎用変数:基礎成功確率 = 85
			アビ汎用文字列:バフ・デバフ枠 = 属性被ダメージ_水
			アビ汎用文字列:バフ・デバフ対象能力 = 属性被ダメージ_水
			CALL アビテンプレート_不利効果_デバフ効果セット
			アビ汎用文字列:バフ・デバフ枠 = 属性被ダメージ_土
			アビ汎用文字列:バフ・デバフ対象能力 = 属性被ダメージ_土
			CALL アビテンプレート_不利効果_デバフ効果セット
			アビ汎用文字列:バフ・デバフ枠 = 属性被ダメージ_風
			アビ汎用文字列:バフ・デバフ対象能力 = 属性被ダメージ_風
			CALL アビテンプレート_不利効果_デバフ効果セット
			アビ汎用文字列:バフ・デバフ枠 = 属性被ダメージ_光
			アビ汎用文字列:バフ・デバフ対象能力 = 属性被ダメージ_光
			CALL アビテンプレート_不利効果_デバフ効果セット
			アビ汎用文字列:バフ・デバフ枠 = 属性被ダメージ_闇
			アビ汎用文字列:バフ・デバフ対象能力 = 属性被ダメージ_闇
			CALL アビテンプレート_不利効果_デバフ効果セット
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 100
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			CALL アビテンプレート_ダメージ処理_火属性
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			CALL アビテンプレート_ダメージ処理_水属性
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			CALL アビテンプレート_ダメージ処理_風属性
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			CALL アビテンプレート_ダメージ処理_土属性
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			CALL アビテンプレート_ダメージ処理_光属性
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			CALL アビテンプレート_ダメージ処理_闇属性
		ELSE
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 80
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			CALL アビテンプレート_ダメージ処理_火属性
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			CALL アビテンプレート_ダメージ処理_水属性
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			CALL アビテンプレート_ダメージ処理_風属性
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			CALL アビテンプレート_ダメージ処理_土属性
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			CALL アビテンプレート_ダメージ処理_光属性
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			CALL アビテンプレート_ダメージ処理_闇属性
		ENDIF
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 追撃A
		アビ汎用文字列:バフ・デバフ対象能力 = 追撃
		IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 80
			アビ汎用変数:効果割合 = 40
		ELSE
			アビ汎用変数:効果割合 = 20
		ENDIF
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT



@固有アビ_1_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 65
	アビ名 = 色即是空＋
	消費ＭＰ = 120
ELSE
	アビ名 = 色即是空
	消費ＭＰ = 150
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵全体に［攻撃力✕200％］の火属性ダメージを与える。
		詳細文文字列受け渡し変数 += @"<br>［1］의 슬로우 효과를 부여한다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 200
		CALL アビテンプレート_ダメージ処理_火属性
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 1
		CALL アビテンプレート_不利効果_スロウ
ENDSELECT



@固有アビ_1_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 対象番号
#DIM 効果ターン
#DIM バフ行数

IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 75
	アビ名 = 竜吟虎嘯＋
	消費ＭＰ = 250
	効果ターン = 5
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 45
	アビ名 = 竜吟虎嘯
	消費ＭＰ = 250
	効果ターン = 3
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 75
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に（独自枠・5ターン・消去不可）の竜吟虎嘯効果を与える
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に（独自枠・3ターン・消去不可）の竜吟虎嘯効果を与える
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 75
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に（独自枠・5ターン・消去不可）の竜吟虎嘯効果を与える
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に（独自枠・3ターン・消去不可）の竜吟虎嘯効果を与える
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>＞［50％］의 공격력 상승 효과를 부여한다."
		詳細文文字列受け渡し変数 += @"<br>＞［50％］의 DA율 상승 효과를 부여한다."
		詳細文文字列受け渡し変数 += @"<br>＞［80％］의 크리티컬율 상승 효과를 부여한다."
		詳細文文字列受け渡し変数 += @"<br>＞［50％］의 게이지 상승 효율 상승 효과를 부여한다."
		詳細文文字列受け渡し変数 += @"<br>＞［50％］의 약체 내성 상승 효과를 부여한다."
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 竜吟虎嘯
		アビ汎用文字列:バフ・デバフ対象能力 = 竜吟虎嘯
		IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 75
			アビ汎用変数:持続ターン = 5
		ELSE
			アビ汎用変数:持続ターン = 3
		ENDIF
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 竜吟虎嘯_攻撃力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 50
		CALL アビテンプレート_有利効果_付随バフ効果セット("竜吟虎嘯")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 竜吟虎嘯_DA率
		アビ汎用文字列:バフ・デバフ対象能力 = DA率
		アビ汎用変数:効果量 = 50
		CALL アビテンプレート_有利効果_付随バフ効果セット("竜吟虎嘯")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 竜吟虎嘯_クリ率
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
		アビ汎用変数:効果量 = 80
		CALL アビテンプレート_有利効果_付随バフ効果セット("竜吟虎嘯")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 竜吟虎嘯_ゲージ上昇効率
		アビ汎用文字列:バフ・デバフ対象能力 = ゲージ上昇効率
		アビ汎用変数:効果量 = 50
		CALL アビテンプレート_有利効果_付随バフ効果セット("竜吟虎嘯")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 竜吟虎嘯_弱体耐性
		アビ汎用文字列:バフ・デバフ対象能力 = 弱体耐性
		アビ汎用変数:効果量 = 50
		CALL アビテンプレート_有利効果_付随バフ効果セット("竜吟虎嘯")
ENDSELECT



@固有アビ_1_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 対象番号

IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70 && イベント完了フラグ確認("未神宮への参拝") > 0
	アビ名 = 意気羊々
	消費ＭＰ = 120
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に［215］のＨＰ回復を与える。
		詳細文文字列受け渡し変数 += @"<br>［1］의 약체 회복을 부여한다."
		詳細文文字列受け渡し変数 += @"<br>［15％］(일반 프레임・3턴)의 스트렝스 효과를 부여한다"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果量 = 215
		CALL アビテンプレート_回復処理_MAXでも回復
		
		CALL アビ汎用変数文字列リセット
		CALL アビテンプレート_有利効果_デバフ消去()
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = ストレングス
		アビ汎用文字列:バフ・デバフ対象能力 = 与ダメージ
		アビ汎用変数:効果割合 = 15
		アビ汎用変数:持続ターン = 3
		; アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT



@固有戦闘開始時処理_キャラ_1
#DIM バフ・デバフ行数
#DIM 隊列番号
#DIM 対象番号

;バフ付け文章は出さないので1を入れておく
アビテンプレート用_アビ名表示済フラグ = 1

CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:適用範囲 = 味方全体
アビ汎用文字列:バフ・デバフ枠 = 羊神宮の主
アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
アビ汎用変数:効果割合 = 30
アビ汎用変数:持続ターン = -1
アビ汎用変数:特殊表示オプション = 1
CALL アビテンプレート_有利効果_パッシブバフ効果セット

CALL アビ対象選択テンプレート_自己のみ
CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 一羊来復
アビ汎用文字列:バフ・デバフ対象能力 = 一羊来復
アビ汎用変数:持続ターン = -1
アビ汎用変数:特殊表示オプション = 1
CALL アビテンプレート_有利効果_パッシブバフ効果セット

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 一羊来復_DA率
アビ汎用文字列:バフ・デバフ対象能力 = DA率
アビ汎用変数:効果量 = 15
アビ汎用文字列:適用範囲 = 味方全体
アビ汎用文字列:付随先_隊列設定 = {戦闘行動キャラ}
CALL アビテンプレート_有利効果_付随バフ効果セット("一羊来復")

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 一羊来復_防御力
アビ汎用文字列:バフ・デバフ対象能力 = 防御力
アビ汎用変数:効果割合 = 30
アビ汎用文字列:適用範囲 = 味方全体
アビ汎用文字列:付随先_隊列設定 = {戦闘行動キャラ}
CALL アビテンプレート_有利効果_付随バフ効果セット("一羊来復")

CALL アビテンプレ変数リセット


@固有戦闘不能時処理_キャラ_1
#DIM 対象番号
#DIM 羊神宮の主
#DIM 一羊来復
#DIM DA率
#DIM 防御力

FOR 対象番号, 0, 4
	羊神宮の主 = バフ番号取得_枠("羊神宮の主", 対象番号)
	CALL 戦闘効果削除(羊神宮の主)
NEXT



@固有行動開始時処理_バフ_ストレングス(バフ・デバフ行数)
#DIM バフ・デバフ行数
#DIM 効果量

効果量 = (BATTLE_STATE:戦闘行動キャラ:ＨＰ / BATTLE_STATE:戦闘行動キャラ:最大ＨＰ) * 10

DT_CELL_SET "戦闘効果データベース", バフ・デバフ行数, "効果量_割合", 効果量 + 5



@固有行動開始時処理_バフ_羊神宮の主(バフ・デバフ行数)
#DIM バフ・デバフ行数
#DIM アニラ番号
#DIM DA率
#DIM 防御力

アニラ番号 = キャラ隊列検索(GETCHARA(1))

IF 弱体存在判定(戦闘行動キャラ)
	DT_CELL_SET "戦闘効果データベース", バフ・デバフ行数, "対象能力", "防御力"
ELSE
	DT_CELL_SET "戦闘効果データベース", バフ・デバフ行数, "対象能力", "攻撃力"
ENDIF

DA率 = バフ番号取得_枠("一羊来復_DA率", 戦闘行動キャラ)
防御力 = バフ番号取得_枠("一羊来復_防御力", 戦闘行動キャラ)

IF BATTLE_STATE:アニラ番号:ＨＰ < BATTLE_STATE:アニラ番号:最大ＨＰ / 4
	DT_CELL_SET "戦闘効果データベース", DA率, "効果量_固定値", 0
	DT_CELL_SET "戦闘効果データベース", 防御力, "効果量_割合", 50
ELSEIF BATTLE_STATE:アニラ番号:ＨＰ < BATTLE_STATE:アニラ番号:最大ＨＰ / 2
	DT_CELL_SET "戦闘効果データベース", DA率, "効果量_固定値", 5
	DT_CELL_SET "戦闘効果データベース", 防御力, "効果量_割合", 30
ELSEIF BATTLE_STATE:アニラ番号:ＨＰ < (BATTLE_STATE:アニラ番号:最大ＨＰ / 4) * 3
	DT_CELL_SET "戦闘効果データベース", DA率, "効果量_固定値", 10
	DT_CELL_SET "戦闘効果データベース", 防御力, "効果量_割合", 10
ELSE
	DT_CELL_SET "戦闘効果データベース", DA率, "効果量_固定値", 15
	DT_CELL_SET "戦闘効果データベース", 防御力, "効果量_割合", 0
ENDIF



@バフ・デバフ特殊表示_竜吟虎嘯(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 용음호소
PRINTFORML 공격력・DA율・크리티컬율・오의 게이지 상승량・약체 내성이 상승하고 있는 상태［소거 불가］
PRINTFORML 지속 턴：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}



; @バフ・デバフ特殊表示_ストレングス(隊列番号, バフ・デバフ行数)
; #DIM 隊列番号
; #DIM バフ・デバフ行数
; VARSET LOCALS

; PRINTFORML ストレングス
; PRINTFORML 現在HPが多いほど、与えるダメージが上昇する状態
; PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}



@バフ・デバフ特殊表示_羊神宮の主(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 양신궁의 주인
PRINTFORML 약체 효과를 받지 않을 때, 공격력이 상승/약체 효과를 받을 때, 방어력이 상승하는 상태
PRINTFORML 지속 턴: 영속



@バフ・デバフ特殊表示_一羊来復(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 일양내복
PRINTFORML 아닐라의 현재 HP가 많을수록, DA율이 상승/아닐라의 현재 HP가 적을수록, 방어력이 상승하는 상태
PRINTFORML 지속 턴: 영속



;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_1(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 특성: 『양신궁의 주인』 □---------------------------------------------------------------
KSTR:(K++) = 　아군이 약체 효과를 받지 않을 때, 그 아군의 공격력 업
KSTR:(K++) = 　아군이 약체 효과를 받을 때, 그 아군의 방어력 업
KSTR:(K++) = 　
KSTR:(K++) = □ 특성: 『일양내복』 □-----------------------------------------------------------------
KSTR:(K++) = 　자신의 현재 HP가 많을수록 아군 전체의 DA율 업
KSTR:(K++) = 　자신의 현재 HP가 적을수록 아군 전체의 방어력 업
KSTR:(K++) = 　

CALL 口上変数表示(対象キャラ, 1)


