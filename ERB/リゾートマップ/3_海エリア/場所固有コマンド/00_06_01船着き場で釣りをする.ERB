
;-------------------------------------------------
;03_305_01渓流エリアで釣りをする
;-------------------------------------------------
@COMNAME_PLACE_380_3_6
#FUNCTION

TSTR:コマンド名受渡 = 낚시를 하다
RETURNF 1

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_380_3_6
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("単独可能")
		
@COM380_3_6
;RFLAG:コマンド結果受渡し変数　釣りモードの種類


$INPUT_LOOP
;釣りの種類を決定
DRAWLINE
PRINTL 
PRINTL 어떻게 낚시하시겠습니까?
PRINTBUTTONLC "[1]간이 낚시 모드", 1
PRINTBUTTON "[설명]", 10
PRINTL 
PRINTBUTTONLC "[2]제대로 낚시 모드", 2
PRINTBUTTON "[설명]", 20
PRINTL 
PRINTBUTTON "[999]낚시를 그만두다", 999
INPUT

SELECTCASE RESULT
	CASE 1
		RFLAG:コマンド結果受渡し変数 = RESULT
		CALL 簡易釣りモード(6)
	CASE 10
		CALL 簡易釣りモード説明
		GOTO INPUT_LOOP
	CASE 2
		RFLAG:コマンド結果受渡し変数 = RESULT
		CALL ちゃんと釣りモード(6)
	CASE 20
		CALL ちゃんと釣りモード説明
		GOTO INPUT_LOOP
	CASE 999
		RFLAG:コマンド結果受渡し変数 = RESULT
		RETURN 1
	CASEELSE
		GOTO INPUT_LOOP
ENDSELECT

;歓楽強度
LOCAL:0 = 500
;征服強度
LOCAL:1 = 100

IF RFLAG:コマンド結果受渡し変数２ >= 10
	;大漁
	TIMES LOCAL:0 , 2.00
	TIMES LOCAL:1 , 2.00
ELSEIF RFLAG:コマンド結果受渡し変数２ >= 5
	;そこそこ釣れた
	TIMES LOCAL:0 , 1.50
	TIMES LOCAL:1 , 1.50
ELSEIF  RFLAG:コマンド結果受渡し変数２ > 0
	;とりあえず０じゃない
	TIMES LOCAL:0 , 1.00
	TIMES LOCAL:1 , 1.00
ELSE
	;ボウズ
	TIMES LOCAL:0 , 0.50
	TIMES LOCAL:1 , 0.50
ENDIF

IF TARGET
	
	
	CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL:0)
	CALL SOURCE_CALC_征服(TARGET, PLAYER, LOCAL:1)
	CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, LOCAL:0 / 2)
ENDIF

TIME += 60
RETURN 1



;-------------------------------------------------
;実行判定
;------------------------------------------------
@COM_ABLE380_3_6
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(380)
	RETURN RESULT
;隠密中はダメ
SIF CFLAG:PLAYER:隠密
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM380_3_6
SIF RFLAG:コマンド結果受渡し変数 == 999
	RETURN 1
IF TARGET
	PRINTFORML %CALLNAME:PLAYER%은(는)%CALLNAME:TARGET%\@ GET_TARGETNUM() > 1 ? 들 # \@과(와) 낚시를 하며 즐거워했다.
	IF RFLAG:コマンド結果受渡し変数２ >= 10
		;大漁
		PRINTL 산더미 같은 물고기가 잡히는 대어였다!
		PRINTFORML %CALLNAME:TARGET%와(과) 함께 기뻐하며 가져갔다.
	ELSEIF RFLAG:コマンド結果受渡し変数２ >= 5
		;そこそこ釣れた
		PRINTFORML 조과는 최고였다. %CALLNAME:TARGET%도 기뻐하는 것 같다.
	ELSEIF  RFLAG:コマンド結果受渡し変数２ > 0
		;とりあえず０ではない
		PRINTL 별다른 성과는 없었지만, 즐거운 한때를 보낼 수 있었다.
	ELSE
		;ボウズ
		PRINTL 아쉽게도, 한 마리도 잡지 못했다…
	ENDIF
ELSE
	PRINTFORML %CALLNAME:PLAYER%은(는) 혼자 낚시를 즐겼다.
ENDIF

