
;-------------------------------------------------
;ビーチフラッグで遊ぶ
;-------------------------------------------------
@COMNAME_PLACE_383_3_3
#FUNCTION
TSTR:コマンド名受渡 = 비치 플래그로 놀다
RETURNF 1

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_383_3_3
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_383_3_3
LOCALS = <br>■비치 플래그로 놀다<br>
LOCALS += "상대와 비치 플래그로 함께 노는 커맨드. 미니게임 『비치 플래그』가 발생."
LOCALS += "<br>COM 타입: 일상<br>획득 소스: 환락・정복"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%
		
@COM383_3_3
RFLAG:コマンド結果受渡し変数 = 0 ;勝敗
RFLAG:コマンド結果受渡し変数２ = 0 ;難易度
RFLAG:コマンド結果受渡し変数３ = 0

SIF GETBIT(ミニゲーム初回説明フラグ, 1)
	GOTO 初回説明スキップ
SETBIT ミニゲーム初回説明フラグ, 1
PRINTL 
PRINTL 미니게임 『비치 플래그』
PRINTL 
PRINTW 
PRINTL READY 표시가 나오면 노릴 플래그를 선택하고 GO 신호를 기다립니다
PRINTW 신호가 나오면 빠르게 입력해 주세요
PRINTL
PRINTL 상대보다 빨리 입력하면 이깁니다
PRINTW 신호가 나오기 전에 입력하면 플라잉으로 지게 됩니다
PRINTL 
PRINTL 
PRINTL 
$INPUT
PRINTBUTTON "[0]놀다 ", 0
PRINTBUTTON "[1]놀지 않는다 ", 1
BINPUT
SELECTCASE RESULT 
CASE 0
CASE 1
	PRINTFORML %CALLNAME:PLAYER%은(는) 그 자리에서 떠났다…
	RETURN
ENDSELECT

$初回説明スキップ


CALL ビーチフラッグ
RFLAG:コマンド結果受渡し変数 = RESULT;0=遊ばない 1=フライング 2=押し負け 3=勝ち 4=ズル

CALL COM383_3_3_追加処理(TARGET)
;全員が楽しむ
LOCAL = 1
RFLAG:コマンド結果受渡し変数 = -1 ;ギャラリー
WHILE TARGET:LOCAL > 0
	IF CFLAG:(TARGET:LOCAL):睡眠 || CFLAG:(TARGET:LOCAL):隠密 || TARGET:LOCAL == TARGET:0
	ELSE
		CALL COM383_3_3_追加処理(TARGET:LOCAL)
	ENDIF
	LOCAL ++
WEND

TIME += 60
RETURN 1

@COM383_3_3_追加処理(ARG)
LOCAL:0 = 0
LOCAL:1 = 0

SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE -1 ;ギャラリー
		LOCAL:0 = 500
	CASE 3
		;勝ちは征服が上がる
		;歓楽強度
		LOCAL:0 = 500
		;征服強度
		LOCAL:1 = 100
	CASEELSE
		;勝ち以外
		LOCAL:0 = 500
ENDSELECT
;関係性基本上昇量
LOCAL:2 = 1

;難易度一段上昇につき２割のボーナス
IF RFLAG:コマンド結果受渡し変数２ > 0
	FOR LOCAL:10, 0, 3
		LOCAL:(LOCAL:10) = LOCAL:(LOCAL:10) * (5 + RFLAG:コマンド結果受渡し変数２) / 5
	NEXT
ENDIF

;勝利でさらに２倍 ズルでも盛り上がる
SELECTCASE RFLAG:コマンド結果受渡し変数 == 3
CASE 3
	FOR LOCAL:10, 0, 3
		TIMES LOCAL:(LOCAL:10), 2
	NEXT
CASE 4
	FOR LOCAL:10, 0, 3
		TIMES LOCAL:(LOCAL:10), 1.5
	NEXT
ENDSELECT


CALL SOURCE_CALC_歓楽(ARG, PLAYER, LOCAL:0)
CALL SOURCE_CALC_征服(ARG, PLAYER, LOCAL:1)
CALL SOURCE_CALC_好感度要素_友情度UP(ARG, PLAYER, LOCAL:0 / 2)

CALL 関係性一括上昇(ARG, 4, LOCAL:2)

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE383_3_3
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(383)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;隠密中はダメ
SIF CFLAG:PLAYER:隠密
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM383_3_3

SIF !RFLAG:コマンド結果受渡し変数 ;遊べなかった
	RETURN

PRINTFORM %CALLNAME:PLAYER%은(는)%CALLNAME:TARGET%\@ GET_TARGETNUM() > 1 ? 들 # \@와(과)
PRINTW 비치 플래그로 즐거웠다

