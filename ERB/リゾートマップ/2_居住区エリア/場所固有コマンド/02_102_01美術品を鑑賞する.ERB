
;-------------------------------------------------
;美術館で美術品を鑑賞する
;-------------------------------------------------
@COMNAME_PLACE_380_2_102
#FUNCTION
TSTR:コマンド名受渡 = 미술품을 감상하다
RETURNF 1
;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_380_2_102
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("単独可能")
		
@COM380_2_102
#DIM DYNAMIC 順番シャッフル用配列, キャラクタ数上限
#DIM シャッフル数

IF 知識素質:TARGET:絵師 != 0 && !TCVAR:TARGET:美術館鑑賞フラグ
	;ターゲットが作品ある場合、一日1回は絶対それを見る
	RFLAG:コマンド結果受渡し変数 = TARGET
	RETURN 1
ENDIF

;仮置きなので適当に定住者からランダムに選ぶ
IF RAND:2
	;キャラの作品
	CALL キャラ番号配列_初期化_シャッフル(順番シャッフル用配列, 0)
	シャッフル数 = RESULT

	FOR LOCAL, 0, シャッフル数
		;定住でも従業員でもないキャラはスルー
		SIF !TALENT:(順番シャッフル用配列:LOCAL):従業員 && !TALENT:(順番シャッフル用配列:LOCAL):定住者
			CONTINUE
		;定住でも従業員でもないキャラはスルー
		SIF !TALENT:(順番シャッフル用配列:LOCAL):従業員 && !TALENT:(順番シャッフル用配列:LOCAL):定住者
			CONTINUE
		;絵師が０だとスルー（マイナスはOK）
		SIF 知識素質:(順番シャッフル用配列:LOCAL):絵師 == 0
			CONTINUE
		;一番最初に見つけたキャラを登録しておしまい
		RFLAG:コマンド結果受渡し変数 = 順番シャッフル用配列:LOCAL
		BREAK
	NEXT
	RETURN 1
ENDIF

;キャラ作品ではない時は汎用
RFLAG:コマンド結果受渡し変数 = -1

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE380_2_102
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(380)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;隠密中はダメ
SIF CFLAG:PLAYER:隠密
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM380_2_102

IF RFLAG:コマンド結果受渡し変数 == -1
	PRINTFORM %CALLNAME:PLAYER%은(는)
	PRINTDATA
		DATA 고물상에서 사들인
		DATA 던전에서 발견한
		DATA 만물상에서 팔린
		DATA 유랑 예술가에게서 물려받은
	ENDDATA
	PRINTDATA
		DATA 회화
		DATA 조상
		DATA 항아리
		DATA 보도
	ENDDATA
	PRINTL 을(를) 바라보고 있다
	PRINTDATAL
		DATA 심미안이 연마된 것 같은 기분이 든다!
		DATA 조용히 마음이 가라앉는 것 같다……
		DATA 정말 흥미로운 작품이라고 느낀 것 같다
		DATA 보는 각도를 바꾸니, 유방처럼 보이는 부위를 발견했다!
	ENDDATA
ELSE
	;ここに口上用の分岐いれるけど今はまだ空っぽ
	PRINTFORM %CALLNAME:PLAYER%은(는)%CALLNAME:(RFLAG:コマンド結果受渡し変数)%이(가) 그린
	PRINTDATA
		DATA 풍경화
		DATA 일러스트
		DATA 유화
		DATA 인물화
		DATA 초상화
		DATA 마물의 그림
	ENDDATA
	PRINTL 을(를) 바라보고 있다
	SELECTCASE 知識素質:(RFLAG:コマンド結果受渡し変数):絵師
		CASE -1
			PRINTDATAL
				DATAFORM 이것은……무엇일까. 고양이? 개? 제목을 보니, %CALLNAME:PLAYER%라고 쓰여 있었다……
				DATA 형언하기 어려운 그림이다. 지금이라도 움직일 것 같은 박력은 있지만……아니, 실제로 움직이고 있는 건가? 미지의 생명이 태어나고 있는 건 아닌가?
				DATA 착시 그림처럼 보고만 있어도 불안정해진다. 마음을 뒤흔든다는 의미에서는 명작의 범주에 들어갈지도 모른다
			ENDDATA
		CASE 1
			PRINTDATAL
				DATA 거칠지만 의지가 느껴지는 좋은 그림이다
				DATA 프로에게는 미치지 못하지만, 이것은 이것대로 운치 있는 좋은 그림일 것이다
				DATA 꽤 잘 그린 그림이다. 보는 사람이 본다면 재능을 느끼게 할 것이다
			ENDDATA
		CASE 2
			PRINTDATAL
				DATA 필치의 하나하나에 의도가 느껴지는 좋은 그림이다
				DATA 전체적인 구도에서 훌륭한 기술을 느낄 수 있는, 계산된 그림이다
				DATA 참신한 색채를 훌륭하게 정리한 도전적인 그림이다
			ENDDATA
		CASE 3
			PRINTDATAL
				DATA ……세계에 자랑할 만한 작품이다! 이것 하나만으로도 관광객을 부를 수 있을 정도겠지
				DATA 그야말로 명작이다! 가격을 매긴다면 몇억 루피가 될까……
				DATA 아름다움을 극에 달했다고 해도 과언이 아니다. 보고만 있어도 마음이 맑아지는 것 같다
			ENDDATA
	ENDSELECT
ENDIF
