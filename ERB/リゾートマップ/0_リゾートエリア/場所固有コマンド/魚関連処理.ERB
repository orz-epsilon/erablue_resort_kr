
;-------------------------------------------------
;魚周りの処理をまとめる
;-------------------------------------------------
;魚の効力
;-------------------------------------------------
@食材効力(ARGS)
#FUNCTION

;食材を食事に追加した際、歓楽ソースがどのくらい増えるかを返す
SELECTCASE ARGS
	CASE "小魚"
		RETURNF 20
	CASE "ランページシェル"
		RETURNF 30
	CASE "マイトフィッシュ"
		RETURNF 40
	CASE "レインボーマス"
		RETURNF 100
	CASE "サケモン"
		RETURNF 120
	CASE "ンナギ"
		RETURNF 140
	CASE "アユウ"
		RETURNF 160
	CASE "ヤマァメ"
		RETURNF 180
	CASE "イワナン"
		RETURNF 200
	CASE "オパールエヴィ"
		RETURNF 250
	CASEELSE
		;未設定時の食材は50を返す
		RETURNF 50
ENDSELECT

;-------------------------------------------------
;解説表示
;-------------------------------------------------
@簡易釣りモード説明
DRAWLINE
PRINTL 
PRINTL 즉시 물고기가 잡힙니다.
PRINTL 잡히는 물고기는 지금까지 잡았던 물고기 중에서 무작위로 선택됩니다.
PRINTL 아무것도 잡은 적이 없는 포인트에서 간이 낚시를 해도 아무것도 잡히지 않는다는 점에 주의하십시오.
PRINTL 
PRINTFORML 잡히는 물고기의 수는 %CALLNAME:MASTER%의 낚시 기술 및 함께 낚시한 캐릭터의 낚시 기술에 따라 증가합니다.
PRINTL 낚시 기술은 낚시 경험이 일정치 이상 누적되었을 때 상승합니다.
PRINTW 또한 소질 「낚시를 좋아함」을 가진 캐릭터는 잡히는 물고기의 수를 증가시킵니다.

@ちゃんと釣りモード説明
DRAWLINE
PRINTL 
PRINTL 신경쇠약 미니게임으로 물고기를 잡습니다.
PRINTL 카드를 규정 횟수만큼 뒤집어, 그림이 일치하는 카드에 따라 물고기를 얻습니다.
PRINTW 혼자서 낚시할 수도 있지만, 누군가와 함께 낚시하는 것이 뒤집는 횟수가 증가합니다.
DRAWLINE
PRINTL 
PRINTL 카드의 총수는 24장 12세트입니다.
PRINTL 내역은 N 카드가 12장 6세트, R 카드가 6장 3세트
PRINTL SR 카드가 4장 2세트, SSR 카드가 2장 1세트입니다.
PRINTL 
PRINTW 맞추는 데 성공한 세트의 희귀도에 따라, 희귀한 물고기가 잡힐 가능성이 높아집니다.
DRAWLINE
PRINTL 
PRINTFORML 카드를 뒤집는 횟수는, %CALLNAME:MASTER%의 낚시 기술 및 함께 낚시한 캐릭터의 낚시 기술에 따라 증가합니다.
PRINTL 낚시 기술은 낚시 경험이 일정치 이상 누적되었을 때 상승합니다.
PRINTW 또한 소질 「낚시꾼」을 가진 캐릭터는 넘기는 횟수를 증가시킵니다.



;-------------------------------------------------
;簡易釣りモード処理
;-------------------------------------------------
@簡易釣りモード(場所番号)
#DIM 場所番号
#DIMS 釣れる魚種類, 20
#DIM 魚候補, 20
#DIM 総確率
#DIM 個別確率, 20
#DIM 候補の数
#DIM 釣果, 50

VARSET 釣れる魚種類
VARSET 魚候補
VARSET 総確率
VARSET 個別確率
VARSET 候補の数
VARSET 釣果
VARSET LOCAL

;場所ごとに釣れる魚をセット
;釣れる魚種類:0が最も釣れ易く、数字が増えるにつれ釣れづらくなる
SELECTCASE 場所番号
	CASE 305
		釣れる魚種類:1 = 小魚
		釣れる魚種類:2 = ランページシェル
		釣れる魚種類:3 = マイトフィッシュ
		釣れる魚種類:4 = レインボーマス
		釣れる魚種類:5 = サケモン
		釣れる魚種類:6 = ンナギ
		釣れる魚種類:7 = アユウ
		釣れる魚種類:8 = ヤマァメ
		釣れる魚種類:9 = イワナン
		釣れる魚種類:10 = オパールエヴィ
	CASE 6
		釣れる魚種類:1 = 小魚
		釣れる魚種類:2 = ランページシェル
		釣れる魚種類:3 = マイトフィッシュ
		釣れる魚種類:4 = ンニ
		釣れる魚種類:5 = カキフライ
		釣れる魚種類:6 = イカ
		釣れる魚種類:7 = シャーモン
		釣れる魚種類:8 = トゥナ
		釣れる魚種類:9 = カツウォヌス
		釣れる魚種類:10 = アルバコア
ENDSELECT

;釣ったことがあるかどうか判定
FOR LOCAL, 1, 20
	SIF 釣れる魚種類:LOCAL == ""
		BREAK
	IF 素材アイテム_数値データ取得("累計入手素材数", 釣れる魚種類:LOCAL)
		候補の数 += 1
		魚候補:候補の数 = LOCAL
	ENDIF
NEXT

IF 候補の数 == 0
	PRINTW 아무것도 낚지 못했습니다.
	RETURN 0
ENDIF

;釣れる魚の数を産出
RFLAG:コマンド結果受渡し変数２ = RAND:5 + ABL:PLAYER:釣り技能
RFLAG:コマンド結果受渡し変数２ += (知識素質:PLAYER:釣り好き * 3)
IF TARGET
	RFLAG:コマンド結果受渡し変数２ += RAND:5 + ABL:TARGET:釣り技能
	RFLAG:コマンド結果受渡し変数２ += (知識素質:TARGET:釣り好き * 3)
ENDIF
;最低１保障
RFLAG:コマンド結果受渡し変数２ = MAX(RFLAG:コマンド結果受渡し変数２, 1)

;確率の作成
総確率 = SUMARRAY(魚候補, 1, 20)
FOR LOCAL, 1, 21
	SIF !魚候補:LOCAL
		BREAK
	個別確率:LOCAL = 魚候補:(候補の数 - LOCAL + 1)
NEXT

;魚種類の決定
FOR LOCAL, 0, RFLAG:コマンド結果受渡し変数２
	LOCAL:1 = RAND:総確率
	LOCAL:3 = 0
	FOR LOCAL:2, 候補の数, 0, -1
		LOCAL:3 += 個別確率:(LOCAL:2)
		IF LOCAL:1 < LOCAL:3
			釣果:(魚候補:(LOCAL:2)) += 1
			BREAK
		ENDIF
	NEXT
NEXT

;入手物記述
FOR LOCAL, 0, 20
	IF 釣果:LOCAL
		PRINTFORML %釣れる魚種類:LOCAL%을(를) {釣果:LOCAL}마리 낚았다!
		CALL 素材入手処理(釣れる魚種類:LOCAL, 釣果:LOCAL)
	ENDIF
NEXT

EXP:MASTER:釣り経験 += RFLAG:コマンド結果受渡し変数２
SIF TARGET
	EXP:TARGET:釣り経験 += RFLAG:コマンド結果受渡し変数２

;-------------------------------------------------
;ちゃんと釣りモード処理
;-------------------------------------------------
@ちゃんと釣りモード(場所番号)
#DIM 場所番号
#DIMS 釣れる魚種類, 20
#DIM 候補の数
#DIM 配置カード, 24
#DIM 表カード位置, 2
#DIM 釣り回数
#DIM 釣果, 50
#DIM クリア基準

VARSET 釣れる魚種類
VARSET 配置カード
VARSET 釣果
VARSET LOCAL

;場所ごとに釣れる魚をセット
;釣れる魚種類:0が最も釣れ易く、数字が増えるにつれ釣れづらくなる
SELECTCASE 場所番号
	CASE 305
		釣れる魚種類:1 = 小魚
		釣れる魚種類:2 = ランページシェル
		釣れる魚種類:3 = マイトフィッシュ
		釣れる魚種類:4 = レインボーマス
		釣れる魚種類:5 = サケモン
		釣れる魚種類:6 = ンナギ
		釣れる魚種類:7 = アユウ
		釣れる魚種類:8 = ヤマァメ
		釣れる魚種類:9 = イワナン
		釣れる魚種類:10 = オパールエヴィ
		候補の数 = 10
	CASE 6
		釣れる魚種類:1 = 小魚
		釣れる魚種類:2 = ランページシェル
		釣れる魚種類:3 = マイトフィッシュ
		釣れる魚種類:4 = ンニ
		釣れる魚種類:5 = カキフライ
		釣れる魚種類:6 = イカ
		釣れる魚種類:7 = シャーモン
		釣れる魚種類:8 = トゥナ
		釣れる魚種類:9 = カツウォヌス
		釣れる魚種類:10 = アルバコア
		候補の数 = 10
ENDSELECT

;何回カードをめくれるかをセット
釣り回数 = (ABL:PLAYER:釣り技能 * 2) + 3
釣り回数 += (知識素質:PLAYER:釣り好き * 3)
IF TARGET
	釣り回数 += (ABL:TARGET:釣り技能 * 2) + 1
	釣り回数 += (知識素質:TARGET:釣り好き * 3)
ENDIF

;神経衰弱カード配置を設定
FOR LOCAL, 0, 24
	$CARD_LOOP
	SELECTCASE RAND:24
		CASE IS < 12
			SIF LOCAL:1 >= 12
				GOTO CARD_LOOP
			配置カード:LOCAL = 1
			LOCAL:1 += 1
		CASE IS < 18
			SIF LOCAL:2 >= 6
				GOTO CARD_LOOP
			配置カード:LOCAL = 2
			LOCAL:2 += 1
		CASE IS < 22
			SIF LOCAL:3 >= 4
				GOTO CARD_LOOP
			配置カード:LOCAL = 3
			LOCAL:3 += 1
		CASEELSE
			SIF LOCAL:4 >= 2
				GOTO CARD_LOOP
			配置カード:LOCAL = 4
			LOCAL:4 += 1
	ENDSELECT
NEXT

;どこを表にしているかの数値をリセット
表カード位置:0 = -1
表カード位置:1 = -1

;カードの描画
$描画_LOOP
クリア基準 = LINECOUNT

DRAWLINE
PRINTL 

PRINTFORML 남은 낚시 횟수: {釣り回数}

LOCAL:1 = 0
LOCAL:2 = 0
FOR LOCAL, 0, 24
	IF 配置カード:LOCAL == -1
		CALL ダミーセット( 0, 5, 50)
		LOCAL:1 += 1
		LOCAL:2 += 1
	ELSEIF 表カード位置:0 == LOCAL
		SELECTCASE 配置カード:LOCAL
			CASE 1
				CALL 画像セット("ノーマル", 0, 5, 200)
			CASE 2
				CALL 画像セット("レア", 0, 5, 200)
			CASE 3
				CALL 画像セット("ＳＲ", 0, 5, 200)
			CASE 4
				CALL 画像セット("ＳＳＲ", 0, 5, 200)
		ENDSELECT
	ELSEIF 表カード位置:1 == LOCAL
		SELECTCASE 配置カード:LOCAL
			CASE 1
				CALL 画像セット("ノーマル", 0, 5, 200)
			CASE 2
				CALL 画像セット("レア", 0, 5, 200)
			CASE 3
				CALL 画像セット("ＳＲ", 0, 5, 200)
			CASE 4
				CALL 画像セット("ＳＳＲ", 0, 5, 200)
		ENDSELECT
	ELSE
		CALL 画像セット_P("白抜き", 0, 5, 100, 100, 1, TOSTR(LOCAL))
	ENDIF
	IF ((LOCAL + 1) % 6) == 0
		CALL 画像一斉表示("left", 0, 1)
		IF LOCAL:2 == 6
			PRINTL 
			PRINTL 
			PRINTL 
			PRINTL 
		ENDIF
		LOCAL:2 = 0
	ENDIF
NEXT


;カードが全部なくなったら残り回数分魚を手に入れて終了
IF LOCAL:1 == 24
	PRINTL 이 근처의 물고기는 모두 낚은 것 같다!
	GOTO 釣りリザルト
ENDIF


DRAWLINE
PRINTL 


IF 表カード位置:1 >= 0
	;二枚空いてるはずなので次へ進む
ELSEIF 表カード位置:0 >= 0
	INPUT
	;一枚空いてるハズなので二枚目をめくって描画へ
	SIF RESULT >= 0 && RESULT <= 23 && 配置カード:RESULT != -1
			表カード位置:1 = RESULT
	CLEARLINE LINECOUNT - クリア基準
	GOTO 描画_LOOP
ELSEIF 表カード位置:0 == -1
	INPUT
	;どこも空いて無いはずなので一枚目をめくって描画へ
	SIF RESULT >= 0 && RESULT <= 23 && 配置カード:RESULT != -1
			表カード位置:0 = RESULT
	CLEARLINE LINECOUNT - クリア基準
	GOTO 描画_LOOP
ENDIF

;カードの当たり外れ判定
LOCAL = 0
IF 配置カード:(表カード位置:0) == 配置カード:(表カード位置:1)
	;当たり
	SELECTCASE 配置カード:(表カード位置:0)
		CASE 1
			PRINTL 평범한 물고기다
			;候補のうち、下位４割が登場
			LOCAL = (候補の数 * 2) / 5
			LOCAL = RAND:LOCAL + 1
		CASE 2
			PRINTL 조금 좋은 물고기다!
			;候補のうち、上位６割が登場（最上位は出ない）
			LOCAL = (候補の数 / 3) + 1
			LOCAL = RAND:(候補の数 - LOCAL) + LOCAL + 1
			SIF LOCAL == 10
				LOCAL -= RAND:2 + 1
		CASE 3
			PRINTL 꽤 희귀한 물고기다!
			;候補のうち、上位３割が登場
			LOCAL = (候補の数 / 3) * 2 + 1
			LOCAL = RAND:(候補の数 - LOCAL) + LOCAL + 1
		CASE 4
			PRINTL 매우 희귀한 물고기다!!
			;候補の最上位確定
			LOCAL = 候補の数
	ENDSELECT
	
	LOCAL:1 = RAND:2 + 1
	PRINTFORMW %釣れる魚種類:LOCAL%을(를) {LOCAL:1}마리 낚았다!
	釣果:LOCAL += LOCAL:1
	
	;当たったカードを削除
	配置カード:(表カード位置:0) = -1
	配置カード:(表カード位置:1) = -1
	釣り回数 -= 1
ELSE
	;ハズレ
	PRINTW 아무것도 낚지 못했다…
	釣り回数 -= 1
ENDIF

;回数がまだあるなら再チャレンジ
IF 釣り回数 > 0
	表カード位置:0 = -1
	表カード位置:1 = -1
	CLEARLINE LINECOUNT - クリア基準
	GOTO 描画_LOOP
ENDIF

$釣りリザルト
;回数かカードが枯渇したらリザルト
DRAWLINE

PRINTL 이번 낚시에서 합계
FOR LOCAL, 0, 20
	IF 釣果:LOCAL
		PRINTFORML %釣れる魚種類:LOCAL%을(를) {釣果:LOCAL}마리 낚았다!
		CALL 素材入手処理(釣れる魚種類:LOCAL, 釣果:LOCAL)
		RFLAG:コマンド結果受渡し変数２ += 釣果:LOCAL
	ENDIF
NEXT

EXP:MASTER:釣り経験 += RFLAG:コマンド結果受渡し変数２
SIF RFLAG:コマンド結果受渡し変数２ == 0
	EXP:MASTER:釣り経験 += 1
IF TARGET
	EXP:TARGET:釣り経験 += RFLAG:コマンド結果受渡し変数２
	SIF RFLAG:コマンド結果受渡し変数２ == 0
		EXP:MASTER:釣り経験 += 1
ENDIF

