
;-------------------------------------------------
;プールで一緒に遊ぶ
;-------------------------------------------------
@COMNAME_PLACE_380_0_15
#FUNCTION
TSTR:コマンド名受渡 = 함께 놀다
IF 施設改造度:15:0
	TSTR:コマンド名受渡 = 
	RETURNF 0
ENDIF
RETURNF 1

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_380_0_15
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_380_0_15
LOCALS = <br>■함께 놀다<br>
LOCALS += "그 자리에 있는 모든 캐릭터와 수영장에서 함께 노는 커맨드. 일정 확률로 대성공, 성공, 실패로 분기. <br>평범하게 놀기, 경쟁, 튜브로 뜨기 중 3가지에서 무작위로 선택된다."
LOCALS += "<br>COM 타입: 일상<br>획득 소스: 환락・정복"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%
		
@COM380_0_15

;成功度の判定
LOCAL = RAND:100
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= 9
	;10％で大成功
	RFLAG:コマンド結果受渡し変数 = 10
	CALL 同室者縁結び進行処理(RAND:10 + 4)
ELSEIF LOCAL >= LOCAL:1
	;10～1％で失敗
	RFLAG:コマンド結果受渡し変数 = -1
ELSE
	;残りは成功
	RFLAG:コマンド結果受渡し変数 = 1
	CALL 同室者縁結び進行処理(4)
ENDIF

;遊びの種類をランダム決定
;１：普通に遊ぶ
;２：泳ぎで競争
;３：浮き輪でのんびり浮かぶ
RFLAG:コマンド結果受渡し変数３ = RAND:3 + 1
RFLAG:コマンド結果受渡し変数 = RFLAG:コマンド結果受渡し変数 * RFLAG:コマンド結果受渡し変数３

;その場に居るTARGET全員で回す
LOCAL:1 = TARGET
FOR LOCAL,1,CHARANUM
	SIF TARGET:LOCAL <= 0
		CONTINUE
	SIF CFLAG:(TARGET:LOCAL):睡眠
		CONTINUE
	SIF CFLAG:(TARGET:LOCAL):隠密
		CONTINUE
	TARGET = TARGET:LOCAL
	CALL COM380_0_15_追加処理
NEXT
TARGET = LOCAL:1

TIME += 60
RETURN 1

@COM380_0_15_追加処理



SELECTCASE RFLAG:コマンド結果受渡し変数３
	CASE 1
		;普通に遊ぶ
		
		;歓楽強度
		LOCAL:0 = 500
		;征服強度
		LOCAL:1 = 100
	CASE 2
		;競争
		
		LOCAL:0 = 300
		LOCAL:1 = 500
	CASE 3
		;浮き輪で浮かぶ
		LOCAL:0 = 700
		LOCAL:1 = 10
ENDSELECT

IF RFLAG:コマンド結果受渡し変数 <= -1
	TIMES LOCAL:0 , 0.10
	TIMES LOCAL:1 , 0.50
ELSEIF RFLAG:コマンド結果受渡し変数 <= 9
	TIMES LOCAL:0 , 1.00
	TIMES LOCAL:1 , 1.00
ELSE
	TIMES LOCAL:0 , 2.00
	TIMES LOCAL:1 , 2.00
ENDIF

CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL:0)
CALL SOURCE_CALC_征服(TARGET, PLAYER, LOCAL:1)
CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, LOCAL:0 / 2)

;関係性上昇(基本15分で+1、大成功で1.5倍、失敗時は半減)
LOCAL:2 = RFLAG:コマンド結果受渡し変数 > 0 ? RFLAG:コマンド結果受渡し変数 / 10# -1
CALL 関係性一括上昇(TARGET, 4, LOCAL:2)

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE380_0_15
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(380)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;隠密中はダメ
SIF CFLAG:PLAYER:隠密
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM380_0_15

PRINTFORM %CALLNAME:PLAYER%은(는)%CALLNAME:TARGET%\@ GET_TARGETNUM() > 1 ? 들 # \@과
SELECTCASE RFLAG:コマンド結果受渡し変数３
	CASE 1
		;普通に遊ぶ
		PRINT 수영장에서 물놀이를 했다
	CASE 2
		;競争
		PRINT 누가 가장 빨리 헤엄칠 수 있는지 경쟁했다
	CASE 3
		;浮き輪で浮かぶ
		PRINT 느긋하게 튜브로 수면에 떠올랐다
ENDSELECT

SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE IS <= -1
		;失敗時
		PRINTL 하지만,
		PRINTW 별로 분위기가 달아오르지 않았다…
	CASE IS <= 9
		;成功時
		PRINTL 
		PRINTFORMW \@ GET_TARGETNUM() > 1 ? 다 같이 # 둘이서\@즐거운 시간을 보낼 수 있었다…
	CASEELSE
		;大成功時
		PRINTL 
		PRINTFORMW \@ GET_TARGETNUM() > 1 ? 다 같이 # 둘이서\@정말 분위기가 달아올랐다!
ENDSELECT


