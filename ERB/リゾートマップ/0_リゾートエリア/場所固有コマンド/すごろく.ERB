
;-------------------------------------------------
;ポーカー処理関連
;-------------------------------------------------
;あなた勝利時は１
;代入番号キャラ勝利時は-１
;引き分け時は０を返り値とする
;-------------------------------------------------
		
@すごろく勝負
#DIM ターン終了, 10
#DIM 分岐情報
#DIM プレイヤー番号
#DIM ターン数
;ここからマップ描画用DIM
#DIMS AA, 40
#DIM SPOT
#DIM MODE
#DIM LOCATION
#DIM LINENUM
#DIM X_POINT
;ここまでマップ描画用DIM

VARSET 移動状態
VARSET すごろく現在地, -1
VARSET 参加者, -1
VARSET ターン終了
VARSET 分岐情報
VARSET 一回休みフラグ
VARSET すごろくマス情報
VARSET AA
ターン数 = 1
行動手番 = 0

;参加者を登録する（あなた含め１０人まで）
;その場にいるキャラを全員登録。あなたとTARGETは参加保障
参加者:0 = 0
すごろく現在地:0 = 1
参加者:1 = TARGET
すごろく現在地:1 = 1
LOCAL:1 = 2
FOR LOCAL, 1, CHARANUM
	SIF TARGET:LOCAL <= 0
		CONTINUE
	SIF CFLAG:(TARGET:LOCAL):睡眠
		CONTINUE
	SIF CFLAG:(TARGET:LOCAL):隠密
		CONTINUE
	SIF TARGET == TARGET:LOCAL
		CONTINUE
	;１０人目以降は非参加
	;基本ここに１０人集まることはないと思うので問題ないはず……
	IF LOCAL:1 < 10
		参加者:(LOCAL:1) = TARGET:LOCAL
		すごろく現在地:(LOCAL:1) = 1
		LOCAL:1 += 1
	ENDIF
NEXT

;人数ですごろくMAP変化
IF LOCAL:1 < 4
	;２人～３人
	すごろくマップ種別 = 1
	;32マス分情報入力
	FOR LOCAL, 1, 33
		すごろくマス情報:LOCAL = RAND:4
	NEXT
	すごろくマス情報:1 = 10
	すごろくマス情報:26 = 11
ELSEIF LOCAL:1 < 7
	;４人～６人
	PRINTL 4~6
	すごろくマップ種別 = 2
	;61マス分情報入力
	FOR LOCAL, 1, 62
		すごろくマス情報:LOCAL = RAND:4
	NEXT
	すごろくマス情報:1 = 10
	すごろくマス情報:47 = 11
ELSE
	;７人～１０人
	PRINTL 7~10
	すごろくマップ種別 = 3
	FOR LOCAL, 1, 104
		すごろくマス情報:LOCAL = RAND:4
	NEXT
	すごろくマス情報:1 = 10
	すごろくマス情報:44 = 11
ENDIF

$すごろく_LOOP
;マップ描画処理

IF 行動手番 >= 10 || 参加者:行動手番 < 0
	;全員参加し終わったら抜ける
	VARSET ターン終了
	行動手番 = 0
	ターン数 += 1
	PRINTL 턴을 종료합니다.
	GOTO すごろく_LOOP
ENDIF

PRINTL 
DRAWLINE
PRINTFORML {ターン数}턴째
SETCOLOR すごろくカラーパレット(行動手番)
PRINTFORML 　　차례:%CALLNAME:(参加者:行動手番)%
RESETCOLOR
TRYCCALLFORM すごろくマップ_{すごろくマップ種別}(AA)
CATCH
	RETURN 0
ENDCATCH

FOR LINENUM, 0, VARSIZE("AA")
	SIF AA:LINENUM == ""
		CONTINUE
	;行ごとに初期化
	X_POINT = 0
	RESULTS = I
	;行の終端までループ
	WHILE RESULTS != "" && STRLENSU(RESULTS) < 3
		;1文字取り出す
		SUBSTRINGU AA:LINENUM, X_POINT, 1
		;数字は２文字分取る
		IF ISNUMERIC(RESULTS)
			SUBSTRINGU AA:LINENUM, X_POINT, 2
			TRYCALLFORM DRAW_すごろくMAP_{すごろくマップ種別}(RESULTS)
			X_POINT += 2
		;ふつうの文字
		ELSE
			TRYCALLFORM DRAW_すごろくMAP_{すごろくマップ種別}(RESULTS)
			X_POINT ++
		ENDIF
	WEND
	PRINTL 
NEXT

RESETCOLOR

IF すごろくマス情報:(すごろく現在地:行動手番) == 11
;ゴールにたどり着いたらゴールラベルまで飛ばす
	GOTO ゴールラベル
ENDIF

$移動処理スタート

DRAWLINE
IF !移動状態 && ターン終了:0
	;自身の移動が終わり、マスのイベントを起こす
	IF 行動手番 == 0
		CALLFORM マスイベント処理_{すごろくマス情報:(すごろく現在地:行動手番)}(行動手番)
		FOR プレイヤー番号, 0, 10
			;イベントでゴールした人がいないかどうかチェック
			SIF 参加者:プレイヤー番号 < 0
				CONTINUE
			IF すごろくマス情報:(すごろく現在地:プレイヤー番号) == 11
				;ゴールにたどり着いたらゴールラベルまで飛ばす
				行動手番 = プレイヤー番号
				GOTO ゴールラベル
			ENDIF
		NEXT
		行動手番 += 1
		GOTO すごろく_LOOP
	ENDIF
	;NPCの行動手番
	IF 参加者:行動手番 < 0
		;全員参加し終わったら抜ける
		VARSET ターン終了
		行動手番 = 0
		ターン数 += 1
		PRINTL 턴을 종료합니다.
		GOTO すごろく_LOOP
	ENDIF
	IF !移動状態:行動手番 && ターン終了:行動手番
		;ダイス振り済みかつ移動量が残ってないなら、イベント処理後に手番を切り替えて次のループへ
		CALLFORM マスイベント処理_{すごろくマス情報:(すごろく現在地:行動手番)}(行動手番)
		FOR プレイヤー番号, 0, 10
			;イベントでゴールした人がいないかどうかチェック
			SIF 参加者:プレイヤー番号 < 0
				CONTINUE
			IF すごろくマス情報:(すごろく現在地:プレイヤー番号) == 11
				;ゴールにたどり着いたらゴールラベルまで飛ばす
				行動手番 = プレイヤー番号
				GOTO ゴールラベル
			ENDIF
		NEXT
		行動手番 += 1
		GOTO 移動処理スタート
	ELSEIF !移動状態:行動手番
		IF 一回休みフラグ:行動手番
			;一回休みだとまるまるスキップ（コマのイベントも起こさない）
			SETCOLOR すごろくカラーパレット(行動手番)
			TRYCALLFORM KOJO_EVENT_すごろく_{NO:(参加者:行動手番)}(参加者:行動手番, "手番_一回休み")
			PRINTFORMW ■%CALLNAME:(参加者:行動手番)%은(는) 한 턴 쉬는 중입니다…
			RESETCOLOR
			一回休みフラグ:行動手番 = 0
			ターン終了:行動手番 = 1
			行動手番 += 1
			GOTO すごろく_LOOP
		ENDIF
		移動状態:行動手番 = RAND:6 + 1
		SETCOLOR すごろくカラーパレット(行動手番)
		TRYCALLFORM KOJO_EVENT_すごろく_{NO:(参加者:行動手番)}(参加者:行動手番, "手番_ダイスロール", ターン数)
		PRINTFORML ■%CALLNAME:(参加者:行動手番)%의 주사위
		PRINT 데굴데굴……
		TWAIT 200, 0
		PRINTFORMW %TOFULL(TOSTR(移動状態:行動手番))%！
		RESETCOLOR
		GOTO 移動処理スタート
	ELSE
		;ダイス振り済みフラグを建てる
		ターン終了:行動手番 = 1
		;第二引数に１を入れると分かれ道が自動でランダムに振り分けられる
		TWAIT 500, 0
		CALL MOVE_すごろく(すごろくマップ種別, 1, 行動手番)
		GOTO すごろく_LOOP
	ENDIF
ELSEIF !移動状態
	;まだダイス振ってない状態
	IF 一回休みフラグ:0
		;一回休みだとまるまるスキップ（コマのイベントも起こさない）
		一回休みフラグ:0 = 0
		ターン終了:0 = 1
		行動手番 = 1
		SETCOLOR すごろくカラーパレット(0)
		PRINTFORMW ■%CALLNAME:(参加者:0)%은(는) 한 턴 쉬는 중입니다…
		RESETCOLOR
		GOTO すごろく_LOOP
	ENDIF
	PRINTBUTTON "[0]주사위를 굴린다", 0
	PRINT 　　　　 │■각 플레이어 현재 위치
	PRINTL
	PRINTBUTTON "[9]설명", 9
	PRINT 　　　　　　　　 │
	LOCAL:1 = 現在地算出(0, すごろくマップ種別)
	SETCOLOR すごろくカラーパレット(0)
	PRINTFORM ■%CALLNAME:(参加者:0),14,LEFT%: 골까지 남은{LOCAL:1,8,LEFT}
	RESETCOLOR
	LOCAL:1 = 現在地算出(1, すごろくマップ種別)
	SETCOLOR すごろくカラーパレット(1)
	PRINTFORM ■%CALLNAME:(参加者:1),14,LEFT%：골까지 남은{LOCAL:1,8,LEFT}
	RESETCOLOR
	PRINTL
	PRINT 　　　　　　　　　　　　│
	LOCAL:2 = 0
	FOR LOCAL, 2, 10
		IF 参加者:LOCAL < 0
			PRINTL
			BREAK
		ENDIF
		LOCAL:1 = 現在地算出(LOCAL, すごろくマップ種別)
		SETCOLOR すごろくカラーパレット(LOCAL)
		PRINTFORM ■%CALLNAME:(参加者:LOCAL),14,LEFT%：골까지 남은{LOCAL:1,8,LEFT}
		RESETCOLOR
		LOCAL:2 += 1
		IF (LOCAL:2 % 2) == 0
			PRINTL
			PRINT 　　　　　　　　　　　　│
		ENDIF
	NEXT
	SIF LOCAL:2 == 0
		PRINTL
	DRAWLINE
ELSE
	;ダイスを振って移動中の処理
	;行動済みフラグをまず立て、次の行動手番者を入れる
	ターン終了:0 = 1
	TWAIT 500, 0
	CALL MOVE_すごろく(すごろくマップ種別, 0, 0)
	;入力が必要（＝分かれ道）なら道先のビットが、そうでないなら０が帰ってくる
	分岐情報 = RESULT
	IF 分岐情報 == 0
		GOTO すごろく_LOOP
	ELSE
		;ビット０：上　ビット１：下　ビット２：左　ビット３：右
		;へ道が続いている形
		FOR LOCAL, 0, 3
			IF GETBIT(分岐情報, LOCAL)
				SELECTCASE LOCAL
					CASE 0
						PRINTBUTTON "[8]위로 이동", 8
					CASE 1
						PRINTBUTTON "[2]아래로 이동", 2
					CASE 2
						PRINTBUTTON "[4]왼쪽으로 이동", 4
				ENDSELECT
				BREAK
			ENDIF
		NEXT
		PRINTL 　　　　　　 │■각 플레이어 현재 위치
		
		FOR LOCAL, LOCAL, 4
			IF GETBIT(分岐情報, LOCAL)
				SELECTCASE LOCAL
					CASE 1
						PRINTBUTTON "[2]아래로 이동", 2
					CASE 2
						PRINTBUTTON "[4]왼쪽으로 이동", 4
					CASE 3
						PRINTBUTTON "[6]오른쪽으로 이동", 6
				ENDSELECT
				BREAK
			ENDIF
		NEXT
		PRINT 　　　　　　 │
		LOCAL:1 = 現在地算出(0, すごろくマップ種別)
		SETCOLOR すごろくカラーパレット(0)
		PRINTFORM ■%CALLNAME:(参加者:0),14,LEFT%：골까지 남은{LOCAL:1,8,LEFT}
		RESETCOLOR
		LOCAL:1 = 現在地算出(1, すごろくマップ種別)
		SETCOLOR すごろくカラーパレット(1)
		PRINTFORM ■%CALLNAME:(参加者:1),14,LEFT%：골까지 남은{LOCAL:1,8,LEFT}
		RESETCOLOR
		PRINTL
		
		LOCAL:1 = 0
		FOR LOCAL, LOCAL, 4
			IF GETBIT(分岐情報, LOCAL)
				SELECTCASE LOCAL
					CASE 2
						PRINTBUTTON "[4]왼쪽으로 이동", 4
					CASE 3
						PRINTBUTTON "[6]오른쪽으로 이동", 6
				ENDSELECT
				BREAK
				LOCAL:1 = 1
			ENDIF
		NEXT
		SIF !LOCAL:1
			PRINT 　 　　　　
		PRINT 　　　　　　 │
		LOCAL:2 = 0
		FOR LOCAL, 2, 10
			IF 参加者:LOCAL < 0
				PRINTL 
				BREAK
			ENDIF
			LOCAL:1 = 現在地算出(LOCAL, すごろくマップ種別)
			SETCOLOR すごろくカラーパレット(LOCAL)
			PRINTFORM ■%CALLNAME:(参加者:LOCAL),14,LEFT%：골까지 남은{LOCAL:1,8,LEFT}
			RESETCOLOR
			LOCAL:2 += 1
			IF (LOCAL:2 % 2) == 0
				PRINTL
				PRINT 　　　　　　　　　　　　│
			ENDIF
		NEXT
		SIF LOCAL:2 == 0
			PRINTL
		DRAWLINE
	ENDIF
ENDIF
$INPUT_LOOP
ONEINPUT

SELECTCASE RESULT
	CASE 0
		IF 移動状態
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		移動状態 = RAND:6 + 1
		SETCOLOR すごろくカラーパレット(行動手番)
		PRINTFORML ■%CALLNAME:(参加者:0)%의 주사위
PRINT 데구르르……
		TWAIT 500, 0
PRINTFORMW %TOFULL(TOSTR(移動状態))%!
		RESETCOLOR
		GOTO 移動処理スタート
	CASE 2
		IF !移動状態
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		TWAIT 500, 0
		CALL MOVE_すごろく(すごろくマップ種別, 2, MASTER)
		GOTO すごろく_LOOP
	CASE 4
		IF !移動状態
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		TWAIT 500, 0
		CALL MOVE_すごろく(すごろくマップ種別, 4, MASTER)
		GOTO すごろく_LOOP
	CASE 6
		IF !移動状態
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		TWAIT 500, 0
		CALL MOVE_すごろく(すごろくマップ種別, 6, MASTER)
		GOTO すごろく_LOOP
	CASE 8
		IF !移動状態
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		TWAIT 500, 0
		CALL MOVE_すごろく(すごろくマップ種別, 8, MASTER)
		GOTO すごろく_LOOP
	CASE 9
PRINTL 주사위 놀이 미니게임입니다.
PRINTFORML %CALLNAME:MASTER%부터 순서대로 주사위를 굴려, 가장 먼저 골에 도착한 사람의 승리입니다.
PRINTW (정확히 멈출 필요는 없습니다)
		PRINTL 
PRINTL 4종류의 칸으로 구성되어 있으며, 멈추면 각 칸 종류에 따른 이벤트가 무작위로 발생합니다.
PRINTL 예외적으로 「스」 칸은 시작 칸, 「골」 칸은 골 칸입니다.
PRINTL ■칸 종류에 따른 경향
		PRINT_IMG "ダメアビ"
PRINTL …그 자리에 있는 전원에게 영향을 주는 이벤트가 발생하는 칸입니다.
PRINTL 　　칸을 전진하거나 후퇴하거나 벌칙 게임이거나 하는 등, 유리한지 불리한지는 알 수 없습니다.
PRINTL 　　이 이벤트로 칸을 이동한 경우, 이동한 곳의 이벤트는 발생하지 않습니다.
		PRINT_IMG "回復アビ"
PRINTL …의상 체인지나 간단한 벌칙 게임 등의 이벤트가 발생하는 칸입니다.
PRINTL 　　놀고 있는 멤버에 따라서는, 더욱 과격한 내용의 벌칙 게임이 발생할지도 모릅니다…(미구현)
		PRINT_IMG "バフアビ"
PRINTL …「○칸 전진」이나 「한 번 더 주사위를 굴린다」 등, 유리한 이벤트가 발생하는 칸입니다.
PRINTL 　　이 이벤트로 칸을 이동한 경우, 이동한 곳의 이벤트가 추가로 발생합니다.
		PRINT_IMG "弱体アビ"
		PRINTW …「○칸 뒤로」나 「한 번 쉬기」 등, 불리한 이벤트가 발생하는 칸입니다.
		PRINTL 　　이 이벤트로 칸을 이동했을 경우, 이동한 곳의 이벤트가 추가로 발생합니다.
		GOTO すごろく_LOOP
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT

$ゴールラベル

;現在の行動手番のキャラが勝利
DRAWLINE
SETCOLOR すごろくカラーパレット(行動手番)
RFLAG:コマンド結果受渡し変数 = 参加者:行動手番
TRYCALLFORM KOJO_EVENT_すごろく_{NO:(参加者:行動手番)}(参加者:行動手番, "ゴール！", ターン数)
PRINTFORML %CALLNAME:(参加者:行動手番)%의 승리!!
履歴用実績:(参加者:行動手番):すごろく勝利 += 1
CALL 履歴登録チェック(参加者:行動手番, "すごろく勝利")
RESETCOLOR
DRAWLINE

@すごろくカラーパレット(ARG)
#FUNCTION
;何番手のプレイヤーかで色を変える

SELECTCASE ARG
	CASE 0
		RETURNF 0xC0C0C0
	CASE 1
		RETURNF 0xff3333
	CASE 2
		RETURNF 0x00c0f0
	CASE 3
		RETURNF 0xf0c000
	CASE 4
		RETURNF 0x30c090
	CASE 5
		RETURNF 0xff99ff
	CASE 6
		RETURNF 0x9999ff
	CASE 7
		RETURNF 0x33ffff
	CASE 8
		RETURNF 0xff9966
	CASE 9
		RETURNF 0x33ff33
ENDSELECT

@マスイベント処理_0(行動手番)
;ダメアビマスイベント、全体影響
PRINTW 전체 이벤트 칸이다!
LOCAL = RAND:2
SELECTCASE LOCAL
	CASE 0
		LOCAL:1 = RAND:3 + 1
		PRINTFORMW 전원 %TOFULL(TOSTR(LOCAL:1))%칸 앞으로 나아간다!　※갈림길은 랜덤!
		FOR LOCAL, 0, 10
			SIF 参加者:LOCAL < 0
				BREAK
			移動状態:LOCAL = LOCAL:1
			TRYCALLFORM KOJO_EVENT_すごろく_{NO:(参加者:LOCAL)}(参加者:LOCAL, "全体イベント", LOCAL:1)
			WHILE 移動状態:LOCAL
				CALL MOVE_すごろく(すごろくマップ種別, 1, LOCAL)
			WEND
		NEXT
	CASE 1
		LOCAL:1 = (RAND:3 + 1) * -1
		PRINTFORMW 전원 %TOFULL(TOSTR(LOCAL:1))%칸 뒤로 돌아간다!　※갈림길은 랜덤!
		FOR LOCAL, 0, 10
			SIF 参加者:LOCAL < 0
				BREAK
			移動状態:LOCAL = LOCAL:1
			TRYCALLFORM KOJO_EVENT_すごろく_{NO:(参加者:LOCAL)}(参加者:LOCAL, "全体イベント", LOCAL:1 * -1)
			WHILE 移動状態:LOCAL
				CALL MOVE_すごろく(すごろくマップ種別, 1, LOCAL)
			WEND
		NEXT
ENDSELECT

@マスイベント処理_1(行動手番)
#DIM 参加人数
;回復アビマスイベント、罰ゲームその他
PRINTW 벌칙 게임 칸이다!
SETCOLOR すごろくカラーパレット(行動手番)
FOR 参加人数, 0, 10
	SIF 参加者:(参加人数) < 0
		BREAK
NEXT
参加人数 -= 1
LOCAL:1 = RAND:6
SELECTCASE LOCAL:1
	CASE 0
		PRINTL 신비한 힘으로 의상이 바니가 되어버렸다!
		PRINTW ※목욕하거나 하루가 끝날 때까지 지속
		CALL CLOTHES_CHANGE_バニー服(参加者:行動手番)
		SIF TALENT:(参加者:行動手番):羞恥心 > 0
			CALL SOURCE_CALC_露出(参加者:行動手番, PLAYER, 50)
	CASE 1
		PRINTL 개인기를 선보이게 되었다!
		PRINTFORM %CALLNAME:(参加者:行動手番)%은(는)
		IF TALENT:(参加者:行動手番):羞恥心 > 0
			PRINT 부끄러움에 어색한 움직임이 되면서, 어떻게든
		ELSEIF TALENT:(参加者:行動手番):羞恥心 < 0 || TALENT:(参加者:行動手番):芸達者 > 0
			PRINT 신나게 준비를 마치고, 멋진
		ENDIF
		PRINTW 개인기를 선보였다.
		SIF TALENT:(参加者:行動手番):羞恥心 >= 0 && TALENT:(参加者:行動手番):芸達者 <= 0
			CALL SOURCE_CALC_露出(参加者:行動手番, PLAYER, 10)
		SIF TALENT:(参加者:行動手番):羞恥心 > 0
			CALL SOURCE_CALC_露出(参加者:行動手番, PLAYER, 50)
	CASE 2
		PRINTL 신비한 힘으로 의상이 메이드복이 되어버렸다!
PRINTW ※목욕하거나 하루가 끝날 때까지 계속
		CALL CLOTHES_CHANGE_メイド服(参加者:行動手番)
	CASE 3
		LOCAL = RAND:参加人数
		;自分が自分にしないように
		WHILE LOCAL == 行動手番
			LOCAL = RAND:参加人数
		WEND
PRINTL 다음 턴까지 누군가를 무릎베개한 채로 플레이할 것!
PRINTFORMW 주사위를 굴려, %CALLNAME:(参加者:行動手番)%은(는) %CALLNAME:(参加者:LOCAL)%의 머리를 무릎에 얹었다.
		SIF LOCAL == 0
			CALL SOURCE_CALC_接触(参加者:行動手番, PLAYER, 50)
	CASE 4
		LOCAL = RAND:参加人数
		;自分が自分にしないように
		WHILE LOCAL == 行動手番
			LOCAL = RAND:参加人数
		WEND
PRINTL 누군가에게 마음껏 간지럼을 당할 것!
PRINTFORML 주사위를 굴려, %CALLNAME:(参加者:LOCAL)%은(는) 마음껏 %CALLNAME:(参加者:行動手番)%을(를) 간지럽혔다!
		IF TALENT:(参加者:行動手番):Ｓ感度 > 0
	PRINTFORMW %CALLNAME:(参加者:行動手番)%은(는) 눈물이 맺힐 정도로 웃고 있다…
		ELSEIF TALENT:(参加者:行動手番):Ｓ感度 < 0
	PRINTFORMW 간지럼에 강한 건지, %CALLNAME:(参加者:行動手番)%은(는) 가볍게 웃으며 간지럼을 당하고 있다.
		ELSE
	PRINTFORMW 크게 웃으면서, %CALLNAME:(参加者:行動手番)%은(는) 간지럼을 참고 있다.
		ENDIF
		SIF LOCAL == 0
			CALL SOURCE_CALC_接触(参加者:行動手番, PLAYER, 100)
	CASE 5
		LOCAL = RAND:参加人数
		;自分が自分にしないように
		WHILE LOCAL == 行動手番
			LOCAL = RAND:参加人数
		WEND
PRINTL 누군가를 마음껏 안아줄 것!
PRINTFORM 주사위를 굴려, %CALLNAME:(参加者:行動手番)%은(는) 
		IF TALENT:(参加者:行動手番):羞恥心 > 0
	PRINT 머뭇거리며, 망설이면서
		ELSEIF TALENT:(参加者:行動手番):羞恥心 < 0
	PRINT 두 팔을 벌려, 마음껏
		ENDIF
PRINTFORMW %CALLNAME:(参加者:LOCAL)%을(를) 꼬옥 안아주었다.
		SIF LOCAL == 0
			CALL SOURCE_CALC_接触(参加者:行動手番, PLAYER, 50)
ENDSELECT
RFLAG:コマンド結果受渡し変数２ = 参加者:LOCAL
TRYCALLFORM KOJO_EVENT_すごろく_{NO:(参加者:行動手番)}(参加者:行動手番, "罰ゲームイベント", LOCAL:1)
IF GROUPMATCH(LOCAL:1, 3, 4, 5)
	;相手が必要な罰ゲームの場合、相手側の口上も出す
	SETCOLOR すごろくカラーパレット(LOCAL)
	TRYCALLFORM KOJO_EVENT_すごろく_{NO:(RFLAG:コマンド結果受渡し変数２)}(RFLAG:コマンド結果受渡し変数２, "罰ゲームイベント_反応", LOCAL:1)
ENDIF
RESETCOLOR

@マスイベント処理_2(行動手番)
;バフアビマスイベント、有利イベ
PRINTL 유리한 이벤트 칸이다!
SETCOLOR すごろくカラーパレット(行動手番)

LOCAL = RAND:4
SELECTCASE LOCAL
	CASE 0
		;もっかいダイス
PRINTW 다시 한번 주사위를 굴려 나아갈 것!
		移動状態:行動手番 = RAND:6 + 1
		PRINTFORML ■%CALLNAME:(参加者:行動手番)%의 주사위
		PRINT 데구르르……
		TWAIT 200, 0
		PRINTFORMW %TOFULL(TOSTR(移動状態:行動手番))%!
	CASE 1
		;２マス進む
		PRINTW 2칸 전진
		移動状態:行動手番 = 2
	CASE 2
		;３マス進む
		PRINTW 3칸 전진
		移動状態:行動手番 = 3
	CASE 3
		;４マス進む
		PRINTW 4칸 전진
		移動状態:行動手番 = 4
ENDSELECT
TRYCALLFORM KOJO_EVENT_すごろく_{NO:(参加者:行動手番)}(参加者:行動手番, "有利イベント", LOCAL)

行動手番 -= 1
RESETCOLOR


@マスイベント処理_3(行動手番)
;弱体アビマスイベント、不利イベ
PRINTL 불리 이벤트 칸이다!
SETCOLOR すごろくカラーパレット(行動手番)
LOCAL = RAND:4
SELECTCASE LOCAL
	CASE 0
		;一回休み
		PRINTW 한 번 쉬게 되어버렸다…
		一回休みフラグ:行動手番 = 1
	CASE 1
		;２マス戻る
		PRINTW 2칸 뒤로 돌아가 버린다
		移動状態:行動手番 = -2
	CASE 2
		;３マス戻る
		PRINTW 3칸 뒤로 돌아가 버린다
		移動状態:行動手番 = -3
	CASE 3
		;４マス戻る
		PRINTW 4칸 뒤로 돌아가 버린다
		移動状態:行動手番 = -4
ENDSELECT
TRYCALLFORM KOJO_EVENT_すごろく_{NO:(参加者:行動手番)}(参加者:行動手番, "不利イベント", LOCAL)
IF GROUPMATCH(LOCAL, 1, 2, 3)
	行動手番 -= 1
ENDIF
RESETCOLOR

@マスイベント処理_10(行動手番)
;スタートマスなので特に何もなし

@マスイベント処理_11(行動手番)
;ゴールマスなので特に何もなし
;ゴール処理は別の場所にあるのでこれは呼ばれないはずだけど一応記載


