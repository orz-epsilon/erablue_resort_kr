
;-------------------------------------------------
;水鉄砲遊び
;-------------------------------------------------
		
@水鉄砲遊び(キャラ番号)
#DIM キャラ番号
#DIM 難易度, 2
#DIM タゲ行
#DIM タゲ列
#DIM クリック成功回数
;ここからマップ描画用DIM
#DIMS AA, 40
#DIM SPOT
#DIM MODE
#DIM LOCATION
#DIM LINENUM
#DIM X_POINT
;ここまでマップ描画用DIM

クリック成功回数 = 0

$難易度_LOOP
;難易度選択
DRAWLINE
PRINTL 어떤 난이도로 플레이하시겠습니까?
PRINTBUTTON "[1]EASY (유예 시간 2초: 횟수 5회)", 1
PRINTL 
PRINTBUTTON "[2]NORMAL (유예 시간 1초: 횟수 7회)", 2
PRINTL 
PRINTBUTTON "[3]HARD (유예 시간 0.5초: 횟수 10회)", 3
PRINTL 
PRINTBUTTON "[4]십천중 (유예 시간 0.1초: 횟수 15회)", 4
PRINTL 
DRAWLINE
PRINTBUTTONLC "[설명]", 998
PRINTBUTTON "[돌아가기]", 999
PRINTL

$INPUT_LOOP
INPUT

SELECTCASE RESULT
	CASE 999
		RETURN -1
	CASE 998
		DRAWLINE
		PRINTL 화면 안에서 무작위로 출현하는 타겟을, 시간 내에 클릭하는 미니게임입니다.
		PRINTL 클릭에 성공한 횟수가 많을수록 캐릭터와 즐겁게 놀았다는 것이 되어, 파라미터 상승량이 증가합니다.
		PRINTW 또한, 난이도가 높을수록 클릭할 기회가 늘어납니다.
		PRINTL 
		PRINTL ■유예 시간・횟수에 대하여
		PRINTL 유예 시간이란, 타겟이 표시되고 나서 실패가 될 때까지의 시간을 말합니다
		PRINTL 횟수란, 몇 번 타겟을 노릴 기회가 있는지를 말합니다.
		PRINTW 유예 시간이 길수록 간단하며, 횟수가 많을수록 성공 횟수를 벌 기회가 있습니다.
		PRINTL 
		PRINTL (난이도 십천중은 인간이 성공할 수 있도록 만들지 않았습니다)
		PRINTW (앞으로 유예 시간이 늘어나거나 횟수를 늘릴 수 있는 시스템을 예정하고 있지만 미구현입니다)
		GOTO 難易度_LOOP
	CASE 1
		難易度:0 = 2000
		難易度:1 = 5
	CASE 2
		難易度:0 = 1000
		難易度:1 = 7
	CASE 3
		難易度:0 = 500
		難易度:1 = 10
	CASE 4
		難易度:0 = 100
		難易度:1 = 15
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT

;クリック領域生成
AA:00 = ┌────────────────────┐
AA:01 = │0102030405060708091011121314151617181920│
AA:02 = │0102030405060708091011121314151617181920│
AA:03 = │0102030405060708091011121314151617181920│
AA:04 = │0102030405060708091011121314151617181920│
AA:05 = │0102030405060708091011121314151617181920│
AA:06 = │0102030405060708091011121314151617181920│
AA:07 = │0102030405060708091011121314151617181920│
AA:08 = │0102030405060708091011121314151617181920│
AA:09 = │0102030405060708091011121314151617181920│
AA:10 = │0102030405060708091011121314151617181920│
AA:11 = │0102030405060708091011121314151617181920│
AA:12 = │0102030405060708091011121314151617181920│
AA:13 = │0102030405060708091011121314151617181920│
AA:14 = │0102030405060708091011121314151617181920│
AA:15 = │0102030405060708091011121314151617181920│
AA:16 = │0102030405060708091011121314151617181920│
AA:17 = └────────────────────┘

$描画_LOOP
;ターゲットの再設定
タゲ行 = RAND:16 + 1
タゲ列 = RAND:20 + 1

;クリック領域描画
FOR LINENUM, 0, VARSIZE("AA")
	SIF AA:LINENUM == ""
		CONTINUE
	;行ごとに初期化
	X_POINT = 0
	RESULTS = I
	;行の終端までループ
	WHILE RESULTS != "" && STRLENSU(RESULTS) < 3
		SUBSTRINGU AA:LINENUM, X_POINT, 1
		IF ISNUMERIC(RESULTS)
			;数字は２文字取る
			SUBSTRINGU AA:LINENUM, X_POINT, 2
			IF LINENUM == タゲ行 && TOINT(RESULTS) == タゲ列
				SETCOLOR すごろくカラーパレット(1)
				PRINTBUTTON "○", 999
				RESETCOLOR
			ELSE
				PRINT 　
			ENDIF
			X_POINT += 2
		ELSE
			PRINTS RESULTS
			X_POINT ++
		ENDIF
	WEND
	PRINTL 
NEXT

;制限時間付きクリックタイム
TINPUT 難易度, 0, 1, "失敗！"
CLEARLINE 1

IF RESULT == 999
	PRINTL 성공!
	クリック成功回数 += 1
ENDIF

難易度:1 -= 1
IF 難易度:1
	PRINTL 
	PRINTFORM 남은 횟수:%TOFULL(TOSTR(難易度:1))%회　　　
	PRINTFORMW 성공 횟수:%TOFULL(TOSTR(クリック成功回数))%회
	GOTO 描画_LOOP
ELSE
	PRINTW 
	DRAWLINE
	PRINTL 게임 종료!
	PRINTFORMW 성공 횟수:%TOFULL(TOSTR(クリック成功回数))%회
	履歴用実績:MASTER:水鉄砲回数 += 1
	CALL 履歴登録チェック(MASTER, "水鉄砲回数")
	履歴用実績:キャラ番号:水鉄砲回数 += 1
	CALL 履歴登録チェック(キャラ番号, "水鉄砲回数")
ENDIF

IF 難易度:0 == 100 && クリック成功回数 > 0
	CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"難易度：十天衆の水鉄砲遊びで１回以上成功させた！","実績")
ENDIF

RETURN クリック成功回数

