
;-------------------------------------------------
;03_305_02渓流エリアで水遊び
;-------------------------------------------------
@COMNAME_PLACE_381_0_305
#FUNCTION

IF 施設改造度:3:0 == 0
	TSTR:コマンド名受渡 = 
	RETURNF 0
ENDIF
TSTR:コマンド名受渡 = 물놀이를 하다
RETURNF 1

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_381_0_305
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")
		
@COM381_0_305

;成功度の判定
LOCAL = RAND:100
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= 9
	;10％で大成功
	RFLAG:コマンド結果受渡し変数 = 10
	CALL 同室者縁結び進行処理(RAND:10 + 4)
ELSEIF LOCAL >= LOCAL:1
	;10～1％で失敗
	RFLAG:コマンド結果受渡し変数 = -1
ELSE
	;残りは成功
	RFLAG:コマンド結果受渡し変数 = 1
	CALL 同室者縁結び進行処理(4)
ENDIF

;その場に居るTARGET全員で回す
LOCAL:1 = TARGET
FOR LOCAL,1,CHARANUM
	SIF TARGET:LOCAL <= 0
		CONTINUE
	SIF CFLAG:(TARGET:LOCAL):睡眠
		CONTINUE
	SIF CFLAG:(TARGET:LOCAL):隠密
		CONTINUE
	TARGET = TARGET:LOCAL
	CALL COM381_0_305_追加処理
NEXT
TARGET = LOCAL:1

TIME += 60
RETURN 1


@COM381_0_305_追加処理




;歓楽強度
LOCAL:0 = 500
;征服強度
LOCAL:1 = 100

IF RFLAG:コマンド結果受渡し変数 <= -1
	TIMES LOCAL:0 , 0.10
	TIMES LOCAL:1 , 0.50
ELSEIF RFLAG:コマンド結果受渡し変数 <= 9
	TIMES LOCAL:0 , 1.00
	TIMES LOCAL:1 , 1.00
ELSE
	TIMES LOCAL:0 , 2.00
	TIMES LOCAL:1 , 2.00
ENDIF

CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL:0)
CALL SOURCE_CALC_征服(TARGET, PLAYER, LOCAL:1)
CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, LOCAL:0 / 2)



;-------------------------------------------------
;実行判定
;------------------------------------------------
@COM_ABLE381_0_305
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(381)
	RETURN RESULT
;隠密中はダメ
SIF CFLAG:PLAYER:隠密
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM381_0_305
PRINTFORM %CALLNAME:PLAYER%은(는)%CALLNAME:TARGET%\@ GET_TARGETNUM() > 1 ? 들 # \@과 강가에서 물놀이를 했다
SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE IS <= -1
		;失敗時
		PRINTL 하지만,
		PRINTW 별로 분위기가 달아오르지 않았다…
	CASE IS <= 9
		;成功時
		PRINTL 
		PRINTFORMW \@ GET_TARGETNUM() > 1 ? 모두 함께 # 둘이서\@즐거운 시간을 보낼 수 있었다…
	CASEELSE
		;大成功時
		PRINTL 
		PRINTFORMW \@ GET_TARGETNUM() > 1 ? 모두 함께 # 둘이서\@정말 분위기가 달아올랐다!
ENDSELECT


