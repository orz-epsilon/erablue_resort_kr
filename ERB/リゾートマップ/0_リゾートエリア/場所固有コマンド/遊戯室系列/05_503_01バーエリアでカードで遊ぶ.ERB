
;-------------------------------------------------
;05_503_01バーエリアでカードで遊ぶ
;-------------------------------------------------

@COMNAME_PLACE_380_0_503
#FUNCTION
IF 施設改造度:5:0 == 0
	TSTR:コマンド名受渡 = 
	RETURNF 0
ENDIF
TSTR:コマンド名受渡 = 카드로 놀다
RETURNF 1
;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_380_0_503
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_380_0_503
LOCALS = <br>■카드로 놀다<br>
LOCALS += "상대와 간이 포커로 함께 노는 커맨드. 미니게임 『간이 포커』가 발생. <br>일정 확률로 벌칙 게임이나 루피를 건 도박을 할 수 있다."
LOCALS += "<br>COM 타입: 일상<br>획득 소스: 환락・정복(일정 조건 시)"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM380_0_503

RFLAG:コマンド結果受渡し変数３ = 0

;賭けるかの判定
LOCAL = RAND:100
IF TALENT:ギャンブラー < 0
	;賭け事嫌いだと絶対にギャンブルしない
	RFLAG:コマンド結果受渡し変数 = 0
ELSEIF LOCAL < 25
	;1/4で賭けに誘ってくる
	RFLAG:コマンド結果受渡し変数 = 1
ELSEIF TALENT:ギャンブラー || あなた特殊能力:ギャンブラー
	;ギャンブラーだと確実に誘ってくる
	RFLAG:コマンド結果受渡し変数 = 1
ELSE
	RFLAG:コマンド結果受渡し変数 = 0
ENDIF

;賭け内容の決定
IF RFLAG:コマンド結果受渡し変数
	PRINTFORML %CALLNAME:TARGET%이(가) 뭔가 걸지 않겠냐고 권유했다.
	PRINTBUTTON "[0]1000 루피를 걸다", 0
	PRINTL 
	PRINTBUTTON "[1]벌칙 게임을 걸다", 1
	PRINTL 
	PRINTBUTTON "[9]걸지 않는다", 9
	PRINTL 
	INPUT

	SELECTCASE RESULT
		CASE 0
			RFLAG:コマンド結果受渡し変数３ = 1
			PRINTFORML %CALLNAME:PLAYER%와(과) %CALLNAME:TARGET%은(는) 루피를 걸고 승부하기로 했다.
			EXP:PLAYER:ギャンブル経験 += 1
		CASE 1
			RFLAG:コマンド結果受渡し変数３ = 2
			PRINTFORML %CALLNAME:PLAYER%와(과) %CALLNAME:TARGET%은(는) 벌칙 게임을 걸고 승부하기로 했다.
			EXP:PLAYER:ギャンブル経験 += 2
		CASEELSE
			RFLAG:コマンド結果受渡し変数３ = 0
			RFLAG:コマンド結果受渡し変数 = 0
			PRINTFORML %CALLNAME:TARGET%은(는) 조금 아쉬운 얼굴로 내기를 철회했다
	ENDSELECT
ENDIF



;歓楽強度
LOCAL:0 = 500
;征服強度
LOCAL:1 = 300
;賭けてる時は歓楽・征服共に増加
IF RFLAG:コマンド結果受渡し変数
	LOCAL:0 += 500
	LOCAL:1 += 300
ENDIF

;勝敗決定
CALL 簡易ポーカー(TARGET)
;あなた勝利ならRESULTに１が、敗北なら-１が返ってくる

RFLAG:コマンド結果受渡し変数３ = RFLAG:コマンド結果受渡し変数３ * RESULT

;基本歓楽のみで、相手が勝った時のみ征服が増加
CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL:0)
CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, LOCAL:0 / 2)
SIF RESULT == -1
	CALL SOURCE_CALC_征服(TARGET, PLAYER, LOCAL:1)


TIME += 30
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE380_0_503
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(380)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;隠密中はダメ
SIF CFLAG:PLAYER:隠密
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM380_0_503

PRINTFORML %CALLNAME:PLAYER%와(과) %CALLNAME:TARGET%은(는) 카드로 한판 승부했다.

TRYCCALLFORM KOJO_EVENT_カード勝負_{NO:TARGET}(TARGET)
	PRINTL
CATCH
ENDCATCH

;賭け事処理
;口上などの表示順のためここで処理
;0:引き分け　1:ルピ賭け勝利　-1:ルピ賭け敗北　2:罰ゲーム賭け勝利　-2:罰ゲーム賭け敗北
IF RFLAG:コマンド結果受渡し変数
	SELECTCASE RFLAG:コマンド結果受渡し変数３
		CASE 0
			;引き分け時は何もなし
			PRINTFORML 내기는 무승부였다…
		CASE 1
			;ルピ増加
			PRINTFORML 내기에서 승리하여, %CALLNAME:TARGET%에게서 1000 루피를 받았다
			MONEY += 1000
		CASE -1
			;ルピ減少
			PRINTFORML 내기에서 패배하여, %CALLNAME:TARGET%에게 1000루피를 지불했다…
			MONEY -= 1000
		CASE 2
			;罰ゲーム処理を呼ぶ
			PRINTFORML 내기에서 승리하여, %CALLNAME:TARGET%에게 벌칙 게임을 주게 되었다
			CALL 罰ゲーム(TARGET, "勝利")
		CASE -2
			;罰ゲーム処理を呼ぶ
			PRINTFORML 내기에서 패배하여, %CALLNAME:TARGET%에게서 벌칙 게임을 받게 되었다…
			CALL 罰ゲーム(TARGET, "敗北")
	ENDSELECT
ENDIF
