@発注依頼_素材収集

依頼文章保存 = 
依頼文章保存 += "임의의 소재를 지정하여 모아오게 합니다. <br>"
依頼文章保存 += "(한 번이라도 입수한 적이 있는 소재만)<br>"
依頼文章保存 += "소재의 양·필요 보수액은 소재의 랭크에 따라 변동합니다. <br>"
依頼文章保存 += "필요 보수액은 소재의 시세에 따라 변동합니다. <br>"
依頼文章保存 += "<br>"
依頼文章保存 += @"의뢰 아이템:"
IF 依頼アイテム名 != ""
	LOCALS = %属性数値文字色変換_HTML(素材アイテム_数値データ取得("素材属性", 依頼アイテム名))%
	依頼文章保存 += @"<font color='#%LOCALS%'>%依頼アイテム名%</font><br>"
	LOCAL = 素材アイテム_数値データ取得("素材ランク", 依頼アイテム名)
	LOCAL:1 = 素材アイテム_数値データ取得("素材値相場", 依頼アイテム名)
	依頼報酬額 = (LOCAL + 1) * (LOCAL + 1) * 500 * LOCAL:1 / 100
	依頼文章保存 += @"필요 보수액:%NUM_FORMAT(依頼報酬額)%<br>"
ENDIF
依頼文章保存 += "<br>"
依頼文章保存 += "<button value='200'>[200] 의뢰 아이템을 변경한다</button><br>"
依頼文章保存 += "<button value='201'>[201] 이것으로 발주한다</button><br><br>"

@依頼追加表示情報_素材収集(表示依頼枠番号)
#DIM 表示依頼枠番号

詳細文文字列受け渡し変数 = 
IF 遂行中依頼_対象名:表示依頼枠番号 !=""
	LOCALS = %属性数値文字色変換_HTML(素材アイテム_数値データ取得("素材属性", 遂行中依頼_対象名:表示依頼枠番号))%
	詳細文文字列受け渡し変数 = %STRFORM(@"依頼アイテム：<font color='#%LOCALS%'>%遂行中依頼_対象名:表示依頼枠番号%</font>")%
ENDIF

@発注実処理_素材収集(表示依頼枠番号)
#DIM 表示依頼枠番号

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 200
		CALL 素材アイテム一覧表示("累計入手素材数 > 0", 1)
		IF RESULT > -1
			依頼アイテム名 = %DT_CELL_GETS("所持素材データベース", RESULT, "素材アイテム名", 1)%
		ENDIF
	CASE 201
		IF 依頼アイテム名 != ""
			IF MONEY < 依頼報酬額
				PRINTL 
				PRINTW 루피가 부족합니다.
				RETURN 0
			ENDIF
			PRINTL
			PRINTFORMW 의뢰: 소재 수집　　의뢰 아이템:%依頼アイテム名%　　을(를) 발주했습니다.
			MONEY -= 依頼報酬額
			遂行中依頼_対象名:表示依頼枠番号 = %依頼アイテム名%
			LOCAL = 素材アイテム_数値データ取得("素材ランク", 依頼アイテム名)
			依頼完了確率_初期値:表示依頼枠番号 = MAX(0, (5 - LOCAL) * 10)
			依頼完了確率_進数:表示依頼枠番号 = MAX(5, (5 - LOCAL) * 3)
			RETURN 1
		ELSE
			PRINTL
			PRINTFORMW 아이템이 지정되지 않았습니다.
			RETURN 0
		ENDIF
	CASE 999
		依頼アイテム名 = 
		RETURN -1
ENDSELECT

@依頼完了時処理_素材収集(表示依頼枠番号)
#DIM 表示依頼枠番号

LOCAL = 素材アイテム_数値データ取得("素材ランク", 遂行中依頼_対象名:表示依頼枠番号)
LOCAL = RAND:2 + (6 - LOCAL)
依頼報告文字列:表示依頼枠番号 += @"%依頼受注者名:表示依頼枠番号% 이(가) %遂行中依頼:表示依頼枠番号% 의 의뢰를 달성한 것 같다!<br>"
依頼報告文字列:表示依頼枠番号 += @"%遂行中依頼_対象名:表示依頼枠番号% 을(를) {LOCAL}개 손에 넣었다!<br>"
CALL 素材入手処理(遂行中依頼_対象名:表示依頼枠番号, LOCAL)
