
;-------------------------------------------------
;兵装開発
;-------------------------------------------------
@COMNAME_PLACE_382_1_11
#FUNCTION
TSTR:コマンド名受渡 = 병장 개발
RETURNF 1

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_382_1_11
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("単独可能")

@COM_TOOLTIP_382_1_11
LOCALS = <br>■병장 개발<br>
LOCALS += "새로운 탑승기 또는 탑재 병기를 설계하는 커맨드. 그 자리에 캐릭터가 있다면 모두와 함께 설계한다."
LOCALS += "<br>충분한 진척과 영감이 있다면 새로운 설계도를 획득할 수 있다."
LOCALS += "<br>COM 타입: 단독 가능<br>획득 소스: 환락・정복(일정 조건 시)"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM382_1_11

CALL 兵装開発
;設計結果　0=未設計 1=設計中 2=完成 -1=うまくいかなかった
RFLAG:コマンド結果受渡し変数 = RESULT
SIF RFLAG:コマンド結果受渡し変数 == 2
	RSTR:コマンド結果受渡し文字列 '= RESULTS

SIF RFLAG:コマンド結果受渡し変数 == 0
	RETURN 0

;縁結びの進行
SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE 1
		;設計中
		CALL 同室者縁結び進行処理(5)
	CASE 2
		;完成
		CALL 同室者縁結び進行処理(10)
ENDSELECT

;同室の全員にSOURCE
IF TARGET > 0
	FOR LOCAL, 1, VARSIZE("TARGET")
		SIF TARGET:LOCAL <= 0
			BREAK
		SIF !COMABLEキャラ個別_382_1_11(TARGET:LOCAL)
			CONTINUE

		CALL COM382_1_11_追加処理(TARGET:LOCAL)
	NEXT
ENDIF

TIME += 60
RETURN 1

@COM382_1_11_追加処理(ARG)
#LOCALSIZE 3

VARSET LOCAL, 0

SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE -1
		;うまくいかなかった
		;歓楽
		LOCAL:0 = 50
		;関係性補正 半減
		LOCAL:2 = -1
	CASE 1
		;設計中
		;歓楽
		LOCAL:0 = 500
	CASE 2
		;完成
		;歓楽
		LOCAL:0 = 1000
		;征服
		LOCAL:1 = 200
		;関係性補正 2倍
		LOCAL:2 = 2
ENDSELECT

CALL SOURCE_CALC_歓楽(ARG, PLAYER, LOCAL:0)
CALL SOURCE_CALC_征服(ARG, PLAYER, LOCAL:1)
CALL SOURCE_CALC_好感度要素_友情度UP(ARG, PLAYER, LOCAL:0 / 2)

CALL 関係性一括上昇(ARG, 4, LOCAL:2)

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE382_1_11
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(382)
	RETURN RESULT
SIF TARGET > 0 && !COMABLEキャラ個別_382_1_11(TARGET)
	RETURN 0
;隠密中はダメ
SIF CFLAG:PLAYER:隠密
	RETURN 0
RETURN 1

;進捗などの計算で何度も使うので共通関数化
@COMABLEキャラ個別_382_1_11(ARG)
#FUNCTION

;睡眠中
SIF CFLAG:ARG:睡眠
	RETURNF 0
;隠密中はダメ
SIF CFLAG:ARG:隠密
	RETURNF 0
RETURNF 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM382_1_11

;設計してない
SIF RFLAG:コマンド結果受渡し変数 == 0
	RETURN

IF TARGET > 0
	PRINTFORML %CALLNAME:PLAYER%은(는)%CALLNAME:TARGET%\@ GET_TARGETNUM() > 1 ? 들 # \@과 신병장 개발을 했다

	SELECTCASE RFLAG:コマンド結果受渡し変数
		CASE -1
			;うまくいかなかった
			IF 兵装開発_進捗 > 0
				PRINTL 하지만, 진척은 좋지 않았다
				PRINTL 좀 더 기계나 마법에 능통한 인물에게 도움을 받는 편이 좋을지도 모른다…
			ELSE
				PRINTL 하지만, 좋은 아이디어가 떠오르지 않았다
				PRINTL 재미있는 아이디어가 있을 법한 인물을 찾아보는 편이 좋을지도 모른다…
			ENDIF
		CASE 1
			;設計中
			SELECTCASE 兵装開発_進捗
				CASE IS < 兵装開発_案出し
					PRINTFORML %CALLNAME:TARGET%\@ GET_TARGETNUM() > 1 ? 들 # \@은(는), 즐거운 듯 여러 아이디어를 내고 있다…
				CASE IS < 兵装開発_仕様決め
					PRINTFORML \@ GET_TARGETNUM() > 1 ? 모두 # 두 사람 \@이 낸 아이디어를 바탕으로, 구체적인 병장안을 확정했다…
				CASEELSE
					PRINTFORML %CALLNAME:TARGET%\@ GET_TARGETNUM() > 1 ? 들 # \@은(는), 진지한 표정으로 병장안을 설계도에 담고 있다…
			ENDSELECT
		CASE 2
			;完成
			PRINTFORML \@ GET_TARGETNUM() > 1 ? 모두 # 두 사람 \@의 힘으로, 새로운 병장 「%RSTR:コマンド結果受渡し文字列%」의 설계도가 완성되었다!
	ENDSELECT
ELSE
	PRINTFORML %CALLNAME:PLAYER%은(는) 새로운 병장 개발을 했다

	SELECTCASE RFLAG:コマンド結果受渡し変数
		CASE -1
			;うまくいかなかった
			IF 兵装開発_進捗 > 0
				PRINTL 하지만, 진척은 좋지 않았다
				PRINTL 조금 더 기계나 마법에 능통한 인물에게 도움을 받는 편이 좋을지도 모른다…
			ELSE
				PRINTL 하지만, 좋은 아이디어가 떠오르지 않았다
				PRINTL 재미있는 아이디어가 있을 법한 인물을 찾는 편이 좋을지도 모른다…
			ENDIF
		CASE 1
			;設計中
			SELECTCASE 兵装開発_進捗
				CASE IS < 兵装開発_案出し
					PRINTL 여러 가지 아이디어가 떠올랐다가 사라져 갔다…
				CASE IS < 兵装開発_仕様決め
					PRINTL 이것이다, 라는 아이디어를 바탕으로, 구체적인 병장안을 굳혀 나갔다…
				CASEELSE
					PRINTFORML %CALLNAME:PLAYER%은(는) 집중해서 병장안의 제도를 진행했다…
			ENDSELECT
		CASE 2
			;完成
			PRINTFORML 새로운 병장 「%RSTR:コマンド結果受渡し文字列%」의 설계도가 완성되었다!
	ENDSELECT
ENDIF

WAIT
