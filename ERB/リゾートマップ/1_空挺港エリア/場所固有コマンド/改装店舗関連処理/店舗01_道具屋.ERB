
@空艇港店舗名表示_1(店舗番号)
#FUNCTION
#DIM 店舗番号

TSTR:場所名受渡 = 도구점『%空艇港店舗屋号記録:店舗番号%』

@空艇港店舗解説文_1(建物文章受け渡し文字列, 店舗番号)
#DIMS REF 建物文章受け渡し文字列
#DIM 店舗番号

建物文章受け渡し文字列 = ■도구점『%空艇港店舗屋号記録:店舗番号%』<br><br>
建物文章受け渡し文字列 += "　생활용품이나 간단한 약 등, 편리한 도구를 파는 점포. <br>"
建物文章受け渡し文字列 += "　이익률은 낮지만, 이런 가게가 있다는 것은 관광객의 만족도로 이어질 것이다. <br>"
建物文章受け渡し文字列 += "　<br>"
建物文章受け渡し文字列 += "　시설 효과: 하루 종료 시 루피 수입 증가 (극소)<br>"
建物文章受け渡し文字列 += "　　　　　　인기도 상승량의 감쇠 라인 증가 (소)<br>"
建物文章受け渡し文字列 += "　　　　　　도구 구매가 가능해진다<br>"

RETURN 15000

@空艇港店舗効果_1(ARGS)

SELECTCASE ARGS
	CASE "一日終了時のルピ収入"
		LOCAL = 滞在キャラ数算出関数() * 20 * RAND(95, 106) / 100
		RETURN LOCAL
	CASE "人気度上昇量の減衰ライン"
		;キャラ上限0.5人分の上昇
		RETURN 1000
ENDSELECT

RETURN 0

@空艇港店舗用コマンド_COMNAME_1
#FUNCTION
TSTR:コマンド名受渡 = 쇼핑하다

@空艇港店舗用コマンド_COMTYPE_1
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("単独可能")

@空艇港店舗用コマンド_TOOLTIP_1
LOCALS = <br>■쇼핑하다<br>
LOCALS += "루피를 소비하여 도구점에서 쇼핑하는 커맨드."
LOCALS += "<br>리조트의 관리인이므로 원가로 구매할 수 있다. 상당히 이득."
LOCALS += "<br>COM 타입: 단독 가능<br>획득 소스: 없음"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@空艇港店舗用コマンド_COM_1
;店舗処理
#DIM 品番号

品番号 = 0
汎用アイテム購入用_序文 = ■道具屋<br>　ダンジョンで使うアイテムを購入することが出来ます。<br>　※開発中のため品揃え不足中
汎用アイテム購入用_品揃え登録:(品番号++) = 薬草
汎用アイテム購入用_品揃え登録:(品番号++) = 魔石
汎用アイテム購入用_品揃え登録:(品番号++) = 手投げ爆弾
汎用アイテム購入用_品揃え登録:(品番号++) = 胆力の薬
IF イベント完了フラグ確認("大拡張！空艇港") > 0
	汎用アイテム購入用_品揃え登録:(品番号++) = 蘇生薬
	汎用アイテム購入用_品揃え登録:(品番号++) = 解毒薬
	汎用アイテム購入用_品揃え登録:(品番号++) = ポーション
	汎用アイテム購入用_品揃え登録:(品番号++) = クリアソルト
	汎用アイテム購入用_品揃え登録:(品番号++) = マナポーション
	汎用アイテム購入用_品揃え登録:(品番号++) = ディスペルハーブ
ENDIF
SIF 鍛冶場作成許可フラグ
	汎用アイテム購入用_品揃え登録:(品番号++) = 火の魔石
SIF 海作成許可フラグ
	汎用アイテム購入用_品揃え登録:(品番号++) = 水の魔石
SIF 温泉作成許可フラグ
	汎用アイテム購入用_品揃え登録:(品番号++) = 土の魔石
SIF ダンジョンクリアフラグ_風遊ぶ渓谷の参道
	汎用アイテム購入用_品揃え登録:(品番号++) = 風の魔石

CALL 汎用アイテム購入画面処理(1)
RETURN RESULT

@空艇港店舗用コマンド_ABLE_1
;隠密中はダメ
SIF CFLAG:PLAYER:隠密
	RETURN 0
RETURN 1

@空艇港店舗用コマンド_MESSAGE_1
