
@MAP_VIEWING_1(AA)
#DIMS REF AA, 0
TRYCALLFORM MAP_RESORT_1_{TFLAG:マップ種別}(AA)


@エリア情報登録_1(ARGS)
SELECTCASE ARGS
	CASE "エリア名"
		IF ダンジョンクリアフラグ_沿岸部の森
			;沿岸部の森クリアで解放
			TSTR:場所名受渡 = 비행선 항구 구역
		ENDIF
	CASE "エリア移動可能場所"
		RETURN 1
ENDSELECT



@MAP_RESORT_1_0(AA)
#DIMS REF AA, 0
#DIM MAP

MAP = 0
IF イベント完了フラグ確認("大拡張！空艇港") < 1
	AA:00 = 木木木木木木木木木木木　　01　　木木木木木木木木木木木
	AA:01 = 木┌─────┐木木　　　　　　　木木┌─────┐木
	AA:02 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木
	AA:03 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木
	AA:04 = 木│　　03　　│　　　　　　　　　　　│　　06　　│木
	AA:05 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木
	AA:06 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木
	AA:07 = 木└─────┘　　　　　　　　　　　└─────┘木
	AA:08 = 木┌─────┐　　　　　　　　　　　┌─────┐木
	AA:09 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木
	AA:10 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木
	AA:11 = 木│　　04　　│　　　　　02　　　　　│　　07　　│木
	AA:12 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木
	AA:13 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木
	AA:14 = 木└─────┘　　　　　　　　　　　└─────┘木
	AA:15 = 木┌─────┐　　　　　　　　　　　┌─────┐木
	AA:16 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木
	AA:17 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木
	AA:18 = 木│　　05　　│　　　　　　　　　　　│　　08　　│木
	AA:19 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木
	AA:20 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木
	AA:21 = 木└─────┘　　　　　　　　　　　└─────┘木
	AA:22 = 木　　　　　　　　　　　　　　　　　　　　　　　　　木
	AA:23 = 木　　　　　　　　　　　　　　　　　　　　　　　　　木
	AA:24 = 木　　　　　　　　　　　　　　　　　　　　　　　　　木
	AA:25 = 木　　　　　　　　　　　　09　　　　　　　　　　　　木
	AA:26 = ┗━━━━━━━━━━┓━━━┏━━━━━━━━━━┛
	AA:27 = 　　　　　　　　　　　┃━━━┃
	AA:28 = 　　　　　　　　　　　┃━━━┃
	AA:29 = 　　　　　　　　　　　┃━━━┃
	AA:30 = 　　　　　　　　　　　┗━━━┛
	AA:31 = 
	AA:32 = 01：他エリアへ　　　02：港前広場
	AA:33 = 03：三04：四05：五
	AA:34 = 06：六07：七08：八
	AA:35 = 09：空艇港
	IF イベント完了フラグ確認("渡りに船の定期便") > 0 && 渡りに船の定期便_受諾日付 + 3 > DAY
		AA:35 += @"　(현재 공사 중! 남은%TOFULL(TOSTR(3 - (DAY - 渡りに船の定期便_受諾日付)))%일)"
	ENDIF
ELSEIF 港改造度:11:0 == 0
	AA:00 = 木木木木木木木木木木木　　01　　木木木木木木木木木木木┌───────┐木木木木木木┃
	AA:01 = 木┌─────┐木木　　　　　　　木木┌─────┐木│　　　　　　　│　　　　　　┗━━━━━━━━━━━
	AA:02 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木│　　　　　　　│　　　　　　　┃┃┃┃┃┃┃┃┃┃┃
	AA:03 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木│　　　　　　　│　　　　　　　┃┃┃┃┃┃┃┃┃┃┃
	AA:04 = 木│　　03　　│　　　　　　　　　　　│　　06　　│木│　　　　　　　│　　　　　　　┃┃┃┃┃┃┃┃┃┃┃
	AA:05 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木│　　　　　　　│　　　　　　┏━━━━━━━━━━━
	AA:06 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木│　　　　　　　│　　　　　　┃
	AA:07 = 木└─────┘　　　　　　　　　　　└─────┘木│　　　　　　　│　　　　　　┃
	AA:08 = 木┌─────┐　　　　　　　　　　　┌─────┐木│　　　　　　　│　　　　　　┃
	AA:09 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木│　　　10　　　│　　　　　　┃
	AA:10 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木│　　　　　　　│　　　　　　┗━━━━━━━━━━━
	AA:11 = 木│　　04　　│　　　　　02　　　　　│　　07　　│木│　　　　　　　│　　　　　　　┃┃┃┃┃┃┃┃┃┃┃
	AA:12 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木│　　　　　　　│　　　　　　　┃┃┃┃┃┃┃┃┃┃┃
	AA:13 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木│　　　　　　　└┐　　　　　　┃┃┃┃┃┃┃┃┃┃┃
	AA:14 = 木└─────┘　　　　┌─┐　　　　└─────┘木│　　　　　　　　│　　　　　┏━━━━━━━━━━━
	AA:15 = 木┌─────┐　　　┌┘≡└┐　　　┌─────┐木│　　　　　　　　│　　　　　┃
	AA:16 = 木│　　　　　│　　　│≡≡≡│　　　│　　　　　│木│　　　　　　　　│　　　　　┃
	AA:17 = 木│　　　　　│　　　└┐≡┌┘　　　│　　　　　│木│　　　　　　　┌┘　　　　　┃
	AA:18 = 木│　　05　　│　　　　└─┘　　　　│　　08　　│木└───────┘　　　　　　┃
	AA:19 = 木│　　　　　│　　　　　　　　　　　│　　　　　│木　　　　　　　　　　　　　　　┗━━━━━━━━━━━
	AA:20 = 木│　　　　　│　　　　　　　　　　　│　　　　　│　　　　　　　　　　　　　　　　　┃┃┃┃┃┃┃┃┃┃┃
	AA:21 = 木└─────┘　　　　　　　　　　　└─────┘　　　　　　　　　　　　　　　　　┃┃┃┃┃┃┃┃┃┃┃
	AA:22 = 木　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃┃┃┃┃┃┃┃┃┃┃
	AA:23 = 木　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┏━━━━━━━━━━━
	AA:24 = 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
	IF 港改造度:0:0
		AA:25 = ←　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　09　　　　　　┃
		AA:26 = ←　商業区　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┗━┓
		AA:27 = ←　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
	ELSE
		AA:25 = 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　09　　　　　　┃
		AA:26 = 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┗━┓
		AA:27 = 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
	ENDIF
	AA:28 = 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
	AA:29 = 　・　・　・　・　・　・　・　・　・　・　・　・　・　・　・　・　・　・　・　・　・　・┃
	AA:30 = ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛
	AA:31 = 
	AA:32 = 01：他エリアへ　　　02：港前広場
	AA:33 = 03：三04：四05：五
	AA:34 = 06：六07：七08：八
	AA:35 = 09：空艇港　　　　　10：よろず屋支店
ELSE
	AA:(MAP++) = 木木木木木木木木木木木　　01　　木木木木木木木木木木木木木木木木木木　　　　　木木木┃
	AA:(MAP++) = 木木木木木木木木木木木　　　　　木木┌───────────────　　　　　─┐木┃
	AA:(MAP++) = 木木木木木木木木木木木　　　　　木木│　　　　　　　　　　　　　　　　　　　　　│木┃
	AA:(MAP++) = 木木木木木木木木木木木　　　　　木木│　　　　　　　　　　　　　　　　　　　　　│木┃
	AA:(MAP++) = 木木木木木木木木木木木　　　　　　　　　　　　　　　　　　　　　　　　　　　　　│木┃
	IF 港改造度:0:0
		AA:(MAP++) = ←　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　│木┃
		AA:(MAP++) = ←　商業区　　　　　　　　　　　　　　　　　　　　　　　　11　　　　　　　　　　│木┃
		AA:(MAP++) = ←　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　│木┃
	ELSE
		AA:(MAP++) = 木木木木木木木木木木木　　　　　　　　　　　　　　　　　　　　　　　　　　　　　│木┃
		AA:(MAP++) = 木木木木木木木木木木木　　　　　　　　　　　　　　　　　　11　　　　　　　　　　│木┃
		AA:(MAP++) = 木木木木木木木木木木木　　　　　　　　　　　　　　　　　　　　　　　　　　　　　│木┃
	ENDIF
	AA:(MAP++) = 木木木木木木木木木木木　　　　　　　　　　　　　　　　　　　　　　　　　　　　　│木┃
	AA:(MAP++) = 木木木木木木木木木木木　　　　　木木│　　　　　　　　　　　　　　　　　　　　　│木┃
	AA:(MAP++) = 木木木木木木木木木木木　　　　　木木│　　　　　　　　　　　　　　　　　　　　　│木┃
	AA:(MAP++) = 木木木木木木木木木木木　　　　　木木└─────────────────────┘木┃
	AA:(MAP++) = 木木木木木木木木木木木　　　　　木木木木木木木木木木木┌───────┐木木木木木木┃
	AA:(MAP++) = 木┌─────┐木木　　　　　　　木木┌─────┐木│　　　　　　　│　　　　　　┗━━━━━━━━━━━
	AA:(MAP++) = 木│　　　　　│　　　　　　　　　　　│　　　　　│木│　　　　　　　│　　　　　　　┃┃┃┃┃┃┃┃┃┃┃
	AA:(MAP++) = 木│　　　　　│　　　　　　　　　　　│　　　　　│木│　　　　　　　│　　　　　　　┃┃┃┃┃┃┃┃┃┃┃
	AA:(MAP++) = 木│　　03　　│　　　　　　　　　　　│　　06　　│木│　　　　　　　│　　　　　　　┃┃┃┃┃┃┃┃┃┃┃
	AA:(MAP++) = 木│　　　　　│　　　　　　　　　　　│　　　　　│木│　　　　　　　│　　　　　　┏━━━━━━━━━━━
	AA:(MAP++) = 木│　　　　　│　　　　　　　　　　　│　　　　　│木│　　　　　　　│　　　　　　┃
	AA:(MAP++) = 木└─────┘　　　　　　　　　　　└─────┘木│　　　　　　　│　　　　　　┃
	AA:(MAP++) = 木┌─────┐　　　　　　　　　　　┌─────┐木│　　　　　　　│　　　　　　┃
	AA:(MAP++) = 木│　　　　　│　　　　　　　　　　　│　　　　　│木│　　　10　　　│　　　　　　┃
	AA:(MAP++) = 木│　　　　　│　　　　　　　　　　　│　　　　　│木│　　　　　　　│　　　　　　┗━━━━━━━━━━━
	AA:(MAP++) = 木│　　04　　│　　　　　02　　　　　│　　07　　│木│　　　　　　　│　　　　　　　┃┃┃┃┃┃┃┃┃┃┃
	AA:(MAP++) = 木│　　　　　│　　　　　　　　　　　│　　　　　│木│　　　　　　　│　　　　　　　┃┃┃┃┃┃┃┃┃┃┃
	AA:(MAP++) = 木│　　　　　│　　　　　　　　　　　│　　　　　│木│　　　　　　　└┐　　　　　　┃┃┃┃┃┃┃┃┃┃┃
	AA:(MAP++) = 木└─────┘　　　　┌─┐　　　　└─────┘木│　　　　　　　　│　　　　　┏━━━━━━━━━━━
	AA:(MAP++) = 木┌─────┐　　　┌┘≡└┐　　　┌─────┐木│　　　　　　　　│　　　　　┃
	AA:(MAP++) = 木│　　　　　│　　　│≡≡≡│　　　│　　　　　│木│　　　　　　　　│　　　　　┃
	AA:(MAP++) = 木│　　　　　│　　　└┐≡┌┘　　　│　　　　　│木│　　　　　　　┌┘　　　　　┃
	AA:(MAP++) = 木│　　05　　│　　　　└─┘　　　　│　　08　　│木└───────┘　　　　　　┃
	AA:(MAP++) = 木│　　　　　│　　　　　　　　　　　│　　　　　│木　　　　　　　　　　　　　　　┗━━━━━━━━━━━
	AA:(MAP++) = 木│　　　　　│　　　　　　　　　　　│　　　　　│　　　　　　　　　　　　　　　　　┃┃┃┃┃┃┃┃┃┃┃
	AA:(MAP++) = 木└─────┘　　　　　　　　　　　└─────┘　　　　　　　　　　　　　　　　　┃┃┃┃┃┃┃┃┃┃┃
	AA:(MAP++) = 木　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃┃┃┃┃┃┃┃┃┃┃
	AA:(MAP++) = 木　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┏━━━━━━━━━━━
	AA:(MAP++) = 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
	IF 港改造度:0:0
		AA:(MAP++) = ←　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　09　　　　　　┃
		AA:(MAP++) = ←　商業区　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┗━┓
		AA:(MAP++) = ←　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
	ELSE
		AA:(MAP++) = 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　09　　　　　　┃
		AA:(MAP++) = 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┗━┓
		AA:(MAP++) = 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
	ENDIF
	AA:(MAP++) = 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　┃
	AA:(MAP++) = 　・　・　・　・　・　・　・　・　・　・　・　・　・　・　・　・　・　・　・　・　・　・┃
	AA:(MAP++) = ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛
	AA:(MAP++) = 
	AA:(MAP++) = 01：他エリアへ　　　02：港前広場
	AA:(MAP++) = 03：三04：四05：五
	AA:(MAP++) = 06：六07：七08：八
	AA:(MAP++) = 09：空艇港　　　　　10：よろず屋支店　　11：鍛冶施設
ENDIF



@GETPLACENAME_1_0(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 1
		TSTR:場所名受渡 = 항구 구역 입구
	CASE 2
		TSTR:場所名受渡 = 비행선 항구 앞 광장
	CASE 3
		CALLFORMF 空艇港店舗名表示_{空艇港店舗状況:3:0}(3)
	CASE 4
		CALLFORMF 空艇港店舗名表示_{空艇港店舗状況:4:0}(4)
	CASE 5
		CALLFORMF 空艇港店舗名表示_{空艇港店舗状況:5:0}(5)
	CASE 6
		CALLFORMF 空艇港店舗名表示_{空艇港店舗状況:6:0}(6)
	CASE 7
		CALLFORMF 空艇港店舗名表示_{空艇港店舗状況:7:0}(7)
	CASE 8
		CALLFORMF 空艇港店舗名表示_{空艇港店舗状況:8:0}(8)
	CASE 9
		TSTR:場所名受渡 = 비행선 항구
	CASE 10
		TSTR:場所名受渡 = 만물상 지점
	CASE 11
		TSTR:場所名受渡 = 대장간 시설
ENDSELECT

@GETPLACENAME_1_10(ARG)
#FUNCTION
#DIMS 配列分解文字列, 2

IF 商業区店割り配列:(ARG - 1000):店員配列 == ""
	TSTR:場所名受渡 = 상업 지구{ARG}번지: 빈 점포
	RETURNF 
ELSE
	VARSET 配列分解文字列
	SPLIT 商業区店割り配列:(ARG - 1000):店員配列, "_", 配列分解文字列
	IF TOINT(配列分解文字列:0) > 0
		LOCALS = %CALLNAME:(TOINT(配列分解文字列:0))%
	ELSE
		SELECTCASE TOINT(配列分解文字列:0)
			CASE -1
				LOCALS = 모브 아저씨
			CASE -2
				LOCALS = 모브 아주머니
			CASE -3
				LOCALS = 모브 청년
			CASE -4
				LOCALS = 모브 여성
		ENDSELECT
	ENDIF
ENDIF
TSTR:場所名受渡 = 상업 지구{ARG}번지: %LOCALS%의 %商業区店割り配列:(ARG - 1000):店舗名%


@DRAW_MAP_BG_1_0(SLICED, 表示文字列)
#DIMS REF 表示文字列
#DIMS SLICED
SELECTCASE SLICED
	CASE "三"
		CALLFORMF 空艇港店舗名表示_{空艇港店舗状況:3:0}(3)
		表示文字列 += @"%TSTR:場所名受渡, 16, LEFT%"
	CASE "四"
		CALLFORMF 空艇港店舗名表示_{空艇港店舗状況:4:0}(4)
		表示文字列 += @"%TSTR:場所名受渡, 16, LEFT%"
	CASE "五"
		CALLFORMF 空艇港店舗名表示_{空艇港店舗状況:5:0}(5)
		表示文字列 += @"%TSTR:場所名受渡, 16, LEFT%"
	CASE "六"
		CALLFORMF 空艇港店舗名表示_{空艇港店舗状況:6:0}(6)
		表示文字列 += @"%TSTR:場所名受渡, 16, LEFT%"
	CASE "七"
		CALLFORMF 空艇港店舗名表示_{空艇港店舗状況:7:0}(7)
		表示文字列 += @"%TSTR:場所名受渡, 16, LEFT%"
	CASE "八"
		CALLFORMF 空艇港店舗名表示_{空艇港店舗状況:8:0}(8)
		表示文字列 += @"%TSTR:場所名受渡, 16, LEFT%"
	CASE "木"
		表示文字列 += @"<font color='#90EE90'>%SLICED%</font>"
	CASE "┏","━","┛","┗","┓","┃"
		表示文字列 += @"<font color='#734e30'>%SLICED%</font>"
	CASE "≡"
		表示文字列 += @"<font color='#0067C0'>%SLICED%</font>"
	CASE "商","業","区"
		IF CFLAG:PLAYER:現在位置 >= 1000
			表示文字列 += @"<font color='#%カラーパレット_HTML("真っ赤")%'>%SLICED%</font>"
		ELSE
			表示文字列 += @"%SLICED%"
		ENDIF
	;ふつうの
	CASEELSE
		表示文字列 += @"%SLICED%"
ENDSELECT





@エリア来訪時イベント_1

IF 空艇港初来訪イベントフラグ == 0
	DRAWLINE
	PRINTL [첫 방문: 비행선 항구 구역]
	DRAWLINE
	PRINTW
	PRINTFORMW 스톤 이터를 토벌한 %CALLNAME:PLAYER%는, 즉시 비행선 항구로 가서 업자 수용 준비를 마쳤다.
	PRINTFORMW 이것으로 리조트 하우스의 증축이나 개조를 할 수 있을 것이다.
	PRINTFORMW 캠프장이나 유희실, 풀의 대형화 등……리조트가 넓어지면, 혼자서는 손이 닿지 않을지도 모른다.
	PRINTFORMW 종업원을 새로 고용하여, 몇 가지 일을 맡기는 것도 생각해 두자.
	PRINTL 
	PRINTFORMW 게다가, 이 공정항 에리어 부근의 토지를 놀려두는 것은 아깝다.
	PRINTFORM 빈 집을 개조하여
	SETCOLOR カラーパレット("真っ赤")
	PRINT 다양한 점포를 유치할 수 있다
	RESETCOLOR
	PRINTW 지도 모른다.
	PRINTFORMW 도구점이나 기념품 가게, 식당 등이 후보가 될까.
	PRINTL 
	PRINTFORMW 점포가 늘어나면 루피의 수입도 늘어난다. 또한, ZP도 회수할 수 있을지도 모른다.
	PRINTFORMW 빈 집인 채로는 아무것도 생산하지 않는다. 어차피라면, 가능한 한 어떤 점포로든 채워두자.
	空艇港初来訪イベントフラグ = 1
ENDIF


@空艇港改造メニュー()
#FUNCTIONS
;100~199と999は大本のメニュー操作で使うため、使用しないこと

LOCALS = 공정항을 발전시켜, 시설을 증가·강화합니다<br><br>
IF 港改造度:11:0 < 1
	LOCALS += @"■<button value='200'>[대장간 시설 생성]</button> Lv{港改造度:11:0}>{港改造度:11:0 + 1} （필요:100,000루피）<br>"
ELSE
	LOCALS += @"■[대장간 시설 생성] Lv{港改造度:11:0}：상한<br>"
ENDIF
IF イベント完了フラグ確認("商業区建設解禁") > 0
	IF 港改造度:0:0 < 1
		LOCALS += @"■<button value='210'>[상업 지구 건설]</button> Lv{港改造度:0:0}>{港改造度:0:0 + 1} （필요:3,500,000루피）<br>"
	ELSE
		LOCALS += @"■[상업 지구 건설] Lv{港改造度:0:0}：상한<br>"
	ENDIF
ENDIF

RETURNF LOCALS

@空艇港改造実行関数(ARG)

SELECTCASE ARG
	CASE 200
		DRAWLINE
		PRINTL 대장간 시설을 만들고, 탑승기·탑재 병기 제작이 가능해집니다.
		PRINTBUTTON "■탑승기에 대하여", 300
		PRINTL 
		PRINTBUTTON "■탑재 병기에 대하여", 301
		PRINTL 
		PRINTL 
		PRINTL 대장간 시설을 만드시겠습니까? (필요:100,000루피)
		PRINTBUTTONLC "[0] 예", 0
		PRINTBUTTONLC "[1] 아니오", 1
		BINPUT
		SELECTCASE RESULT
			CASE 0
				IF MONEY < 100000
					PRINTW 루피가 부족합니다.
					RETURN
				ENDIF
				港改造度:11:0 = 1
				MONEY -= 100000
				CALL RESET_GRAPH_1
				CALL 乗機取得処理_設計図("バトルケッタギア")
				CALL 乗機取得処理_設計図("サイドカー付ケッタギア")
				CALL 乗機取得処理_設計図("汎用型走艇")
				CALL 乗機取得処理_設計図("バトルトラクター")
				CALL 汎用搭載兵器取得処理_設計図("アースジュエル")
				CALL 汎用搭載兵器取得処理_設計図("ウインドジュエル")
				CALL 汎用搭載兵器取得処理_設計図("ウォータジュエル")
				CALL 汎用搭載兵器取得処理_設計図("ファイアジュエル")
				CALL 汎用搭載兵器取得処理_設計図("レーダーパーツ")
				CALL 汎用搭載兵器取得処理_設計図("ヒールパーツ")
				CALL 汎用搭載兵器取得処理_設計図("ダウジングパーツ")
				CALL 汎用搭載兵器取得処理_設計図("近接用スピア")
				CALL 汎用搭載兵器取得処理_設計図("9mm機関銃")
				CALL 汎用搭載兵器取得処理_設計図("20mm擲弾砲")
				PRINTL 대장간 시설을 만들었습니다!
				PRINTL 초기 소지 설계도를 여러 개 입수했습니다.
				PRINTW (공정항 맵에 출현)
				RETURN
			CASE 1
				RETURN
			CASE 300
				DRAWLINE
				PRINTL ■탑승기에 대하여
				PRINTL 던전 탐색 중, 당신이 전투에 참가하기 위한 기체입니다.
				PRINTL 고유의 무장, 혹은 탑재 병기를 장비할 수 있는 칸을 가지며, 전투 중에 행동을 할 수 있습니다.
				PRINTL 기체마다 공격력·명중률이 설정되어 있으며, 대미지량 등은 그 수치에 따라 산출됩니다.
				PRINTL
				PRINTL 무장이나 병기는 각각, 「전투 중 ◯회」「탐색 중 ◯회」「쿨다운 ◯턴 발생」의 제한을 가집니다.
				PRINTL 일반적으로, 탐색 중 제한의 무장이 가장 강하고, 전투 중 제한의 무장이 가장 약하게 설정되는 경우가 많습니다.
				PRINTW
			CASE 301
				DRAWLINE
				PRINTL ■탑재 병기에 대해
				PRINTL 탑승기의 장비 가능한 칸에 장비할 수 있는 병기입니다.
				PRINTL 병기 각각에 카테고리(기계 계열·마법 계열 등)와 거리 적성(근거리·원거리 등)이 설정되어 있으며,
				PRINTL 조건이 일치하는 칸에만 장비할 수 있습니다.
				PRINTL 
				PRINTL 병기에는 공격 보정·공격 변동·명중 보정·명중 변동·발동 기술이 설정되어 있습니다.
				PRINTL 공격 보정·명중 보정은 「병기를 사용했을 때 일시적으로 합산되는 수치」이며
				PRINTL 공격 변동·명중 변동은 「병기를 장비하고 있는 것만으로 항상 탑승기에 영향을 주는 수치」입니다.
				PRINTL 또한, 발동 기술은 어빌리티와 마찬가지로 다양한 효과를 가집니다.
				PRINTL
				PRINTL 무장이나 병기는 각각, 「전투 중 ◯회」「탐색 중 ◯회」「쿨다운 ◯턴 발생」의 제한을 가집니다.
				PRINTL 일반적으로, 탐색 중 제한의 병기가 가장 강하고, 전투 중 제한의 병기가 가장 약하게 설정되는 경우가 많습니다.
				PRINTW
		ENDSELECT
		RESTART
	CASE 210
		DRAWLINE
		PRINTL 대량의 점포를 건설하고, 상업 지구로 개방합니다.
		PRINTBUTTON "■점포에 대해", 300
		PRINTL 
		PRINTBUTTON "■점포를 늘리는 방법에 대해", 301
		PRINTL 
		PRINTL 
		PRINTL 상업 지구를 건설하시겠습니까? (필요:3,500,000루피)
		PRINTBUTTONLC "[0] 예", 0
		PRINTBUTTONLC "[1] 아니오", 1
		BINPUT
		SELECTCASE RESULT
			CASE 0
				IF MONEY < 3500000
					PRINTW 루피가 부족합니다.
					RETURN
				ENDIF
				港改造度:0:0 = 1
				MONEY -= 3500000
				PRINTL 상업 지구를 건설했습니다!
				PRINTW (비행선 항구 맵에서 이동 가능)
				RETURN
			CASE 1
				RETURN
			CASE 300
				DRAWLINE
				PRINTL ■점포에 대하여
				PRINTL 상업 지구를 건설하면, 정주 캐릭터나 몹이 점포를 가질 수 있습니다.
				PRINTL 점포는 존재하는 것만으로 리조트의 인기 상한을 상승시킵니다.
				PRINTL 또한, 구상 측에서 설정함으로써 특별한 점포가 발생할 수 있습니다. (미구현)
				PRINTL 특별한 점포에는, 인기 상한 이외의 효과가 있거나 특별한 명령어가 준비되어 있을 수 있습니다. (미구현)
				PRINTW
			CASE 301
				DRAWLINE
				PRINTL ■점포를 늘리는 방법에 대하여
				PRINTL 몹의 점포는 하루 시작 시 자동으로 늘어납니다.
				PRINTL 정주 캐릭터는 특정 지식 레벨이 2 이상일 때, 범용 점포를 자동으로 가질 수 있습니다.
				PRINTL 또한, 점포 칸이 가득 찼을 때 정주 캐릭터가 가게를 가질 때, 몹 점포가 무작위로 덮어쓰기됩니다.
				PRINTL 정주 캐릭터의 점포는 덮어쓰기되지 않습니다.
				PRINTL 정주 캐릭터의 점포만으로 점포 칸이 가득 찼을 때는 점포가 늘어나지 않습니다.
				PRINTL 
				PRINTL 구상 측에서 설정된 특수 점포는, 각각에 건설 조건이 설정되어 있을 수 있습니다. (미구현)
				PRINTL 달성했을 때 자동으로 건설되는 것과, 범용 점포와 마찬가지로 하루 시작 시에 건설 후보에 들어가는 것이 있습니다. (미구현)
				PRINTW
		ENDSELECT
		RESTART
ENDSELECT







