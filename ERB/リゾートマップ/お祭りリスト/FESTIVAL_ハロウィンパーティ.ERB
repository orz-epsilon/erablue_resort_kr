@MOVE_ROUTINE_FESTIVAL_ハロウィンパーティ(対象キャラ, ARGS)
#DIM 対象キャラ
#DIM 時間計算
#DIM 候補数
#DIMS 候補配列, 500
#DIM CONST 開催費用 = 10000

SELECTCASE ARGS
	CASE "お祭り説明"
		LOCALS = <div rect='2125,500,3762,2812'>
		LOCALS += "■할로윈 파티<br>"
		LOCALS += "<br>"
		LOCALS += "리조트 전체를 할로윈 분위기로 띄웁니다.<br>"
		LOCALS += "축제 전용 커맨드 「과자를 주다」「장난을 당하다」를 실행할 수 있게 됩니다.<br>"
		LOCALS += "같은 캐릭터에게는 2시간에 1회, 둘 중 하나만 실행 가능합니다.<br>"
		LOCALS += "<br>"
		LOCALS += "과자를 주다: 호감도 요소: 우정이 상승합니다<br>"
		LOCALS += "장난을 당하다: 호감도 요소: 우정이 상승합니다. 성적인 장난이 될 가능성이 있습니다.<br>"
		LOCALS += "대사로 설정된 캐릭터가 할로윈 의상을 입을 수 있습니다.<br>"
		LOCALS += "<br>"
		IF 10000 > MONEY
			LOCALS += @"루피가 부족합니다. (필요 루피：%NUM_FORMAT(開催費用)%）<br>"
		ELSE
			LOCALS += @"<button value='0'>[0] 이 축제를 기획한다 (필요 루피：%NUM_FORMAT(開催費用)%）</button><br>"
		ENDIF
		LOCALS += "</div>"
		HTML_PRINT LOCALS, 1
	CASE "出現条件"
		;秋の月限定
		SIF DAY % 120 / 30 != 2
			RETURN -1
		RETURN 1
	CASE "参加判定"
		;ルーチンは変動させないので0を返す
		RETURN 0
	CASE "特設マップ"
		;存在しない
		RETURN 0
	CASE "起床時刻"
		;ルーチンは変動しないので必要なし
	CASE "就寝時刻"
		;ルーチンは変動しないので必要なし
	CASE "就寝場所"
		;ルーチンは変動しないので必要なし
	CASE "初期位置"
		;ルーチンは変動しないので必要なし
	CASE "お祭り補正条件"
		;条件なし
		RETURN 1
	CASE "お祭り補正内容"
		;コマンドのみなのでなし
	CASE "服装変更"
		;口上設定があればそれ
		TRYCCALLFORM KOJO_CLOTHES_FESTIVAL_ハロウィンパーティ_{NO:対象キャラ}(対象キャラ)
		CATCH
			;ない場合はハロウィン属性を持った衣装の画像を見る
			VARSET 候補配列
			LOCAL:1 = 0
			候補数 = ENUMFUNCBEGINSWITH("CLOTHES_衣装属性_")
			FOR LOCAL, 0, 候補数
				TSTR:衣装属性受け渡し = 
				TRYCALLFORM %RESULTS:LOCAL%
				SIF STRFIND(TSTR:衣装属性受け渡し, "ハロウィン") < 0
					CONTINUE
				SIF !EXIST画像FILE("resources/" + CSTR:対象キャラ:画像フォルダ + "/短冊_" + REPLACE(RESULTS:LOCAL, "CLOTHES_衣装属性_", ""))
					CONTINUE
				RESULT = 1
				TRYCALLFORM CLOTHES_着用条件_%REPLACE(RESULTS:LOCAL, "CLOTHES_衣装属性_", "")%(対象キャラ)
				SIF RESULT == 0
					CONTINUE
				候補配列:(LOCAL:1) '= REPLACE(RESULTS:LOCAL, "CLOTHES_衣装属性_", "")
				LOCAL:1 ++
			NEXT
			IF LOCAL:1
				RSTR:服装名受け渡し '= 候補配列:(RAND:(LOCAL:1))
			ENDIF
		ENDCATCH
ENDSELECT


;-------------------------------------------------
;お菓子をあげる
;-------------------------------------------------
@COMNAME_PLACE_385_FESTIVAL_ハロウィンパーティ
#FUNCTION
TSTR:コマンド名受渡 = 과자를 주다
RETURNF 1

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_385_FESTIVAL_ハロウィンパーティ
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_385_FESTIVAL_ハロウィンパーティ
LOCALS = <br>■과자를 주다<br>
LOCALS += "TARGET에게 과자를 주는 커맨드.<br>트릭 오어 트릿!"
LOCALS += "<br>COM 타입: 일상<br>획득 소스: 환락"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM385_FESTIVAL_ハロウィンパーティ

CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 800)
CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 500)
TCVAR:TARGET:ハロウィンコマンド時刻 = TIME
TIME += 5
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE385_FESTIVAL_ハロウィンパーティ
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(385)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;隠密が一致してないとダメ
SIF CFLAG:PLAYER:隠密 != CFLAG:TARGET:隠密
	RETURN 0
;２時間に１回
SIF TCVAR:TARGET:ハロウィンコマンド時刻 + 120 > TIME
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM385_FESTIVAL_ハロウィンパーティ

PRINTFORML %CALLNAME:PLAYER%은(는) %CALLNAME:TARGET%에게 할로윈 과자를 주었다

@MESSAGE_COM385_FESTIVAL_ハロウィンパーティ_非口上時
SELECTCASE RAND:3
	CASE 2
		PRINTFORML %CALLNAME:TARGET%은(는) 장난을 치지 못했다고 농담하고 있다
	CASE 1
		PRINTFORML %CALLNAME:TARGET%은(는) 답례로 과자를 %CALLNAME:PLAYER%에게 건네주었다
	CASE 0
		PRINTFORML %CALLNAME:TARGET%은(는) 기쁜 듯이 고맙다고 말해주었다
ENDSELECT


;-------------------------------------------------
;いたずらされる
;-------------------------------------------------
@COMNAME_PLACE_386_FESTIVAL_ハロウィンパーティ
#FUNCTION
TSTR:コマンド名受渡 = 장난을 당하다
RETURNF 1

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_386_FESTIVAL_ハロウィンパーティ
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_386_FESTIVAL_ハロウィンパーティ
LOCALS = <br>■장난을 당하다<br>
LOCALS += "TARGET에게 장난을 당하는 커맨드. <br>상대가 함락 완료 & 우후후 경험 완료인 경우, 1/2 확률로 성적인 장난이 된다. <br>트릭 오어 트릿!"
LOCALS += "<br>COM 타입: 일상<br>획득 소스: 환락・정복"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM386_FESTIVAL_ハロウィンパーティ
#DIM セクハラ部位
#DIM セクハラ快楽量
セクハラ快楽量 = 0

CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 700)
CALL SOURCE_CALC_征服(TARGET, PLAYER, 200)
CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 400)

TCVAR:TARGET:ハロウィンコマンド時刻 = TIME
TIME += 5

IF 陥落チェック(TARGET) && 初体験日取得("初うふふ", TARGET, MASTER) && RAND:2
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
	セクハラ部位 = WEIGHTED_RANDOM_PART(PLAYER)
	SIF セクハラ部位 == -1
		RETURN 1

	;快楽量決定、基本値から諸々補正
	セクハラ快楽量 = RAND:100 + 100

	SELECTCASE セクハラ部位
		CASE 0
			CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, セクハラ快楽量)
		CASE 1
			CALL SOURCE_CALC_快Ｖ(PLAYER, TARGET, セクハラ快楽量)
		CASE 2
			CALL SOURCE_CALC_快Ａ(PLAYER, TARGET, セクハラ快楽量)
		CASE 3
			CALL SOURCE_CALC_快Ｂ(PLAYER, TARGET, セクハラ快楽量)
	ENDSELECT

	;口上用に数値受け渡し
	RFLAG:コマンド結果受渡し変数 = セクハラ部位
	RFLAG:コマンド結果受渡し変数２ = セクハラ快楽量
	;非うふふ時射精口上用に保存
	AUTOCOM_実行番号保存 = TARGET
ENDIF

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE386_FESTIVAL_ハロウィンパーティ
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(386)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;隠密が一致してないとダメ
SIF CFLAG:PLAYER:隠密 != CFLAG:TARGET:隠密
	RETURN 0
;２時間に１回
SIF TCVAR:TARGET:ハロウィンコマンド時刻 + 120 > TIME
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM386_FESTIVAL_ハロウィンパーティ

PRINTFORML %CALLNAME:PLAYER%은(는) %CALLNAME:TARGET%에게 장난을 당했다
IF RFLAG:コマンド結果受渡し変数２ > 0
	PRINTFORM %CALLNAME:TARGET%은(는) %CALLNAME:PLAYER%의
	SELECTCASE RFLAG:コマンド結果受渡し変数
		CASE 0
			IF GETBIT(TALENT:PLAYER:性別, 1)
				PRINT 음경에
			ELSE
				PRINT 클리토리스에
			ENDIF
		CASE 1
			PRINT 사타구니에
		CASE 2
			PRINT 엉덩이에
		CASE 3
			PRINT 가슴에
	ENDSELECT
	PRINTL 부드럽게 손을 훑었다…
ENDIF

@MESSAGE_COM386_FESTIVAL_ハロウィンパーティ_非口上時

IF RFLAG:コマンド結果受渡し変数２ < 1
	SELECTCASE RAND:3
		CASE 2
			PRINTFORML %CALLNAME:TARGET%은(는) %CALLNAME:PLAYER%을(를) 간지럽혔다!
		CASE 1
			PRINTFORML %CALLNAME:TARGET%은(는) %CALLNAME:PLAYER%의 손에 커다란 개구리를 놓았다! ……라고 생각했는데, 장난감이었다.
		CASE 0
			PRINTFORML %CALLNAME:TARGET%은(는) 어딘가에서 크래커를 꺼내, 펑! 하고 큰 소리를 울렸다.
	ENDSELECT
ENDIF
