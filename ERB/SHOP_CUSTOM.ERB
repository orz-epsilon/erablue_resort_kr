@CUSTOM_CHARAMAKE(ARG)
#DIM スタイル記録
;NOは適当に設定
NO:ARG = 0

;種族(1=ヒューマン 2=エルーン 3=ドラフ 4=ハーヴィン 5=星晶獣 6=その他)
TALENT:ARG:種族 = 1
;性別(1=女性 2= 男性 3=ふたなり)
TALENT:ARG:性別 = 1
SIF ARG == 0
	TALENT:ARG:性別 = 2
;年齢層
TALENT:ARG:年齢層 = 年齢層_大人
;体力
MAXBASE:ARG:体力 = 2000
BASE:ARG:体力 = MAXBASE:ARG:体力

DRAWLINE
PRINTL 스타일을 선택해 주세요.
DRAWLINE
PRINTBUTTON "[0] 일반 스타일", 0
PRINTL
PRINTL 　　…당신의 설정을 자유롭게 결정하는 스타일.
PRINTL
PRINTBUTTON "[1] 일반 스타일_스타트 대시 모드", 1
PRINTL
PRINTL 　　…당신의 설정을 자유롭게 결정하는 스타일. 초기 특전 포함
PRINTL
PRINTBUTTON "[10] 그랑 스타일", 10
PRINTL
PRINTL 　　…당신을 그랑으로 설정하는 스타일. 개발 중이므로 예상치 못한 동작을 할 수 있습니다.
PRINTL
PRINTBUTTON "[11] 지타 스타일", 11
PRINTL
PRINTL 　　…당신을 지타로 설정하는 스타일. 개발 중이므로 예상치 못한 동작을 할 수 있습니다.
PRINTL
PRINTBUTTON "[20] 임의 캐릭터 스타일", 20
PRINTL
PRINTL 　　…당신을 그 외의 캐릭터로 설정하는 스타일. 개발 중이므로 예상치 못한 동작을 할 수 있습니다.
PRINTL
BINPUT
スタイル記録 = RESULT
SELECTCASE スタイル記録
	CASE 0
		PRINTL ■일반 스타일
		PRINTL 　당신의 설정을 자유롭게 결정하는 스타일.
		PRINTL 　기본적으로 이 배리언트는 이 스타일을 상정하고 작성되었습니다.
	CASE 1
		PRINTL ■일반 스타일
		PRINTL 　당신의 설정을 자유롭게 결정하는 스타일.
		PRINTL 　기본적으로 이 배리언트는 이 스타일을 상정하고 작성되었습니다.
		PRINTL
		PRINTL ■스타트 대시 모드
		PRINTL 　최초반 시작을 지원하는 초기 특전.
		PRINTL 　랜덤 캐릭터, 혹은 임의의 캐릭터(EX 캐릭터 포함) 4명을 선택하여 초기 호감도를 증가시키고 고용인으로 만듭니다.
		PRINTL 　(일부 선택 불가능한 캐릭터 있음)
		PRINTL 　첫 한 달 동안, 직원 급여 무료.
		PRINTL 　이를 선택해도 이벤트나 시나리오에 영향을 주지 않습니다.
	CASE 10
		PRINTL ■그랑군 스타일
		PRINTL 　그랑군을 "당신"으로 설정하는 스타일.
		PRINTL 　당신의 스테이터스가 그랑과 동일해지며, 리조트에 그랑이 등장하지 않게 됩니다.
		PRINTL 　초기 직원으로 "루리아", "비"가 등록됩니다. 소질 "생명의 링크" 소유자가 당신과 루리아로 설정됩니다.
		PRINTL 　일부 캐릭터의 초기 호감도가 상승하며, 단골 소질을 부여합니다.
		PRINTL 　당신이 전투에 참가할 수 없는 것은 통상과 동일합니다.
		PRINTL
		PRINTL ※개발 중이므로 예상치 못한 버그가 발생할 수 있습니다.
		PRINTL ※이벤트 문장 등은 통상 스타일을 상정하고 있으므로, 이야기의 흐름이 이상해질 수 있습니다.
	CASE 11
		PRINTL ■지타짱 스타일
		PRINTL 　지타짱을 "당신"으로 설정하는 스타일.
		PRINTL 　당신의 스테이터스가 지타와 동일해지며, 리조트에 지타가 등장하지 않게 됩니다.
		PRINTL 　초기 직원으로 "루리아", "비"가 등록됩니다. 소질 "생명의 링크" 소유자가 당신과 루리아로 설정됩니다.
		PRINTL 　일부 캐릭터의 초기 호감도가 상승하며, 단골 소질을 부여합니다.
		PRINTL 　당신이 전투에 참가할 수 없는 것은 통상과 동일합니다.
		PRINTL
		PRINTL ※개발 중이므로 예상치 못한 버그가 발생할 수 있습니다.
		PRINTL ※이벤트 문장 등은 통상 스타일을 상정하고 있으므로, 이야기의 흐름이 이상해질 수 있습니다.
	CASE 20
		PRINTL ■임의 캐릭터 스타일
		PRINTL 　원하는 캐릭터를 「당신」으로 설정하는 스타일.
		PRINTL 　당신의 스테이터스가 해당 캐릭터와 같아지며, 리조트에 해당 캐릭터가 등장하지 않게 됩니다.
		PRINTL 　당신이 전투에 참가할 수 없는 것은 통상과 동일합니다.
		PRINTL 　그 외의 보너스 등은 존재하지 않습니다.
		PRINTL
		PRINTL ※개발 중이므로 예상치 못한 버그가 발생할 수 있습니다.
		PRINTL ※이벤트 문장 등은 통상 스타일을 상정하고 있으므로, 이야기의 흐름이 이상해질 수 있습니다.
ENDSELECT
PRINTL
PRINTL 이 스타일로 시작해도 괜찮겠습니까?
PRINTBUTTONLC "[0] 예", 0
PRINTBUTTONLC "[1] 아니요", 1
PRINTL
BINPUT
IF RESULT == 0
	SELECTCASE スタイル記録
		CASE 0
			SAVESTR:あなたスタイル = 
			CALL NAME_CUSTOM(ARG)
			;簡易的に種族と性別だけ選ぶ形のを仮置きする
			CALL 仮置きCUSTOM_TERMINAL(ARG)
		CASE 1
			PRINTW 초기 종업원은 초기 설정이 모두 끝난 후에 선택할 수 있습니다.
			SAVESTR:あなたスタイル = スタートダッシュモード
			CALL NAME_CUSTOM(ARG)
			;簡易的に種族と性別だけ選ぶ形のを仮置きする
			CALL 仮置きCUSTOM_TERMINAL(ARG)
		CASE 10
			SAVESTR:あなたスタイル = グランくんスタイル
		CASE 11
			SAVESTR:あなたスタイル = ジータちゃんスタイル
		CASE 20
			SAVESTR:あなたスタイル = 任意キャラスタイル
			PRINTFORMW 원하는 캐릭터는 초기 설정이 모두 끝난 후에 선택합니다.
	ENDSELECT
ELSE
	RESTART
ENDIF


@仮置きCUSTOM_TERMINAL(ARG)
LOCAL = 3
SIF BASE:ARG:身長
	CLEARBIT LOCAL, 0
SIF BASE:ARG:バスト
	CLEARBIT LOCAL, 1
SIF LOCAL
	CALL SET_BODYSIZE(ARG, LOCAL)

$個別素質変更_LOOP
PRINTL
DRAWLINE
PRINTL 각종 스테이터스를 결정해 주세요.
DRAWLINE
PRINTL ■종족
FOR LOCAL, 1, 7
	IF TALENT:ARG:種族 == LOCAL
		PRINTFORM [%GET_TALENTNAME(200, LOCAL)%]
		SIF LOCAL != 6
			PRINT  /
	ELSE
		SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(200, LOCAL)%]", 100 + LOCAL
		RESETCOLOR
		SIF LOCAL != 6
			PRINT  / 
	ENDIF
NEXT
PRINTL
PRINTL ■성별
SELECTCASE TALENT:ARG:性別
	CASE 2
		PRINT [남성]
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[여성]", 201
		RESETCOLOR
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[후타나리]", 203
		RESETCOLOR
	CASE 1
		SETCOLOR 0x666666
		PRINTBUTTON "[남성]", 202
		RESETCOLOR
		PRINT  / 
		PRINT [여성]
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[후타나리]", 203
		RESETCOLOR
	CASE 3
		SETCOLOR 0x666666
		PRINTBUTTON "[남성]", 202
		RESETCOLOR
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[여성]", 201
		RESETCOLOR
		PRINT  / 
		PRINT [후타나리]
ENDSELECT
PRINTL
DRAWLINE
PRINTL ■외모
FOR LOCAL, -1, 3
	IF TALENT:ARG:容姿 == LOCAL
		PRINTFORM [%GET_TALENTNAME(160, LOCAL)%]
	ELSE
		SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(160, LOCAL)%]", 300 + LOCAL
		RESETCOLOR
	ENDIF
	SIF LOCAL != 2
		PRINT  / 
NEXT
PRINTL
PRINTL ■인상
FOR LOCAL, 0, 5
	IF TALENT:ARG:印象 == LOCAL
		IF LOCAL == 0
			PRINTFORM [담백]
		ELSE
			PRINTFORM [%GET_TALENTNAME(95, LOCAL)%]
		ENDIF
	ELSE
		SETCOLOR 0x666666
		IF LOCAL == 0
			PRINTBUTTON "[담백]", 330
		ELSE
			PRINTBUTTON @"[%GET_TALENTNAME(95, LOCAL)%]", 330 + LOCAL
		ENDIF
		RESETCOLOR
	ENDIF
	SIF LOCAL != 4
		PRINT  / 
NEXT
IF TALENT:ARG:印象 == 0
	HTML_PRINT "<br>■담백<br>성격과 태도가 깔끔한 것. 고집이나 끈질김이 없는 것. 또는 그런 상태.<br>호감도 요소에 대한 보정 없음.<br> "
ELSE
	HTML_PRINT GET_TALENTNAME(95, TALENT:ARG:印象, 1)
ENDIF
PRINTL
PRINTL ■ 나이
FOR LOCAL, 年齢層_幼い, 年齢層_人外 + 1
	IF TALENT:ARG:年齢層 == LOCAL
		PRINTFORM [%GET_TALENTNAME(164, LOCAL)%]
	ELSE
		SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(164, LOCAL)%]", 340 + LOCAL
		RESETCOLOR
	ENDIF
	SIF LOCAL != 2
		PRINT  / 
NEXT
PRINTL
PRINTFORML ■ 체격 \@ BASE:ARG:身長 ? （현재 %DECIMAL_STRING(BASE:ARG:身長, 1), 6, RIGHT%cm）# \@
FOR LOCAL, -3, 3
	IF TALENT:ARG:体格 == LOCAL
		PRINTFORM [%GET_TALENTNAME(161, LOCAL)%]
	ELSE
		SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(161, LOCAL)%]", 400 + LOCAL
		RESETCOLOR
	ENDIF
	SIF LOCAL != 2
		PRINT  / 
NEXT
PRINT  / 
PRINTBUTTON @"[수치 지정]", 410
PRINTL
PRINTL ■ 체형
FOR LOCAL, -3, 4
	IF TALENT:ARG:体型 == LOCAL
		PRINTFORM [%GET_TALENTNAME(163, LOCAL)%]
	ELSE
		SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(163, LOCAL)%]", 450 + LOCAL
		RESETCOLOR
	ENDIF
	SIF LOCAL != 3
		PRINT  / 
NEXT
PRINTL
IF TALENT:ARG:性別 & 2
	PRINTL ■ 남성기 사이즈
	FOR LOCAL, -2, 3
		IF TALENT:ARG:男性器サイズ == LOCAL
			PRINTFORM [%GET_TALENTNAME(162, LOCAL)%]
		ELSE
			SETCOLOR 0x666666
			PRINTBUTTON @"[%GET_TALENTNAME(162, LOCAL)%]", 500 + LOCAL
			RESETCOLOR
		ENDIF
		SIF LOCAL != 2
			PRINT  / 
	NEXT
	PRINTL
ENDIF
IF TALENT:ARG:性別 & 1
	PRINTFORML ■ 가슴 사이즈 \@ BASE:ARG:バスト ? (현재%DECIMAL_STRING(BASE:ARG:バスト, 1), 6, RIGHT%cm %GET_CUPSIZE(ARG), LOCAL, LEFT%)# \@
	FOR LOCAL, -2, 4
		IF TALENT:ARG:バストサイズ == LOCAL
			PRINTFORM [%GET_TALENTNAME(205, LOCAL)%]
		ELSE
			SETCOLOR 0x666666
			PRINTBUTTON @"[%GET_TALENTNAME(205, LOCAL)%]", 600 + LOCAL
			RESETCOLOR
		ENDIF
		SIF LOCAL != 3
			PRINT  / 
	NEXT
	PRINT  / 
	PRINTBUTTON @"[숫자 지정]", 610
	PRINTL
ENDIF

PRINTL
PRINTBUTTON "[999] 이걸로 결정한다", 999
PRINTL

BINPUT

SELECTCASE RESULT
	CASE 101 TO 106
		TALENT:ARG:種族 = RESULT - 100
		CALL RESET_種族(ARG)
		GOTO 個別素質変更_LOOP
	CASE 201 TO 203
		TALENT:ARG:性別 = RESULT - 200
		CALL RESET_性別(ARG)
		GOTO 個別素質変更_LOOP
	CASE 299 TO 302
		TALENT:ARG:容姿 = RESULT - 300
		GOTO 個別素質変更_LOOP
	CASE 330 TO 334
		TALENT:ARG:印象 = RESULT - 330
		GOTO 個別素質変更_LOOP
	CASE 350 TO 357
		TALENT:ARG:年齢層 = RESULT - 340
		GOTO 個別素質変更_LOOP
	CASE 397 TO 402
		TALENT:ARG:体格 = RESULT - 400
		CALL SET_BODYSIZE(ARG, 0)
		GOTO 個別素質変更_LOOP
	CASE 410
		DRAWLINE
		PRINTL 신장을 mm 단위로 입력해주세요
		INPUT
		BASE:ARG:身長 = RESULT
		PRINTFORML 신장을 %DECIMAL_STRING(BASE:ARG:身長, 1), 6, RIGHT%cm로 설정했습니다
		TALENT:ARG:体格 = 0
		CALL INIT_BODYSIZE_HEIGHT(ARG)
		PRINTFORMW 체격이 %GET_TALENTNAME(161, TALENT:ARG:体格)%로 설정되었습니다
		GOTO 個別素質変更_LOOP
	CASE 447 TO 453
		TALENT:ARG:体型 = RESULT - 450
		CALL SET_BODYSIZE(ARG, 2)
		GOTO 個別素質変更_LOOP
	CASE 498 TO 502
		TALENT:ARG:男性器サイズ = RESULT - 500
		GOTO 個別素質変更_LOOP
	CASE 598 TO 603
		TALENT:ARG:バストサイズ = RESULT - 600
		CALL SET_BODYSIZE(ARG, 2)
		GOTO 個別素質変更_LOOP
	CASE 610
		DRAWLINE
		PRINTL 가슴 사이즈를 mm 단위로 입력해주세요
		INPUT
		BASE:ARG:バスト = RESULT
		PRINTFORML 가슴 사이즈를 %DECIMAL_STRING(BASE:ARG:バスト, 1), 6, RIGHT%cm로 설정했습니다
		TALENT:ARG:バストサイズ = 0
		CALL INIT_BODYSIZE_BUST(ARG)
		PRINTFORMW 가슴 사이즈 재능이 %GET_TALENTNAME(205, TALENT:ARG:バストサイズ)%로 설정되었습니다
		GOTO 個別素質変更_LOOP
	CASE 999
		RETURN 0
ENDSELECT


;性別に関連する経験等の初期化
@RESET_性別(ARG)
IF !(TALENT:ARG:性別 & 1)
	ABL:ARG:Ｖ感覚 = 0
	EXP:ARG:噴乳経験 = 0
	EXP:ARG:Ｖ経験 = 0
	EXP:ARG:パイズリ経験 = 0
	EXP:ARG:Ｖ性交経験 = 0
	EXP:ARG:Ｖ拡張経験 = 0
	TALENT:ARG:Ｖ感度 = 0
	TALENT:ARG:処女 = 0
	TALENT:ARG:母乳体質 = 0
	TALENT:ARG:バストサイズ = 0
ENDIF

IF !(TALENT:ARG:性別 & 2)
	EXP:ARG:射精経験 = 0
ENDIF

IF TALENT:ARG:性別 == 1
	EQUIP:ARG:上半身下着あり = 1
	EQUIP:ARG:スカートめくり可 = 1
	EQUIP:ARG:下半身下着抜き取り可 = 1
	TALENT:ARG:処女 = 1
	TALENT:ARG:非童貞 = 0
	TALENT:ARG:性転換 = 0
ELSEIF TALENT:ARG:性別 == 2
	EQUIP:ARG:上半身下着あり = 0
	EQUIP:ARG:スカートめくり可 = 0
	EQUIP:ARG:下半身下着抜き取り可 = 0
	TALENT:ARG:処女 = 0
	TALENT:ARG:非童貞 = 0
	TALENT:ARG:性転換 = 0
ELSE
	EQUIP:ARG:上半身下着あり = 1
	EQUIP:ARG:スカートめくり可 = 1
	EQUIP:ARG:下半身下着抜き取り可 = 1
	TALENT:ARG:処女 = 1
	TALENT:ARG:非童貞 = 0
	TALENT:ARG:性転換 = 0
ENDIF

CALL SET_BODYSIZE(ARG, 0)


;種族に関連する素質等の初期化
@RESET_種族(ARG)
SELECTCASE TALENT:ARG:種族
	CASE 1
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 0
		TALENT:ARG:体格 = 0
	CASE 2
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 1
		TALENT:ARG:エルーン耳 = 1
		TALENT:ARG:ハーヴィン耳 = 0
		TALENT:ARG:体格 = 0
	CASE 3
		TALENT:ARG:角あり = 1
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 0
		IF TALENT:ARG:性別 == 2
			TALENT:ARG:体格 = 2
		ELSE
			TALENT:ARG:体格 = -1
		ENDIF
	CASE 4
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 1
		TALENT:ARG:体格 = -2
	CASEELSE
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 0
ENDSELECT
CALL SET_BODYSIZE(ARG, 0)


;名前の設定
@NAME_CUSTOM(ARG)
PRINTL 캐릭터의 이름을 입력해주세요
IF STRLENS(NAME:ARG)
	PRINTFORM 현재 이름: %NAME表示(ARG)%      
	PRINTBUTTON "[변경 안 함]", ""
	PRINTL
ENDIF
CALL 入力処理_HTML記法警告付き()
IF ARG == 0 && RESULTS == ""
	NAME:ARG = %NAME表示(ARG)%
ELSE
	NAME:ARG = %RESULTS%
ENDIF

PRINTL 캐릭터의 호칭을 입력해주세요
IF STRLENS(CALLNAME:ARG)
	PRINTFORM 현재 호칭: %CALLNAME:ARG%      
	PRINTBUTTON "[변경 안 함]", ""
	PRINTL
ENDIF
CALL 入力処理_HTML記法警告付き()
IF ARG == 0 && RESULTS == ""
	CALLNAME:ARG = %CALLNAME:ARG%
ELSE
	CALLNAME:ARG = %RESULTS%
ENDIF


;ベースの設定
@BASE_CUSTOM(ARG)
PRINT 체력:
BAR BASE:ARG:体力, MAXBASE:ARG:体力, 16
PRINTFORM ({BASE:ARG:体力, 4}/{MAXBASE:ARG:体力, 4})
IF BASE:ARG:体力 != 1000
	PRINTBUTTON "[－]", 0
ELSE
	SETCOLOR 0x969696
	PRINT [－]
	RESETCOLOR
ENDIF
IF BASE:ARG:体力 != 3000
	PRINTBUTTON "[＋]", 1
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
	RESETCOLOR
ENDIF
PRINTL


;素質の設定
@TALENT_CUSTOM(ARG, ARG:1)
VARSET LOCAL
FOR LOCAL, ARG:1 * 40, 40 + ARG:1 * 40
	IF LOCAL % 10 == 0
		PRINTL
		SIF LOCAL:1 % 2
			PRINTL
		LOCAL:1 = 0
	ENDIF
	;陥落素質ほか
	SIF (LOCAL >= 3 && LOCAL <= 5) || LOCAL == 9 || LOCAL == 34 || (LOCAL >= 72 && LOCAL <= 76) || LOCAL == 84 || LOCAL == 85 || (LOCAL >= 153 && LOCAL <= 155) || (LOCAL >= 211 && LOCAL <= 215)
		CONTINUE
	;TALENT.CSVに登録されていない
	SIF TALENTNAME:LOCAL == ""
		CONTINUE
	;オンオフ型の素質(TALENTNAMEと素質表示が一致)
	IF TALENTNAME:LOCAL == GET_TALENTNAME(LOCAL, 1)
		SIF !TALENT:ARG:LOCAL
			SETCOLOR 100, 100, 100
		LOCALS = [{LOCAL, 3}] - %TALENTNAME:LOCAL%
	;童貞
	ELSEIF LOCAL == 1
		SIF TALENT:ARG:LOCAL
			SETCOLOR 100, 100, 100
		LOCALS = [{LOCAL, 3}] - %TALENTNAME:LOCAL%
	;量的素質
	ELSE
		SIF !TALENT:ARG:LOCAL
			SETCOLOR 100, 100, 100
		LOCALS =  [{LOCAL, 3}] - %TALENTNAME:LOCAL%
		IF STRLENS(GET_TALENTNAME(LOCAL, TALENT:ARG:LOCAL))
			LOCALS:1 =  …【%GET_TALENTNAME(LOCAL, TALENT:ARG:LOCAL)%】
		ELSE
			LOCALS:1 = …
		ENDIF
		LOCALS += LOCALS:1
	ENDIF
	PRINTFORM %LOCALS, 35, LEFT%
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL
	RESETCOLOR
NEXT
PRINTL

IF ARG:1 != 0
	PRINTBUTTON "[998] - 이전 페이지                 ", 998
ELSE
	SETCOLOR 0x969696
	PRINTFORM %"[---] - 이전 페이지", 35, LEFT%
	RESETCOLOR
ENDIF
IF ARG:1 != 5
	PRINTBUTTON "[999] - 다음 페이지", 999
ELSE
	SETCOLOR 0x969696
	PRINTFORM %"[---] - 다음 페이지", 35, LEFT%
	RESETCOLOR
ENDIF
PRINTL


@ABL_CUSTOM(ARG)
LOCAL:1 = 0
FOR LOCAL, 0, 50
	SIF !STRLENS(ABLNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(TALENT:ARG:性別 & 1) && (LOCAL == 17 || LOCAL == 32)
		CONTINUE
	ELSEIF TALENT:ARG:性別 & 1 && (LOCAL == 18 || LOCAL == 33)
		CONTINUE
	ENDIF
	;中毒も無し
	SIF LOCAL >= 30
		CONTINUE
	PRINTFORM %ABLNAME:LOCAL, 12%:{ABL:ARG:LOCAL, 5, LEFT} 
	IF ABL:ARG:LOCAL != 0
		PRINTBUTTON "[－]", LOCAL * 2
	ELSE
		SETCOLOR 0x969696
		PRINT [－]
		RESETCOLOR
	ENDIF
	IF ABL:ARG:LOCAL != 2
		PRINTBUTTON "[＋]", LOCAL * 2 + 1
	ELSE
		SETCOLOR 0x969696
		PRINT [＋]
		RESETCOLOR
	ENDIF
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL 
NEXT
PRINTL


@EXP_CUSTOM(ARG)
VARSET LOCAL
FOR LOCAL, 0, 200
	SIF !STRLENS(EXPNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(TALENT:ARG:性別 & 1) && LOCAL == 41
		CONTINUE
	ELSEIF TALENT:ARG:性別 & 1 && LOCAL == 42
		CONTINUE
	ENDIF
	PRINTFORM [ {LOCAL, 2}] - %EXPNAME:LOCAL, 12%:{EXP:ARG:LOCAL, 5, LEFT} 
	LOCAL:1 += 1
	SIF LOCAL:1 % 3 == 0
		PRINTL 
NEXT
PRINTL


@任意キャラスタイル_キャラ選定()
#DIM 候補配列, キャラクタ数上限
#DIM 候補数
#DIM キャラ番号
#DIM ページ数

;キャラを選んだ後、キャラのNOを返す
VARSET 候補配列
候補数 = 0
FOR キャラ番号, 1, CHARANUM
	;ランダムキャラはダメ
	SIF NO:キャラ番号 == 999
		CONTINUE
	;ジータとグランはダメ
	SIF NO:キャラ番号 == 27
		CONTINUE
	SIF NO:キャラ番号 == 146
		CONTINUE
	候補配列:候補数 = キャラ番号
	候補数 ++
NEXT
CALL キャラ配列フィルタ処理(候補配列)
候補数 = RESULT


$表示位置

;キャラ一覧を表示
DRAWLINE
PRINTL 누구를 오너로 시작하시겠습니까?
PRINTL ※지타・그랑을 제외한 모든 캐릭터를 대상으로 선택할 수 있습니다.
PRINTL ※대상 캐릭터에 따라서는 이벤트・시나리오 전개에 모순이 생길 수 있습니다.
DRAWLINE

LOCAL:1 = 0
FOR LOCAL, 0, CHARANUM
	SIF 候補配列:LOCAL < 1
		BREAK
	IF ページ数 == LOCAL:1 / 項目表示数
		LOCALS += @"<button value='{LOCAL}'>[{LOCAL,3}] %GET_NAME(候補配列:LOCAL, 1)%</button>　%ソート情報表示(候補配列:LOCAL, 1)%　"
	ENDIF
	LOCAL:1 += 1
	IF LOCAL:1 % 2 == 0
		HTML_PRINT LOCALS
		LOCALS = 
	ENDIF
NEXT
IF LOCALS != ""
	HTML_PRINT LOCALS
	LOCALS = 
ENDIF
DRAWLINE
;IF ページ数 > 0
;	PRINTBUTTONLC "[9000] 前のページへ", 9000
;ELSE
;	PRINTC   
;ENDIF
;IF ページ数 < (LOCAL:1 + 1) / 30
;	PRINTBUTTONLC "[9001] 次のページへ", 9001
;ELSE
;	PRINTC   
;ENDIF
;PRINTL
;ページめくり表示
CALL ページ送り共通表示(ページ数, 項目表示数, 最大表示数, 9000, 9003)
DRAWLINE
CALL フィルタソートボタンセット表示(9020, 9030, 9031, 9040)
;DRAWLINE
;PRINTBUTTON "[9999] 戻る", 9999
PRINTL 

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 9020, 9030, 9031, 9040
		CALL フィルタソートボタンセット実行部(LOCAL, 9020, 9030, 9031, 9040)
		ページ数 = 0
		RESTART
	CASE 9000 TO 9007
		CALL ページ送り共通実行(LOCAL, ページ数, 項目表示数, 最大表示数, 9000, 9003)
		GOTO 表示位置
	CASE 9999
		WHILE 1
			CALL 任意キャラスタイル_セット確認(候補配列:LOCAL)
			SELECTCASE RESULT
				CASE 0
					;- キャンセル
					; リスト表示へ戻る
					GOTO 表示位置
				CASE 1
					;- あなたで始める
					ページ数 = 0
					RETURN 0
				CASE 10
					;- 前のキャラ
					; 候補番号を更新、ループして前のキャラを表示
					LOCAL -=  1
					SIF LOCAL < 0
						LOCAL = 候補数 - 1
				CASE 11
					;- 次のキャラ
					; 候補番号を更新、ループして次のキャラを表示
					LOCAL +=  1
					SIF LOCAL >= 候補数
						LOCAL = 0
			ENDSELECT
		WEND
;	CASE 9000
;		ページ数 --
;		GOTO 表示位置
;	CASE 9001
;		ページ数 ++
;		GOTO 表示位置
	CASEELSE
		WHILE 1
			CALL 任意キャラスタイル_セット確認(候補配列:LOCAL)
			SELECTCASE RESULT
				CASE 0
					;- キャンセル
					; リスト表示へ戻る
					GOTO 表示位置
				CASE 1
					;- キャラ選択
					COPYCHARA 候補配列:LOCAL, MASTER
					NO:MASTER = 0
					CFLAG:(候補配列:LOCAL):招待不可フラグ = -1
					FLAG:あなたキャラ化記録 = NO:(候補配列:LOCAL)
					RETURN NO:(候補配列:LOCAL)
				CASE 10
					;- 前のキャラ
					; 候補番号を更新、ループして前のキャラを表示
					LOCAL -=  1
					SIF LOCAL < 0
						LOCAL = 候補数 - 1
				CASE 11
					;- 次のキャラ
					; 候補番号を更新、ループして次のキャラを表示
					LOCAL +=  1
					SIF LOCAL >= 候補数
						LOCAL = 0
			ENDSELECT
		WEND
ENDSELECT

RESTART

@任意キャラスタイル_セット確認(対象キャラ)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 対象キャラ
#DIM 返り値
#DIM 描画開始行
#DIM 対象画像数
#DIM リロールボタン有効
#DIM 実行結果
#DIM セットフラグ
#DIM リスタートフラグ
#DIM TFLAG保存
#DIM DYNAMIC 初回フラグ = 1

IF 初回フラグ
	初回フラグ = 0
	; 番号の範囲チェック
	SIF !INRANGE(対象キャラ, 0, CHARANUM - 1)
		RETURN -1
	描画開始行 = LINECOUNT
ENDIF

PRINTL
; キャラ確認用の簡易紹介
CALL キャラ簡易紹介(対象キャラ)
対象画像数 = RESULT
リロールボタン有効 = 対象画像数 > 5
PRINTL
; 確認メッセージ
PRINTFORML %NAME表示(対象キャラ)%를 오너로 하여 시작하시겠습니까?
; ボタン表示
PRINTBUTTONLC "[0]예", 0
PRINTBUTTONLC "[1]아니요", 1
PRINTBUTTONLC "[10]상세 능력 표시", 10
PRINTL
PRINTBUTTONLC "[2]이전 캐릭터", 2
PRINTBUTTONLC "[3]다음 캐릭터", 3
SIF リロールボタン有効
	PRINTBUTTONLC "[4]이미지 랜덤 표시 리롤", 4
PRINTL

; 入力ループ
WHILE 1
	BINPUT
	SELECTCASE RESULT
		CASE 0
			返り値 = 1
			BREAK
		CASE 1
			返り値 = 0
			BREAK
		CASE 4
			RESTART
		CASE 2
			;- [2]前のキャラ
			返り値 = 10
			BREAK
		CASE 3
			;- [3]次のキャラ
			返り値 = 11
			BREAK
		CASE 10
			TFLAG保存 = TFLAG:調教モード
			TFLAG:調教モード = -2
			CALL PRINT_STATE(対象キャラ, 3)
			TFLAG:調教モード = TFLAG保存
			SELECTCASE RESULT
				CASE 100
					;前のキャラへ
					返り値 = 10
					BREAK
				CASE 101
					;次のキャラへ
					返り値 = 11
					BREAK
				CASE 1
					;はい
					返り値 = 1
					BREAK
				CASE 0
					;いいえ
					返り値 = 0
					BREAK
			ENDSELECT
		CASEELSE
			; 入力エラー
	ENDSELECT
	; 入力エラー時は消してやり直し
	CLEARLINE 1
	REUSELASTLINE 
WEND

; 表示した内容を消去
CLEARLINE LINECOUNT - 描画開始行

; 簡易紹介で使用したリソースを解放
CALL ランダム画像表示_リソース解放(1)

RETURN 返り値
