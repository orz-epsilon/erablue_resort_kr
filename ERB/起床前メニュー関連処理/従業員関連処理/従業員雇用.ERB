;Ver57にて新関数に移行。Ver59にて削除予定
[SKIPSTART]
@従業員雇用
#DIM ランダムキャラ素質, 70
#DIM ランダムキャラ戦闘タイプ, 10
#DIMS ランダムキャラネーム
#DIMS ランダムキャラ知識素質, 2
#DIM 素質選択用, 10
#DIM バスト種別
#DIM メニュー調理法一時保存, 6
#DIMS 料理コンボ一時保存
#DIMS 汎用画像キー
#DIM Ａ処女保存
#DIMS ランダムキャラ種族
#DIMS 得意武器変換

VARSET メニュー調理法一時保存
FOR LOCAL, 1, 6
	メニュー調理法一時保存:LOCAL = DT_CELL_GET("料理メニューデータベース", 現在設定メニュー:LOCAL, "調理器具ID", 1)
NEXT
CALL 料理メニューコンボ判定処理
SIF !RESULT
	発動中コンボ名 '= ""

$従業員_LOOP
DRAWLINE
PRINTL 직원(랜덤 캐릭터)을 고용합니다.
PRINTPLAIN [110] 다른 후보 보기를 선택한 경우, 이전 캐릭터로 돌아갈 수 없으니 주의해 주세요.
PRINTL
DRAWLINE
VARSET ランダムキャラ素質:0
VARSET ランダムキャラ戦闘タイプ:0
VARSET ランダムキャラ知識素質:0, "料理人Lv0"
ランダムキャラ種族 = 
LOCALS = <div rect='81,31,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2050,31,3875,2812' border='31' bcolor='#C0C0C0'></div>
LOCALS += "<div rect='156,156,1937,2812'>"
LOCALS += "<button value='100'>[100] 이 캐릭터를 고용한다</button>"
LOCALS += "<br>"
LOCALS += "<button value='110'>[110] 다른 후보 보기</button>"
LOCALS += "<br>"
LOCALS += "<button value='120'>[120] 범용 스탠딩 CG 기능을 전환한다</button>"
LOCALS += "<br>"
LOCALS += "<button value='130'>[130] 워울프의 스탠딩 CG를 변경한다</button>"
LOCALS += "<br>"
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 돌아가기</button>"
LOCALS += "</div>"

LOCALS += "<div rect='2125,156,3675,2812'>"
;戦闘タイプ：食堂のメニューによって出やすいものがある
SELECTCASE メニュー調理法一時保存:(RAND:6)
	CASE 1
		;包丁は水・風属性が出やすい
		ランダムキャラ戦闘タイプ:0 = RAND:2 ? 1 # 2
	CASE 2
		;オーブンは火・風属性が出やすい
		ランダムキャラ戦闘タイプ:0 = RAND:2 ? 0 # 2
	CASE 3
		;鍋は光・闇属性が出やすい
		ランダムキャラ戦闘タイプ:0 = RAND:2 ? 4 # 5
	CASE 4
		;フライパンは水・土属性が出やすい
		ランダムキャラ戦闘タイプ:0 = RAND:2 ? 1 # 3
	CASE 5
		;蒸し器は火・土属性が出やすい
		ランダムキャラ戦闘タイプ:0 = RAND:2 ? 0 # 3
	CASEELSE
		ランダムキャラ戦闘タイプ:0 = RAND:6
ENDSELECT
ランダムキャラ戦闘タイプ:1 = 0
SETBIT ランダムキャラ戦闘タイプ:1, RAND:10
;たまに2種得意持ちが現れる
SIF RAND:10 == 0
	SETBIT ランダムキャラ戦闘タイプ:1, RAND:10
IF RAND:3 == 0 || !GROUPMATCH(発動中コンボ名, "バランスの良い食事処", "新鮮海鮮レストラン", "肉食べ放題レストラン", "健康野菜レストラン", "満腹穀物レストラン")
	ランダムキャラ戦闘タイプ:2 = RAND:8
ELSE
	SELECTCASE 発動中コンボ名
		CASE "新鮮海鮮レストラン"
			;海鮮だと連撃・回避が出やすい
			ランダムキャラ戦闘タイプ:2 = RAND:2 ? 5 # 6
		CASE "肉食べ放題レストラン"
			;肉だと攻撃・奥義が出やすい
			ランダムキャラ戦闘タイプ:2 = RAND:2 ? 1 # 7
		CASE "健康野菜レストラン"
			;野菜だと回復・サポートが出やすい
			ランダムキャラ戦闘タイプ:2 = RAND:2 ? 3 # 4
		CASE "満腹穀物レストラン"
			;穀物だと防御が出やすい
			ランダムキャラ戦闘タイプ:2 = RAND:2 ? 2 # RAND:8
		CASEELSE
			;バランスの良い食事処だとバランスが出やすい
			ランダムキャラ戦闘タイプ:2 = RAND:2 ? 0 # RAND:8
	ENDSELECT
ENDIF

;種族：ヒューマンが若干多い
ランダムキャラ素質:0 = LIMIT(RAND:5, 1, 4)

;施設開放条件に応じて火水風土光闇それぞれの星晶獣の雇用が開放される
LOCALS:1 = 
SIF 火の星晶獣_陥落チェック() || ダンジョンクリアフラグ_バルツ地下廃工場
	LOCALS:1 += "0/"
SIF 水の星晶獣_陥落チェック() || ダンジョンクリアフラグ_深海の古戦場跡
	LOCALS:1 += "1/"
SIF 風の星晶獣_陥落チェック() || ダンジョンクリアフラグ_風遊ぶ渓谷の参道
	LOCALS:1 += "2/"
SIF 土の星晶獣_陥落チェック() || ダンジョンクリアフラグ_名前を亡くした遺跡
	LOCALS:1 += "3/"
;SIF 光の星晶獣_陥落チェック() || ダンジョンクリアフラグ_
;	LOCALS:1 += "4/"
;SIF 闇の星晶獣_陥落チェック() || ダンジョンクリアフラグ_
;	LOCALS:1 += "5/"

IF LOCALS:1 != "" && !RAND:(50 + 25 * (12 - STRLENS(LOCALS:1)))
	;1/50～300で星晶獣がやってくる。施設開放条件を満たすたびに1/50ずつ緩和される
	;1/200で野生のロリババアが来るんだしそれくらい来るだろ。なお星晶獣は1/4でロリババア。やったね！
	ランダムキャラ素質:0 = 5
	;施設開放条件に応じてやってくる属性が開放される
	ランダムキャラ戦闘タイプ:0 = TOINT(TEXTR(LOCALS:1))
ELSEIF RAND:99 < 2
	;1/50で亜人がやってくる。1/200で野生のロリババアが来るんだし（ｒｙ 
	;特定の種族に来てほしい場合さらに確率下がるので、要望を受けもともとの確率上昇
	ランダムキャラ素質:0 = 6
	ランダムキャラ種族 = %TEXTR("クリスタリア/スヴァル/ワーウルフ/ヴァンパイア/烈人/魔ヒ人")%
ENDIF

;性別：招待OPTIONに適したものが選ばれる、不可能な場合は女性
ランダムキャラ素質:1 = (RAND:20 / 19 * 2) + 1
SIF 素質切り替えOPTION記憶:ふたなり_女性 ||  GETBIT(OPTION変数:性別招待制御, 2) || ランダムキャラ種族 == "クリスタリア"
	ランダムキャラ素質:1 = 1
;男性が許可されてるなら1/4で男
SIF GETBIT(OPTION変数:性別招待制御, 1) == 0 && RAND:4 == 0 && ランダムキャラ種族 != "クリスタリア"
	ランダムキャラ素質:1 = 2
;女性が許可されてないなら男化orふたなり化
IF GETBIT(OPTION変数:性別招待制御, 0) && ランダムキャラ素質:1 == 1
	IF 素質切り替えOPTION記憶:ふたなり_女性 == 0 &&  GETBIT(OPTION変数:性別招待制御, 2) == 0
		ランダムキャラ素質:1 = 3
	ELSEIF GETBIT(OPTION変数:性別招待制御, 1) == 0
		ランダムキャラ素質:1 = 2
		SIF ランダムキャラ種族 == "クリスタリア"
			ランダムキャラ種族 = %TEXTR("スヴァル/ワーウルフ/ヴァンパイア/烈人/魔ヒ人")%
	ENDIF
ENDIF
IF RAND:20 == 0
	;5%で非A処女
	Ａ処女保存 = 1
ELSE
	Ａ処女保存 = 0
ENDIF

ランダムキャラネーム = %ランダムキャラ名前生成(ランダムキャラ素質:0, ランダムキャラ素質:1)%
LOCALS += @"■[직원]%ランダムキャラネーム, 18, LEFT%"
LOCALS:1 = %属性数値文字色変換_HTML(ランダムキャラ戦闘タイプ:0)%
LOCALS += @"<font color='#%LOCALS:1%'>속성：%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%</font>"
LOCALS += @"　특기 무기：%得意武器表示文字列(ランダムキャラ戦闘タイプ:1)%　타입：%成長タイプ:(ランダムキャラ戦闘タイプ:2)%"
LOCALS += "<br><br>"
LOCALS += "□ 정보 □---------------------------------------------------------------<br>"

;処女/童貞（10%で非処女/非童貞）
ランダムキャラ素質:2 = MIN(RAND:10, 1)

;年齢層/外見年齢
ランダムキャラ素質:51 = ランダム年齢(ランダムキャラ素質:0)

;ランダムキャラ素質:4、5に種族系素質
LOCALS += "　종족："
LOCALS:1 = 
SELECTCASE ランダムキャラ素質:0
	CASE 1
		LOCALS += "휴먼"
	CASE 2
		LOCALS += "엘룬"
		ランダムキャラ素質:4 = 208
		LOCALS:1 += "（엘룬 귀）"
		IF RAND:5 == 0
			ランダムキャラ素質:5 = 207
			LOCALS:1 += "（꼬리）"
		ENDIF
	CASE 3
		LOCALS += "드라프"
		ランダムキャラ素質:4 = 206
		LOCALS:1 += "（뿔 있음）"
	CASE 4
		LOCALS += "하빈"
		ランダムキャラ素質:4 = 209
		LOCALS:1 += "（하빈 귀）"
	CASE 5
		LOCALS += "성정수"
	CASE 6
		LOCALS += "그 외"
		SIF ランダムキャラ種族 != ""
			LOCALS += @"[%ランダムキャラ種族%]"
		SELECTCASE ランダムキャラ種族
			CASE "クリスタリア"
				;クリスタリア
				ランダムキャラ素質:4 = 209
				LOCALS:1 += "（하빈 귀）"
			CASE "ヴァンパイア"
				;ヴァンパイア
				ランダムキャラ素質:4 = 209
				LOCALS:1 += "（하빈 귀）"
				ランダムキャラ素質:5 = 132
				LOCALS:1 += "（흡혈귀）"
			CASE "ワーウルフ"
				;ワーウルフ
				TALENT:ハーヴィン耳 = 0
				ランダムキャラ素質:4 = 208
				LOCALS:1 += "（에룬 귀）"
				ランダムキャラ素質:5 = 207
				LOCALS:1 += "（꼬리）"
				ランダムキャラ素質:7 = 203
				LOCALS:1 += "（발정기 있음）"
		ENDSELECT
ENDSELECT
LOCALS += REPLACE(LOCALS:1, "）（", "／")
LOCALS += "<br>"
LOCALS += "　성별："
LOCALS:1 = 
SELECTCASE ランダムキャラ素質:1
	CASE 1
		LOCALS += "여성"
		IF ランダムキャラ素質:2 == 0
		ELSE
			LOCALS:1 += "（처녀）"
		ENDIF
	CASE 2
		LOCALS += "남성"
		IF ランダムキャラ素質:2 == 0
		ELSE
			LOCALS:1 += "（동정）"
		ENDIF
	CASE 3
		LOCALS += "후타나리"
		IF ランダムキャラ素質:2 == 0
		ELSE
			LOCALS:1 += "（처녀）（동정）"
		ENDIF
ENDSELECT
IF Ａ処女保存 == 0
	LOCALS:1 += "（A처녀）"
ENDIF
LOCALS += REPLACE(LOCALS:1, "）（", "／")
LOCALS += "<br>"
IF GETBIT(ランダムキャラ素質:1, 0)
	;バストサイズ：種族、年齢層で分布が異なる
	ランダムキャラ素質:3 = ランダムバストサイズ(ランダムキャラ素質:0, ランダムキャラ年齢層(ランダムキャラ素質:51))
	LOCALS += @"　가슴 사이즈：%GET_TALENTNAME(205,ランダムキャラ素質:3)%　　"
ENDIF
LOCALS += @"　성지식 레벨："
;性知識：処女/童貞、年齢層、エルーンかどうかで分布が異なる
ランダムキャラ素質:6 = ランダム性知識(ランダムキャラ素質:2, ランダムキャラ年齢層(ランダムキャラ素質:51), ランダムキャラ素質:0 == 種族_エルーン)
SELECTCASE ランダムキャラ素質:6
	CASE -2
		LOCALS += "무지"
		ランダムキャラ知識素質:0 '= "性知識Lv-2"
	CASE -1
		LOCALS += "짐승 수준"
		ランダムキャラ知識素質:0 '= "性知識Lv-1"
	CASE 0
		LOCALS += "서투름"
		ランダムキャラ知識素質:0 '= "性知識Lv0"
	CASE 1
		LOCALS += "귀동냥"
		ランダムキャラ素質:18 = 23
		ランダムキャラ知識素質:0 '= "性知識Lv0"
	CASE 2
		LOCALS += "필요충분"
		ランダムキャラ知識素質:0 '= "性知識Lv1"
	CASE 3
		LOCALS += "유식자"
		ランダムキャラ知識素質:0 '= "性知識Lv2"
ENDSELECT
LOCALS += "<br>　용모:"
ランダムキャラ素質:50 = ランダム容姿()
LOCALS += @"%GET_TALENTNAME(160,ランダムキャラ素質:50), 14, LEFT%"
LOCALS += "연령:"
LOCALS += @"%GET_年齢文字列(ランダムキャラ素質:1, ランダムキャラ年齢層(ランダムキャラ素質:51), ランダムキャラ外見年齢(ランダムキャラ素質:51)), 17, LEFT%"
LOCALS += "<br>　체격:"
;体格：種族、性別、見た目年齢、ドラフ♀のみバストサイズ、長命者かどうかで分布が異なる
ランダムキャラ素質:52 = ランダム体格(ランダムキャラ素質:0, ランダムキャラ素質:1, ランダムキャラ見た目年齢(ランダムキャラ素質:51), ランダムキャラ素質:3, ランダムキャラ長命チェック(ランダムキャラ素質:51))
LOCALS += @"%GET_TALENTNAME(161,ランダムキャラ素質:52), 14, LEFT%"
LOCALS += "체형:"
;体型：種族で分布が異なる
ランダムキャラ素質:54 = ランダム体型(ランダムキャラ素質:0)
LOCALS += @"%GET_TALENTNAME(163,ランダムキャラ素質:54)%"
IF GETBIT(ランダムキャラ素質:1, 1)
	;男性器サイズ：種族、性別、年齢層で分布が異なる
	ランダムキャラ素質:53 = ランダム男性器サイズ(ランダムキャラ素質:0, ランダムキャラ素質:1, ランダムキャラ年齢層(ランダムキャラ素質:51))
	IF ランダムキャラ素質:1 == 3
		;表示はふたなりのみ
		LOCALS += "<br>　남성기 사이즈:"
		LOCALS += @"%GET_TALENTNAME(162,ランダムキャラ素質:53)%"
	ENDIF
ENDIF
LOCALS += "<br>"
LOCALS += "<br>"
;ランダムキャラ素質:10～29にランダムで素質
;6個～8個（うち2つは性格系の素質確定）
CALL ランダム素質選択(素質選択用)
FOR LOCAL, 0, 8
	IF ランダムキャラ素質:0 == 種族_その他 && ランダムキャラ種族 == "ワーウルフ"
		SIF (!素質選択用:LOCAL || ランダムキャラ素質番号(素質選択用:LOCAL) == GETNUM(TALENT, "汚臭耐性")) && !MATCH(素質選択用, -61) && !MATCH(素質選択用, -10061)
			素質選択用:LOCAL = (RAND:5? -61# -10061)
	ENDIF
	ランダムキャラ素質:(LOCAL + 10) = 素質選択用:LOCAL
NEXT
LOCALS += "□ 소질 □---------------------------------------------------------------<br>"
LOCALS:1 = 　
FOR LOCAL, 0, 8
	SIF ランダムキャラ素質:(LOCAL + 10) == 0
		BREAK
;	LOCALS += @"[%GET_TALENTNAME(ランダムキャラ素質番号(ランダムキャラ素質:(LOCAL + 10)), ランダムキャラ素質値(ランダムキャラ素質:(LOCAL + 10)))%]"
	LOCALS:2 '= @"[%GET_TALENTNAME(ランダムキャラ素質番号(ランダムキャラ素質:(LOCAL + 10)), ランダムキャラ素質値(ランダムキャラ素質:(LOCAL + 10)))%]"
	IF STRLENS(HTML_TOPLAINTEXT(LOCALS:1+LOCALS:2)) / (3625/50) > 0
		LOCALS += LOCALS:1 + "<br>"
		LOCALS:1 '= "　"
	ENDIF
	LOCALS:1 += LOCALS:2
	LOCALS:2 = 
NEXT
;こっから敏感系
FOR LOCAL, 0, 5
	LOCAL:1 = RAND:10
	IF LOCAL:1 < 2
		;鈍感
		ランダムキャラ素質:(LOCAL + 20) = (LOCAL + 101) * -1
;		LOCALS += @"[%GET_TALENTNAME(ABS(ランダムキャラ素質:(LOCAL + 20)), -1)%]"
		LOCALS:2 '= @"[%GET_TALENTNAME(ABS(ランダムキャラ素質:(LOCAL + 20)), -1)%]"
	ELSEIF LOCAL:1 < 7
	ELSE
		;敏感
		ランダムキャラ素質:(LOCAL + 20) = LOCAL + 101
;		LOCALS += @"[%GET_TALENTNAME(ランダムキャラ素質:(LOCAL + 20), 1)%]"
		LOCALS:2 '= @"[%GET_TALENTNAME(ランダムキャラ素質:(LOCAL + 20), 1)%]"
	ENDIF
	IF STRLENS(HTML_TOPLAINTEXT(LOCALS:1+LOCALS:2)) / (3625/50) > 0
		LOCALS += LOCALS:1 + "<br>"
		LOCALS:1 '= "　"
	ENDIF
	LOCALS:1 += LOCALS:2
	LOCALS:2 = 
NEXT
IF RAND:100 == 0
	ランダムキャラ素質:26 = 1086
;	LOCALS += "[元男]"
	LOCALS:1 += "[전 남자]"
ENDIF
IF ランダムキャラ素質:3 > 1 && RAND:10 == 0 && 素質切り替えOPTION記憶:陥没乳首_なし == 0
	ランダムキャラ素質:27 = 111
;	LOCALS += "[陥没乳首]"
	LOCALS:1 += "[함몰 유두]"
ENDIF
IF ランダムキャラ素質:3 > 1 && RAND:10 == 0 && 素質切り替えOPTION記憶:母乳体質_なし == 0
	ランダムキャラ素質:28 = 149
;	LOCALS += "[母乳体質]"
	LOCALS:1 += "[모유 체질]"
ENDIF
IF ランダムキャラ素質:1 == 3 && RAND:3 == 0
	ランダムキャラ素質:29 = 152
;	LOCALS += "[絶倫]"
	LOCALS:1 += "[절륜]"
ENDIF
;交際経験(処女/童貞、見た目年齢、長命者かどうかで分布が異なる)
ランダムキャラ素質:55 = ランダム交際経験(ランダムキャラ素質:2, ランダムキャラ見た目年齢(ランダムキャラ素質:51), ランダムキャラ長命チェック(ランダムキャラ素質:51))
IF ランダムキャラ出産経験(ランダムキャラ素質:55) >= 1
;	LOCALS += @"[子持ち]"
	LOCALS:1 += @"[자식 있음]"
ENDIF
IF ランダムキャラ恋人持ち(ランダムキャラ素質:55) != 0
;	LOCALS += @"[%GET_TALENTNAME(234, ランダムキャラ恋人持ち(ランダムキャラ素質:55))%]"
	LOCALS:1 += @"[%GET_TALENTNAME(234, ランダムキャラ恋人持ち(ランダムキャラ素質:55))%]"
ENDIF
IF ランダムキャラセフレあり(ランダムキャラ素質:55) != 0
;	LOCALS += @"[%GET_TALENTNAME(244, ランダムキャラセフレあり(ランダムキャラ素質:55))%]"
	LOCALS:1 += @"[%GET_TALENTNAME(244, ランダムキャラセフレあり(ランダムキャラ素質:55))%]"
ENDIF
IF RAND:10 == 0
	STRDATA ランダムキャラ知識素質:1
		DATA 조합 지식 Lv1
		DATA 마법 지식 Lv1
		DATA 기계 지식 Lv1
		DATA 연금 지식 Lv1
		DATA 마물 지식 Lv1
		DATA 화가 Lv1
		DATA 고귀 Lv1
		DATA 낚시꾼 Lv1
		DATA 무용수 Lv1
		DATA 가창 기술 Lv1
		DATA 요리사 Lv1
		DATA 역사 지식 Lv1
		DATA 경영 지식 Lv1
		DATA 수영 기술 Lv1
		DATA 농업 지식 Lv1
	ENDDATA
;	LOCALS += @"[%ランダムキャラ知識素質:1%]"
	LOCALS:1 += @"[%ランダムキャラ知識素質:1%]"
ENDIF
LOCALS += LOCALS:1
LOCALS += "<br>"
LOCALS += "<br>"
LOCALS += "□ 능력 □---------------------------------------------------------------<br>"
LOCAL:2 = 0
FOR LOCAL, 0, 6
	LOCAL:1 = RAND:30
	SELECTCASE LOCAL:1
		CASE 29
			ランダムキャラ素質:(LOCAL + 30) = 2
			LOCALS += @"%ABLNAME:LOCAL, 16%:2   "
			LOCAL:2 += 1
		CASE IS > 23
			ランダムキャラ素質:(LOCAL + 30) = 1
			LOCALS += @"%ABLNAME:LOCAL, 16%:1   "
			LOCAL:2 += 1
	ENDSELECT
	IF LOCAL:2 == 3
		LOCALS += "<br>"
		LOCAL:2 = 0
	ENDIF
NEXT
FOR LOCAL, 0, 4
	LOCAL:1 = RAND:30
	SELECTCASE LOCAL:1
		CASE 29
			ランダムキャラ素質:(LOCAL + 40) = 2
			LOCALS += @"%ABLNAME:(LOCAL + 10), 16%:2   "
			LOCAL:2 += 1
		CASE IS > 23
			ランダムキャラ素質:(LOCAL + 40) = 1
			LOCALS += @"%ABLNAME:(LOCAL + 10), 16%:1   "
			LOCAL:2 += 1
	ENDSELECT
	IF LOCAL:2 == 3
		LOCALS += "<br>"
		LOCAL:2 = 0
	ENDIF
NEXT
SIF LOCAL:2 != 0
	LOCALS += "<br>"
LOCALS += "<br>"
LOCALS += "□ 경험 □---------------------------------------------------------------<br>"
LOCAL:2 = 0
LOCAL:3 = 0
FOR LOCAL, 0, 5
	IF ランダムキャラ素質:(LOCAL + 30) == 2
		LOCALS += @"%EXPNAME:LOCAL, 16%:100  "
		LOCAL:2 += 1
	ELSEIF ランダムキャラ素質:(LOCAL + 30) == 1
		LOCALS += @"%EXPNAME:LOCAL, 16%:30  "
		LOCAL:2 += 1
	ENDIF
	IF LOCAL:2 == 3
		LOCALS += "<br>"
		LOCAL:2 = 0
	ENDIF
	IF ランダムキャラ素質:(LOCAL + 30) == 2
		LOCALS += @"%(EXPNAME:(LOCAL + 5)), 16%:20  "
		LOCAL:2 += 1
		LOCAL:3 += 10
	ELSEIF ランダムキャラ素質:(LOCAL + 30) == 1
		LOCALS += @"%(EXPNAME:(LOCAL + 5)), 16%:5   "
		LOCAL:2 += 1
		LOCAL:3 += 5
	ENDIF
	IF LOCAL:2 == 3
		LOCALS += "<br>"
		LOCAL:2 = 0
	ENDIF
NEXT
IF ランダムキャラ出産経験(ランダムキャラ素質:55) >= 1
	LOCALS += @"%(EXPNAME:30), 16%:{ランダムキャラ出産経験(ランダムキャラ素質:55)}  "
	LOCAL:2 += 1
	IF LOCAL:2 == 3
		LOCALS += "<br>"
		LOCAL:2 = 0
	ENDIF
	LOCALS += @"%(EXPNAME:31), 16%:{ランダムキャラ出産経験(ランダムキャラ素質:55)}  "
	LOCAL:2 += 1
	IF LOCAL:2 == 3
		LOCALS += "<br>"
		LOCAL:2 = 0
	ENDIF
ENDIF
LOCALS += @"%(EXPNAME:10), 16%:{LOCAL:3}"
LOCALS += "</div>"

$表示_LOOP
汎用画像キー '= 汎用立ち絵取得処理(ランダムキャラネーム, ランダムキャラ素質:0, ランダムキャラ素質:1, ランダムキャラ素質:52, ランダムキャラ素質:3, ランダムキャラ種族)
LOCALS:1 = <div rect='81,31,5860,1200' padding='6' border='31' bcolor='#C0C0C0'>
LOCALS:1 += "<br>"
IF OPTION変数:汎用立ち絵制御
	LOCALS:1 += @"<button value='120'>[120] 범용 스탠딩 CG ON / <font color='#%カラーパレット_HTML("グレーアウト")%'>OFF</font></button>"
	LOCALS:1 += "<br>"
	LOCALS:1 += @"<button value='130'>[130] 워울프의 스탠딩 CG를 변경한다<br>　　　（현재：\@ OPTION変数:ワーウルフ画像ケモ度? 수인 쪽# 인간 쪽\@）</button>"
	LOCALS:1 += "<br>"
ELSE
	LOCALS:1 += @"<button value='120'>[120] 범용 스탠딩 CG <font color='#%カラーパレット_HTML("グレーアウト")%'>ON</font> / OFF</button>"
	LOCALS:1 += "<br>"
ENDIF
LOCALS:1 += "</div>"
IF OPTION変数:汎用立ち絵制御
	LOCALS:1 += "<div rect='2050,31,2400,1200' padding='6' border='31' bcolor='#C0C0C0'>"
	IF 汎用画像キー == ""
		LOCALS:1 += "<img src='顔ノーイメ' height='1125'>"
	ELSE
		SPRITEDISPOSE @"顔グラ3000"
		GDISPOSE 3000
		CALL GCREATE_拡張子(3000, @"汎用グラフィック/顔_%汎用画像キー%")
		SPRITEDISPOSE @"短冊グラ3000"
		GDISPOSE 3001
		CALL GCREATE_拡張子(3001, @"汎用グラフィック/短冊_%汎用画像キー%")
		;設定中の顔グラが存在すればそちらを拡縮で表示
		LOCAL = 画像サイズ切り替えOPTION記憶:1
		画像サイズ切り替えOPTION記憶:1 = 1
		SIF ランダムキャラ種族 != "ワーウルフ"
			LOCALS:1 += "<clearbutton>"
		SIF スプライト作成(@"顔グラ3000", 3000, 0)
			LOCALS:1 += @"<button value='130'><img src='顔グラ3000'height='1125'></button>"
		SIF スプライト作成(@"短冊グラ3000", 3001, 0)
			LOCALS:1 += @"<button value='130'><img src='短冊グラ3000'height='1125'></button>"
		SIF ランダムキャラ種族 != "ワーウルフ"
			LOCALS:1 += "</clearbutton>"
		画像サイズ切り替えOPTION記憶:1 = LOCAL
	ENDIF
	LOCALS:1 += "</div>"
ENDIF
HTML_PRINT LOCALS:1
FOR LOCAL, 0, 10
	PRINTL
NEXT

HTML_PRINT LOCALS
FOR LOCAL, 0, 25
	PRINTL
NEXT
DRAWLINE

$INPUT_LOOP
INPUT

LOCAL = RESULT
SELECTCASE LOCAL
	CASE 100
		LOCAL:10 = 0
		FOR LOCAL, 0, 従業員部屋数
			IF 従業員部屋割り配列:LOCAL < 1
				LOCAL:10 = 1
				BREAK
			ENDIF
		NEXT
		IF LOCAL:10 == 0
			PRINTW 직원 방이 가득 찼습니다.
			RETURN 0
		ENDIF
		PRINTFORMW [직원]%ランダムキャラネーム%을(를) 고용했습니다.
		LOCAL:1 = CHARANUM
		ADDCHARA 999
		TALENT:(LOCAL:1):従業員 = 1
		NAME:(LOCAL:1) = [従業員]%ランダムキャラネーム%
		CALLNAME:(LOCAL:1) = %ランダムキャラネーム%
		TALENT:(LOCAL:1):200 = ランダムキャラ素質:0
		SIF ランダムキャラ素質:0 == 6
			CSTR:(LOCAL:1):特殊種族 = %ランダムキャラ種族%
		TALENT:(LOCAL:1):2 = ランダムキャラ素質:1
		IF ランダムキャラ素質:2
			TALENT:(LOCAL:1):0 = 1
			TALENT:(LOCAL:1):1 = 0
		ELSE
			TALENT:(LOCAL:1):0 = 0
			TALENT:(LOCAL:1):1 = 1
		ENDIF
		IF Ａ処女保存
			TALENT:(LOCAL:1):Ａ処女 = 0
		ELSE
			TALENT:(LOCAL:1):Ａ処女 = 1
		ENDIF
		TALENT:(LOCAL:1):205 = ランダムキャラ素質:3
		SIF ランダムキャラ素質:4
			TALENT:(LOCAL:1):(ランダムキャラ素質:4) = 1
		SIF ランダムキャラ素質:5
			TALENT:(LOCAL:1):(ランダムキャラ素質:5) = 1
		SIF ランダムキャラ素質:7
			TALENT:(LOCAL:1):(ランダムキャラ素質:7) = 1
		FOR LOCAL, 10, 19
			SIF ランダムキャラ素質:LOCAL == 0
				CONTINUE
			TALENT:(LOCAL:1):(ランダムキャラ素質番号(ランダムキャラ素質:LOCAL)) = ランダムキャラ素質値(ランダムキャラ素質:LOCAL)
		NEXT
		FOR LOCAL, 20, 25
			SIF ランダムキャラ素質:LOCAL == 0
				CONTINUE
			TALENT:(LOCAL:1):(ランダムキャラ素質番号(ランダムキャラ素質:LOCAL)) = ランダムキャラ素質値(ランダムキャラ素質:LOCAL)
		NEXT
		IF ランダムキャラ素質:26
			TALENT:(LOCAL:1):86 = 2
		ENDIF
		IF ランダムキャラ素質:27
			TALENT:(LOCAL:1):111 = 1
		ENDIF
		IF ランダムキャラ素質:28
			TALENT:(LOCAL:1):149 = 1
		ENDIF
		IF ランダムキャラ素質:29
			TALENT:(LOCAL:1):152 = 1
		ENDIF
		FOR LOCAL, 30, 36
			ABL:(LOCAL:1):(LOCAL - 30) = ランダムキャラ素質:LOCAL
			IF ランダムキャラ素質:LOCAL == 1
				EXP:(LOCAL:1):(LOCAL - 30) = ランダムキャラ素質:LOCAL * 20
				EXP:(LOCAL:1):(LOCAL - 25) = ランダムキャラ素質:LOCAL * 5
			ELSEIF ランダムキャラ素質:LOCAL == 2
				EXP:(LOCAL:1):(LOCAL - 30) = ランダムキャラ素質:LOCAL * 50
				EXP:(LOCAL:1):(LOCAL - 25) = ランダムキャラ素質:LOCAL * 15
			ENDIF
		NEXT
		FOR LOCAL, 40, 46
			IF ランダムキャラ素質:LOCAL
				ABL:(LOCAL:1):(LOCAL - 30) = ランダムキャラ素質:LOCAL
			ENDIF
		NEXT
		EXP:(LOCAL:1):10 = LOCAL:3
		TALENT:(LOCAL:1):容姿 = ランダムキャラ素質:50
		TALENT:(LOCAL:1):体格 = ランダムキャラ素質:52
		TALENT:(LOCAL:1):男性器サイズ = ランダムキャラ素質:53
		TALENT:(LOCAL:1):体型 = ランダムキャラ素質:54
		TALENT:(LOCAL:1):年齢層 = ランダムキャラ年齢層(ランダムキャラ素質:51)
		TALENT:(LOCAL:1):外見年齢 = ランダムキャラ外見年齢(ランダムキャラ素質:51)
		TALENT:(LOCAL:1):恋人持ち = ランダムキャラ恋人持ち(ランダムキャラ素質:55)
		TALENT:(LOCAL:1):セフレあり = ランダムキャラセフレあり(ランダムキャラ素質:55)
		IF ランダムキャラ出産経験(ランダムキャラ素質:55) >= 1
			TALENT:(LOCAL:1):子持ち = 1
			EXP:(LOCAL:1):妊娠経験 = ランダムキャラ出産経験(ランダムキャラ素質:55)
			EXP:(LOCAL:1):出産経験 = ランダムキャラ出産経験(ランダムキャラ素質:55)
		ENDIF
		;性知識はここに含まれる
		CSTR:(LOCAL:1):知識素質設定 '= @" %STRJOIN(ランダムキャラ知識素質, " ")% "
		STRDATA CSTR:(LOCAL:1):プレゼント好み
			DATA 음식 계열
			DATA 고기
			DATA 생선
			DATA 야채
			DATA 과자
			DATA 진미
			DATA 술
			DATA 차류
			DATA 예술품
			DATA 보석
			DATA 회화
			DATA 조각
			DATA 시
			DATA 미용계
			DATA 액세서리
			DATA 화장품
			DATA 실용품
			DATA 문방구
			DATA 건강기구
			DATA 일용품
			DATA 조리기구
			DATA 공구
			DATA 오락
DATA 꽃
DATA 책
DATA 장난감
DATA 기계
		ENDDATA
		SIF TALENT:(LOCAL:1):飲兵衛 > 0 && STRFIND(CSTR:(LOCAL:1):プレゼント好み, "酒") < 0
			CSTR:(LOCAL:1):プレゼント好み += "_술"

		;戦闘関連設定
		得意武器変換 = 
		FOR LOCAL, 0, 10
			IF GETBIT(ランダムキャラ戦闘タイプ:1, LOCAL)
				SIF 得意武器変換 != ""
					得意武器変換 += "・"
				得意武器変換 += @"%得意武器数値文字列変換(LOCAL)%"
			ENDIF
		NEXT
		CSTR:(LOCAL:1):戦闘基礎ステータス設定 = 속성_%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)% 특기 무기_%得意武器変換% 초기 스테이터스 타입_%成長タイプ:(ランダムキャラ戦闘タイプ:2)%

		CALL ADD_PERSON_FROM_CHARA(LOCAL:1)
		CALL コマンド存在チェック
		CALL キャラクター初期化(LOCAL:1)

		CFLAG:(LOCAL:1):滞在期間 = 999
		CFLAG:(LOCAL:1):招待不可フラグ = 0
		CFLAG:(LOCAL:1):探索参加許可フラグ = 1
		CSTR:(LOCAL:1):汎用立ち絵登録文字列 = %汎用画像キー%
		CALL ステータス記録(LOCAL:1)

		FOR LOCAL:2, 0, 従業員部屋数
			IF 従業員部屋割り配列:(LOCAL:2) < 1
				従業員部屋割り配列:(LOCAL:2) = LOCAL:1
				BREAK
			ENDIF
		NEXT
		CALL 従業員人数チェック()
		RETURN 0
	CASE 110
		GOTO 従業員_LOOP
	CASE 120
		INVERTBIT OPTION変数:汎用立ち絵制御, 0
		GOTO 表示_LOOP
	CASE 130
		INVERTBIT OPTION変数:ワーウルフ画像ケモ度, 0
		GOTO 表示_LOOP
	CASE 999
		RETURN 0
	CASEELSE
ENDSELECT
[SKIPEND]
