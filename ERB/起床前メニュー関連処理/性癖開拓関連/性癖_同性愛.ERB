
@性癖開拓チャレンジ処理_同性愛(ARG)
#DIMS 素質名

;同性愛
CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:PLAYER%은(는) %CALLNAME:ARG%에게 슬쩍"
素質名 '= GET_性癖素質NAME(GETNUM(性癖素質, "同性愛"), ARG)
IF 素質名 == "異性愛"
	CSTR:ARG:性癖開拓報告_記録文字列 += "「남녀가 몸을 겹치는 상황」의 야한 책이 건네지도록 잔꾀를 부렸다…<br>"
ELSEIF TALENT:ARG:性別 == 2
	CSTR:ARG:性癖開拓報告_記録文字列 += "「남성끼리 몸을 겹치는 상황」의 야한 책이 건네지도록 잔꾀를 부렸다…<br>"
ELSE
	CSTR:ARG:性癖開拓報告_記録文字列 += "「여성끼리 몸을 겹치는 상황」의 야한 책이 건네지도록 잔꾀를 부렸다…<br>"
ENDIF
CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"

;固有処理
RESULT = 0
CALL 口上変数初期化
TRYCALLFORM 性癖開拓_固有条件_同性愛_{NO:ARG}(ARG)
LOCAL = RESULT
SIF 口上有無確認(0, 1)
	CSTR:ARG:性癖開拓報告_記録文字列 += @"성벽 개척_고유 조건"
IF LOCAL == 1
	CSTR:ARG:性癖開拓報告_記録文字列 += @"<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%은(는) 성벽 소질[%素質名%(흥미)]을(를) 획득했다<br>"
	性癖素質:ARG:同性愛 = 1
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[%素質名%(興味)]を取得した</font>","うふふ")
	RETURN 1
ELSEIF LOCAL == -1
	RETURN 0
ENDIF

;エロ本を読むかどうか
IF 知識素質:ARG:性知識 < 性知識条件_性癖開拓チャレンジ()
	;性知識が-1か-2だと本の内容を理解できない
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%은(는) 어느새 근처에 있던 야한 책을 휙휙 넘겨 보았지만, 무슨 의미인지 잘 몰랐던 것 같다…<br>"
	RETURN -1
ENDIF

;基礎確率は2/10
LOCAL = 2
;叡智の殿堂補正
SIF 高速船記録:派遣先登録 == "叡智の殿堂"
	LOCAL += 1
;自制心、性的興味、性欲が影響
LOCAL -= TALENT:ARG:自制心
LOCAL += TALENT:ARG:性的興味 * 2
LOCAL += TALENT:ARG:性欲
SIF BASE:ARG:性欲 > 1500
	LOCAL += 1

SELECTCASE TALENT:ARG:性別嗜好
	CASE 1
		;男の男嫌いパターン
		SIF TALENT:ARG:性別 == 2
			LOCAL -= 5
	CASE 2
		;女の女嫌いパターン
		SIF TALENT:ARG:性別 == 1
			LOCAL -= 5
	CASE 3
		;そもそも人間が嫌い
		LOCAL -= 5
ENDSELECT

;最低確率1割は確保
LOCAL = MAX(LOCAL, 1)
;性欲無しだと読まない
SIF TALENT:ARG:性欲 == -2
	LOCAL = -1
;取得対象不適の場合も読まない
SIF 素質名 == ""
	LOCAL = -1
;補正確率より低ければ読む
IF RAND:10 < LOCAL
	;読むパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"…%CALLNAME:ARG%은(는) 어느새 근처에 있던 야한 책을 발견했다. <br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"주위를 두리번거리며 자신 혼자임을 확인하자, %CALLNAME:ARG%은(는) 몰래 그 책을 펼쳤다……<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%은(는) 성벽 소질[%素質名%(흥미)]을(를) 획득했다<br>"
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[%素質名%(興味)]を取得した</font>","うふふ")
	性癖素質:ARG:同性愛 = 1
	RETURN 1
ELSE
	;読まないパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%은(는) 어느새 근처에 있던 야한 책에 의아한 얼굴을 하고, 쓰레기통에 던져 버렸다<br>"
ENDIF

RETURN 0

@性癖段階成長_同性愛(キャラ番号)
#DIM キャラ番号
#DIMS 素質名
#DIMS 性愛対象
素質名 '= GET_性癖素質NAME(GETNUM(性癖素質, "同性愛"), キャラ番号)
SIF 素質名 == ""
	RETURN 0

性愛対象 '= SUBSTRINGU(素質名, 0, STRLENSU(素質名) - 1)

SELECTCASE 性癖素質:キャラ番号:同性愛
	CASE 1
		;経験が足りないとダメ
		SIF EXP:キャラ番号:同性愛経験 < 500
			RETURN 0
		CALL 性癖段階成長メッセージ_同性愛(キャラ番号, 性癖素質:キャラ番号:同性愛)
		性癖素質:キャラ番号:同性愛 = 2
		;取得メッセージ
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[%素質名%(興味)]が[%素質名%(愛好)]に成長した</font>","うふふ")

		RETURN 1
	CASE 2
		;経験が足りないとダメ
		SIF EXP:キャラ番号:同性愛経験 < 2000
			RETURN 0
		CALL 性癖段階成長メッセージ_同性愛(キャラ番号, 性癖素質:キャラ番号:同性愛)
		性癖素質:キャラ番号:同性愛 = 3
		;取得メッセージ
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[%素質名%(愛好)]が[%素質名%(中毒)]に成長した</font>","うふふ")

		RETURN 2
	CASE 3
		;既に中毒ならスキップ
		RETURN 0
ENDSELECT

@性癖段階成長メッセージ_同性愛(キャラ番号, 表示段階)
#DIM キャラ番号
#DIM 表示段階
#DIMS 素質名
#DIMS 性愛対象

素質名 '= GET_性癖素質NAME(GETNUM(性癖素質, "同性愛"), キャラ番号)
性愛対象 '= SUBSTRINGU(素質名, 0, STRLENSU(素質名) - 1)

SELECTCASE 表示段階
	CASE 1
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%의 성벽은 %性愛対象%와(과) 살을 맞대는 행위를 반복함으로써 더욱 강해진 것 같다…
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%의 성벽 소질[%素質名%(흥미)]은(는) [%素質名%(애호)]으로 성장했다
		PRINTL
	CASE 2
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%은(는), 이제 %性愛対象%와의 정사에 매료되어 버렸다…
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%의 성벽 소질[%素質名%(애호)]은(는) [%素質名%(중독)]으로 성장했다
		PRINTL
ENDSELECT
