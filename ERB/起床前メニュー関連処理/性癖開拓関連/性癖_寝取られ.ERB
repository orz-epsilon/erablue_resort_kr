
@性癖開拓チャレンジ処理_寝取られ(ARG)
;寝取られ

;気になってどういうものか見に来たあなたに向けて簡単な仕様説明
;
;・性癖開拓システムでリゾート客(仮にＡとする)にエロ本を押し付けてＡの脳を破壊し、性癖素質[寝取られ(興味)]を与えます。
;・縁結びシステムを使って、Ａと他のキャラ(仮にＢとする)をくっつけます。関係性は恋慕でもセフレでもＯＫ。
;・あなたとＡの「信頼度」を、一定以上にします。その際あなたが「遊び人」として極まってきているなら、このボーダーはちょっとだけ下がります。Ａが「従業員」であるなら、このボーダーは劇的に下がります。
;上記のみっつの条件を、すべて満たせば準備完了！(別にＡもＢも両方寝取られ性癖持ってても大丈夫だよ！)
;
;「ＡとＢがどちらも島にいる」状態で、あなたが「自室」で「就寝」する際、「他に同衾するキャラがいなければ(一人寝であるなら)」確率でイベントが発生。
;一日の終わりに、度し難い性癖に脳を破壊されたＡがあなたの部屋を訪ねてきて、「Ｂを抱いてくれ」とお願いされます。性癖名は「寝取られ」だけど、Ａがやる実際のプレイは「寝取らせ」が正しいわけですな。
;いやぁ、頼まれちゃったからなぁ～！しょうがないなぁ～～～！！ほな、いただきま～～～っす！！
;
;毎日来られても困るし特別感もないので、発生確率は低め。「こんなもんかな～」で適当に決めてるので、低すぎる！と思ったら本スレとかDiscordで教えてください。調整します。
;実際問題、発生に一人寝を要求すること自体がプレイに対する一つの縛りになっちゃってるから、もうちょい高くてもいいかもな～とは書いた本人も思ってます。
;抽出処理とタイミングの関係で、条件を満たしたカップルが増えるほどに発生確率は平均化されちゃうし、狙った組み合わせも出づらくなるし。
;あなたの「遊び人」が５とかだと確率がちょっと上がる。理由は確率処理んとこに書いた。
;
;さいごに
;エロ本ばら撒きには要注意。みんながみんな性癖持ちだと、「男あなたにグラン(♂)を差し出してくるジータ(寝取られ性癖持ち)」とかそういう事故が起きます。
;まぁ、意図して除外処理しなかったんですけども。だ、だってそういう需要もあるかもだし……。

CSTR:ARG:性癖開拓報告_記録文字列 += @" %CALLNAME:PLAYER%은(는) %CALLNAME:ARG%에게 슬쩍 「소중한 사람이 다른 사람에게 안기는 상황」의 야한 책이 건네지도록 잔꾀를 부렸다…<br>"
CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"

;固有処理
RESULT = 0
CALL 口上変数初期化
TRYCALLFORM 性癖開拓_固有条件_寝取られ_{NO:ARG}(ARG)
LOCAL = RESULT
SIF 口上有無確認(0, 1)
	CSTR:ARG:性癖開拓報告_記録文字列 += @"성벽 개척_고유 조건"
IF LOCAL == 1
	CSTR:ARG:性癖開拓報告_記録文字列 += @"<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%은(는) 성벽 소질[네토리(흥미)]를 취득했다<br>"
	性癖素質:ARG:寝取られ = 1
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[寝取られ(興味)]を取得した</font>","うふふ")
	RETURN 1
ELSEIF LOCAL == -1
	RETURN 0
ENDIF

;エロ本を読むかどうか
IF 知識素質:ARG:性知識 < 性知識条件_性癖開拓チャレンジ()
	;性知識が-1か-2だと本の内容を理解できない
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%은(는) 어느새 근처에 있던 야한 책을 휙휙 넘겨 보았지만, 무슨 의미인지 잘 몰랐던 것 같다…<br>"
	RETURN -1
ENDIF

;基礎確率は2/10
LOCAL = 2
;叡智の殿堂補正
SIF 高速船記録:派遣先登録 == "叡智の殿堂"
	LOCAL += 1
;自制心、プライド、性的興味、性欲が影響
LOCAL -= TALENT:ARG:自制心
LOCAL -= TALENT:ARG:プライド
LOCAL += TALENT:ARG:性的興味 * 2
LOCAL += TALENT:ARG:性欲
SIF BASE:ARG:性欲 > 1500
	LOCAL += 1

;マゾ性感が影響
LOCAL += ABL:ARG:マゾ性感

;男性器サイズ
;自分の「男としての才能/素養」にコンプレックスがあるほどこういうのに陥りやすいんじゃなかろうか、という発想
IF TALENT:ARG:男性器サイズ == -2
	LOCAL += 2
ELSEIF TALENT:ARG:男性器サイズ == -1
	LOCAL += 1
ENDIF

;最低確率1割は確保
LOCAL = MAX(LOCAL, 1)
;性欲無しだと読まない
SIF TALENT:ARG:性欲 == -2
	LOCAL = -1
;補正確率より低ければ読む
IF RAND:10 < LOCAL
	;読むパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"…%CALLNAME:ARG%은(는) 어느새 근처에 있던 야한 책을 발견했다. <br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"주위를 두리번거리며 자신 혼자임을 확인하자, %CALLNAME:ARG%은(는) 몰래 그 책을 펼쳤다……<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"자신의 소중한 사람이 다른 사람에게 안기고, 결국에는 빼앗겨 버린다는 이 책의 내용에, %CALLNAME:ARG%은(는) 왠지 모르게, 애달프고 괴로운 마음과는 반대로"
	IF GETBIT(TALENT:ARG:性別, 1)
	;ちんちんあるなら 鬱勃起(七五調)(字余り)
		CSTR:ARG:性癖開拓報告_記録文字列 += "울기"
	ELSE
		CSTR:ARG:性癖開拓報告_記録文字列 += "흥분"
	ENDIF
	CSTR:ARG:性癖開拓報告_記録文字列 += @"이 멈추지 않아!<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"……이윽고, 하나의 이야기에 뇌를 불태운 %CALLNAME:ARG%은(는) 성벽 소질[네토리(흥미)]를 취득해 버렸다!<br>"
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[寝取られ(興味)]を取得した</font>","うふふ")
	性癖素質:ARG:寝取られ = 1
	RETURN 1
ELSE
	;読まないパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%은(는) 어느새 근처에 있던 야한 책에 의아한 얼굴을 하고, 쓰레기통에 던져 버렸다<br>"
ENDIF

RETURN 0

@性癖段階成長_寝取られ(キャラ番号)
#DIM キャラ番号

SELECTCASE 性癖素質:キャラ番号:寝取られ
	CASE 1
		;経験が足りないとダメ
		;いままで興味だったものが、実践を経て確信に変わり、自身の中に深く根付く。故に、愛好になるのは「最初の一歩を踏み出してしまったその直後」だと思う。異論は認める。
		SIF EXP:キャラ番号:寝取られ経験 < 1
			RETURN 0
		CALL 性癖段階成長メッセージ_寝取られ(キャラ番号, 性癖素質:キャラ番号:寝取られ)
		性癖素質:キャラ番号:寝取られ = 2
		;取得メッセージ
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[寝取られ(興味)]が[寝取られ(愛好)]に成長した</font>","うふふ")

		RETURN 1
	CASE 2
		;経験が足りないとダメ
		;事が事だけに、5回もリピれば立派な中毒だと思う
		SIF EXP:キャラ番号:寝取られ経験 < 5
			RETURN 0
		CALL 性癖段階成長メッセージ_寝取られ(キャラ番号, 性癖素質:キャラ番号:寝取られ)
		性癖素質:キャラ番号:寝取られ = 3
		;取得メッセージ
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[寝取られ(愛好)]が[寝取られ(中毒)]に成長した</font>","うふふ")

		RETURN 2
	CASE 3
		;既に中毒ならスキップ
		RETURN 0
ENDSELECT

@性癖段階成長メッセージ_寝取られ(キャラ番号, 表示段階)
#DIM キャラ番号
#DIM 表示段階

SELECTCASE 表示段階
	CASE 1
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%의 네토리에 대한 흥미는, 실제로 안게 해 본 것으로 확실한 성벽으로서 %CALLNAME:キャラ番号% 안에 깊이 뿌리내려 버린 것 같다……
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%의 성벽 소질[네토리(흥미)]는 [네토리(애호)]로 성장해 버렸다!
		PRINTL
	CASE 2
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%은(는) 이제 %CALLNAME:PLAYER%에게 자신의 여자를 내주는 것에, 파멸적인 매력을 느끼고 있다……
		PRINTL 
PRINTFORML %CALLNAME:キャラ番号%의 성벽 소질[네토리(애호)]은(는) [네토리(중독)]으로 성장해 버렸다!
		PRINTL
ENDSELECT

