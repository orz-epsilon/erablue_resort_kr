;==================================================================================================
@性癖開拓チャレンジ処理_ゆきずりセックス(ARG)
;-------------------------------------------------
;ゆきずりセックスを取得するための処理
;--------------------------------------------------------------------------------------------------
#DIM 興味度
;--------------------------------------------------------------------------------------------------
CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:PLAYER%은(는) %CALLNAME:ARG%에게 슬쩍 「우연한 성교를 즐기는 여성」을(를) 그린 에로책이 건네지도록 계략을 꾸몄다…<br><br>"

;固有処理
RESULT = 0
CALL 口上変数初期化
TRYCALLFORM 性癖開拓_固有条件_ゆきずりセックス_{NO:ARG}(ARG)
LOCAL = RESULT
SIF 口上有無確認(0, 1)
	CSTR:ARG:性癖開拓報告_記録文字列 += @"성벽 개척_고유 조건"
IF LOCAL == 1
	CSTR:ARG:性癖開拓報告_記録文字列 += @"<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%은(는) 성벽 소질[우연한 섹스(흥미)]을(를) 획득했다<br>"
	性癖素質:ARG:ゆきずりセックス = 1
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[ゆきずりセックス(興味)]を取得した</font>","うふふ")
	RETURN 1
ELSEIF LOCAL == -1
	RETURN 0
ENDIF


;---------------------------------------
;エロ本を読むかどうか
IF 知識素質:ARG:性知識 < 性知識条件_性癖開拓チャレンジ()
	;性知識が-1か-2だと本の内容を理解できない
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%은(는) 어느새 근처에 있던 에로책을 휙휙 넘겨봤지만, 무슨 의미인지 잘 몰랐던 것 같다…<br>"
	RETURN -1
ENDIF

;基礎確率は10%
LOCAL = 10

;興味度を初期化
興味度 = 10

;--- プラス補正 ------------------------
;[性欲強い]             … +10%
;[好奇心]               … +10%
;現在の性欲値が1500以上 … +10%
;[ABL:欲望]             … 欲望 * 10%
;一番多い性経験         … 経験数10につき+1%

;マイナス補正が激しい性癖の為、調教による補正が高め


;好奇心が影響
SIF TALENT:ARG:性的興味 > 0
	興味度 += 10

;性欲強いが影響
SIF TALENT:ARG:性欲 > 0
	興味度 += 10

;現在の性欲値が影響
SIF BASE:ARG:性欲 > 1500
	興味度 += 10

;欲望が影響
興味度 += ABL:ARG:欲望 * 10

;それぞれの性交経験のうち一番多い経験が影響
興味度 += MAX(EXP:ARG:Ｖ性交経験, EXP:ARG:Ａ性交経験, EXP:ARG:手淫経験, EXP:ARG:口淫経験, EXP:ARG:Ｃ性交経験_Ｖ挿入, EXP:ARG:Ｃ性交経験_Ａ挿入) / 10


;--- マイナス補正 ----------------------
;[プライドが高い] …  -5%
;[恥じらい]       … -10%
;[保守的]         … -10%
;[性欲薄い]       … -10%
;[自制心]         … -20%
;[抵抗]           … -20%
;[一線を越えない] … -20%
;[貞操観念]       … -30%
;全部盛で-125%

;プライドが影響
SIF TALENT:ARG:プライド > 0
	興味度 -= 5

;恥じらいが影響
SIF TALENT:ARG:羞恥心 > 0
	興味度 -= 10

;保守的が影響
SIF TALENT:ARG:性的興味 < 0
	興味度 -= 10

;性欲薄いが影響
SIF TALENT:ARG:性欲 == -1
	興味度 -= 10

;自制心なら大きく影響
SIF TALENT:ARG:自制心 > 1
	興味度 -= 20

;抵抗持ちなら大きく影響
SIF TALENT:ARG:抵抗
	興味度 -= 20

;一線を越えない持ちなら大きく影響
SIF TALENT:ARG:一線越えない
	興味度 -= 20

;貞操観念ならかなり大きく影響
SIF TALENT:ARG:貞操
	興味度 -= 30


;--- 最終補正 --------------------------

;最低確率1割は確保
LOCAL = MAX(LOCAL, 興味度)

;[処女][恋人持ち][人妻][未亡人][想い人あり]なら最終確率を半減させる
SIF TALENT:ARG:処女 || TALENT:ARG:恋人持ち
	LOCAL /= 2

;叡智の殿堂なら最終確率に直接加算
SIF 高速船記録:派遣先登録 == "叡智の殿堂"
	LOCAL += 10

;経験豊富、淫乱であるなら確定
SIF TALENT:ARG:経験豊富 || TALENT:ARG:淫乱
	LOCAL = 100

;性欲無しだと読まない
SIF TALENT:ARG:性欲 == -2
	LOCAL = -1
	
;現在は女性しか読まない
SIF TALENT:ARG:性別 != 1
	LOCAL = -1


;---------------------------------------

;補正確率より低ければ読む
IF RAND:100 < LOCAL
	;読むパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"…%CALLNAME:ARG%은(는) 어느새 근처에 있던 에로책을 발견했다.<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"주위를 두리번거리며 자신 혼자임을 확인하자, %CALLNAME:ARG%은(는) 몰래 그 책을 펼쳤다……<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	
	;性別で分岐
	IF TALENT:ARG:性別 == 1
		CSTR:ARG:性癖開拓報告_記録文字列 += @"그곳에는 막 만난 모르는 남자에게 다리를 벌리고, 즉석에서 쾌락을 즐기는 암컷의 모습이 그려져 있었다.<br>"
	ELSE  
		CSTR:ARG:性癖開拓報告_記録文字列 += @"그곳에는 막 만난 모르는 암컷을 오나홀 대신으로, 즉석에서 성욕을 발산하는 남자의 모습이 그려져 있었다.<br>"
	ENDIF
	
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	
	;貞操観念で分岐
	IF TALENT:ARG:貞操 == 1
		CSTR:ARG:性癖開拓報告_記録文字列 += @"정조를 중시하는%CALLNAME:ARG%은(는), 처음에는 그 모습에 혐오감을 느꼈지만……<br>"
		CSTR:ARG:性癖開拓報告_記録文字列 += @"가볍게 성을 탐하는 그 모습에 어딘가 동경과 비슷한 끌리는 것을 느끼며, 페이지를 넘기는 손을 멈출 수 없었다.<br>"
		CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
		CSTR:ARG:性癖開拓報告_記録文字列 += @"페이지를 넘길 때마다 %CALLNAME:ARG%의 마음속 깊이 있던 정조 관념이 무너져간다……<br>"
	ELSE
		CSTR:ARG:性癖開拓報告_記録文字列 += @"페이지를 넘길 때마다 %CALLNAME:ARG%의 가치관이 음란한 방향으로 업데이트되어간다……<br>"
		CSTR:ARG:性癖開拓報告_記録文字列 += @"그리고 %CALLNAME:ARG%은(는) 깨닫고 말았다.<br>"
		CSTR:ARG:性癖開拓報告_記録文字列 += @"섹스는 「언제든지」「어디서든지」「누구와도」할 수 있는 행위라는 것을……！<br>"
	ENDIF
	
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%은(는) 성벽 소질[우연한 섹스(흥미)]을(를) 획득했다<br>"
	性癖素質:ARG:ゆきずりセックス = 1
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[ゆきずりセックス(興味)]を取得した</font>","うふふ")
	RETURN 1
	
ELSEIF 興味度 <= 0
	;興味が0もしくはマイナスなら読みもしない
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%은(는) 어느새 근처에 있던 야한 책을 의심스러운 얼굴로 바라보며 쓰레기통에 던져버렸다<br>"
	
ELSE
	;興味が少しでもあれば読むけど癖の扉は開かない
	CSTR:ARG:性癖開拓報告_記録文字列 += @"…%CALLNAME:ARG%은(는) 어느새 근처에 있던 야한 책을 발견했다. <br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"주위를 두리번거리며 자신 혼자임을 확인하자, %CALLNAME:ARG%은(는) 몰래 그 책을 펼쳤다……<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"손에 들고 즐긴 듯하지만, 새로운 성벽이 될 정도까지는 이르지 못한 듯하다……<br>"

ENDIF

RETURN 0

;==================================================================================================
@性癖段階非同室成長_ゆきずりセックス(キャラ番号)
;-------------------------------------------------
;ゆきずりセックスの性癖レベルの上昇処理
;--------------------------------------------------------------------------------------------------
#DIM キャラ番号
;--------------------------------------------------------------------------------------------------

SELECTCASE 性癖素質:キャラ番号:ゆきずりセックス
	CASE 1
		;経験が足りないとダメ
		SIF EXP:キャラ番号:ゆきずりセックス経験 < 30
			RETURN 0
		性癖素質:キャラ番号:ゆきずりセックス = 2
		;取得メッセージ
		DRAWLINE
		IF 知識素質:キャラ番号:高貴 >= 1
			PRINTFORML 지위 있는 몸이면서도, 한순간의 쾌락을 위해 경솔하게 몸을 허락하고 피를 더럽히는 행위.
			PRINTFORML 모르는 상대에게 본래 숨겨야 할 옥체를 드러내고, 격렬하게 탐닉당함으로써 얻는 고귀한 몸의 가치 재인식.
			PRINTL
			PRINTFORML 고귀한 %TEXTR("혈통/선조/혈통")%에 대한 모독. 그 배덕적인 쾌락의 맛을 알아버린 %CALLNAME:キャラ番号%은(는) 더 이상 단순한 흥미라고는 할 수 없을 정도로 이 행위에 빠져들고 있는 듯하다…
		ELSEIF アイドル判定(キャラ番号)
			PRINTFORML 팬들이 아무리 많은 재산을 바쳐도 뵙는 것조차 허락되지 않는 몸.
			PRINTFORML 그 만질 수 없는 부드러운 살결을, 침 흘리는 입술을, 극상의 암컷 구멍을 모르는 상대에게 드러내고, 내어주고, 구석구석까지 탐닉하게 하는 방탕.
			PRINTFORML 아이돌로서 팬들에게 꿈을 보여주는 입장이면서도, 한순간의 쾌락을 위해 다리를 벌리는 배신 행위.
			PRINTL
			PRINTFORML 아이돌로서의 상품을 훼손하고, 여자로서의 가치마저도 내버리는 파멸적인 도취 쾌락의 맛을 알아버린 %CALLNAME:キャラ番号%은(는),
			PRINTFORML 더 이상 단순한 흥미라고는 할 수 없을 정도로 이 행위에 빠져들고 있는 듯하다…
		ELSE
			PRINTFORML %CALLNAME:キャラ番号%은(는) 스쳐 지나가는 상대와의 손쉬운 교미를 반복함으로써, 처음 만나는 상대에게도 다리를 벌리는 것에 저항감이 없어진 듯하다…
		ENDIF
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%의 성벽 소질[스쳐 지나가는 섹스(흥미)]은(는)[스쳐 지나가는 섹스(애호)]로 성장했다
		IF TALENT:キャラ番号:貞操 == 1
			PRINTFORML %CALLNAME:キャラ番号%은(는)[정조 관념]을 잃었다…
			TALENT:キャラ番号:貞操 = 0
		ENDIF
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[ゆきずりセックス(興味)]が[ゆきずりセックス(愛好)]に成長した</font>","うふふ")
		PRINTL

		RETURN 1
	CASE 2
		;経験が足りないとダメ
		SIF EXP:キャラ番号:ゆきずりセックス経験 < 100
			RETURN 0
		性癖素質:キャラ番号:ゆきずりセックス = 3
		;取得メッセージ
		DRAWLINE
		IF 知識素質:キャラ番号:高貴 >= 1
			PRINTFORML 고향에서 멀리 떨어진 곳이라는 것을 빌미로, 입장을 벗어던지고 그 피의 책무마저 내던지고.
			PRINTFORML 도를 넘어 누구에게나 마구 관계를 맺는 방종한 쾌락의 포로가 되어버린 %CALLNAME:キャラ番号%는,
			PRINTFORML 그 고귀한 입술이나 귀한 암컷 구멍을, 신분도 입장도 훨씬 열등한 백성들이 욕망을 쏟아내는 변기처럼 내미는 것에조차 도취된 쾌락을 느끼게 되어버렸다…
		ELSEIF アイドル判定(キャラ番号)
			PRINTFORML 팬들의 눈이 없다는 것을 빌미로, 입장을 벗어던지고 아이돌로서의 프로 의식마저 내던지고.
			PRINTFORML 팬들에게서 바쳐지고 벌어들인 재산을, 때로는 상대에게 내주면서까지 쾌락을 탐하는 음탕함.
			PRINTL
			PRINTFORML 완전히 이 찰나적인 쾌락의 포로가 되어버린 %CALLNAME:キャラ番号%에게는, 이제 아이돌로서 갈고닦아온 그 몸은
			PRINTFORML 팬이 아닌 하룻밤 상대를 매료시키고 유혹하는, 성적 쾌락의 유아등이 되어버렸다…
		ELSE
			PRINTFORML 이제 %CALLNAME:キャラ番号%는, 적당한 상대로 일기일회의 쾌락을 탐하는 행위가 생활의 일부가 되어버렸다…
		ENDIF
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%의 성벽 소질[원나잇 섹스(애호)]은 [원나잇 섹스(중독)]으로 성장했다
		IF TALENT:キャラ番号:貞操 == 0
			PRINTFORML 원나잇으로 맺은 관계도 이제 백을 헤아리고, 손님들 사이에서도 "누구에게나 다리를 벌린다"고 소문나는 %CALLNAME:キャラ番号%는 [정조 무관심]을 얻었다
			TALENT:キャラ番号:貞操 = -1
		ENDIF
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[ゆきずりセックス(愛好)]が[ゆきずりセックス(中毒)]に成長した</font>","うふふ")
		PRINTL

		RETURN 1
	CASE 3
		;既に中毒ならスキップ
		RETURN 0
ENDSELECT

