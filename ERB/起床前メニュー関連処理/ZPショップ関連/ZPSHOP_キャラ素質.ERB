;-------------------------------------------------
;ZPショップキャラの素質変化メニュー(キャラ選択)
;-------------------------------------------------
@ZP_SHOP_素質変化
;現在滞在しているキャラ一覧
#DIM 表示候補, キャラクタ数上限
#DIM 現在ページ
#DIM 最大ページ
#DIM 広告対象
#DIM 表示候補人数
#DIM CONST ページ表示人数 = 15
#DIM 表示番号
#DIM 入力値
#DIM 対象キャラ
#DIM BTN_NUM
#DIMS BTN_TEXT
#DIMS ソート情報
#DIM DYNAMIC 初回実行フラグ = 1
#DIM DYNAMIC リスト生成フラグ = 1
#DIM DYNAMIC フィルタ適用フラグ = 1
#DIM 描画開始行
#DIM RETURN_VAL
#DIM REDRAW_TEMP
#DIM NO_LEN
#DIM PAGE_LEN
#DIM CONST LINE_LEN  = 80
#DIM CONST SPACE_LEN = 56
#DIMS ページ番号文字列
#DIM ページ番号文字列長
; 内部変数：フィルタソートボタンセット用
#DIM フィルタボタン番号
#DIM ソートボタン番号
#DIM ソート順ボタン番号
#DIM 素質絞込ボタン番号

IF 初回実行フラグ
	初回実行フラグ = 0
	VARSET 表示候補, -1
	現在ページ = 0
	描画開始行 = LINECOUNT
	REDRAW_TEMP = CURRENTREDRAW()
	REDRAW 1
ENDIF
IF リスト生成フラグ
	リスト生成フラグ = 0
	;表示キャラを登録
	表示候補人数 = 0
	FOR 対象キャラ, 1, CHARANUM
		;滞在してないキャラは出さない
		SIF CFLAG:対象キャラ:滞在期間 < 0
			CONTINUE
		;eraは出さない
		SIF NO:対象キャラ == 998
			CONTINUE
		表示候補:表示候補人数 = 対象キャラ
		表示候補人数 ++
	NEXT
	フィルタ適用フラグ = 1
ENDIF
IF フィルタ適用フラグ
	フィルタ適用フラグ = 0
	CALL キャラ配列フィルタ処理(表示候補, -1)
	表示候補人数 = RESULT
	; 人数からページ数計算(切り上げ除算)
	; 最小ページ番号が1ではなく0なので最大ページを-1
	最大ページ = MAX((表示候補人数 + ページ表示人数 - 1) / ページ表示人数, 1) - 1

	; 表示する数値の長さを測定
	NO_LEN   = LOG10(MAX(表示候補人数 , 1)) + 1
	PAGE_LEN = LOG10(MAX(最大ページ +1, 1)) + 1
	; 表示ページの補正
	SIF 現在ページ > 最大ページ
		現在ページ = 最大ページ
ENDIF

;変更対象の表示
PRINTL
DRAWLINE
PRINTL 누구의 소질을 강화하시겠습니까?
PRINTFORML 보유 ZP:{FLAG:ZP所持量}.{(ZP小数点以下所持量 % 1000) / 100}{(ZP小数点以下所持量 % 100) / 10}{(ZP小数点以下所持量 % 10)}
PRINTSL "-" * LINE_LEN

; あなたは別枠で常に表示
PRINTBUTTON @"[9998] %NAME表示(MASTER)%", 9998
PRINTL
PRINTSL "-" * LINE_LEN
;キャラの表示
FOR 表示番号, (現在ページ * ページ表示人数), (現在ページ + 1) * ページ表示人数
	対象キャラ = 表示候補:表示番号
	;登録が終わったら表示終了
	IF 対象キャラ == -1
		PRINTL
		CONTINUE
	ENDIF
	ソート情報 '= ソート情報表示(対象キャラ)
	IF ソート情報 != ""
		PRINTBUTTON @"[{表示番号, NO_LEN}] %GET_NAME(対象キャラ, 1)%", 表示番号
		PRINTFORM 　%ソート情報%
	ELSE
		PRINTBUTTON @"[{表示番号, NO_LEN}] %GET_NAME(対象キャラ)%", 表示番号
	ENDIF
	PRINTL
NEXT

;- ページ操作ボタン
PRINTSL "-" * LINE_LEN
; 1ページ前へ
BTN_NUM   = 9001
BTN_TEXT '= @"[前ページへ]"
IF 現在ページ > 0
	PRINTBUTTON BTN_TEXT, BTN_NUM
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAINFORM %BTN_TEXT%
	RESETCOLOR
ENDIF
; ページ数
ページ番号文字列  '= @"- {現在ページ + 1, LOG10(MAX(最大ページ +1, 1)) + 1}/{最大ページ + 1, LOG10(MAX(最大ページ +1, 1)) + 1} -"
ページ番号文字列長 =SPACE_LEN - STRLENS(ページ番号文字列)
PRINTS " " * (ページ番号文字列長 / 2)
PRINTFORM %ページ番号文字列%
; 割り切れなかった分のスペースは後半に足す
PRINTS " " * (ページ番号文字列長 / 2 + (ページ番号文字列長 % 2))
; 1ページ次へ
BTN_NUM   = 9011
BTN_TEXT '= @"[次ページへ]"
IF 現在ページ < 最大ページ
	PRINTBUTTON BTN_TEXT, BTN_NUM
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAINFORM %BTN_TEXT%
	RESETCOLOR
ENDIF
PRINTL
; 最初のページへ
BTN_NUM   = 9009
BTN_TEXT '= "[|≪]"
IF 現在ページ > 0
	PRINTBUTTON BTN_TEXT, BTN_NUM
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAINFORM %BTN_TEXT%
	RESETCOLOR
ENDIF
PRINTS " " * 1
; 5ページ前へ
BTN_NUM   = 9005
BTN_TEXT '= "[－５]"
IF 現在ページ > 0
	PRINTBUTTON BTN_TEXT, BTN_NUM
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAINFORM %BTN_TEXT%
	RESETCOLOR
ENDIF
; 余白
PRINTS " " * SPACE_LEN

; 5ページ次へ
BTN_NUM   = 9015
BTN_TEXT '= "[＋５]"
IF 現在ページ < 最大ページ
	PRINTBUTTON BTN_TEXT, BTN_NUM
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAINFORM %BTN_TEXT%
	RESETCOLOR
ENDIF
PRINTS " " * 1
; 最後のページへ
BTN_NUM   = 9019
BTN_TEXT '= "[≫|]"
IF 現在ページ < 最大ページ
	PRINTBUTTON BTN_TEXT, BTN_NUM
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAINFORM %BTN_TEXT%
	RESETCOLOR
ENDIF
PRINTL

PRINTSL "-" * LINE_LEN
;- その他ボタン
;フィルタソートボタン
フィルタボタン番号 = 9020
ソートボタン番号   = 9030
ソート順ボタン番号 = 9031
素質絞込ボタン番号 = 9040
CALL フィルタソートボタンセット表示(フィルタボタン番号, ソートボタン番号, ソート順ボタン番号, 素質絞込ボタン番号)

PRINTSL "-" * LINE_LEN
; 戻るボタン
BTN_NUM   = 9999
BTN_TEXT '= @"[{BTN_NUM}]戻る"
PRINTBUTTON BTN_TEXT, BTN_NUM
PRINTS " " * 5
PRINTL
REDRAW 0

BINPUT
入力値 = RESULT
RETURN_VAL = 0
SELECTCASE 入力値
	CASE フィルタボタン番号, ソートボタン番号, ソート順ボタン番号, 素質絞込ボタン番号
		CALL フィルタソートボタンセット実行部(入力値, フィルタボタン番号, ソートボタン番号, ソート順ボタン番号, 素質絞込ボタン番号)
		現在ページ = 0
		リスト生成フラグ = 1
	CASE 9001, 9005
		; ページ戻し
		現在ページ = MAX(現在ページ - (入力値 - 9000), 0)
	CASE 9011, 9015
		; ページ送り
		現在ページ = MIN(現在ページ + (入力値 - 9010), 最大ページ)
	CASE 9009
		; 最初のページ
		現在ページ = 0
	CASE 9019
		; 最後のページ
		現在ページ = 最大ページ
	CASE 9999
		; 戻る
		RETURN_VAL = -1
	CASE 9998
		; キャラ選択：あなた
		CALL ZP_素質変化(MASTER)

	CASEELSE
		; キャラ選択
		表示番号 = 入力値
		DO
			CALL ZP_素質変化(表示候補:表示番号)
			SELECTCASE RESULT
				CASE 1
					; 前のキャラ
					表示番号 --
					SIF 表示番号 < 0
						表示番号 = 表示候補人数 - 1
				CASE 2
					; 次のキャラ
					表示番号 ++
					SIF 表示候補人数 <= 表示番号
						表示番号 = 0
				CASE -1
					; 終了
					リスト生成フラグ = 1
					BREAK
			ENDSELECT
		LOOP 1
ENDSELECT
IF !RETURN_VAL
	CLEARLINE LINECOUNT - 描画開始行
	RESTART
ENDIF
REDRAW REDRAW_TEMP
RETURN RETURN_VAL


@ZP_素質変化(キャラ番号)
#DIM キャラ番号
#DIM 対象画像数
#DIM リロールボタン有効

TRYCALLFORM ZP_素質変化_固有処理_{NO:キャラ番号}(キャラ番号)
SIF RESULT == -1
	RETURN -1

; キャラ確認用の簡易紹介
CALL キャラ簡易紹介(キャラ番号)
対象画像数 = RESULT
リロールボタン有効 = 対象画像数 > 5

PRINTL ZP를 소비하여 캐릭터의 소질을 변화시킬 수 있습니다.
PRINTL 어떤 소질을 변화시키겠습니까?
PRINTFORML 보유 ZP:{FLAG:ZP所持量}.{(ZP小数点以下所持量 % 1000) / 100}{(ZP小数点以下所持量 % 100) / 10}{(ZP小数点以下所持量 % 10)}
DRAWLINE
PRINTBUTTONLC "[1]감각계 소질을 강화한다", 1
PRINTL
IF GETBIT(TALENT:キャラ番号:性別, 0) && TALENT:キャラ番号:Ｂ感度 > -2
	PRINTBUTTONLC "[2]가슴을 변화시킨다", 2
	PRINTL
ENDIF
IF GETBIT(TALENT:キャラ番号:性別, 1) && TALENT:キャラ番号:Ｃ感度 > -2
	PRINTBUTTONLC "[3]남성기를 변화시킨다", 3
	PRINTL
ENDIF
PRINTBUTTONLC "[4]성별을 변화시킨다", 4
PRINTL
PRINTBUTTONLC "[5]종족을 변화시킨다", 5
PRINTL
IF TALENT:キャラ番号:妊娠不可 && GETBIT(TALENT:キャラ番号:性別, 0) && TALENT:キャラ番号:Ｖ感度 > -2
	PRINTBUTTONLC "[6]임신을 가능하게 하다", 6
	PRINTL
ENDIF
IF !TALENT:キャラ番号:妊娠不可 && GETBIT(TALENT:キャラ番号:性別, 0) && TALENT:キャラ番号:Ｖ感度 > -2 && CFLAG:キャラ番号:特殊妊娠蓄積度 >= 2
	PRINTBUTTONLC "[7]수태 체질을 변화시키다", 7
	PRINTL
ENDIF
IF TALENT:キャラ番号:性欲 != -2
	;オプションを見ておく
	IF TALENT:キャラ番号:種族 == 2 && TALENT:キャラ番号:性別 != 2 && GETBIT(OPTION変数:発情期切り替え, 0)
	ELSEIF TALENT:キャラ番号:種族 == 2 && TALENT:キャラ番号:性別 == 2 && GETBIT(OPTION変数:発情期切り替え, 1)
	ELSEIF (TALENT:キャラ番号:種族 == 3|| キャラ一致チェック(キャラ番号, "[六竜の『金』]ガレヲン")) && TALENT:キャラ番号:性別 != 2 && GETBIT(OPTION変数:発情期切り替え, 2)
	ELSEIF (TALENT:キャラ番号:種族 == 3|| キャラ一致チェック(キャラ番号, "[六竜の『金』]ガレヲン")) && TALENT:キャラ番号:性別 == 2 && GETBIT(OPTION変数:発情期切り替え, 3)
	ELSEIF TALENT:キャラ番号:発情期あり > 0 && TALENT:キャラ番号:性別 != 2 && GETBIT(OPTION変数:発情期切り替え, 4)
	ELSEIF TALENT:キャラ番号:発情期あり > 0 && TALENT:キャラ番号:性別 == 2 && GETBIT(OPTION変数:発情期切り替え, 5)
	ELSE
		PRINTBUTTONLC "[8]발정기 소질을 변화시키다", 8
		PRINTL
	ENDIF
ENDIF
PRINTBUTTONLC "[9]체격·체형을 변화시키다", 9
PRINTL
IF あなた特殊能力:ハーレム >= 2
	IF TALENT:キャラ番号:ハーレムの心得
		SETCOLOR カラーパレット("グレーアウト")
		PRINTPLAIN [10]하렘의 마음가짐을 취득시키다 (취득 완료)
		RESETCOLOR
	ELSE
		;ハーレム一人前以上だと他者にハーレムの心得を付与可能
		PRINTBUTTONLC "[10]하렘의 마음가짐을 취득시키다", 10
	ENDIF
	PRINTL
ENDIF
IF フレーバー素質:キャラ番号:フレーバー素質_素質表示設定 == 0 || フレーバー素質:キャラ番号:フレーバー素質_素質表示設定 == 1
	PRINTBUTTONLC "[20]플레이버 소질을 재설정하다", 20
	PRINTL
ENDIF
PRINTBUTTONLC "[30]약점 커맨드를 추가하다", 30
PRINTL

PRINTL
IF キャラ番号 == MASTER
	PRINTBUTTONLC "[998]당신의 설정을 다시 하다", 998
	PRINTL
ELSE
	PRINTBUTTONLC "[996] 이전 캐릭터로", 996
	PRINTBUTTONLC "[997] 다음 캐릭터로", 997
	SIF リロールボタン有効
		PRINTBUTTONLC "[995]이미지 랜덤 표시를 리롤", 995
	PRINTL
ENDIF
PRINTBUTTONLC "[999]돌아가기", 999
PRINTL

$INPUT_LOOP
BINPUT
SELECTCASE RESULT
	CASE 1
		CALL 敏感素質変化(キャラ番号)
		RESTART
	CASE 2
		CALL バスト素質変化(キャラ番号)
		RESTART
	CASE 3
		CALL 男性器素質変化(キャラ番号)
		RESTART
	CASE 4
		CALL 性別素質変化(キャラ番号)
		RESTART
	CASE 5
		CALL 種族素質変化(キャラ番号)
		RESTART
	CASE 6
		CALL 妊娠不可素質変化(キャラ番号)
		RESTART
	CASE 7
		CALL 受胎体質素質変化(キャラ番号)
		RESTART
	CASE 8
		CALL 発情期素質設定(キャラ番号)
		RESTART
	CASE 9
		CALL 体格・体型素質設定(キャラ番号)
		RESTART
	CASE 10
		CALL ハーレムの心得素質設定(キャラ番号)
		RESTART
	CASE 20
		CALL フレーバー素質再設定(キャラ番号)
		RESTART
	CASE 30
		CALL 弱点コマンド追加(キャラ番号)
		RESTART
	CASE 995
		RESTART
	CASE 996
		RETURN 1
	CASE 997
		RETURN 2
	CASE 998
		CALL キャラメイク設定
		RESTART
	CASE 999
		RETURN -1
ENDSELECT

@敏感素質変化(対象キャラ)
#DIM 対象キャラ
#DIM CONST COST_TO_敏感 = 300
#DIM CONST COST_TO_通常 = 100
#DIM CONST COST_TO_鈍感 = 300
#DIM 必要COST

DRAWLINE
PRINTL ZP를 소비하여 캐릭터에게 민감계 소질을 부여할 수 있습니다.
PRINTL 어떤 부위를 민감하게 만드시겠습니까?
PRINTFORML 소지 ZP:{FLAG:ZP所持量}.{(ZP小数点以下所持量 % 1000) / 100}{(ZP小数点以下所持量 % 100) / 10}{(ZP小数点以下所持量 % 10)}
DRAWLINE
FOR LOCAL:1 , 101, 106
	IF TALENT:対象キャラ:(LOCAL:1) > 0
		PRINTPLAINFORM [{LOCAL:1}]%TALENTNAME:(LOCAL:1)%　　은(는) 이미 민감합니다
		PRINTL
		CONTINUE
	ENDIF
	IF TALENT:対象キャラ:(LOCAL:1) == -2
		PRINTBUTTON @"[{LOCAL:1}]%TALENTNAME:(LOCAL:1)%　　:없음 > 둔감", LOCAL:1
		PRINTFORML 　　　필요 ZP:{COST_TO_鈍感}
	ELSEIF TALENT:対象キャラ:(LOCAL:1) == -1
		PRINTBUTTON @"[{LOCAL:1}]%TALENTNAME:(LOCAL:1)%　　:둔감 > 보통", LOCAL:1
		PRINTFORML 　　　필요 ZP:{COST_TO_通常}
	ELSE
		PRINTBUTTON @"[{LOCAL:1}]%TALENTNAME:(LOCAL:1)%　　:보통 > 민감", LOCAL:1
		PRINTFORML 　　　필요 ZP:{COST_TO_敏感}
	ENDIF
NEXT

IF TALENT:対象キャラ:イキやすさ == 2
	PRINTPLAIN 현재:[즉시 절정]취득 완료
	PRINTL
ELSE
	PRINTBUTTON @"[241] [즉시 절정]을(를) 취득한다", 241
	PRINTFORML 　　　필요 ZP:{COST_TO_敏感 * 2}
ENDIF
IF TALENT:対象キャラ:イキやすさ == 1
	PRINTPLAIN 현재:[조기 절정]취득 완료
	PRINTL
ELSE
	PRINTBUTTON @"[242] [조기 절정]을(를) 취득한다", 242
	PRINTFORML 　　　필요 ZP:{COST_TO_敏感}
ENDIF
IF TALENT:対象キャラ:イキやすさ == 0
	PRINTPLAIN 현재:절정 용이성 소질 없음
	PRINTL
ELSE
	PRINTBUTTON @"[243] 절정하기 쉬운 소질을 삭제한다", 243
	PRINTFORML 　　　필요 ZP:{COST_TO_通常}
ENDIF
IF TALENT:対象キャラ:イキやすさ == -1
	PRINTPLAIN 현재:[늦절정]취득 완료
	PRINTL
ELSE
	PRINTBUTTON @"[244] [늦절정]을 취득한다", 244
	PRINTFORML 　　　필요 ZP:{COST_TO_通常 / 2}
ENDIF
IF GETBIT(TALENT:対象キャラ:性別, 1)
	IF TALENT:対象キャラ:ＥＤ == 1
		PRINTBUTTON @"[251] [ED]를 삭제한다", 251
		PRINTFORML 　　　필요 ZP:{COST_TO_通常}
	ELSE
		PRINTBUTTON @"[250] [ED]를 취득한다", 250
		PRINTFORML 　　　필요 ZP:{COST_TO_通常}
	ENDIF
ENDIF

PRINTL
PRINTBUTTON "[999]돌아가기", 999
$INPUT_LOOP2
BINPUT
LOCAL:1 = RESULT
SELECTCASE RESULT
	CASE 999
		RETURN 0
	CASE 101 TO 105
		;敏感なのに入ってきてない？
		IF TALENT:対象キャラ:(LOCAL:1) > 0
			REUSELASTLINE 
			GOTO INPUT_LOOP2
		ENDIF
		SELECTCASE TALENT:対象キャラ:(LOCAL:1)
			CASE -2
				必要COST = COST_TO_鈍感
			CASE -1
				必要COST = COST_TO_通常
			CASE 0
				必要COST = COST_TO_敏感
		ENDSELECT
		;ZP足りる？
		IF FLAG:ZP所持量 < 必要COST
			PRINTW ZP가 부족합니다
			RESTART
		ENDIF
		PRINTFORML {必要COST}ZP를 지불하고, %TALENTNAME:(LOCAL:1)%을(를) 상승시키겠습니까?
		PRINTFORML (소지 ZP:{FLAG:ZP所持量})
		PRINTBUTTONLC "[0]예", 0
		PRINTBUTTONLC "[1]아니요", 1
		INPUT
		IF RESULT == 0
			FLAG:ZP所持量 -= 必要COST
			PRINTFORM %TALENTNAME:(LOCAL:1)%이(가)
			IF TALENT:対象キャラ:(LOCAL:1) == -2
				PRINTFORMW 새로 발생했습니다.
			ELSEIF TALENT:対象キャラ:(LOCAL:1) == -1
				PRINTFORMW 둔감에서 보통으로 상승했습니다.
			ELSEIF TALENT:対象キャラ:(LOCAL:1) == 0
				PRINTFORMW 보통에서 민감으로 상승했습니다.
			ENDIF
			TALENT:対象キャラ:(LOCAL:1) += 1
		ENDIF
		RETURN 0
	CASE 241
		必要COST = COST_TO_敏感 * 2
		;ZP足りる？
		IF FLAG:ZP所持量 < 必要COST
			PRINTW ZP가 부족합니다
			RESTART
		ENDIF
		PRINTFORML {必要COST}ZP를 지불하고, [즉시 절정]을 획득하시겠습니까?
		PRINTFORML (소지 ZP: {FLAG:ZP所持量})
		PRINTBUTTONLC "[0]예", 0
		PRINTBUTTONLC "[1]아니오", 1
		INPUT
		IF RESULT == 0
			FLAG:ZP所持量 -= 必要COST
			PRINTFORMW 절정하기 쉬움이 [즉시 절정]으로 변화했습니다.
			TALENT:対象キャラ:イキやすさ = 2
		ENDIF
		RETURN 0
	CASE 242
		必要COST = COST_TO_敏感
		;ZP足りる？
		IF FLAG:ZP所持量 < 必要COST
			PRINTW ZP가 부족합니다
			RESTART
		ENDIF
		PRINTFORML {必要COST}ZP를 지불하고, [조기 절정]을 획득하시겠습니까?
		PRINTFORML (소지 ZP: {FLAG:ZP所持量})
		PRINTBUTTONLC "[0]예", 0
		PRINTBUTTONLC "[1]아니오", 1
		INPUT
		IF RESULT == 0
			FLAG:ZP所持量 -= 必要COST
			PRINTFORMW 절정하기 쉬움이 [조루]로 변화했습니다.
			TALENT:対象キャラ:イキやすさ = 1
		ENDIF
		RETURN 0
	CASE 243
		必要COST = COST_TO_通常
		;ZP足りる？
		IF FLAG:ZP所持量 < 必要COST
			PRINTW ZP가 부족합니다
			RESTART
		ENDIF
		PRINTFORML {必要COST}ZP을(를) 지불하고, 절정하기 쉬움 소질을 삭제하시겠습니까?
		PRINTFORML (소지 ZP:{FLAG:ZP所持量})
		PRINTBUTTONLC "[0]네", 0
		PRINTBUTTONLC "[1]아니오", 1
		INPUT
		IF RESULT == 0
			FLAG:ZP所持量 -= 必要COST
			PRINTFORMW 절정하기 쉬움 소질을 삭제했습니다.
			TALENT:対象キャラ:イキやすさ = 0
		ENDIF
		RETURN 0
	CASE 244
		必要COST = COST_TO_通常 / 2
		;ZP足りる？
		IF FLAG:ZP所持量 < 必要COST
			PRINTW ZP가 부족합니다
			RESTART
		ENDIF
		PRINTFORML {必要COST}ZP을(를) 지불하고, [지루]를 취득하시겠습니까?
		PRINTFORML (소지 ZP:{FLAG:ZP所持量})
		PRINTBUTTONLC "[0]네", 0
		PRINTBUTTONLC "[1]아니오", 1
		INPUT
		IF RESULT == 0
			FLAG:ZP所持量 -= 必要COST
			PRINTFORMW 절정하기 쉬움이 [지루]로 변화했습니다.
			TALENT:対象キャラ:イキやすさ = -1
		ENDIF
		RETURN 0
	CASE 250
		必要COST = COST_TO_通常
		;ZP足りる？
		IF FLAG:ZP所持量 < 必要COST
			PRINTW ZP가 부족합니다
			RESTART
		ENDIF
		PRINTFORML {必要COST}ZP을(를) 지불하고, [ED]를 취득하시겠습니까?
		PRINTFORML （보유 ZP：{FLAG:ZP所持量}）
		PRINTBUTTONLC "[0]예", 0
		PRINTBUTTONLC "[1]아니요", 1
		INPUT
		IF RESULT == 0
			FLAG:ZP所持量 -= 必要COST
			PRINTFORMW [ＥＤ]을(를) 획득했습니다.
			TALENT:対象キャラ:ＥＤ = 1
		ENDIF
		RETURN 0
	CASE 251
		必要COST = COST_TO_通常
		;ZP足りる？
		IF FLAG:ZP所持量 < 必要COST
			PRINTW ZP가 부족합니다
			RESTART
		ENDIF
		PRINTFORML {必要COST}ZP를 지불하고, [ＥＤ]를 삭제하시겠습니까?
		PRINTFORML （보유 ZP：{FLAG:ZP所持量}）
		PRINTBUTTONLC "[0]예", 0
		PRINTBUTTONLC "[1]아니요", 1
		INPUT
		IF RESULT == 0
			FLAG:ZP所持量 -= 必要COST
			PRINTFORMW [ＥＤ]을(를) 삭제했습니다.
			TALENT:対象キャラ:ＥＤ = 0
		ENDIF
		RETURN 0
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP2
ENDSELECT

@バスト素質変化(対象キャラ)
#DIM 対象キャラ
#DIM CONST COST_胸定数 = 20
#DIM 必要COST

DRAWLINE
PRINTL ZP를 소비하여 캐릭터의 가슴 크기 등을 변화시킬 수 있습니다.
PRINTL 원하는 크기를 선택해 주세요
PRINTFORML 보유 ZP:{FLAG:ZP所持量}.{(ZP小数点以下所持量 % 1000) / 100}{(ZP小数点以下所持量 % 100) / 10}{(ZP小数点以下所持量 % 10)}
DRAWLINE
FOR LOCAL:1 , -2, 4
	IF TALENT:対象キャラ:バストサイズ == LOCAL:1
		SETCOLOR 0x666666
		IF GET_TALENTNAME(205, LOCAL:1,,対象キャラ) != GET_TALENTNAME(205, LOCAL:1)
			PRINTPLAINFORM [{LOCAL:1}]%GET_TALENTNAME(205, LOCAL:1,,対象キャラ), 20, LEFT%：%GET_TALENTNAME(205, LOCAL:1)%호환 (현재 크기)
		ELSE
			PRINTPLAINFORM [{LOCAL:1}]%GET_TALENTNAME(205, LOCAL:1,,対象キャラ), 20, LEFT%(현재 크기)
		ENDIF
		PRINTL
		RESETCOLOR
	ELSE
		IF GET_TALENTNAME(205, LOCAL:1,,対象キャラ) != GET_TALENTNAME(205, LOCAL:1)
			PRINTBUTTON @"[{LOCAL:1}]%GET_TALENTNAME(205, LOCAL:1,,対象キャラ), 20, LEFT%:%GET_TALENTNAME(205, LOCAL:1)%호환({ABS(TALENT:対象キャラ:バストサイズ - LOCAL:1) * COST_胸定数})", LOCAL:1
		ELSE
			PRINTBUTTON @"[{LOCAL:1}]%GET_TALENTNAME(205, LOCAL:1,,対象キャラ), 20, LEFT%({ABS(TALENT:対象キャラ:バストサイズ - LOCAL:1) * COST_胸定数})", LOCAL:1
		ENDIF
		PRINTL
	ENDIF
NEXT
IF 素質切り替えOPTION記憶:陥没乳首_なし == 0
	IF TALENT:対象キャラ:陥没乳首
		PRINTBUTTON @"[10] 함몰 유두 소질을 삭제한다({COST_胸定数})", 10
	ELSE
		PRINTBUTTON @"[10] 함몰 유두 소질을 부여한다({COST_胸定数})", 10
	ENDIF
	PRINTL
ENDIF
PRINTL
PRINTBUTTON "[999]돌아가기", 999
$INPUT_LOOP3
INPUT
LOCAL:1 = RESULT
SELECTCASE LOCAL:1
	CASE 999
		RETURN 0
	CASE TALENT:対象キャラ:バストサイズ
		REUSELASTLINE 
		GOTO INPUT_LOOP3
	CASE 10
		IF FLAG:ZP所持量 < COST_胸定数
			PRINTW ZP가 부족합니다
			GOTO INPUT_LOOP3
		ENDIF
		IF TALENT:対象キャラ:陥没乳首
			PRINTFORML {COST_胸定数}ZP를 지불하고, 함몰 유두 소질을 삭제하시겠습니까?
		ELSE
			PRINTFORML {COST_胸定数}ZP를 지불하고, 함몰 유두 소질을 부여하시겠습니까?
		ENDIF
		PRINTFORML (소지 ZP:{FLAG:ZP所持量})
		PRINTBUTTONLC "[0]예", 0
		PRINTBUTTONLC "[1]아니요", 1
		INPUT
		IF RESULT == 0
			FLAG:ZP所持量 -= COST_胸定数
			IF TALENT:対象キャラ:陥没乳首
				PRINTFORMW %CALLNAME:対象キャラ%의 함몰 유두 소질이 소멸했습니다.
				TALENT:対象キャラ:陥没乳首 = 0
			ELSE
				PRINTFORMW %CALLNAME:対象キャラ%이(가) 함몰 유두가 되었습니다.
				TALENT:対象キャラ:陥没乳首 = 1
			ENDIF
		ENDIF
		RETURN 0
	CASE -2 TO 3
		;ZP足りる？
		IF FLAG:ZP所持量 < ABS(TALENT:対象キャラ:バストサイズ - LOCAL:1) * COST_胸定数
			PRINTW ZP가 부족합니다
			GOTO INPUT_LOOP3
		ENDIF
		PRINTFORM {ABS(TALENT:対象キャラ:バストサイズ - LOCAL:1) * COST_胸定数}
		PRINTFORML ZP를 지불하고, 가슴 사이즈를 변화시키겠습니까?
		PRINTFORML （소지 ZP:{FLAG:ZP所持量}）
		PRINTBUTTONLC "[0]예", 0
		PRINTBUTTONLC "[1]아니요", 1
		INPUT
		IF RESULT == 0
			FLAG:ZP所持量 -= ABS(TALENT:対象キャラ:バストサイズ - LOCAL:1) * COST_胸定数
			PRINTFORMW %CALLNAME:対象キャラ%의 가슴 사이즈가 %GET_TALENTNAME(205, TALENT:対象キャラ:バストサイズ,,対象キャラ)%에서 %GET_TALENTNAME(205, LOCAL:1,,対象キャラ)%(으)로 변화했습니다.
			TALENT:対象キャラ:バストサイズ = LOCAL:1
			CALL SET_BODYSIZE(対象キャラ,2)
		ENDIF
		RETURN 0
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP3
ENDSELECT

@男性器素質変化(対象キャラ)
#DIM 対象キャラ
#DIM CONST COST_男性器定数 = 20
#DIM CONST COST_TO_射精可能 = 300
#DIM 必要COST

DRAWLINE
PRINTL ZP를 소비하여 캐릭터의 남성기 크기 등을 변화시킬 수 있습니다.
PRINTL 원하는 크기를 선택해 주세요
PRINTFORML 소지 ZP:{FLAG:ZP所持量}.{(ZP小数点以下所持量 % 1000) / 100}{(ZP小数点以下所持量 % 100) / 10}{(ZP小数点以下所持量 % 10)}
DRAWLINE
FOR LOCAL:1 , -2, 3
	IF TALENT:対象キャラ:男性器サイズ == LOCAL:1
		SETCOLOR 0x666666
		IF GET_TALENTNAME(162, LOCAL:1,,対象キャラ) != GET_TALENTNAME(162, LOCAL:1)
			PRINTPLAINFORM [{LOCAL:1}]%GET_TALENTNAME(162, LOCAL:1,,対象キャラ), 20, LEFT%:%GET_TALENTNAME(162, LOCAL:1)%호환 (현재 크기)
		ELSE
			PRINTPLAINFORM [{LOCAL:1}]%GET_TALENTNAME(162, LOCAL:1,,対象キャラ), 20, LEFT% (현재 크기)
		ENDIF
		PRINTL
		RESETCOLOR
	ELSE
		IF GET_TALENTNAME(162, LOCAL:1,,対象キャラ) != GET_TALENTNAME(162, LOCAL:1)
			PRINTBUTTON @"[{LOCAL:1}]%GET_TALENTNAME(162, LOCAL:1,,対象キャラ), 20, LEFT%:%GET_TALENTNAME(162, LOCAL:1)%호환 ({ABS(TALENT:対象キャラ:男性器サイズ - LOCAL:1) * COST_男性器定数})", LOCAL:1
		ELSE
			PRINTBUTTON @"[{LOCAL:1}]%GET_TALENTNAME(162, LOCAL:1,,対象キャラ), 20, LEFT% ({ABS(TALENT:対象キャラ:男性器サイズ - LOCAL:1) * COST_男性器定数})", LOCAL:1
		ENDIF
		PRINTL
	ENDIF
NEXT
IF TALENT:対象キャラ:射精不可
	PRINTBUTTON @"[10] 사정을 가능하게 하다 ({COST_TO_射精可能})", 10
	PRINTL
ENDIF
PRINTL 
PRINTBUTTON "[999]돌아가기", 999
$INPUT_LOOP3
INPUT
LOCAL:1 = RESULT
SELECTCASE LOCAL:1
	CASE 999
		RETURN 0
	CASE TALENT:対象キャラ:男性器サイズ
		REUSELASTLINE 
		GOTO INPUT_LOOP3
	CASE -2 TO 2
		;ZP足りる？
		IF FLAG:ZP所持量 < ABS(TALENT:対象キャラ:男性器サイズ - LOCAL:1) * COST_男性器定数
			PRINTW ZP가 부족합니다
		GOTO INPUT_LOOP3
		ENDIF
		PRINTFORM {ABS(TALENT:対象キャラ:男性器サイズ - LOCAL:1) * COST_男性器定数}
		PRINTFORML ZP를 지불하고, 남성기 사이즈를 변화시키겠습니까?
		PRINTFORML （보유 ZP:{FLAG:ZP所持量}）
		PRINTBUTTONLC "[0]예", 0
		PRINTBUTTONLC "[1]아니요", 1
		INPUT
		IF RESULT == 0
			FLAG:ZP所持量 -= ABS(TALENT:対象キャラ:男性器サイズ - LOCAL:1) * COST_男性器定数
			PRINTFORMW %CALLNAME:対象キャラ%의 남성기 사이즈가 %GET_TALENTNAME(162, TALENT:対象キャラ:男性器サイズ,,対象キャラ)%에서 %GET_TALENTNAME(162, LOCAL:1,,対象キャラ)%(으)로 변화했습니다.
			TALENT:対象キャラ:男性器サイズ = LOCAL:1
		ENDIF
		RETURN 0
	CASE 10
		;ZP足りる？
		IF FLAG:ZP所持量 < COST_TO_射精可能
			PRINTW ZP가 부족합니다
		GOTO INPUT_LOOP3
		ENDIF
		IF TALENT:対象キャラ:射精不可 == 2
			PRINTFORML {COST_TO_射精可能}ZP를 지불하고, %CALLNAME:対象キャラ%을(를) 정통시킬까요?
		ELSE
			PRINTFORML {COST_TO_射精可能}ZP를 지불하고, [사정 불가]를 삭제할까요?
		ENDIF
		PRINTFORML （보유 ZP:{FLAG:ZP所持量}）
		PRINTBUTTONLC "[0]예", 0
		PRINTBUTTONLC "[1]아니요", 1
		INPUT
		IF RESULT == 0
			FLAG:ZP所持量 -= COST_TO_射精可能
			PRINTFORMW %CALLNAME:対象キャラ%이(가) 사정 가능해졌습니다.
			TALENT:対象キャラ:射精不可 = 0
		ENDIF
		RETURN 0
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP3
ENDSELECT

@性別素質変化(対象キャラ)
#DIM 対象キャラ
#DIM CONST COST_性別定数 = 30
#DIM 必要COST
#DIM 変更位置
#DIM 元の性別

DRAWLINE
PRINTL ZP를 소비하여 캐릭터의 성별을 변화시킬 수 있습니다.
PRINTL 발정 후타나리 등, 일시적으로 성별이 변화하는 상태일 경우, 변화 전의 성별을 변경합니다.
PRINTL 임의의 성별을 선택해 주세요.
PRINTL ※그 외・무성으로의 변화는 불가능합니다. 원래 해당 성별일 경우, 성별을 변화시키면 원래대로 되돌릴 수 없게 됩니다.
PRINTFORML 소지ZP:{FLAG:ZP所持量}.{(ZP小数点以下所持量 % 1000) / 100}{(ZP小数点以下所持量 % 100) / 10}{(ZP小数点以下所持量 % 10)}
DRAWLINE
FOR LOCAL:1, 1, 3
	IF CFLAG:対象キャラ:元性別保存 == LOCAL:1
		SETCOLOR 0x666666
		IF GET_TALENTNAME(2, LOCAL:1,,対象キャラ) != GET_TALENTNAME(2, LOCAL:1)
			IF CFLAG:対象キャラ:元性別保存 == TALENT:対象キャラ:性別
				PRINTPLAINFORM [{LOCAL:1}]%GET_TALENTNAME(2, LOCAL:1,,対象キャラ), 20, LEFT%:%GET_TALENTNAME(2, LOCAL:1)%호환(현재 성별)
			ELSE
				PRINTPLAINFORM [{LOCAL:1}]%GET_TALENTNAME(2, LOCAL:1,,対象キャラ), 20, LEFT%:%GET_TALENTNAME(2, LOCAL:1)%호환(변화 전 성별)
			ENDIF
		ELSE
			IF CFLAG:対象キャラ:元性別保存 == TALENT:対象キャラ:性別
				PRINTPLAINFORM [{LOCAL:1}]%GET_TALENTNAME(2, LOCAL:1,,対象キャラ), 20, LEFT%(현재 성별)
			ELSE
				PRINTPLAINFORM [{LOCAL:1}]%GET_TALENTNAME(2, LOCAL:1,,対象キャラ), 20, LEFT%(변화 전 성별)
			ENDIF
		ENDIF
		PRINTL
		RESETCOLOR
	ELSEIF TALENT:対象キャラ:妊娠 && GETBIT(LOCAL:1, 0) == 0
		SETCOLOR 0x666666
		IF GET_TALENTNAME(2, LOCAL:1,,対象キャラ) != GET_TALENTNAME(2, LOCAL:1)
			PRINTPLAINFORM [{LOCAL:1}]%GET_TALENTNAME(2, LOCAL:1,,対象キャラ), 20, LEFT%:%GET_TALENTNAME(2, LOCAL:1)%호환(임신 중이므로 불가)
		ELSE
			PRINTPLAINFORM [{LOCAL:1}]%GET_TALENTNAME(2, LOCAL:1,,対象キャラ), 20, LEFT%(임신 중이므로 불가)
		ENDIF
		PRINTL
		RESETCOLOR
	ELSE
		IF GET_TALENTNAME(2, LOCAL:1,,対象キャラ) != GET_TALENTNAME(2, LOCAL:1)
			PRINTBUTTON @"[{LOCAL:1}]%GET_TALENTNAME(2, LOCAL:1,,対象キャラ), 20, LEFT%:%GET_TALENTNAME(2, LOCAL:1)%호환({COST_性別定数})", LOCAL:1
		ELSE
			PRINTBUTTON @"[{LOCAL:1}]%GET_TALENTNAME(2, LOCAL:1,,対象キャラ), 20, LEFT%({COST_性別定数})", LOCAL:1
		ENDIF
		PRINTL
	ENDIF
NEXT
IF 素質切り替えOPTION記憶:ふたなり_女性 == 0
	IF OPTION変数:ふたなり玉設定 == 0
		IF TALENT:対象キャラ:性別 == 3
			IF フレーバー素質:対象キャラ:ふたなり時_玉ありなし == 2
				PRINTBUTTON @"[3]후타나리 구슬 없음({COST_性別定数})", 3
				PRINTL
				PRINTPLAINFORM [4]후타나리 구슬 있음(현재 성별)
			ELSE
				PRINTPLAINFORM [3]후타나리 구슬 없음(현재 성별)
				PRINTL
				PRINTBUTTON @"[4]후타나리 구슬 있음({COST_性別定数})", 4
			ENDIF
		ELSE
			PRINTBUTTON @"[3]후타나리 구슬 없음({COST_性別定数})", 3
			PRINTL
			PRINTBUTTON @"[4]후타나리 구슬 있음({COST_性別定数})", 4
		ENDIF
	ELSEIF OPTION変数:ふたなり玉設定 == 1
		IF TALENT:対象キャラ:性別 == 3
			PRINTPLAINFORM [3]후타나리 고환 없음 (현재 성별)
		ELSE
			PRINTBUTTON @"[3]후타나리 고환 없음 ({COST_性別定数})", 3
		ENDIF
	ELSEIF OPTION変数:ふたなり玉設定 == 2
		IF TALENT:対象キャラ:性別 == 3
			PRINTPLAINFORM [4]후타나리 고환 있음 (현재 성별)
		ELSE
			PRINTBUTTON @"[4]후타나리 고환 있음 ({COST_性別定数})", 4
		ENDIF
	ENDIF
	PRINTL
ENDIF
PRINTBUTTON "[999]돌아가기", 999
$INPUT_LOOP3
BINPUT
LOCAL:1 = RESULT
SELECTCASE LOCAL:1
	CASE 999
		RETURN 0
	CASE 1 TO 4
		IF (LOCAL:1 == TALENT:対象キャラ:性別 && LOCAL:1 < 3) || (TALENT:対象キャラ:性別 == 3 && ((LOCAL:1 == 3 && フレーバー素質:対象キャラ:ふたなり時_玉ありなし == 1) || (LOCAL:1 == 4 && フレーバー素質:対象キャラ:ふたなり時_玉ありなし == 2)))
			REUSELASTLINE 
			GOTO INPUT_LOOP3
		ENDIF
		;ZP足りる？
		IF FLAG:ZP所持量 < COST_性別定数
			PRINTW ZP가 부족합니다
		GOTO INPUT_LOOP3
		ENDIF
		PRINTFORM {COST_性別定数}
		PRINTFORML ZP를 지불하고, 성별을 변화시키겠습니까?
		PRINTFORML (소지 ZP: {FLAG:ZP所持量})
		PRINTBUTTONLC "[0]예", 0
		PRINTBUTTONLC "[1]아니요", 1
		INPUT
		IF RESULT == 0
			FLAG:ZP所持量 -= COST_性別定数
			元の性別 = TALENT:対象キャラ:性別
			IF LOCAL:1 <= 2
				PRINTFORMW %CALLNAME:対象キャラ%의 성별이 %GET_TALENTNAME(2, TALENT:対象キャラ:性別,,対象キャラ)%에서 %GET_TALENTNAME(2, LOCAL:1,,対象キャラ)%(으)로 변화했습니다.
				TALENT:対象キャラ:性別 = LOCAL:1
			ELSEIF LOCAL:1 == 3
				PRINTFORMW %CALLNAME:対象キャラ%의 성별이 %GET_TALENTNAME(2, TALENT:対象キャラ:性別,,対象キャラ)%에서 후타나리 (고환 없음)(으)로 변화했습니다.
				TALENT:対象キャラ:性別 = 3
				IF フレーバー素質:対象キャラ:素質表示設定 == 0
					フレーバー素質:対象キャラ:素質表示設定 = 1
					フレーバー素質:対象キャラ:陰毛濃さ = 0
					フレーバー素質:対象キャラ:乳首大きさ = 0
					フレーバー素質:対象キャラ:乳輪大きさ = 0
					フレーバー素質:対象キャラ:性器種類 = 0
					フレーバー素質:対象キャラ:クリ大きさ = 0
					フレーバー素質:対象キャラ:ほくろ位置 = 0
				ENDIF
				フレーバー素質:対象キャラ:ふたなり時_玉ありなし = 1
			ELSEIF LOCAL:1 == 4
				PRINTFORMW %CALLNAME:対象キャラ%의 성별이 %GET_TALENTNAME(2, TALENT:対象キャラ:性別,,対象キャラ)%에서 후타나리 (고환 있음)(으)로 변화했습니다.
				TALENT:対象キャラ:性別 = 3
				IF フレーバー素質:対象キャラ:素質表示設定 == 0
					フレーバー素質:対象キャラ:素質表示設定 = 1
					フレーバー素質:対象キャラ:陰毛濃さ = 0
					フレーバー素質:対象キャラ:乳首大きさ = 0
					フレーバー素質:対象キャラ:乳輪大きさ = 0
					フレーバー素質:対象キャラ:性器種類 = 0
					フレーバー素質:対象キャラ:クリ大きさ = 0
					フレーバー素質:対象キャラ:ほくろ位置 = 0
				ENDIF
				フレーバー素質:対象キャラ:ふたなり時_玉ありなし = 2
			ENDIF
			;男で初性転換の場合処女にする
			SIF !TALENT:対象キャラ:性転換 && 元の性別 == 2
				TALENT:対象キャラ:処女 = 1
			CALL ZP_SHOP_性別特徴変換(対象キャラ, 元の性別, TALENT:対象キャラ:性別)
			IF RESULT == 0
				SETBIT 変更位置, 0
				SETBIT 変更位置, 1
				SETBIT 変更位置, 2
				SETBIT 変更位置, 3
				IF 身長固定キャラ(対象キャラ)
					CLEARBIT 変更位置, 0
				ELSE
					;身長任意設定
					CALL 性別変化時_身長任意設定(対象キャラ)
					SIF RESULT == 1
						CLEARBIT 変更位置, 0
				ENDIF
				IF バスト固定キャラ(対象キャラ)
					CLEARBIT 変更位置, 1
				ENDIF
				SIF 変更位置
					CALL SET_BODYSIZE(対象キャラ, 変更位置)
			ENDIF
			;性転換が0なら元の性別(ふたなりは-3)を代入し、再度元の性別やふたなりになった場合は-をかけて非表示に
			IF !TALENT:対象キャラ:性転換
				IF TALENT:対象キャラ:性別 == 2 && (元の性別 == 1 || (元の性別 == 3 && 素質切り替えOPTION記憶:ふたなり_女性))
					TALENT:対象キャラ:性転換 = 1
				ELSEIF TALENT:対象キャラ:性別 == 1 && 元の性別 == 2
					TALENT:対象キャラ:性転換 = 2
				ELSE
					TALENT:対象キャラ:性転換 = 元の性別 * -1
				ENDIF
			ELSEIF TALENT:対象キャラ:性転換 < 0 && TALENT:対象キャラ:性転換 > -3 && TALENT:対象キャラ:性別 != TALENT:対象キャラ:性転換 * -1 && (TALENT:対象キャラ:性別 == 1 || TALENT:対象キャラ:性別 == 2)
				TALENT:対象キャラ:性転換 *= -1
			ELSEIF TALENT:対象キャラ:性転換 > 0
				TALENT:対象キャラ:性転換 *= -1
			ENDIF
			;ふたなり、その他の場合は何に変化しても元女・元男はつかない
			;元性別も処理しておく
			CFLAG:対象キャラ:元性別保存 = TALENT:対象キャラ:性別
		ENDIF
		RETURN 0
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP3
ENDSELECT

@ZP_SHOP_性別特徴変換(キャラ番号, 変更元性別, 変更先性別)
#DIM キャラ番号
#DIM 変更元性別
#DIM 変更先性別

IF GETBIT(変更先性別, 0) != GETBIT(変更元性別, 0)
	IF !TALENT:キャラ番号:ランダムキャラ && GETBIT(CSVTALENT(NO:キャラ番号, GETNUM(TALENT, "性別")), 0) == GETBIT(変更先性別, 0) && CSVTALENT(NO:キャラ番号, GETNUM(TALENT, "種族")) == TALENT:キャラ番号:種族
		;元の性別・種族に戻ったのなら初期化
		BASE:キャラ番号:身長 = 0
		BASE:キャラ番号:バスト = 0
		CALL INIT_BODYSIZE(キャラ番号)
		RETURN 1
	ELSEIF 変更先性別 == 2
		;女・ふたなり＞男パターン
		SELECTCASE TALENT:キャラ番号:種族
			CASE 3
				;ドラフは男ドラフ並に
				TALENT:キャラ番号:体格 = 2
			CASE 4
				;ハーヴィンは変化なし
			CASEELSE
				;他は体格を＋０～１する
				TALENT:キャラ番号:体格 = MIN(1, TALENT:キャラ番号:体格 + RAND:2)
		ENDSELECT
		SIF 変更元性別 == 1
			TALENT:キャラ番号:男性器サイズ = ランダム男性器サイズ(TALENT:キャラ番号:種族, 変更先性別, TALENT:キャラ番号:年齢層)
		RETURN 0
	ELSEIF 変更元性別 == 2
		;男＞女・ふたなりパターン
		SELECTCASE TALENT:キャラ番号:種族
			CASE 3
				;ドラフは小柄に
				TALENT:キャラ番号:体格 = -1
			CASE 4
				;ハーヴィンは体格変化なし
			CASEELSE
				;他は体格を－０～１する
				TALENT:キャラ番号:体格 = MAX(-3, TALENT:キャラ番号:体格 - RAND:2)
		ENDSELECT
		TALENT:キャラ番号:バストサイズ = ランダムバストサイズ(TALENT:キャラ番号:種族, TALENT:キャラ番号:年齢層)
		RETURN 0
	ELSE
		;その他＞女・ふたなりパターン
		SIF 変更先性別 == 3
			TALENT:キャラ番号:男性器サイズ = ランダム男性器サイズ(TALENT:キャラ番号:種族, 変更先性別, TALENT:キャラ番号:年齢層)
		TALENT:キャラ番号:バストサイズ = ランダムバストサイズ(TALENT:キャラ番号:種族, TALENT:キャラ番号:年齢層)
		RETURN 0
	ENDIF
ELSEIF !GETBIT(変更元性別, 1) && GETBIT(変更先性別, 1)
	TALENT:キャラ番号:男性器サイズ = ランダム男性器サイズ(TALENT:キャラ番号:種族, 変更先性別, TALENT:キャラ番号:年齢層)
	RETURN 1
ENDIF
RETURN 1


@種族素質変化(対象キャラ)
#DIM 対象キャラ
#DIM 必要COST
#DIM CONST COST_種族変化 = 200
#DIMS 特殊種族一時保存

DRAWLINE
PRINTL ZP를 소비하여 캐릭터의 종족을 변화시킬 수 있습니다.
PRINTL 원하는 종족을 선택해 주세요
PRINTFORML 보유 ZP:{FLAG:ZP所持量}.{(ZP小数点以下所持量 % 1000) / 100}{(ZP小数点以下所持量 % 100) / 10}{(ZP小数点以下所持量 % 10)}
DRAWLINE
FOR LOCAL:1 , 1, 7
	IF TALENT:対象キャラ:種族 == LOCAL:1 && LOCAL:1 != 6
		SETCOLOR 0x666666
		PRINTPLAINFORM [{LOCAL:1}]%GET_TALENTNAME(200, LOCAL:1,,対象キャラ), 14, LEFT%（현재 종족）
		PRINTL
		RESETCOLOR
	ELSE
		PRINTBUTTON @"[{LOCAL:1}]%GET_TALENTNAME(200, LOCAL:1,,対象キャラ), 14, LEFT%필요 ZP:{COST_種族変化}", LOCAL:1
		PRINTL
	ENDIF
NEXT
PRINTBUTTON "[999]돌아가기", 999
BINPUT
LOCAL:1 = RESULT
SELECTCASE LOCAL:1
	CASE 999
		RETURN 0
	CASE 1 TO 6
		;ZP足りる？
		IF FLAG:ZP所持量 < COST_種族変化
			PRINTW ZP가 부족합니다
			RETURN 0
		ENDIF
		特殊種族一時保存 = 
		IF LOCAL:1 == 6
			DRAWLINE
			PRINTL 추가 종족명을 지정할 수 있습니다.
			PRINTL 임의의 종족명을 입력해 주세요.
			INPUTS
			特殊種族一時保存 = %RESULTS%
		ENDIF
		PRINTFORML 200ZP를 지불하고, 종족을 변화시키겠습니까?
		LOCALS = %GET_TALENTNAME(200, TALENT:対象キャラ:種族,,対象キャラ)%
		SIF TALENT:対象キャラ:種族 == 6
			LOCALS += @"（%CSTR:対象キャラ:特殊種族%）"
		LOCALS:1 = %GET_TALENTNAME(200, LOCAL:1,,対象キャラ)%
		SIF LOCAL:1 == 6
			LOCALS:1 += @"（%特殊種族一時保存%）"
		PRINTFORML 현재 종족：%LOCALS% -> %LOCALS:1%
		PRINTFORML （보유 ZP：{FLAG:ZP所持量}）
		PRINTBUTTONLC "[0]예", 0
		PRINTBUTTONLC "[1]아니오", 1
		BINPUT
		IF RESULT == 0
			FLAG:ZP所持量 -= COST_種族変化
			PRINTFORMW %CALLNAME:対象キャラ%의 종족이 %LOCALS%에서 %LOCALS:1%으로 변화했습니다.
			CSTR:対象キャラ:特殊種族 = %特殊種族一時保存%
			TALENT:対象キャラ:種族 = LOCAL:1
			CALL ZP_SHOP_種族特徴変換(対象キャラ, TALENT:対象キャラ:種族, LOCAL:1)
			SIF RESULT == 0
				CALL SET_BODYSIZE(対象キャラ,0)
		ENDIF
		RETURN 0
ENDSELECT


@妊娠不可素質変化(対象キャラ)
#DIM 対象キャラ
#DIM CONST COST_TO_妊娠可能 = 300
#DIM 必要COST

IF FLAG:ZP所持量 < COST_TO_妊娠可能
	PRINTW ZP가 부족합니다
	RETURN 0
ENDIF
IF TALENT:対象キャラ:妊娠不可 == 2
	PRINTFORML {COST_TO_妊娠可能}ZP를 지불하고, %CALLNAME:対象キャラ%에게 초경을 맞이하게 하시겠습니까?
ELSE
	PRINTFORML {COST_TO_妊娠可能}ZP를 지불하고, [임신 불가]를 삭제하시겠습니까?
ENDIF
PRINTFORML (소지 ZP: {FLAG:ZP所持量})
PRINTBUTTONLC "[0]예", 0
PRINTBUTTONLC "[1]아니오", 1
INPUT
IF RESULT == 0
	FLAG:ZP所持量 -= COST_TO_妊娠可能
	PRINTFORMW %CALLNAME:対象キャラ%가 임신 가능하게 되었습니다.
	TALENT:対象キャラ:妊娠不可 = 0
ENDIF

@受胎体質素質変化(対象キャラ)
#DIM 対象キャラ
#DIM CONST 必要COST = 100
#DIM CONST 必要蓄積度 = 2, 2, 400, 1200

DRAWLINE
PRINTL ZP를 소비하여 캐릭터의 수태 체질을 변화시킬 수 있습니다.
PRINTL 원하는 체질을 선택해 주세요
PRINTL ※모체에 대한 부담이 크기 때문에, 특수한 상황에서의 임신·출산 경험이 필요합니다.
PRINTL 　짧은 기간에 출산을 하거나, 쌍둥이 등의 다태아 출산을 반복함으로써 해방됩니다.
PRINTFORML 소지ZP:{FLAG:ZP所持量}.{(ZP小数点以下所持量 % 1000) / 100}{(ZP小数点以下所持量 % 100) / 10}{(ZP小数点以下所持量 % 10)}
DRAWLINE
FOR LOCAL:1 , 0, 4
	IF TALENT:対象キャラ:受胎体質 == LOCAL:1
		SETCOLOR 0x666666
		PRINTPLAINFORM [{LOCAL:1}]%受胎体質素質名(LOCAL:1)%(현재 체질)
		PRINTL
		RESETCOLOR
	ELSEIF CFLAG:対象キャラ:特殊妊娠蓄積度 < 必要蓄積度:(LOCAL:1)
		SETCOLOR 0x666666
		PRINTPLAINFORM [{LOCAL:1}]%受胎体質素質名(LOCAL:1)%(경험 부족)
		PRINTL
		RESETCOLOR
	ELSE
		PRINTBUTTON @"[{LOCAL:1}]%受胎体質素質名(LOCAL:1)%(필요 ZP:{必要COST})", LOCAL:1
		PRINTL
	ENDIF
NEXT
PRINTBUTTON "[999]돌아가기", 999
$INPUT_LOOP3
BINPUT
LOCAL:1 = RESULT
SELECTCASE LOCAL:1
	CASE 999
		RETURN 0
	CASE 0 TO 3
		;蓄積度足りる？
		IF CFLAG:対象キャラ:特殊妊娠蓄積度 < 必要蓄積度:(LOCAL:1)
			PRINTL 특별한 임신 경험이 부족하여, 모체가 견디지 못할 것 같다…
			PRINTW (짧은 기간에 출산을 하거나, 쌍둥이 등의 출산을 반복함으로써 해방됩니다)
			GOTO INPUT_LOOP3
		ENDIF
		;ZP足りる？
		IF FLAG:ZP所持量 < 必要COST
			PRINTW ZP가 부족합니다
			GOTO INPUT_LOOP3
		ENDIF
		PRINTFORML {必要COST}ZP를 지불하고, 수태 체질을 변화시키겠습니까?
		PRINTFORML (소지 ZP:{FLAG:ZP所持量})
		PRINTBUTTONLC "[0]예", 0
		PRINTBUTTONLC "[1]아니오", 1
		BINPUT
		IF RESULT == 0
			FLAG:ZP所持量 -= 必要COST
			PRINTFORMW %CALLNAME:対象キャラ%의 수태 체질이 %受胎体質素質名(TALENT:対象キャラ:受胎体質)%에서 %受胎体質素質名(LOCAL:1)%(으)로 변화했습니다.
			TALENT:対象キャラ:受胎体質 = LOCAL:1
		ENDIF
		RETURN 0
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP3
ENDSELECT

@受胎体質素質名(ARG)
#FUNCTIONS

IF ARG == 0
	RETURNF "通常"
ELSE
	RETURNF GET_TALENTNAME(154, ARG, 0)
ENDIF


@ZP_SHOP_種族特徴変換(キャラ番号, 変更元種族, 変更先種族)
#DIM キャラ番号
#DIM 変更元種族
#DIM 変更先種族

CFLAG:キャラ番号:発情期フラグ = 0
BASE:キャラ番号:母乳 = 0
IF TALENT:キャラ番号:母乳体質 == 2
	TALENT:キャラ番号:母乳体質 = 0
ENDIF

;CSVと同じ種族、あるいは星晶獣やその他の場合、CSVと同じ体格体型に戻す
IF 変更先種族 == 5 || 変更先種族 == 6 || (!TALENT:キャラ番号:ランダムキャラ && 変更先種族 == CSVTALENT(NO:キャラ番号, GETNUM(TALENT, "種族")))
	TALENT:キャラ番号:角あり = CSVTALENT(NO:キャラ番号, GETNUM(TALENT, "角あり"))
	TALENT:キャラ番号:尻尾あり = CSVTALENT(NO:キャラ番号, GETNUM(TALENT, "尻尾あり"))
	TALENT:キャラ番号:エルーン耳 = CSVTALENT(NO:キャラ番号, GETNUM(TALENT, "エルーン耳"))
	TALENT:キャラ番号:ハーヴィン耳 = CSVTALENT(NO:キャラ番号, GETNUM(TALENT, "ハーヴィン耳"))
	TALENT:キャラ番号:バストサイズ = CSVTALENT(NO:キャラ番号, GETNUM(TALENT, "バストサイズ"))
	BASE:キャラ番号:身長 = 0
	BASE:キャラ番号:バスト = 0
	CALL INIT_BODYSIZE(キャラ番号)
	RETURN 1
ENDIF

;ここを通るのはヒューマン～ハーヴィンの4種族のみ

;感覚なしの場合、生やす
SIF TALENT:キャラ番号:Ｃ感度 == -2
	TALENT:キャラ番号:Ｃ感度 = -1
SIF TALENT:キャラ番号:Ｂ感度 == -2
	TALENT:キャラ番号:Ｂ感度 = -1
SIF TALENT:キャラ番号:Ｖ感度 == -2
	TALENT:キャラ番号:Ｖ感度 = -1
SIF TALENT:キャラ番号:Ａ感度 == -2
	TALENT:キャラ番号:Ａ感度 = -1
SIF TALENT:キャラ番号:Ｓ感度 == -2
	TALENT:キャラ番号:Ｓ感度 = -1



IF !((変更元種族 == 1 && 変更先種族 == 2) || (変更元種族 == 2 && 変更先種族 == 1))
	;ヒューマン←→エルーンの場合を除き、ランダムでバスト・体格変化
	SIF GETBIT(TALENT:キャラ番号:性別, 0)
		TALENT:キャラ番号:バストサイズ = ランダムバストサイズ(変更先種族, TALENT:キャラ番号:年齢層)
	TALENT:キャラ番号:体格 = ランダム体格(変更先種族, TALENT:キャラ番号:性別, キャラ見た目年齢(キャラ番号), TALENT:キャラ番号:バストサイズ, (TALENT:キャラ番号:年齢層>=年齢層_老年))
ENDIF

;種族に応じた変化
SELECTCASE 変更先種族
	CASE 1
		;ヒューマン
		TALENT:キャラ番号:角あり = 0
		TALENT:キャラ番号:尻尾あり = 0
		TALENT:キャラ番号:エルーン耳 = 0
		TALENT:キャラ番号:ハーヴィン耳 = 0
	CASE 2
		;エルーン
		TALENT:キャラ番号:角あり = 0
		IF RAND:10 == 0
			TALENT:キャラ番号:尻尾あり = 1
		ELSE
			TALENT:キャラ番号:尻尾あり = 0
		ENDIF
		TALENT:キャラ番号:エルーン耳 = 1
		TALENT:キャラ番号:ハーヴィン耳 = 0
	CASE 3
		;ドラフ
		TALENT:キャラ番号:角あり = 1
		TALENT:キャラ番号:尻尾あり = 0
		TALENT:キャラ番号:エルーン耳 = 0
		TALENT:キャラ番号:ハーヴィン耳 = 0
	CASE 4
		;ハーヴィン
		TALENT:キャラ番号:角あり = 0
		TALENT:キャラ番号:尻尾あり = 0
		TALENT:キャラ番号:エルーン耳 = 0
		TALENT:キャラ番号:ハーヴィン耳 = 1
ENDSELECT
RETURN 0


@発情期素質設定(対象キャラ)
#DIM 対象キャラ
#DIM 表示番号
#DIM 発情不可フラグ
#DIM CONST 必要COST = 20
VARSET LOCALS
表示番号 = 0
発情不可フラグ = 0

DRAWLINE
PRINTL ZP를 소비하여 캐릭터의 발정기를 설정하거나, 특수 발정을 부여/삭제할 수 있습니다.
PRINTL 종족에 따라서는 일부 특수 발정을 해제할 수 없는 경우가 있습니다.
PRINTFORML 소지 ZP:{FLAG:ZP所持量}.{(ZP小数点以下所持量 % 1000) / 100}{(ZP小数点以下所持量 % 100) / 10}{(ZP小数点以下所持量 % 10)}
DRAWLINE
PRINTFORML ・현재 %CALLNAME:対象キャラ%의 발정기 상황
SELECTCASE TALENT:対象キャラ:性別
	CASE 0
		PRINTL 무성별의 경우, 발정할 수 없습니다.
		発情不可フラグ = 1
	CASE 1
		IF TALENT:対象キャラ:妊娠不可 == 2
			PRINTL 초경 전의 경우, 발정할 수 없습니다.
			発情不可フラグ = 1
		ENDIF
	CASE 2
		IF TALENT:対象キャラ:射精不可 == 2
			PRINTL 정통 전의 경우, 발정할 수 없습니다.
			発情不可フラグ = 1
		ENDIF
	CASE 3
		IF TALENT:対象キャラ:妊娠不可 == 2 && TALENT:対象キャラ:射精不可 == 2
			PRINTL 정통 전이자 초경 전의 경우, 발정할 수 없습니다.
			発情不可フラグ = 1
		ENDIF
ENDSELECT
IF 発情不可フラグ
ELSEIF 発情期判定(対象キャラ)
	PRINTL ■ 발정기 있음
	IF GETBIT(TALENT:対象キャラ:発情期あり, 1) || TALENT:対象キャラ:種族 == 2
		LOCALS:(表示番号++) = ├욕정 부스트 (대)
	ENDIF
	IF (GETBIT(TALENT:対象キャラ:発情期あり, 2) || TALENT:対象キャラ:種族 == 3) && 素質切り替えOPTION記憶:母乳体質_発情期_なし == 0
		LOCALS:(表示番号++) = ├모유 체질화
	ENDIF
	IF GETBIT(TALENT:対象キャラ:発情期あり, 3) && 素質切り替えOPTION記憶:ふたなり_女性 == 0
		LOCALS:(表示番号++) = ├후타나리화
	ENDIF
	IF GETBIT(TALENT:対象キャラ:発情期あり, 4) || TALENT:対象キャラ:種族 == 2
		LOCALS:(表示番号++) = ├위험일 주기 단축
	ENDIF
	IF GETBIT(TALENT:対象キャラ:発情期あり, 5) || TALENT:対象キャラ:種族 == 3
		LOCALS:(表示番号++) = ├체력 자동 회복
	ENDIF
	IF GETBIT(TALENT:対象キャラ:発情期あり, 6)
		LOCALS:(表示番号++) = ├과민 체질 (쾌락과 고통에 보너스)
	ENDIF
	IF GETBIT(TALENT:対象キャラ:発情期あり, 7) && 素質切り替えOPTION記憶:陥没乳首_なし == 0
		LOCALS:(表示番号++) = ├함몰 유두 상시 발기
	ENDIF
	IF GETBIT(TALENT:対象キャラ:発情期あり, 8) && GETBIT(CFLAG:対象キャラ:元性別保存, 0) == 0
		LOCALS:(表示番号++) = ├여체화
	ENDIF

	;最後の├だけ置き換える
	IF 表示番号 > 0
		LOCALS:(表示番号 - 1) = %REPLACE(LOCALS:(表示番号 - 1), "├", "└")%
		FOR LOCAL, 0, 表示番号
			PRINTFORML 　%LOCALS:LOCAL%
		NEXT
	ENDIF
	DRAWLINE
	IF FLAG:ZP所持量 < 1
		SETCOLOR カラーパレット("グレーアウト")
		PRINTL 　ZP 부족으로 발정기 없음으로 변경 불가
		RESETCOLOR
	ELSE
		PRINTBUTTON "　[-1] 발정기를 없음으로 변경한다", -1
		PRINTL  …  1ZP
	ENDIF
	PRINTL
	IF TALENT:対象キャラ:種族 == 2
		SETCOLOR カラーパレット("グレーアウト")
		PRINTL 　종족: 에룬은 욕정 부스트 (대)를 해제할 수 없습니다.
		RESETCOLOR
	ELSEIF FLAG:ZP所持量 < 必要COST
		SETCOLOR カラーパレット("グレーアウト")
		PRINTL 　ZP 부족으로 욕정 부스트 (대) 변경 불가
		RESETCOLOR
	ELSEIF GETBIT(TALENT:対象キャラ:発情期あり, 1)
		PRINTBUTTON "　[1] 욕정 부스트(대)를 없음으로 변경한다", 1
		PRINTFORML 　　　 … {必要COST}ZP
	ELSE
		PRINTBUTTON "　[1] 욕정 부스트(대)를 있음으로 변경한다", 1
		PRINTFORML 　　　 … {必要COST}ZP
	ENDIF
	IF 素質切り替えOPTION記憶:母乳体質_発情期_なし == 0
		IF TALENT:対象キャラ:種族 == 3
			SETCOLOR カラーパレット("グレーアウト")
			PRINTL 　종족: 드라프는 모유 체질화를 해제할 수 없습니다.
			RESETCOLOR
		ELSEIF FLAG:ZP所持量 < 必要COST
			SETCOLOR カラーパレット("グレーアウト")
			PRINTL 　ZP 부족으로 인해 모유 체질화 변경 불가
			RESETCOLOR
		ELSEIF GETBIT(TALENT:対象キャラ:発情期あり, 2)
			PRINTBUTTON "　[2] 모유 체질화를 없음으로 변경한다", 2
			PRINTFORML 　　　　　　　 … {必要COST}ZP
		ELSE
			PRINTBUTTON "　[2] 모유 체질화를 있음으로 변경한다", 2
			PRINTFORML 　　　　　　　 … {必要COST}ZP
		ENDIF
	ENDIF
	IF 素質切り替えOPTION記憶:ふたなり_女性 == 0
		IF FLAG:ZP所持量 < 必要COST
			SETCOLOR カラーパレット("グレーアウト")
			PRINTL 　ZP 부족으로 인해 후타나리화 변경 불가
			RESETCOLOR
		ELSEIF GETBIT(TALENT:対象キャラ:発情期あり, 3)
			PRINTBUTTON "　[3] 후타나리화를 없음으로 변경한다", 3
			PRINTFORML 　　　　　　　 … {必要COST}ZP
		ELSE
			PRINTBUTTON "　[3] 후타나리화를 있음으로 변경한다", 3
			PRINTFORML 　　　　　　　 … {必要COST}ZP
		ENDIF
	ENDIF
	IF TALENT:対象キャラ:種族 == 2
		SETCOLOR カラーパレット("グレーアウト")
		PRINTL 　종족: 엘룬은 위험일 주기 단축을 해제할 수 없습니다.
		RESETCOLOR
	ELSEIF FLAG:ZP所持量 < 必要COST
		SETCOLOR カラーパレット("グレーアウト")
		PRINTL 　ZP 부족으로 인해 위험일 주기 단축 변경 불가
		RESETCOLOR
	ELSEIF GETBIT(TALENT:対象キャラ:発情期あり, 4)
		PRINTBUTTON "　[4] 위험일 주기 단축을 없음으로 변경한다", 4
		PRINTFORML 　　　　　 … {必要COST}ZP
	ELSE
		PRINTBUTTON "　[4] 위험일 주기 단축을 있음으로 변경한다", 4
		PRINTFORML 　　　　　 … {必要COST}ZP
	ENDIF
	IF TALENT:対象キャラ:種族 == 3
		SETCOLOR カラーパレット("グレーアウト")
		PRINTL 　종족: 드라프는 체력 자동 회복을 해제할 수 없습니다.
		RESETCOLOR
	ELSEIF FLAG:ZP所持量 < 必要COST
		SETCOLOR カラーパレット("グレーアウト")
		PRINTL 　ZP 부족으로 인해 체력 자동 회복 변경 불가
		RESETCOLOR
	ELSEIF GETBIT(TALENT:対象キャラ:発情期あり, 5)
		PRINTBUTTON "　[5] 체력 자동 회복을 없음으로 변경한다", 5
		PRINTFORML 　　　　　　 … {必要COST}ZP
	ELSE
		PRINTBUTTON "　[5] 체력 자동 회복을 있음으로 변경한다", 5
		PRINTFORML 　　　　　　 … {必要COST}ZP
	ENDIF
	IF FLAG:ZP所持量 < 必要COST
		SETCOLOR カラーパレット("グレーアウト")
		PRINTL 　ZP 부족으로 인해 과민 체질(쾌락·고통+) 변경 불가
		RESETCOLOR
	ELSEIF GETBIT(TALENT:対象キャラ:発情期あり, 6)
		PRINTBUTTON "　[6] 과민 체질(쾌락·고통+)을 없음으로 변경한다", 6
		PRINTFORML  … {必要COST}ZP
	ELSE
		PRINTBUTTON "　[6] 과민 체질(쾌락·고통+)을 있음으로 변경한다", 6
		PRINTFORML  … {必要COST}ZP
	ENDIF
	IF 素質切り替えOPTION記憶:陥没乳首_なし == 0
		IF FLAG:ZP所持量 < 必要COST
			SETCOLOR カラーパレット("グレーアウト")
			PRINTL 　ZP 부족으로 인해 함몰 유두 상시 발기 변경 불가
			RESETCOLOR
		ELSEIF GETBIT(TALENT:対象キャラ:発情期あり, 7)
			PRINTBUTTON "　[7] 함몰 유두 상시 발기를 없음으로 변경한다", 7
			PRINTFORML  　　　　… {必要COST}ZP
		ELSE
			PRINTBUTTON "　[7] 함몰 유두 상시 발기를 있음으로 변경한다", 7
			PRINTFORML  　　　　… {必要COST}ZP
		ENDIF
	ENDIF
	IF FLAG:ZP所持量 < 必要COST
		SETCOLOR カラーパレット("グレーアウト")
		PRINTL 　ZP 부족으로 인해 여체화 변경 불가
		RESETCOLOR
	ELSEIF GETBIT(TALENT:対象キャラ:発情期あり, 8)
		PRINTBUTTON "　[8] 여체화를 없음으로 변경한다", 8
		PRINTFORML  … {必要COST}ZP
	ELSE
		PRINTBUTTON "　[8] 여체화를 있음으로 변경한다", 8
		PRINTFORML  … {必要COST}ZP
	ENDIF
ELSE
	PRINTL ■ 발정기 없음
	DRAWLINE
	IF FLAG:ZP所持量 < 必要COST
		SETCOLOR カラーパレット("グレーアウト")
		PRINTL 　ZP 부족으로 인해 발정기 있음으로 변경 불가
		RESETCOLOR
	ELSE
		PRINTBUTTON "　[0] 발정기를 있음으로 변경한다", 0
		PRINTFORML … {必要COST}ZP
	ENDIF
ENDIF
PRINTL
PRINTBUTTON "　[999] 돌아가기", 999
PRINTL
PRINTL

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE -1
		PRINTL 발정기를 없음으로 변경합니다. 괜찮습니까?
		PRINTL ※특수 발정은 종족에 부수되는 것을 제외하고 리셋됩니다.
		PRINTL 사용 ZP: 1
		PRINTBUTTONLC "[0] 예", 0
		PRINTBUTTONLC "[1] 아니오", 1
		BINPUT
		IF RESULT == 0
			TALENT:対象キャラ:発情期あり = -1
			FLAG:ZP所持量 -= 1
		ENDIF
	CASE 0
		PRINTL 발정기를 있음으로 변경합니다. 괜찮습니까?
		PRINTFORML 사용 ZP: {必要COST}ZP
		PRINTBUTTONLC "[0] 예", 0
		PRINTBUTTONLC "[1] 아니오", 1
		BINPUT
		IF RESULT == 0
			TALENT:対象キャラ:発情期あり = 1
			FLAG:ZP所持量 -= 必要COST
		ENDIF
	CASE 999
		RETURN 0
	CASEELSE
		SELECTCASE LOCAL
			CASE 1
				PRINT 욕정 부스트 (대)
			CASE 2
				PRINT 모유 체질화
			CASE 3
				PRINT 후타나리화
			CASE 4
				PRINT 위험일 주기 단축
			CASE 5
				PRINT 체력 자동 회복
			CASE 6
				PRINT 과민 체질 (쾌락·고통+)
			CASE 7
				PRINT 함몰유두 상시발기
			CASE 8
				PRINT 여체화
		ENDSELECT
		IF GETBIT(TALENT:対象キャラ:発情期あり, LOCAL)
			PRINTL 을(를) 없음으로 변경합니다. 괜찮으시겠습니까?
		ELSE
			PRINTL 을(를) 있음으로 변경합니다. 괜찮으시겠습니까?
		ENDIF
		IF LOCAL == 8
			PRINTL ※후타나리화의 발정이 있을 경우, 그쪽이 우선됩니다.
		ENDIF
		PRINTFORML 사용 ZP: {必要COST}
		PRINTBUTTONLC "[0] 예", 0
		PRINTBUTTONLC "[1] 아니요", 1
		BINPUT
		IF RESULT == 0
			INVERTBIT TALENT:対象キャラ:発情期あり, LOCAL
			FLAG:ZP所持量 -= 必要COST
		ENDIF
ENDSELECT

RESTART


@フレーバー素質再設定(対象キャラ)
#DIM 対象キャラ
#DIM CONST 必要COST = 30

DRAWLINE
PRINTFORML ZP를 소비하여 %NAME表示(対象キャラ)%의 플레이버 소질 설정을 다시 할 수 있습니다.
PRINTFORML 현재 성별에 따라 표시되지 않는 항목이 존재할 수 있으니 주의하십시오.
PRINTFORML 소지 ZP:{FLAG:ZP所持量}.{(ZP小数点以下所持量 % 1000) / 100}{(ZP小数点以下所持量 % 100) / 10}{(ZP小数点以下所持量 % 10)}
DRAWLINE

;ZP足りる？
IF FLAG:ZP所持量 < 必要COST
	PRINTW ZP가 부족합니다
	RETURN 0
ENDIF

PRINTFORML {必要COST}ZP를 지불하고, 스테이터스 설정을 다시 하시겠습니까?
PRINTFORML (소지 ZP:{FLAG:ZP所持量})
PRINTBUTTONLC "[0]예", 0
PRINTBUTTONLC "[1]아니오", 1
BINPUT

IF RESULT == 0
	FLAG:ZP所持量 -= 必要COST
	CALL フレーバー素質個別設定画面(対象キャラ)
	RETURN 1
ENDIF
RETURN 0

@キャラメイク設定
#DIM CONST 必要COST = 300

DRAWLINE
PRINTL ZP를 소비하여 게임 시작 시의 당신의 스테이터스 설정을 다시 할 수 있습니다.
PRINTL 설정 내용에 따라서는, 일부 능력, 경험이 초기화될 수 있습니다.
PRINTL 또한, 성별을 변경해도 [원래 남자] 등의 후천적인 소질은 부여되지 않습니다.
PRINTFORML 소지 ZP:{FLAG:ZP所持量}.{(ZP小数点以下所持量 % 1000) / 100}{(ZP小数点以下所持量 % 100) / 10}{(ZP小数点以下所持量 % 10)}
DRAWLINE

;ZP足りる？
IF FLAG:ZP所持量 < 必要COST
	PRINTW ZP가 부족합니다
	RETURN 0
ENDIF

PRINTFORML {必要COST}ZP를 지불하고, 스테이터스 설정을 다시 하시겠습니까?
PRINTFORML (소지 ZP:{FLAG:ZP所持量})
PRINTBUTTONLC "[0]예", 0
PRINTBUTTONLC "[1]아니오", 1
BINPUT

IF RESULT == 0
	FLAG:ZP所持量 -= 必要COST
	CALL NAME_CUSTOM(MASTER)
	CALL 仮置きCUSTOM_TERMINAL(MASTER)
	RETURN 1
ENDIF
RETURN 0


@身長固定キャラ(対象キャラ)
#FUNCTION
#DIM 対象キャラ

;身長が入ってないなら固定も何もないので0
SIF BASE:対象キャラ:身長 == 0
	RETURNF 0

;以下、性転換しても身長を変動させないキャラを置く
IF キャラ一致チェック(対象キャラ, "[작고 붉은 용]비")
	RETURNF 1
ENDIF



@バスト固定キャラ(対象キャラ)
#FUNCTION
#DIM 対象キャラ

;バストが入ってないなら固定も何もないので0
SIF BASE:対象キャラ:バスト == 0
	RETURNF 0

;以下、性転換してもバストを変動させないキャラを置く
IF キャラ一致チェック(対象キャラ, "[작고 붉은 용]비")
	RETURNF 1
ENDIF


@性別変化時_身長任意設定(対象キャラ)
#DIM 対象キャラ
#DIM ループ用
#DIM DYNAMIC 設定モード
#DIM DYNAMIC 補助数値

PRINTL 
PRINTL 
PRINTL 성별 변화 후의 신장을 임의로 설정할 수 있습니다.
PRINTL 변경하시겠습니까?
PRINTL 
IF 設定モード == 0
	SETCOLOR カラーパレット("黄")
	PRINTPLAIN [0] 자동 변경 (선택 중)
	RESETCOLOR
ELSE
	PRINTBUTTON "[0] 자동 변경", 0
ENDIF
PRINTL 
IF 設定モード == 1
	SETCOLOR カラーパレット("黄")
	PRINTPLAIN [1] 원래 수치 유지 (선택 중)
	RESETCOLOR
ELSE
	PRINTBUTTON "[1] 원래 수치를 유지한다", 1
ENDIF
PRINTL 
IF 設定モード == 2
	SETCOLOR カラーパレット("黄")
	PRINTPLAINFORM [2] 체격 소질로 지정한다 (선택 중：%GET_TALENTNAME(161, 補助数値, ,対象キャラ)%）
	RESETCOLOR
ELSE
	PRINTBUTTON "[2] 체격 소질로 지정한다", 2
ENDIF
PRINTL 
IF 設定モード == 3
	SETCOLOR カラーパレット("黄")
	PRINTPLAINFORM [3] 수치로 지정한다 (선택 중：%DECIMAL_STRING(補助数値, 1)%cm）
	RESETCOLOR
ELSE
	PRINTBUTTON "[3] 수치로 지정한다", 3
ENDIF
PRINTL 
PRINTL
PRINTBUTTON "[999] 이걸로 결정한다", 999
PRINTL
PRINTL

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 0 TO 1
		補助数値 = 0
		設定モード = LOCAL
	CASE 2
		補助数値 = TALENT:対象キャラ:体格
		設定モード = LOCAL
		DRAWLINE
		PRINTL 원하는 체격을 선택해 주세요.
		DRAWLINE
		FOR ループ用, -3, 3
			IF GET_TALENTNAME(161, ループ用, ,対象キャラ) != GET_TALENTNAME(161, ループ用)
				PRINTBUTTON @"[{ループ用}] %GET_TALENTNAME(161, ループ用, ,対象キャラ), 20, LEFT%：%GET_TALENTNAME(161, ループ用)%호환", ループ用
			ELSE
				PRINTBUTTON @"[{ループ用}] %GET_TALENTNAME(161, ループ用, ,対象キャラ), 20, LEFT%", ループ用
			ENDIF
			PRINTL 
		NEXT
		PRINTL 
		BINPUT
		補助数値 = RESULT
	CASE 3
		補助数値 = BASE:対象キャラ:身長
		設定モード = LOCAL
		$数値入力
		DRAWLINE
		PRINTL 신장 수치를 mm 단위로 입력해 주세요.
		DRAWLINE
		PRINTL
		INPUT
		IF RESULT <= 0
			PRINTW 마이너스 수치, 0은 입력할 수 없습니다.
			GOTO 数値入力
		ENDIF
		補助数値 = RESULT
	CASE 999
		SELECTCASE 設定モード
			CASE 0
				RETURN 0
			CASE 1
				RETURN 1
			CASE 2
				TALENT:対象キャラ:体格 = 補助数値
				BASE:対象キャラ:身長 = 0
				MAXBASE:対象キャラ:身長 = 0
				CALL RESET_HEIGHTSIZE(対象キャラ)
				RETURN 1
			CASE 3
				TALENT:対象キャラ:体格 = 0
				BASE:対象キャラ:身長 = 補助数値
				MAXBASE:対象キャラ:身長 = 補助数値
				CALL RESET_HEIGHTSIZE(対象キャラ)
				RETURN 1
		ENDSELECT
ENDSELECT

RESTART


@弱点コマンド追加(対象キャラ)
#DIM 対象キャラ
#DIM グレーアウト閾値
#DIM フィルタフラグ
#DIM 枠番号
#DIM 選択枠
#DIM ZP消費量
#DIM 入力値
#DIMS 表示種別
#DIM 表示ID配列, 2000

PRINTL
DRAWLINE
PRINTFORML %NAME表示(対象キャラ)%에 약점을 추가합니다.
PRINTL 추가할 칸과 종류를 선택해 주세요.
DRAWLINE
PRINTL ・현재 추가 약점 칸
FOR 枠番号, 0, 追加弱点コマンド数上限
	SIF 選択枠 == 枠番号
		SETCOLOR カラーパレット("黄")
	IF 追加弱点コマンド枠:対象キャラ:枠番号 == ""
		PRINTBUTTON "[빈 칸]", 枠番号 + 1000
	ELSE
		PRINTBUTTON @"[%弱点の欠片名前表示(追加弱点コマンド枠:対象キャラ:枠番号)%]", 枠番号 + 1000
	ENDIF
	RESETCOLOR
	PRINT  / 
NEXT
PRINTL
SELECTCASE 表示種別
	CASE "モード"
		フィルタフラグ = 1
		グレーアウト閾値 = 999
		ZP消費量 = 100
		PRINTL ・모드 약점 필요 조각 수: 1000개 필요 ZP: 100
	CASE "シチュエーション"
		フィルタフラグ = 2
		グレーアウト閾値 = 2999
		ZP消費量 = 200
		PRINTL ・상황 약점 필요 조각 수: 3000개 필요 ZP: 200
	CASE "マッサージ"
		フィルタフラグ = 3
		グレーアウト閾値 = 99
		ZP消費量 = 30
		PRINTL ・마사지 약점 필요 조각 수: 100개 필요 ZP: 30
	CASE "むりやりうふふ"
		フィルタフラグ = 4
		グレーアウト閾値 = 99
		ZP消費量 = 30
		PRINTL ・억지로 우후후 약점 필요 조각 수: 100개 필요 ZP: 30
	CASEELSE
		フィルタフラグ = 0
		グレーアウト閾値 = 99
		ZP消費量 = 30
		PRINTL ・일반 약점 필요 조각 수: 100개 필요 ZP: 30
ENDSELECT
PRINTFORML 보유 ZP:{FLAG:ZP所持量}.{(ZP小数点以下所持量 % 1000) / 100}{(ZP小数点以下所持量 % 100) / 10}{(ZP小数点以下所持量 % 10)}
DRAWLINE

CALL 弱点の欠片表示画面(フィルタフラグ, 対象キャラ, グレーアウト閾値, 1)
VARSET 表示ID配列
ARRAYCOPY "RESULT", "表示ID配列"
PRINTL
DRAWLINE
PRINTBUTTON "[9999] 돌아가기", 9999
PRINTL
BINPUT
入力値 = RESULT
SELECTCASE 入力値
	CASE 9999
		表示種別 = 
		能力表示現在ページ = 0
		RETURN 0
	CASE 900
		能力表示現在ページ --
	CASE 901
		能力表示現在ページ ++
	CASE 1000 TO 1000 + 追加弱点コマンド数上限
		選択枠 = 入力値 - 1000
	CASE 1014
		表示種別 = 
		能力表示現在ページ = 0
	CASE 1015
		表示種別 = モード
		能力表示現在ページ = 0
	CASE 1016
		表示種別 = シチュエーション
		能力表示現在ページ = 0
	CASE 1017
		表示種別 = マッサージ
		能力表示現在ページ = 0
	CASE 1018
		表示種別 = むりやりうふふ
		能力表示現在ページ = 0
	CASEELSE
		IF ZP消費量 > FLAG:ZP所持量
			PRINTW ZP가 부족합니다
		ELSEIF MATCH(追加弱点コマンド枠:対象キャラ:0, DT_CELL_GETS("弱点の欠片データベース", 表示ID配列:入力値, "弱点キー", 1))
			PRINTW 이미 같은 약점이 추가되어 있습니다
		ELSE
			;欠片を消費して弱点を付与
			PRINT 추가 약점
			IF 追加弱点コマンド枠:対象キャラ:選択枠 == ""
				PRINT [빈 슬롯]
			ELSE
				PRINTFORM [%弱点の欠片名前表示(追加弱点コマンド枠:対象キャラ:選択枠)%]
			ENDIF
			PRINTFORML 을(를)[%弱点の欠片名前表示(DT_CELL_GETS("弱点の欠片データベース", 表示ID配列:入力値, "弱点キー", 1))%]으로 변경하시겠습니까?
			PRINTFORML 소비할 약점 조각:{グレーアウト閾値 + 1, 4}개 / 보유 수량:{DT_CELL_GET("弱点の欠片データベース", 表示ID配列:入力値, "数量", 1)}개
			PRINTFORML 소비할 ZP:{ZP消費量, 4}
			PRINTL
			PRINTBUTTONLC "[0] 예", 0
			PRINTBUTTONLC "[1] 아니요", 1
			PRINTL
			BINPUT
			IF RESULT == 0
				FLAG:ZP所持量 -= ZP消費量
				DT_CELL_SET "弱点の欠片データベース", 表示ID配列:入力値, "数量", DT_CELL_GET("弱点の欠片データベース", 表示ID配列:入力値, "数量", 1) - (グレーアウト閾値 + 1), 1
				PRINTFORMW 추가 약점에[%弱点の欠片名前表示(DT_CELL_GETS("弱点の欠片データベース", 表示ID配列:入力値, "弱点キー", 1))%]을(를) 설정했습니다.
				追加弱点コマンド枠:対象キャラ:選択枠 '= DT_CELL_GETS("弱点の欠片データベース", 表示ID配列:入力値, "弱点キー", 1)
			ENDIF
		ENDIF
ENDSELECT

RESTART

@体格・体型素質設定(対象キャラ)
#DIM 対象キャラ
#DIMS CONST 見出し文字色 = "959595"
#DIM 身長保存
#DIM 体格保存
#DIM 体型保存
#DIM 下半身保存
#DIM 身長数値設定フラグ
#DIM 素質番号
#DIMS HTML文字列
#DIMS 一時文字列
#DIM 変動位置
#DIM 消費ZP

;初期数値
身長保存 = BASE:対象キャラ:身長
体格保存 = TALENT:対象キャラ:体格
体型保存 = TALENT:対象キャラ:体型
下半身保存 = TALENT:対象キャラ:下半身

消費ZP = 60

$表示部分
DRAWLINE
PRINTFORML %NAME表示(対象キャラ)% 의 체격·체형 설정을 합니다.
PRINTFORML 변경 시{消費ZP}ZP를 소비합니다.
PRINTFORML 소지 ZP:{FLAG:ZP所持量}.{(ZP小数点以下所持量 % 1000) / 100}{(ZP小数点以下所持量 % 100) / 10}{(ZP小数点以下所持量 % 10)}
DRAWLINE
HTML文字列 = <font color='#%見出し文字色%'>신장:</font>
IF 身長数値設定フラグ
	一時文字列 = %DECIMAL_STRING(身長保存, 1)%cm

	HTML文字列 += @"<button value='{12025}'>[%一時文字列%]</button>"
	HTML文字列 += " " * (12 - MIN(12-1, STRLENS(一時文字列)))
	HTML文字列 += @"　<button value='{12998}'>[대략적으로 설정한다]</button>"
	HTML文字列 += @"　<button value='{12996}'>[리롤]</button>"
ELSE
	HTML文字列 += @"%GET_TALENTNAME(161, 体格保存), 14, LEFT%"
	HTML文字列 += @"　<button value='{12998}'>[수치로 설정한다]</button>"
ENDIF
HTML文字列 += "<br>"
HTML文字列 += @"<font color='#%見出し文字色%'>체격:</font>"
FOR 素質番号, -3, 3
	IF 体格保存 == 素質番号
		IF 身長数値設定フラグ
			一時文字列 = FFFF00
		ELSE
			一時文字列 = C0C0C0
		ENDIF
	ELSE
		一時文字列 = 333333
	ENDIF
	HTML文字列 += @"<font color='#%一時文字列%'><button value='{12050 + 素質番号}'>[%GET_TALENTNAME(161, 素質番号)%]</button></font>"
NEXT
HTML文字列 += "<br>"

HTML文字列 += @"<font color='#%見出し文字色%'>체형:</font>"
FOR 素質番号, -3, 4
	IF 体型保存 == 素質番号
		一時文字列 = C0C0C0
	ELSE
		一時文字列 = 333333
	ENDIF
	HTML文字列 += @"<font color='#%一時文字列%'><button value='{12080 + 素質番号}'>[%GET_TALENTNAME(163, 素質番号)%]</button></font>"
NEXT
HTML文字列 += "<br>"

HTML文字列 += @"<font color='#%見出し文字色%'>하반신:</font>"
FOR 素質番号, -2, 3, 2
	IF 下半身保存 == 素質番号 || 下半身保存 == (素質番号 / 2)
		一時文字列 = C0C0C0
	ELSE
		一時文字列 = 333333
	ENDIF
	HTML文字列 += @"<font color='#%一時文字列%'><button value='{12090 + 素質番号}' title='%GET_TALENTNAME(210, 素質番号, 1)%'>[%GET_TALENTNAME(210, 素質番号)%]</button></font>"
NEXT
HTML文字列 += "<br>"
HTML_PRINT HTML文字列
DRAWLINE
PRINTBUTTONLC "[999] 변경하지 않고 돌아가기", 999
IF FLAG:ZP所持量 < 消費ZP
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [998] 이것으로 변경한다 (ZP 부족)
	RESETCOLOR

ELSEIF 身長保存 != BASE:対象キャラ:身長 || 体格保存 != TALENT:対象キャラ:体格 || 体型保存 != TALENT:対象キャラ:体型 || 下半身保存 != TALENT:対象キャラ:下半身
	PRINTBUTTONLC "[998] 이것으로 변경한다", 998
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [998] 이것으로 변경한다 (변동 없음)
	RESETCOLOR
ENDIF

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 999
		RETURN 0
	CASE 998
		PRINTL 이상의 설정으로 변경해도 괜찮습니까?
		PRINTBUTTONLC "[0] 예", 0
		PRINTBUTTONLC "[1] 아니요", 1
		PRINTL 
		BINPUT
		IF RESULT == 0
			FLAG:ZP所持量 -= 消費ZP
			;変更情報をセットし、スリーサイズを再設定
			変動位置 = 0
			IF 身長数値設定フラグ
				IF 身長保存 != BASE:対象キャラ:身長
					BASE:対象キャラ:身長 = 身長保存
					TALENT:対象キャラ:体格 = 0
					CALL RESET_HEIGHTSIZE(対象キャラ)
					SETBIT 変動位置, 1
					SETBIT 変動位置, 2
					SETBIT 変動位置, 3
				ENDIF
			ELSE
				IF 体格保存 != TALENT:対象キャラ:体格
					TALENT:対象キャラ:体格 = 体格保存
					BASE:対象キャラ:身長 = 0
					CALL SET_BODYSIZE_HEIGHT(対象キャラ)
					SETBIT 変動位置, 1
					SETBIT 変動位置, 2
					SETBIT 変動位置, 3
				ENDIF
			ENDIF
			IF 体型保存 != TALENT:対象キャラ:体型
				TALENT:対象キャラ:体型 = 体型保存
				SETBIT 変動位置, 1
				SETBIT 変動位置, 2
				SETBIT 変動位置, 3
			ENDIF
			IF 下半身保存 != TALENT:対象キャラ:下半身
				TALENT:対象キャラ:下半身 = 下半身保存
				SETBIT 変動位置, 3
			ENDIF
			IF 変動位置 != 0
				CALL SET_BODYSIZE(対象キャラ, 変動位置)
			ENDIF
			PRINTW 변경이 완료되었습니다.
			RETURN 0
		ENDIF
	CASE 12996
		身長保存 = EDIT用身長算出(TALENT:対象キャラ:性別, TALENT:対象キャラ:種族, 体格保存)
	CASE 12998
		INVERTBIT 身長数値設定フラグ, 0
	CASE 12025
		DRAWLINE
		PRINTL 신장을 mm 단위로 입력해 주세요.
		SETTEXTBOX TOSTR(身長保存)
		INPUT
		身長保存 = RESULT
		体格保存 = EDIT用体格算出(TALENT:対象キャラ:性別, 身長保存)
	CASE 12047 TO 12052
		体格保存 = LOCAL - 12050
		身長保存 = EDIT用身長算出(TALENT:対象キャラ:性別, TALENT:対象キャラ:種族, 体格保存)
	CASE 12077 TO 12083
		体型保存 = LOCAL - 12080
	CASE 12088 TO 12092
		下半身保存 = LOCAL - 12090
ENDSELECT

GOTO 表示部分


@ハーレムの心得素質設定(対象キャラ)
#DIM 対象キャラ
#DIM CONST 消費ZP = 200



$表示部分
DRAWLINE
PRINTFORML %NAME表示(対象キャラ)% 에게 하렘의 마음가짐을 취득시킵니다.
PRINTFORML 연모의 인연 맺기를 할 때, 자신이 연모를 이미 가지고 있어도 인연 맺기가 진행되게 됩니다.
PRINTFORML 변경 시 {消費ZP}ZP를 소비합니다.
PRINTFORML 소지 ZP:{FLAG:ZP所持量}.{(ZP小数点以下所持量 % 1000) / 100}{(ZP小数点以下所持量 % 100) / 10}{(ZP小数点以下所持量 % 10)}
DRAWLINE
IF FLAG:ZP所持量 < 消費ZP
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [0] 취득한다 (ZP 부족)
	RESETCOLOR
ELSE
	PRINTBUTTON "[0] 취득한다", 0
ENDIF
PRINTL
PRINTBUTTON "[999] 취득하지 않고 돌아간다", 999
PRINTL
PRINTL


BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 999
		RETURN 0
	CASE 0
		TALENT:対象キャラ:ハーレムの心得 = 1
		FLAG:ZP所持量 -= 消費ZP
		PRINTL
		PRINTL
		PRINTFORMW %NAME表示(対象キャラ)% 가 하렘의 소양을 취득했습니다!
ENDSELECT
