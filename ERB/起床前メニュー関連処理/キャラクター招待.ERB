@通常広告
#DIM 集客候補, 500
#DIM 候補人数
VARSET 集客候補

;広告プランの表示
DRAWLINE
PRINTL 어느 정도 광고를 내시겠습니까?
DRAWLINE

IF MONEY < 3000
	SETCOLOR 0x666666
	PRINTPLAIN [1]가까운 섬에 광고를 내다 (루피가 부족합니다)
	PRINTL 
	RESETCOLOR
ELSE
	PRINTBUTTONLC "[1]가까운 섬에 광고를 내다", 1
	PRINTL (필요 루피: 3000루피 예상 고객 수: 2~3명)
ENDIF
IF MONEY < 5000
	SETCOLOR 0x666666
	PRINTPLAIN [2]광범위하게 광고를 내다 (루피가 부족합니다)
	PRINTL 
	RESETCOLOR
ELSE
	PRINTBUTTONLC "[2]광범위하게 광고를 내다", 2
	PRINTL (필요 루피: 5000루피 예상 고객 수: 3~5명)
ENDIF
IF MONEY < 10000
	SETCOLOR 0x666666
	PRINTPLAIN [3]공역 전체에 광고를 내다 (루피가 부족합니다)
	PRINTL 
	RESETCOLOR
ELSE
	PRINTBUTTONLC "[3]공역 전체에 광고를 내다", 3
	PRINTL (필요 루피: 10000루피 예상 고객 수: 5~7명)
ENDIF
PRINTL 
RESETCOLOR
PRINTBUTTON "[999]돌아가기", 999
PRINTL 
DRAWLINE

$INPUT_LOOP
INPUT

SELECTCASE RESULT
	CASE 1
		IF MONEY < 3000
			PRINTW 루피가 부족합니다
			GOTO INPUT_LOOP
		ENDIF
		PRINTW 가까운 섬에 광고를 냈습니다.
		MONEY -= 3000
		CALL 人気上昇処理(3000)
		集客人数 = RAND:2 + 2 + 招待キャラ数補正算出()
	CASE 2
		IF MONEY < 5000
			PRINTW 루피가 부족합니다
			GOTO INPUT_LOOP
		ENDIF
		PRINTW 먼 섬까지 광고를 냈습니다.
		MONEY -= 5000
		CALL 人気上昇処理(5000)
		集客人数 = RAND:3 + 3 + 招待キャラ数補正算出()
	CASE 3
		IF MONEY < 10000
			PRINTW 루피가 부족합니다
			GOTO INPUT_LOOP
		ENDIF
		PRINTW 큰맘 먹고 공역 전체에 광고를 냈습니다!
		MONEY -= 10000
		CALL 人気上昇処理(10000)
		集客人数 = RAND:3 + 5 + 招待キャラ数補正算出()
	CASE 999
		RETURN -1
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT

;上限以下にする
CALL ユニーク客人数チェック()
集客人数 = MIN(集客人数, (滞在キャラ上限 - 滞在キャラ数))

LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	;招待不可キャラは排除
	SIF CFLAG:LOCAL:招待不可フラグ
		CONTINUE
	;サプチケで呼ぶ予定のキャラは排除
	SIF LOCAL == サプチケキャラ招待
		CONTINUE
	;オプションによる制御
	SIF 性別招待制御_実処理(LOCAL)
		CONTINUE
	LOCAL:3 = 0
	FOR LOCAL:2, 0, 100
		SIF 解放キャラ招待:(LOCAL:2) == 0
			BREAK
		IF 解放キャラ招待:(LOCAL:2) == LOCAL
			LOCAL:3 = 1
			BREAK
		ELSEIF LOCAL:2 < 20 && スイートルーム招待:(LOCAL:2) == LOCAL
			LOCAL:3 = 1
			BREAK
		ENDIF
	NEXT
	SIF LOCAL:3
		CONTINUE
	;滞在状態ではないキャラをカウント
	IF CFLAG:LOCAL:滞在期間 == -1
		集客候補:(LOCAL:1) = LOCAL
		LOCAL:1 += 1
	ENDIF
NEXT
候補人数 = LOCAL:1

;（たぶんないと思うけど）候補人数が０の場合はそのまま終わる
SIF 候補人数 == 0
	RETURN 0

;候補人数が集客人数より少ない場合は全員登録
IF 候補人数 <= 集客人数
	FOR LOCAL, 0, 候補人数
		通常キャラ招待:LOCAL = 集客候補:LOCAL
	NEXT
ELSE
	LOCAL:2 = 0
	LOCAL = 候補人数 - 1
	;集客候補配列をシャッフル
	WHILE LOCAL > 0
		LOCAL:1 = RAND:(LOCAL - 1)
		SWAP 集客候補:LOCAL, 集客候補:(LOCAL:1)
		LOCAL -= 1
		LOCAL:2 += 1
		;必要分集まったらおしまい
		SIF LOCAL:2 >= 集客人数
			BREAK
	WEND
	;配列の後ろの方にランダムシャッフルされた候補が集まってるはずなので集客人数分取得
	FOR LOCAL, 0, 集客人数
		通常キャラ招待:LOCAL = 集客候補:(候補人数 - 1 - LOCAL)
	NEXT
ENDIF

@ＤＭ広告
#DIM 表示候補, 200
#DIM 現在ページ
#DIM 広告対象

VARSET 表示候補, -1
現在ページ = 0

;広告対象の表示
DRAWLINE
PRINTL 누구에게 다이렉트 메일을 보내시겠습니까?
DRAWLINE

;表示キャラを登録
LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	;招待不可キャラは排除
	SIF CFLAG:LOCAL:招待不可フラグ
		CONTINUE
	;好感度レベル11以上（顔見知り以上）条件
	SIF CFLAG:LOCAL:好感度レベル < 1+関係閾値:1
		CONTINUE
	;滞在済みは出さない
	SIF CFLAG:LOCAL:滞在期間 >= 0
		CONTINUE
	;オプションによる制御
	SIF 性別招待制御_実処理(LOCAL)
		CONTINUE
	LOCAL:3 = 0
	FOR LOCAL:2, 0, 100
		SIF 解放キャラ招待:(LOCAL:2) == 0
			BREAK
		IF 解放キャラ招待:(LOCAL:2) == LOCAL
			LOCAL:3 = 1
			BREAK
		ELSEIF LOCAL:2 < 20 && スイートルーム招待:(LOCAL:2) == LOCAL
			LOCAL:3 = 1
			BREAK
		ENDIF
	NEXT
	SIF LOCAL:3
		CONTINUE
	表示候補:(LOCAL:1) = LOCAL
	LOCAL:1 += 1
NEXT

$表示_LOOP
LOCAL:1 = 0
;キャラの表示（15人まで）
FOR LOCAL, (現在ページ * 30), ((現在ページ * 30) + 30)
	;登録が終わったら表示終了
	IF 表示候補:LOCAL == -1
		LOCAL:1 = 1
		BREAK
	ENDIF
	PRINTBUTTON @"[{LOCAL,3,RIGHT}] %ADD_STR_SPACE(NAME表示(表示候補:LOCAL), "]"), 50, LEFT%", LOCAL
	SIF LOCAL % 2 != 0
		PRINTL 
NEXT
PRINTL
DRAWLINE

;ページめくり表示
IF 現在ページ > 0
	PRINTBUTTONLC "[900]이전 페이지로", 900
ELSE
	PRINTLC 
ENDIF
PRINTFORM - {現在ページ + 1} -
IF LOCAL:1 == 0
	PRINTBUTTONC "[901]다음 페이지로", 901
ENDIF
PRINTL 
PRINTBUTTON "[999]돌아가기", 999

$INPUT_LOOP
INPUT

;広告対象のINPUT
SELECTCASE RESULT
	CASE 900
		現在ページ = MAX(現在ページ - 1, 0)
		GOTO 表示_LOOP
	CASE 901
		SIF LOCAL:1 == 0
			現在ページ += 1
		GOTO 表示_LOOP
	CASE 999
		RETURN -1
	CASE IS >= CHARANUM, IS < 0
		REUSELASTLINE 
		GOTO INPUT_LOOP
	CASEELSE
		IF 表示候補:RESULT < 0
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		広告対象 = 表示候補:RESULT
ENDSELECT

DRAWLINE
PRINTFORMW %CALLNAME:MASTER%은(는) 비용으로 5000루피를 지불하고, %CALLNAME:広告対象%에게 다이렉트 메일을 보냈다.
MONEY -= 5000

LOCAL = RAND:100
LOCAL:1 = 80
集客人数 = 1
;基礎確率50%の予定だけど仮実装なので80％まで上げとく、以下の素質で上下させる
;好感度による確率上昇。＋０％～１８％
LOCAL:1 += (CFLAG:広告対象:好感度レベル - 10) / 5
;恋慕だと確定で来る
IF TALENT:広告対象:恋慕
	LOCAL:1 += 100
ENDIF

;実際に来るかどうかわかるのは起床後の朝だが判定はここで確定させる
ＤＭキャラ招待 = 広告対象
;失敗時、負の数にする
SIF LOCAL >= LOCAL:1
	ＤＭキャラ招待 = ＤＭキャラ招待 * -1

@サプチケ使用
#DIM 表示候補, キャラクタ数上限
#DIM 現在ページ
#DIM 広告対象

VARSET 表示候補, -1
現在ページ = 0

;広告対象の表示
DRAWLINE
PRINTL 누구에게 서프라이즈 티켓을 사용하시겠습니까?
DRAWLINE

;表示キャラを登録
LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	;招待不可キャラは排除
	SIF CFLAG:LOCAL:招待不可フラグ
		CONTINUE
	;DM配達済みは排除
	SIF ABS(ＤＭキャラ招待) == LOCAL
		CONTINUE
	;滞在済みは出さない
	SIF CFLAG:LOCAL:滞在期間 >= 0
		CONTINUE
	;オプションによる制御
	SIF 性別招待制御_実処理(LOCAL)
		CONTINUE
	LOCAL:3 = 0
	FOR LOCAL:2, 0, 100
		SIF 解放キャラ招待:(LOCAL:2) == 0
			BREAK
		IF 解放キャラ招待:(LOCAL:2) == LOCAL
			LOCAL:3 = 1
			BREAK
		ELSEIF LOCAL:2 < 20 && スイートルーム招待:(LOCAL:2) == LOCAL
			LOCAL:3 = 1
			BREAK
		ENDIF
	NEXT
	SIF LOCAL:3
		CONTINUE
	表示候補:(LOCAL:1) = LOCAL
	LOCAL:1 += 1
NEXT

$表示_LOOP
DRAWLINE
LOCAL:1 = 0
;キャラの表示（15人まで）
FOR LOCAL, (現在ページ * 50), ((現在ページ * 50) + 50)
	;登録が終わったら表示終了
	IF 表示候補:LOCAL == -1
		LOCAL:1 = 1
		BREAK
	ENDIF
	PRINTBUTTON @"[{LOCAL,3,RIGHT}] %ADD_STR_SPACE(NAME表示(表示候補:LOCAL), "]"), 50, LEFT%", LOCAL
	SIF LOCAL % 2 != 0 && LOCAL
		PRINTL 
NEXT

PRINTL 
DRAWLINE

;ページめくり表示
IF 現在ページ > 0
	PRINTBUTTONLC "[900]이전 페이지로", 900
ELSE
	PRINTLC 　
ENDIF
PRINTFORM - {現在ページ + 1} -
IF LOCAL:1 == 0
	PRINTBUTTONC "[901]다음 페이지로", 901
ENDIF
PRINTL 
PRINTBUTTON "[999]돌아가기", 999

$INPUT_LOOP
INPUT

;広告対象のINPUT
SELECTCASE RESULT
	CASE 900
		現在ページ = MAX(現在ページ - 1, 0)
		GOTO 表示_LOOP
	CASE 901
		SIF LOCAL:1 == 0
			現在ページ += 1
		GOTO 表示_LOOP
	CASE 999
		RETURN -1
	CASE IS >= CHARANUM, IS < 0
		REUSELASTLINE 
		GOTO INPUT_LOOP
	CASEELSE
		IF 表示候補:RESULT < 0
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		広告対象 = 表示候補:RESULT
ENDSELECT

DRAWLINE
PRINTFORMW %CALLNAME:MASTER%은(는) 서프라이즈 티켓을 사용하여, %CALLNAME:広告対象%을(를) 리조트에 초대했다.
ITEM:サプチケ -= 1

サプチケキャラ招待 = 広告対象
;他の招待と併用出来るので例外的にここで滞在キャラ数を+1する
FLAG:滞在キャラ枠先確保分 += 1

;通常招待とかぶる可能性があるため、被ってたら排除
FOR LOCAL, 0, 10
	SIF 通常キャラ招待:LOCAL == 0
		BREAK
	IF 通常キャラ招待:LOCAL == サプチケキャラ招待
		通常キャラ招待:LOCAL = 10000
		BREAK
	ENDIF
NEXT

@スイートルーム招待処理
#DIM 表示候補, 500
#DIM 現在ページ
#DIM 広告対象

VARSET 表示候補, -1
現在ページ = 0

;広告対象の表示
DRAWLINE
PRINTL 누구를 무료 초대하시겠습니까?
DRAWLINE

;表示キャラを登録
LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	;招待不可キャラは排除
	SIF CFLAG:LOCAL:招待不可フラグ
		CONTINUE
	;DM配達済みは排除
	SIF ABS(ＤＭキャラ招待) == LOCAL
		CONTINUE
	;サプチケ対象は排除
	SIF サプチケキャラ招待 == LOCAL
		CONTINUE
	;滞在済みは出さない
	SIF CFLAG:LOCAL:滞在期間 >= 0
		CONTINUE
	;オプションによる制御
	SIF 性別招待制御_実処理(LOCAL)
		CONTINUE
	LOCAL:3 = 0
	FOR LOCAL:2, 0, 100
		SIF 解放キャラ招待:(LOCAL:2) == 0
			BREAK
		IF 解放キャラ招待:(LOCAL:2) == LOCAL
			LOCAL:3 = 1
			BREAK
		ENDIF
	NEXT
	SIF LOCAL:3
		CONTINUE
	表示候補:(LOCAL:1) = LOCAL
	LOCAL:1 += 1
NEXT

$表示_LOOP
DRAWLINE
LOCAL:1 = 0
;キャラの表示（15人まで）
FOR LOCAL, (現在ページ * 30), ((現在ページ * 30) + 30)
	;登録が終わったら表示終了
	IF 表示候補:LOCAL == -1
		LOCAL:1 = 1
		BREAK
	ENDIF
	PRINTBUTTON @"[{LOCAL,3,RIGHT}]%NAME表示(表示候補:LOCAL), 50, LEFT%", LOCAL
	SIF LOCAL % 2 != 0 && LOCAL
		PRINTL 
NEXT

PRINTL 
DRAWLINE

;ページめくり表示
IF 現在ページ > 0
	PRINTBUTTONLC "[900]이전 페이지로", 900
ELSE
	PRINTLC 　
ENDIF
PRINTFORM - {現在ページ + 1} -
IF LOCAL:1 == 0
	PRINTBUTTONC "[901]다음 페이지로", 901
ENDIF
PRINTL 
PRINTBUTTON "[999]돌아가기", 999

$INPUT_LOOP
INPUT

;広告対象のINPUT
SELECTCASE RESULT
	CASE 900
		現在ページ = MAX(現在ページ - 1, 0)
		GOTO 表示_LOOP
	CASE 901
		SIF LOCAL:1 == 0
			現在ページ += 1
		GOTO 表示_LOOP
	CASE 999
		RETURN -1
	CASE IS >= CHARANUM, IS < 0
		REUSELASTLINE 
		GOTO INPUT_LOOP
	CASEELSE
		IF 表示候補:RESULT < 0
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		広告対象 = 表示候補:RESULT
ENDSELECT

DRAWLINE
PRINTFORMW %CALLNAME:MASTER%은(는) %CALLNAME:広告対象%을(를) 스위트룸에 무료 초대했다.
MONEY -= 20000

FOR LOCAL, 0, スイートルーム部屋数
	SIF スイートルーム部屋割り配列:LOCAL != ""
		CONTINUE
	SIF スイートルーム招待:LOCAL > 0
		CONTINUE
	スイートルーム招待:LOCAL = 広告対象
NEXT

@人気上昇処理(使用ルピ量)
#DIM 使用ルピ量
#DIM 減衰上限ライン
#DIM 仮加算後値
#DIM 加算値
VARSET LOCAL

LOCAL = 使用ルピ量 / 100
加算値 = LOCAL * LOCAL * RAND(90, 111) / 100
IF LOCAL < 0
	加算値 = 加算値 * -1
ENDIF

SIF エンディングイベント完了フラグ確認("最も優秀な契約者") > 0 && 加算値 > 0
	加算値 = 加算値 * 2

;減衰処理
仮加算後値 = (リゾート人気度 * 1000) + 加算値
CALL 人気度上限算出()
減衰上限ライン = RESULT

IF 仮加算後値 > 減衰上限ライン
	仮加算後値 = (仮加算後値 - 減衰上限ライン) / 10 + 減衰上限ライン
ENDIF
IF 仮加算後値 >= 減衰上限ライン * 110 / 100
	仮加算後値 = (仮加算後値 - (減衰上限ライン * 110 / 100)) / 100 + (減衰上限ライン * 110 / 100)
ENDIF
IF 仮加算後値 >= 減衰上限ライン * 120 / 100
	仮加算後値 = (仮加算後値 - (減衰上限ライン * 120 / 100)) / 1000 + (減衰上限ライン * 120 / 100)
ENDIF

IF 人気度キャップ算出(仮加算後値)
	;キャップがある場合、そこまで減らす
	加算値 = 人気度キャップ算出(仮加算後値) - (リゾート人気度 * 1000 + リゾート人気度小数点以下)
	IF 加算値 == 0
		PRINTFORMW 지금은 더 이상 리조트의 인기도가 오르지 않는 것 같다…
	ENDIF
ENDIF
IF 加算値 > 0
	加算値 = MAX(仮加算後値 - (リゾート人気度 * 1000), 10)
	PRINTFORMW 리조트의 인기도가{加算値 / 1000}.{(加算値 % 1000) / 100}{(加算値 % 100) / 10}{(加算値 % 10)}상승했다!
ELSEIF 加算値 < 0
	LOCAL:2 = 加算値 * -1
	PRINTFORMW 리조트의 인기도가{LOCAL:2 / 1000}.{(LOCAL:2 % 1000) / 100}{(LOCAL:2 % 100) / 10}{(LOCAL:2 % 10)}감소했다…
ELSE
	RETURN 0
ENDIF
リゾート人気度小数点以下 += 加算値
IF リゾート人気度小数点以下 < 0
	リゾート人気度 += リゾート人気度小数点以下 / 1000 - 1
	リゾート人気度小数点以下 = 1000 + (リゾート人気度小数点以下 % 1000)
ELSEIF リゾート人気度小数点以下 / 1000 > 0
	リゾート人気度 += リゾート人気度小数点以下 / 1000
	リゾート人気度小数点以下 = リゾート人気度小数点以下 % 1000
ENDIF
IF リゾート人気度 < 0
	リゾート人気度 = 0
	リゾート人気度小数点以下 = 0
ENDIF

RETURN 加算値

@人気度上限算出
VARSET LOCAL

LOCAL = 滞在キャラ上限 * 2 * 1000

;上限上昇要素
FOR LOCAL:1, 3, 9
	TRYCALLFORM 空艇港店舗効果_{空艇港店舗状況:(LOCAL:1):0}("人気度上昇量の減衰ライン")
	LOCAL += RESULT
NEXT
IF ダンジョンクリアフラグ_人食い鳥の島
	;航路開放による上限上昇、キャラクター２０人分
	LOCAL += 40000
ENDIF

;エリアを一つ開拓するたびに上限上昇
LOCAL += 40000 * (10 - MATCH(開拓エリア開発番号, 0))

;海で固定上昇とは別に＋２０
SIF MATCH(開拓エリア開発番号, 大規模番号_海エリア)
	LOCAL += 20000

;高速船で＋２０
SIF 高速船作成済フラグ
	LOCAL += 20000

;遊覧船で＋１０
SIF 海改造度:6:1
	LOCAL += 10000
;海底散歩で＋５
SIF 海改造度:6:2
	LOCAL += 5000

RETURN LOCAL

@人気度キャップ算出(ARG)
#FUNCTION
;小数点以下込なので実際の値より*1000されている

IF ダンジョンクリアフラグ_人食い鳥の島 == 0
	;人食い鳥島クリアまでは99より上にならない
	SIF ARG > 99999
		RETURNF 99999
ENDIF

;高速船を作るまでは人気度300より上にならない
SIF 高速船作成済フラグ == 0 
	RETURNF 299999

;次の段階は人気度1000の予定なので、そこにキャップを予めおいておく
SIF ARG > 999999
	RETURNF 999999

RETURNF 0

@リゾートランク上昇チェック()
SELECTCASE リゾートランク
	CASE 0
		IF リゾート人気度 > 100 && ダンジョンクリアフラグ_人食い鳥の島
			DRAWLINE
			PRINTW 관광객들의 인기가 높아져, 리조트 랭크가 상승했다!
			PRINTW 랭크 F '아무도 모르는 리조트' > 랭크 E '무명 리조트'
			リゾートランク = 1
		ENDIF
	CASE 1
		IF リゾート人気度 > 300
			DRAWLINE
			PRINTW 관광객들의 인기가 높아져, 리조트 랭크가 상승했다!
			PRINTW 랭크 E '무명 리조트' > D '아는 사람만 아는 리조트'
			リゾートランク = 2
		ENDIF
	CASE 2
		IF リゾート人気度 > 1000
			DRAWLINE
			PRINTW 관광객들의 인기가 높아져, 리조트 랭크가 상승했다!
			PRINTW D '아는 사람만 아는 리조트' > C '여행을 좋아하는 사람들은 아는 리조트'
			リゾートランク = 3
		ENDIF
	CASE 3
		IF リゾート人気度 > 3000
			DRAWLINE
			PRINTW 관광객들의 인기가 높아져, 리조트 랭크가 상승했다!
			PRINTW C '여행을 좋아하는 사람들은 아는 리조트' > B '세간에 소문난 리조트'
			リゾートランク = 4
		ENDIF
	CASE 4
		IF リゾート人気度 > 6000
			DRAWLINE
			PRINTW 관광객들의 인기가 높아져, 리조트 랭크가 상승했다!
			PRINTW B '세간에 소문난 리조트' > A '공역에서도 유명한 리조트'
			リゾートランク = 5
		ENDIF
	CASE 5
		IF リゾート人気度 > 9500
			DRAWLINE
			PRINTW 관광객의 인기가 높아져, 리조트 랭크가 상승했다!
			PRINTW A『공역에서도 유명한 리조트』 > S『전 공역에 알려진 리조트』
			リゾートランク = 6
		ENDIF
ENDSELECT


@性別招待制御_実処理(対象キャラ)
#FUNCTION
#DIM 対象キャラ
;制御オプションに引っかかると１を返す、大丈夫なら０

;例外規定されてるキャラは条件全部スルー
SIF CFLAG:対象キャラ:例外招待
	RETURNF 0

SELECTCASE TALENT:対象キャラ:性別
	CASE 0
		SIF GETBIT(OPTION変数:性別招待制御, 3)
			RETURNF 1
	CASE 1
		SIF GETBIT(OPTION変数:性別招待制御, 0)
			RETURNF 1
	CASE 2
		SIF GETBIT(OPTION変数:性別招待制御, 1)
			RETURNF 1
	CASE 3
		SIF GETBIT(OPTION変数:性別招待制御, 2)
			RETURNF 1
ENDSELECT

;ランダムキャラには以下は効かない
SIF NO:対象キャラ == 999
	RETURNF 0

;口上制御もここで
IF ENUMFILES(@"ERB/口上_キャラ個別ERB/{NO:対象キャラ}_%CALLNAME:対象キャラ%", "口上_*.ERB", 1) > 0
	;口上あり
	SIF GETBIT(OPTION変数:口上招待制御, 0)
		RETURNF 1
ELSE
	;口上なし
	SIF GETBIT(OPTION変数:口上招待制御, 1)
		RETURNF 1
ENDIF
IF ENUMFILES(@"resources/%CSTR:対象キャラ:画像フォルダ%", "*.*", 1) < OPTION変数:招待画像枚数閾値
	;画像なし
	SIF GETBIT(OPTION変数:口上招待制御, 3)
		RETURNF 1
ELSE
	;画像あり
	SIF GETBIT(OPTION変数:口上招待制御, 2)
		RETURNF 1
ENDIF

RETURNF 0

;--------------------------------------------------
;滞在期間の基本日数の算出
;--------------------------------------------------
@滞在期間基本日数算出(対象キャラ)
#DIM 対象キャラ
#DIM 基本日数
;基本日数:恋慕補正等をする前の滞在期間。
;同行者は基本日数を算出しない(招待客と同じ日数になる)

基本日数 = -1
TRYCCALLFORM 滞在期間基本日数算出_{NO:対象キャラ}
	;固有処理で滞在期間の指定がある場合はそちらを使用
	基本日数 = RESULT
	;滞在期間設定後に滞在期間が-1されるので「固有処理で指定した日数」と「実際の滞在期間」が合うように+1しておく
	SIF 基本日数 >= 0
		基本日数 += 1
CATCH
ENDCATCH
;固有処理の指定がない場合は乱数で決定
SIF 基本日数 < 0
	基本日数 = RAND:2 + 6
;DEBUGPRINTFORML %CALLNAME:対象キャラ% 基本日数:{基本日数}
RETURN 基本日数

;--------------------------------------------------
;滞在期間の補正日数の算出
;--------------------------------------------------
@滞在期間補正日数算出(対象キャラ)
#DIM 対象キャラ
#DIM 補正日数
;補正日数:恋慕補正など基本日数に加算する日数。招待客と同行者でそれぞれで判定/加算
;スイート招待と常連来訪の場合は補正は行われない

補正日数 = -1
TRYCCALLFORM 滞在期間補正日数算出_{NO:対象キャラ}
	;固有処理で補正期間の指定がある場合はそちらを使用
	補正日数 = RESULT
CATCH
ENDCATCH
IF 補正日数 < 0
	補正日数 = 0
	;固有処理の指定がない場合は恋慕による滞在期間延長判定
	SIF TALENT:対象キャラ:恋慕
		補正日数 = 10
ENDIF
;DEBUGPRINTFORML %CALLNAME:対象キャラ% 補正日数:{補正日数}
RETURN 補正日数
