@干渉強化メニュー

#DIM 表示候補, 2000
#DIM 現在ページ
#DIM 選択キャラ番号
#DIM 詳細表示変数
#DIM 干渉変更費用
#DIM 干渉関数数
#DIM 干渉強化値段
#DIMS 干渉関数配列, 50

VARSET 表示候補, -1
現在ページ = 0
VARSET RESULTS
VARSET 干渉関数配列
干渉関数数 = ENUMFUNCBEGINSWITH("干渉強化関数_")
ARRAYCOPY "RESULTS", "干渉関数配列"
詳細表示変数 = -1

;表示キャラを登録
LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	;非滞在は出さない
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	表示候補:(LOCAL:1) = LOCAL
	LOCAL:1 += 1
NEXT

$表示_LOOP

;広告対象の表示
DRAWLINE
PRINTL 누구에게의 간섭을 강화하시겠습니까?
DRAWLINE

LOCAL:1 = 0
;キャラの表示（15人まで）
FOR LOCAL, (現在ページ * 30), ((現在ページ * 30) + 30)
	;登録が終わったら表示終了
	IF 表示候補:LOCAL == -1
		LOCAL:1 = 1
		BREAK
	ENDIF
	LOCALS = [{LOCAL,3,RIGHT}] %ADD_STR_SPACE(GET_NAME(表示候補:LOCAL,1), "]"), 40, LEFT%
	IF 干渉強化変数:(表示候補:LOCAL):干渉種類 == 0
		LOCALS += "현재 간섭: 없음"
	ELSE
		CALLFORM 干渉強化関数_{干渉強化変数:(表示候補:LOCAL):干渉種類}("名前")
		LOCALS += @"현재 간섭:%TSTR:コマンド名受渡%　　"
		LOCALS += @"랭크:%TOFULL(TOSTR(干渉強化変数:(表示候補:LOCAL):干渉ランク))%"
		CALLFORM 干渉強化関数_{干渉強化変数:(表示候補:LOCAL):干渉種類}("対象値", 表示候補:LOCAL)
		LOCALS += @"　　%TSTR:コマンド名受渡%"
	ENDIF

	PRINTBUTTON @"%LOCALS%", LOCAL


	PRINTL 
NEXT
PRINTL
DRAWLINE

;ページめくり表示
IF 現在ページ > 0
	PRINTBUTTONLC "[9900] 이전 페이지로", 9900
ELSE
	PRINTLC 　
ENDIF
PRINTFORM - {現在ページ + 1} -
IF LOCAL:1 == 0
	PRINTBUTTONC "[9901] 다음 페이지로", 9901
ENDIF
PRINTL 
PRINTL 
PRINTBUTTON "[9998] 일괄 부여 화면으로", 9998
PRINTL 
PRINTL 
PRINTBUTTON "[9997] 시스템 설명", 9997
PRINTL 
PRINTBUTTON "[9999] 돌아가기", 9999
PRINTL 

IF チュートリアルフラグ:干渉力強化 == 0
	RESULT = 9997
ELSE
	BINPUT
ENDIF

;対象のINPUT
SELECTCASE RESULT
	CASE 9900
		現在ページ = MAX(現在ページ - 1, 0)
		GOTO 表示_LOOP
	CASE 9901
		SIF LOCAL:1 == 0
			現在ページ += 1
		GOTO 表示_LOOP
	CASE 9997
		CALL 干渉力強化チュートリアル()
		GOTO 表示_LOOP
	CASE 9998
		CALL まとめて干渉処理(表示候補, 干渉関数配列, 干渉関数数)
		GOTO 表示_LOOP
	CASE 9999
		RETURN -1
	CASEELSE
		選択キャラ番号 = 表示候補:RESULT
ENDSELECT


$干渉表示_LOOP
;どの干渉力を強化するか
DRAWLINE
PRINTFORML ■%NAME表示(選択キャラ番号)%
PRINTL 강화할 간섭을 선택해주세요
DRAWLINE
LOCALS = <div rect='81,0,1912,344' border='31' bcolor='#C0C0C0'></div>
LOCALS += @"<div rect='156,62,1912,281'>소지 루피:%NUM_FORMAT(MONEY)%<br>소지 ZP　:%ZP所持量全文字列()%</div>"
LOCALS += "<div rect='81,375,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2050,375,3875,2812' border='31' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='2125,500,3762,2812'>"
;右側表示
IF 詳細表示変数 == -1
	;現在の干渉詳細を表示
	IF 干渉強化変数:選択キャラ番号:干渉種類 == 0
	ELSE
		CALLFORM 干渉強化関数_{干渉強化変数:選択キャラ番号:干渉種類}("名前")
		LOCALS += @"현재 간섭:%TSTR:コマンド名受渡%<br><br>"
		CALLFORM 干渉強化関数_{干渉強化変数:選択キャラ番号:干渉種類}("詳細文章")
		LOCALS += @"%TSTR:コマンド名受渡%<br><br>"
		LOCALS += @"<button value='0'>[0] 현재 간섭을 해제한다</button><br>"
		IF 干渉強化変数:選択キャラ番号:干渉ランク < 5
			干渉強化値段 = 干渉経験値テーブル:(干渉強化変数:選択キャラ番号:干渉ランク) - 干渉強化変数:選択キャラ番号:干渉経験値
			CALLFORM 干渉強化関数_{干渉強化変数:選択キャラ番号:干渉種類}("値段")
			干渉強化値段 = MAX(干渉強化値段 * RESULT / 5, 1)
			LOCALS += @"<br>간섭 랭크를 올린다 필요 ZP:{干渉強化値段}<br>"
			IF 干渉経験値テーブル:(干渉強化変数:選択キャラ番号:干渉ランク) / 10 > FLAG:ZP所持量
				LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[1] 현재 간섭을 강화한다 (ZP가 부족합니다)</font><br>"
			ELSEIF 干渉強化変数:選択キャラ番号:干渉ランク >= 4
				LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[1] 현재 간섭을 강화한다 (최대 랭크입니다)</font><br>"
			ELSE
				LOCALS += @"<button value='1'>[1] 현재 간섭을 강화한다</button><br>"
			ENDIF
		ENDIF
	ENDIF
ELSE
	CALLFORM %干渉関数配列:詳細表示変数%("名前")
	LOCALS += @"■%TSTR:コマンド名受渡%<br><br>"
	CALLFORM %干渉関数配列:詳細表示変数%("詳細文章")
	LOCALS += @"%TSTR:コマンド名受渡%<br><br>"
	CALLFORM %干渉関数配列:詳細表示変数%("値段")
	干渉変更費用 = RESULT
	LOCALS += @"필요 ZP:{干渉変更費用}<br>"
	IF 干渉変更費用 > FLAG:ZP所持量
		LOCALS += @"ZP가 부족합니다<br>"
	ELSE
		LOCALS += @"<button value='0'>[0] 이 간섭으로 변경한다</button><br>"
	ENDIF
ENDIF
LOCALS += "</div>"

;左側表示
LOCALS += "<div rect='156,500,1937,2812'>"
IF 干渉強化変数:選択キャラ番号:干渉種類 == 0
	LOCALS += @"현재 간섭: 없음"
ELSE
	CALLFORM 干渉強化関数_{干渉強化変数:選択キャラ番号:干渉種類}("名前")
	LOCALS += @"　현재 간섭:<button value='998'>[%TSTR:コマンド名受渡%]</button>"
	LOCALS += @"<br>　간섭 랭크:%TOFULL(TOSTR(干渉強化変数:選択キャラ番号:干渉ランク))%"
ENDIF
LOCALS += "<br>----------<br>"
FOR LOCAL, 0, 干渉関数数
	IF 干渉強化変数:選択キャラ番号:干渉種類 == TOINT(REPLACE(干渉関数配列:LOCAL, "干渉強化関数_", ""))
		CONTINUE
	ENDIF
	CALLFORM %干渉関数配列:LOCAL%("名前")
	LOCALS += @"<button value='{LOCAL + 100}'>[{LOCAL + 100}] %TSTR:コマンド名受渡%</button>"
	LOCALS += "<br>"
NEXT
LOCALS += "<button value='999'>[999] 돌아가기</button>"
LOCALS += "<br>"
LOCALS += "</div>"

HTML_PRINT LOCALS, 1
FOR LOCAL, 0, 29
	PRINTL
NEXT
DRAWLINE

DO
BINPUT
IF 詳細表示変数 == -1
	SELECTCASE RESULT
		CASE 0
			PRINTL 간섭을 해제하면 간섭 랭크가 0으로 돌아갑니다.
			PRINTL 다시 같은 간섭을 해도 간섭 랭크는 돌아오지 않습니다.
			PRINTL
			PRINTL 간섭을 해제하시겠습니까?
			PRINTBUTTONLC "[0] 예", 0
			PRINTBUTTONLC "[1] 아니요", 1
			BINPUT
			SELECTCASE RESULT
				CASE 0
					PRINTW 간섭을 해제했습니다.
					干渉強化変数:選択キャラ番号:干渉種類 = 0
					干渉強化変数:選択キャラ番号:干渉ランク = 0
					干渉強化変数:選択キャラ番号:干渉経験値 = 0
					GOTO 干渉表示_LOOP
				CASE 1
					GOTO 干渉表示_LOOP
			ENDSELECT
		CASE 1
			PRINTL 간섭을 강화해도 괜찮겠습니까?
			PRINTBUTTONLC "[0] 예", 0
			PRINTBUTTONLC "[1] 아니요", 1
			BINPUT
			SELECTCASE RESULT
				CASE 0
					PRINTW 간섭을 강화했습니다.
					FLAG:ZP所持量 -= 干渉強化値段
					干渉強化変数:選択キャラ番号:干渉ランク ++
					干渉強化変数:選択キャラ番号:干渉経験値 = 干渉経験値テーブル:(干渉強化変数:選択キャラ番号:干渉ランク - 1)
					GOTO 干渉表示_LOOP
				CASE 1
					GOTO 干渉表示_LOOP
			ENDSELECT
		CASE 100 TO 149
			詳細表示変数 = RESULT - 100
			GOTO 干渉表示_LOOP
		CASE 998
			詳細表示変数 = -1
			GOTO 干渉表示_LOOP
		CASE 999
			詳細表示変数 = -1
			GOTO 表示_LOOP
	ENDSELECT
ELSE
	SELECTCASE RESULT
		CASE 0
			IF 干渉変更費用 > FLAG:ZP所持量
				PRINTW ZP가 부족합니다.
				詳細表示変数 = -1
				GOTO 干渉表示_LOOP
			ENDIF
			CALLFORM %干渉関数配列:詳細表示変数%("名前")
			PRINTFORML %CALLNAME:選択キャラ番号%에게 %TSTR:コマンド名受渡%을(를) 합니다.
			PRINTL 간섭을 변경하면 간섭 랭크가 0으로 돌아갑니다.
			PRINTL 다시 이전과 같은 간섭을 해도 간섭 랭크는 돌아오지 않습니다.
			PRINTL
			PRINTL 괜찮겠습니까?
			PRINTBUTTONLC "[0] 예", 0
			PRINTBUTTONLC "[1] 아니요", 1
			BINPUT
			SELECTCASE RESULT
				CASE 0
					PRINTFORMW %TSTR:コマンド名受渡%을(를) 했습니다.
					FLAG:ZP所持量 -= 干渉変更費用
					干渉強化変数:選択キャラ番号:干渉種類 = TOINT(REPLACE(干渉関数配列:詳細表示変数, "干渉強化関数_", ""))
					干渉強化変数:選択キャラ番号:干渉ランク = 0
					干渉強化変数:選択キャラ番号:干渉経験値 = 0
					詳細表示変数 = -1
					GOTO 干渉表示_LOOP
				CASE 1
					詳細表示変数 = -1
					GOTO 干渉表示_LOOP
			ENDSELECT
		CASE 100 TO 149
			詳細表示変数 = RESULT - 100
			GOTO 干渉表示_LOOP
		CASE 998
			詳細表示変数 = -1
			GOTO 干渉表示_LOOP
		CASE 999
			詳細表示変数 = -1
			GOTO 表示_LOOP
	ENDSELECT
ENDIF
LOOP 1


@干渉種類チェック_好感度(キャラ番号)
#FUNCTION
#DIM キャラ番号
RETURNF GROUPMATCH(干渉強化変数:キャラ番号:干渉種類, 1, 2, 3, 4) > 0


@干渉種類チェック_快楽(キャラ番号)
#FUNCTION
#DIM キャラ番号
RETURNF GROUPMATCH(干渉強化変数:キャラ番号:干渉種類, 5, 6, 7, 8, 9, 12) > 0


@干渉補正実処理関数(キャラ番号, ARGS)
#DIM キャラ番号

SELECTCASE ARGS
	CASE "好感度増加時"
		IF 干渉種類チェック_好感度(キャラ番号)
			CALLFORM 干渉強化関数_{干渉強化変数:キャラ番号:干渉種類}("実補正処理", キャラ番号)
		ENDIF
	CASE "快楽増加時"
		IF 干渉種類チェック_快楽(キャラ番号)
			CALLFORM 干渉強化関数_{干渉強化変数:キャラ番号:干渉種類}("実補正処理", キャラ番号)
		ENDIF
ENDSELECT


@まとめて干渉処理(表示候補, 干渉関数配列, 干渉関数数)
#DIM REF 表示候補
#DIM 詳細表示変数
#DIMS REF 干渉関数配列
#DIM 干渉関数数
#DIM 干渉番号
#DIM 適用キャラ数
#DIM 干渉変更費用
#DIM 現在ページ

現在ページ = 0
詳細表示変数 = -1

$干渉表示_LOOP
PRINTL
PRINTL
PRINTL
DRAWLINE
PRINTL 임의의 간섭 강화를 여러 캐릭터에게 적용합니다.
PRINTL 간섭의 종류를 선택해 주세요.
DRAWLINE
LOCALS = <div rect='81,0,1912,344' border='31' bcolor='#C0C0C0'></div>
LOCALS += @"<div rect='156,62,1912,281'>보유 루피：%NUM_FORMAT(MONEY)%<br>보유 ZP　：%ZP所持量全文字列()%</div>"
LOCALS += "<div rect='81,375,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2050,375,3875,2812' border='31' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='2125,500,3762,2812'>"
IF 詳細表示変数 == -1
	;現在の干渉詳細を表示
	LOCALS += @"특정 간섭을 여러 캐릭터에게 한꺼번에 할 수 있습니다.<br>"
	LOCALS += @"간섭을 선택한 후 다음 패턴을 실행할 수 있습니다.<br>"
	LOCALS += @"<br>"
	LOCALS += @"　・임의의 캐릭터를 선택하여 간섭 강화<br>"
	LOCALS += @"　・현재 간섭이 「없음」인 캐릭터에게 일괄적으로 간섭 강화<br>"
	LOCALS += @"　・모든 캐릭터에게 일괄적으로 간섭 강화<br>"
	LOCALS += @"<br>"
	LOCALS += @"또한, 왼쪽의 「무작위로 간섭하기」를 선택하면 다음 3가지를 실행할 수 있습니다.<br>"
	LOCALS += @"<br>"
	LOCALS += @"　・임의의 캐릭터를 선택하여 무작위 간섭 강화<br>"
	LOCALS += @"　・현재 간섭이 「없음」인 캐릭터에게 일괄적으로 무작위 간섭 강화<br>"
	LOCALS += @"　・모든 캐릭터에게 일괄적으로 무작위 간섭 강화<br>"
	LOCALS += @"<br>"
	LOCALS += @"（모든 패턴에서 ZP가 부족할 때 부여를 중지합니다）<br>"
ELSEIF 詳細表示変数 == 998
	LOCALS += @"■무작위 간섭 강화<br><br>"
	LOCALS += @"현재 부여 가능한 모든 간섭 중에서 무작위로 적용합니다.<br><br>"
	LOCALS += @"1인당 필요 ZP: 간섭 종류에 따라 변동<br>"
	LOCALS += @"<button value='0'>[0] 적용할 캐릭터를 선택합니다</button><br>"
	LOCALS += @"<button value='1'>[1] 간섭 「없음」인 캐릭터에게 일괄 적용</button><br>"
	LOCALS += @"<button value='2'>[2] 모든 캐릭터에게 일괄 적용</button><br>"
ELSE
	CALLFORM %干渉関数配列:詳細表示変数%("名前")
	LOCALS += @"■%TSTR:コマンド名受渡%<br><br>"
	CALLFORM %干渉関数配列:詳細表示変数%("詳細文章")
	LOCALS += @"%TSTR:コマンド名受渡%<br><br>"
	CALLFORM %干渉関数配列:詳細表示変数%("値段")
	干渉変更費用 = RESULT
	LOCALS += @"1인당 필요 ZP：{干渉変更費用}<br>"
	LOCALS += @"<button value='0'>[0] 적용할 캐릭터를 선택한다</button><br>"
	LOCALS += @"<button value='1'>[1] 간섭 「없음」인 캐릭터에게 일괄 적용</button><br>"
	LOCALS += @"<button value='2'>[2] 모든 캐릭터에게 일괄 적용</button><br>"
ENDIF
LOCALS += "</div>"
LOCALS += "<div rect='156,500,1937,2812'>"
FOR LOCAL, 0, 干渉関数数
	CALLFORM %干渉関数配列:LOCAL%("名前")
	LOCALS += @"<button value='{LOCAL + 100}'>[{LOCAL + 100}] %TSTR:コマンド名受渡%</button>"
	LOCALS += "<br>"
NEXT
LOCALS += "<br>"
LOCALS += "<button value='998'>[998] 무작위로 간섭한다</button>"
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 돌아가기</button>"
LOCALS += "<br>"
LOCALS += "</div>"

HTML_PRINT LOCALS, 1
FOR LOCAL, 0, 29
	PRINTL
NEXT
DRAWLINE

BINPUT
SELECTCASE RESULT
	CASE 0
		;キャラ選択へ
		CALL まとめて干渉処理_キャラ選択(表示候補, 詳細表示変数, 干渉関数配列, 干渉関数数)
		GOTO 干渉表示_LOOP
	CASE 1
		PRINTL
		PRINTL
		IF 詳細表示変数 == 998
			PRINTFORML 현재 간섭 강화가 「없음」인 캐릭터 전원에게 무작위 간섭 강화를 적용합니다.
		ELSE
			CALLFORM %干渉関数配列:詳細表示変数%("名前")
			PRINTFORML 현재 간섭 강화가 「없음」인 캐릭터 전원에게%TSTR:コマンド名受渡%을(를) 적용합니다.
		ENDIF
		PRINTL 괜찮습니까?
		PRINTL
		PRINTBUTTONLC "[0] 예", 0
		PRINTBUTTONLC "[1] 아니요", 1
		PRINTL
		BINPUT
		IF RESULT == 0
			PRINTL
			PRINTL
			適用キャラ数 = 0
			IF 詳細表示変数 == 998
				FOR LOCAL, 0, 2000
					;登録が終わったら表示終了
					SIF 表示候補:LOCAL == -1
						BREAK
					干渉番号 = RAND:干渉関数数
					CALLFORM %干渉関数配列:干渉番号%("値段")
					干渉変更費用 = RESULT
					IF 干渉変更費用 > FLAG:ZP所持量
						PRINTW ZP 부족으로 인해 도중에 적용을 종료합니다.
						BREAK
					ENDIF
					IF 干渉強化変数:(表示候補:LOCAL):干渉種類 == 0
						干渉強化変数:(表示候補:LOCAL):干渉種類 = TOINT(REPLACE(干渉関数配列:干渉番号, "干渉強化関数_", ""))
						干渉強化変数:(表示候補:LOCAL):干渉ランク = 0
						干渉強化変数:(表示候補:LOCAL):干渉経験値 = 0
						FLAG:ZP所持量 -= 干渉変更費用
						適用キャラ数 ++
					ENDIF
				NEXT
				PRINTFORMW {適用キャラ数}명의 캐릭터에게 무작위 간섭 강화 적용을 완료했습니다.
			ELSE
				CALLFORM %干渉関数配列:詳細表示変数%("値段")
				干渉変更費用 = RESULT
				FOR LOCAL, 0, 2000
					;登録が終わったら表示終了
					SIF 表示候補:LOCAL == -1
						BREAK
					IF 干渉変更費用 > FLAG:ZP所持量
						PRINTW ZP 부족으로 도중에 적용을 종료합니다.
						BREAK
					ENDIF
					IF 干渉強化変数:(表示候補:LOCAL):干渉種類 == 0
						干渉強化変数:(表示候補:LOCAL):干渉種類 = TOINT(REPLACE(干渉関数配列:詳細表示変数, "干渉強化関数_", ""))
						干渉強化変数:(表示候補:LOCAL):干渉ランク = 0
						干渉強化変数:(表示候補:LOCAL):干渉経験値 = 0
						FLAG:ZP所持量 -= 干渉変更費用
						適用キャラ数 ++
					ENDIF
				NEXT
				PRINTFORMW {適用キャラ数}명의 캐릭터에게 %TSTR:コマンド名受渡%의 적용을 완료했습니다.
			ENDIF
			RETURN 0
		ELSE
			GOTO 干渉表示_LOOP
		ENDIF
	CASE 2
		PRINTL
		PRINTL
		IF 詳細表示変数 == 998
			PRINTFORML 간섭 강화를 변경할 수 있는 모든 캐릭터에게 무작위 간섭 강화를 적용합니다.
		ELSE
			CALLFORM %干渉関数配列:詳細表示変数%("名前")
			PRINTFORML 간섭 강화를 변경할 수 있는 모든 캐릭터에게 %TSTR:コマンド名受渡%을(를) 적용합니다.
		ENDIF
		PRINTL 괜찮습니까?
		PRINTL ※간섭 강화를 변경하면 간섭 랭크가 0으로 돌아갑니다.
		PRINTL ※원래 간섭으로 변경해도 간섭 랭크는 0이 되므로 주의하십시오.
		PRINTL
		PRINTBUTTONLC "[0] 예", 0
		PRINTBUTTONLC "[1] 아니오", 1
		PRINTL
		BINPUT
		IF RESULT == 0
			PRINTL
			PRINTL
			適用キャラ数 = 0
			IF 詳細表示変数 == 998
				FOR LOCAL, 0, 2000
					;登録が終わったら表示終了
					SIF 表示候補:LOCAL == -1
						BREAK
					干渉番号 = RAND:干渉関数数
					CALLFORM %干渉関数配列:干渉番号%("値段")
					干渉変更費用 = RESULT
					IF 干渉変更費用 > FLAG:ZP所持量
						PRINTW ZP 부족으로 도중에 적용을 종료합니다.
						BREAK
					ENDIF
					IF 干渉強化変数:(表示候補:LOCAL):干渉種類 != TOINT(REPLACE(干渉関数配列:干渉番号, "干渉強化関数_", ""))
						干渉強化変数:(表示候補:LOCAL):干渉種類 = TOINT(REPLACE(干渉関数配列:干渉番号, "干渉強化関数_", ""))
						干渉強化変数:(表示候補:LOCAL):干渉ランク = 0
						干渉強化変数:(表示候補:LOCAL):干渉経験値 = 0
						FLAG:ZP所持量 -= 干渉変更費用
						適用キャラ数 ++
					ENDIF
				NEXT
				PRINTFORMW {適用キャラ数}명의 캐릭터에게 무작위 간섭 강화 적용을 완료했습니다.
			ELSE
				CALLFORM %干渉関数配列:詳細表示変数%("値段")
				干渉変更費用 = RESULT
				FOR LOCAL, 0, 2000
					;登録が終わったら表示終了
					SIF 表示候補:LOCAL == -1
						BREAK
					IF 干渉変更費用 > FLAG:ZP所持量
						PRINTW ZP 부족으로 도중에 적용을 종료합니다.
						BREAK
					ENDIF
					IF 干渉強化変数:(表示候補:LOCAL):干渉種類 != TOINT(REPLACE(干渉関数配列:詳細表示変数, "干渉強化関数_", ""))
						干渉強化変数:(表示候補:LOCAL):干渉種類 = TOINT(REPLACE(干渉関数配列:詳細表示変数, "干渉強化関数_", ""))
						干渉強化変数:(表示候補:LOCAL):干渉ランク = 0
						干渉強化変数:(表示候補:LOCAL):干渉経験値 = 0
						FLAG:ZP所持量 -= 干渉変更費用
						適用キャラ数 ++
					ENDIF
				NEXT
				PRINTFORMW {適用キャラ数}명의 캐릭터에게 %TSTR:コマンド名受渡%의 적용을 완료했습니다.
			ENDIF
			RETURN 0
		ELSE
			GOTO 干渉表示_LOOP
		ENDIF
	CASE 100 TO 149
		詳細表示変数 = RESULT - 100
		GOTO 干渉表示_LOOP
	CASE 998
		詳細表示変数 = 998
		GOTO 干渉表示_LOOP
	CASE 999
		詳細表示変数 = -1
		RETURN 0
ENDSELECT


@まとめて干渉処理_キャラ選択(表示候補, 詳細表示変数, 干渉関数配列, 干渉関数数)
#DIM REF 表示候補
#DIM 詳細表示変数
#DIM 現在ページ
#DIM 選択候補, 2000
#DIM 選択キャラ数
#DIMS REF 干渉関数配列
#DIM 干渉変更費用
#DIM 干渉関数数
#DIM 干渉番号

VARSET 選択候補, -1
現在ページ = 0

$表示_LOOP
;広告対象の表示
DRAWLINE
PRINTL 간섭 강화할 캐릭터를 선택하십시오.
DRAWLINE

;キャラの表示（15人まで）
FOR LOCAL, (現在ページ * 30), ((現在ページ * 30) + 30)
	;登録が終わったら表示終了
	IF 表示候補:LOCAL == -1
		BREAK
	ENDIF
	LOCALS = 
	IF MATCH(選択候補, 表示候補:LOCAL)
		SETCOLOR カラーパレット("黄")
	ELSEIF 詳細表示変数 < 50 && 干渉強化変数:(表示候補:LOCAL):干渉種類 == TOINT(REPLACE(干渉関数配列:詳細表示変数, "干渉強化関数_", ""))
		SETCOLOR カラーパレット("グレーアウト")
	ENDIF
	LOCALS += @"[{LOCAL,3,RIGHT}] %ADD_STR_SPACE(GET_NAME(表示候補:LOCAL,1), "]"), 40, LEFT%"
	IF 干渉強化変数:(表示候補:LOCAL):干渉種類 == 0
		LOCALS += "현재 간섭: 없음"
	ELSE
		CALLFORM 干渉強化関数_{干渉強化変数:(表示候補:LOCAL):干渉種類}("名前")
		LOCALS += @"현재 간섭：%TSTR:コマンド名受渡%　　"
		LOCALS += @"랭크：%TOFULL(TOSTR(干渉強化変数:(表示候補:LOCAL):干渉ランク))%"
		CALLFORM 干渉強化関数_{干渉強化変数:(表示候補:LOCAL):干渉種類}("対象値", 表示候補:LOCAL)
		LOCALS += @"　　%TSTR:コマンド名受渡%"
	ENDIF
	PRINTBUTTON @"%LOCALS%", LOCAL
	RESETCOLOR
	PRINTL 
NEXT
PRINTL
DRAWLINE

;ページめくり表示
IF 現在ページ > 0
	PRINTBUTTONLC "[900] 이전 페이지로", 900
ELSE
	PRINTLC 　
ENDIF
PRINTFORM - {現在ページ + 1} -
IF 表示候補:(現在ページ * 30) + 30 > -1
	PRINTBUTTONC "[901] 다음 페이지로", 901
ENDIF
PRINTL 
PRINTL 
IF 詳細表示変数 == 998
	PRINTBUTTON @"[998] 선택 캐릭터에게 무작위 간섭 강화를 적용한다", 998
ELSE
	CALLFORM %干渉関数配列:詳細表示変数%("名前")
	PRINTBUTTON @"[998] 선택 캐릭터에게%TSTR:コマンド名受渡%을(를) 적용한다", 998
ENDIF
PRINTL 
PRINTBUTTON "[999] 돌아가기", 999

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 900
		現在ページ = MAX(現在ページ - 1, 0)
		GOTO 表示_LOOP
	CASE 901
		現在ページ += 1
		GOTO 表示_LOOP
	CASE 998
		選択キャラ数 = 0
		FOR LOCAL, 0, 2000
			IF 選択候補:LOCAL > -1
				選択キャラ数 ++
			ELSE
				BREAK
			ENDIF
		NEXT
		PRINTFORM 이상의{選択キャラ数, 3}명의 캐릭터에게
		IF 詳細表示変数 == 998
			PRINT 무작위 간섭 강화
		ELSE
			CALLFORM %干渉関数配列:詳細表示変数%("名前")
			PRINTFORM %TSTR:コマンド名受渡%
		ENDIF
		PRINTL 을(를) 적용하시겠습니까?
		PRINTBUTTONLC "[0] 예", 0
		PRINTBUTTONLC "[1] 아니요", 1
		PRINTL
		BINPUT
		IF RESULT == 0
			選択キャラ数 = 0
			IF 詳細表示変数 == 998
				FOR LOCAL, 0, 2000
					;登録が終わったら表示終了
					SIF 選択候補:LOCAL == -1
						BREAK
					干渉番号 = RAND:干渉関数数
					CALLFORM %干渉関数配列:干渉番号%("値段")
					干渉変更費用 = RESULT
					IF 干渉変更費用 > FLAG:ZP所持量
						PRINTW ZP 부족으로 인해 도중에 적용을 종료합니다.
						BREAK
					ENDIF
					IF 干渉強化変数:(選択候補:LOCAL):干渉種類 != TOINT(REPLACE(干渉関数配列:干渉番号, "干渉強化関数_", ""))
						干渉強化変数:(選択候補:LOCAL):干渉種類 = TOINT(REPLACE(干渉関数配列:干渉番号, "干渉強化関数_", ""))
						干渉強化変数:(選択候補:LOCAL):干渉ランク = 0
						干渉強化変数:(選択候補:LOCAL):干渉経験値 = 0
						FLAG:ZP所持量 -= 干渉変更費用
					ENDIF
					選択キャラ数 ++
				NEXT
				PRINTFORMW {選択キャラ数}명의 캐릭터에게 무작위 간섭 강화 적용을 완료했습니다.
			ELSE
				CALLFORM %干渉関数配列:詳細表示変数%("値段")
				干渉変更費用 = RESULT
				FOR LOCAL, 0, 2000
					;登録が終わったら表示終了
					SIF 選択候補:LOCAL == -1
						BREAK
					IF 干渉変更費用 > FLAG:ZP所持量
						PRINTW ZP 부족으로 인해 도중에 적용을 종료합니다.
						BREAK
					ENDIF
					IF 干渉強化変数:(選択候補:LOCAL):干渉種類 != TOINT(REPLACE(干渉関数配列:詳細表示変数, "干渉強化関数_", ""))
						干渉強化変数:(選択候補:LOCAL):干渉種類 = TOINT(REPLACE(干渉関数配列:詳細表示変数, "干渉強化関数_", ""))
						干渉強化変数:(選択候補:LOCAL):干渉ランク = 0
						干渉強化変数:(選択候補:LOCAL):干渉経験値 = 0
						FLAG:ZP所持量 -= 干渉変更費用
						選択キャラ数 ++
					ENDIF
				NEXT
				PRINTFORMW {選択キャラ数}명의 캐릭터에게 %TSTR:コマンド名受渡%의 적용을 완료했습니다.
			ENDIF
			RETURN 0
		ELSE
			GOTO 表示_LOOP
		ENDIF
	CASE 999
		RETURN -1
	CASEELSE
		SIF 詳細表示変数 < 50 && 干渉強化変数:(表示候補:LOCAL):干渉種類 == TOINT(REPLACE(干渉関数配列:詳細表示変数, "干渉強化関数_", ""))
			GOTO 表示_LOOP
		IF MATCH(選択候補, 表示候補:LOCAL)
			ARRAYSHIFT 選択候補, -1, -1, FINDELEMENT(選択候補, 表示候補:LOCAL)
		ELSE
			選択候補:(FINDELEMENT(選択候補, -1)) = 表示候補:LOCAL
		ENDIF
		GOTO 表示_LOOP
ENDSELECT



