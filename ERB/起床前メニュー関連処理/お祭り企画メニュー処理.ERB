@お祭り企画メニュー処理
#DIM ループ用
#DIMS 企画候補文字列

DRAWLINE
PRINTL 리조트에서 개최할 축제 기획을 진행합니다.
PRINTL ※개발 중이므로, 올바르게 작동하지 않을 수 있습니다.
DRAWLINE
LOCALS = <div rect='81,0,1937,344' border='31' bcolor='#C0C0C0'></div>
LOCALS += @"<div rect='156,62,1912,281'>보유 루피:%NUM_FORMAT(MONEY)%<br>보유 ZP　:%ZP所持量全文字列()%</div>"
LOCALS += "<div rect='2050,0,3875,344' border='31' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='2125,62,3812,281'>"
IF 開催予定祭り名 != ""
	LOCALS += @"현재 축제 개최 예정:「%SUBSTRING(開催予定祭り名, 9)%」　"
	IF 開催予定日取り == DAY
		LOCALS += "오늘"
	ELSE
		LOCALS += "내일"
	ENDIF
ELSE
	LOCALS += @"현재 축제 개최 예정: 없음"
ENDIF
LOCALS += "</div>"
LOCALS += "<div rect='81,375,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2050,375,3875,2812' border='31' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='156,500,1937,2812'>"

;お祭り選択時、詳細を表示
IF 企画候補文字列 != ""
	TRYCALLFORM MOVE_ROUTINE_%企画候補文字列%(0, "お祭り説明")
ENDIF


;お祭り個別の出現条件を全部見てヒットしたものを全部表示
VARSET RESULTS
VARSET RESULT

FOR ループ用, 0, ENUMFUNCBEGINSWITH("MOVE_ROUTINE_FESTIVAL_")
	;既に予定に入ってたらアウト
	IF 開催予定祭り名 == REPLACE(RESULTS:ループ用, "MOVE_ROUTINE_", "")
		LOCALS += @"<nonbutton>[{100 + ループ用}] <font color='#808080'>%SUBSTRING(RESULTS:ループ用, 22)%</font>(예정됨)"
		LOCALS += "</nonbutton><br>"
		CONTINUE
	ENDIF
	TRYCALLFORM %RESULTS:ループ用%(0, "出現条件")
	;お祭りが開催できるならLOCALSにボタン挿入
	IF RESULT == 1
		LOCALS += @"<button value='{100 + ループ用}'>[{100 + ループ用}] %SUBSTRING(RESULTS:ループ用, 22)%</button>"
		LOCALS += "<br>"
	ELSEIF RESULT != 0
		LOCALS += @"<nonbutton>[{100 + ループ用}] <font color='#808080'>%SUBSTRING(RESULTS:ループ用, 22)%</font>"
		SELECTCASE RESULT
			CASE -1
				LOCALS += "(시기 외)"
		ENDSELECT
		LOCALS += "</nonbutton><br>"
	ENDIF
NEXT

LOCALS += "<br>"
LOCALS += "<button value='998'>[998] 시스템 설명</button>"
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 돌아가기</button>"
LOCALS += "<br>"
LOCALS += "</div>"

HTML_PRINT LOCALS, 1
FOR LOCAL, 0, 29
	PRINTL
NEXT
DRAWLINE

IF チュートリアルフラグ:お祭り企画 == 0
	RESULT = 998
ELSE
	BINPUT
ENDIF
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 0
		IF 開催予定日取り == DAY
			PRINTL 축제 개최 당일에 다음 일정을 넣을 수 없습니다.
			RETURN 0
		ELSEIF 開催予定祭り名 != ""
			PRINTFORML 현재, %SUBSTRING(開催予定祭り名, 9)%이(가) 개최 예정에 있습니다.
			PRINTFORML %SUBSTRING(企画候補文字列, 9)%(으)로 변경하시겠습니까?
			PRINTBUTTONLC "[0]예", 0
			PRINTBUTTONLC "[1]아니요", 1
			BINPUT
			SELECTCASE RESULT
				CASE 1
					RESTART
			ENDSELECT
		ENDIF
		開催予定日取り = DAY + 1
		開催予定祭り名 = %企画候補文字列%
		PRINTFORMW 내일, 축제 「%SUBSTRING(企画候補文字列, 9)%」를 개최합니다.
		企画候補文字列 = 
	CASE 100 TO 199
		企画候補文字列 = %SUBSTRING(RESULTS:(LOCAL - 100), 13)%
		RESTART
	CASE 998
		CALL お祭り企画チュートリアル()
		RESTART
	CASE 999
		企画候補文字列 = 
		RETURN 0
	CASEELSE
ENDSELECT


