@キャラクター招待
#DIMS 左側表示文字列
#DIMS 右側表示文字列
#DIMS 配列分解文字列, 2

;一日開始時メニューから呼び出し

DRAWLINE
PRINTL 리조트 광고를 냅니다
PRINTL 어떤 광고를 내시겠습니까?
PRINTFORML ・현재 소지금:%NUM_FORMAT(MONEY)%루피　소지 ZP:%ZP所持量全文字列()%
DRAWLINE

左側表示文字列 = 
IF 通常キャラ招待:0 || ＤＭキャラ招待
	左側表示文字列 += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[1] 일반 광고를 내다 (광고 실행 완료)</font><br>"
ELSE
	IF 表示キー_左 == "通常の広告"
		左側表示文字列 += @"<font color='#%カラーパレット_HTML("黄")%'>[1] 일반 광고를 내다</font><br>"
	ELSE
		左側表示文字列 += "<button value='1'>[1] 일반 광고를 내다</button><br>"
	ENDIF
ENDIF

;スイートに空きがあるかチェック
LOCAL:1 = 0
FOR LOCAL, 0, スイートルーム部屋数
	SIF スイートルーム招待:LOCAL > 0
		CONTINUE
	SIF スイートルーム部屋割り配列:LOCAL != ""
		CONTINUE
	LOCAL:1 = 1
NEXT
IF LOCAL:1
	IF MONEY < 20000 && DAY != 1
		左側表示文字列 += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[2] 스위트룸 무료 초대 (소지금 부족)</font><br>"
	ELSE
		IF 表示キー_左 == "スイートルーム"
			左側表示文字列 += @"<font color='#%カラーパレット_HTML("黄")%'>[2] 스위트룸 무료 초대</font>"
		ELSE
			左側表示文字列 += "<button value='2'>[2] 스위트룸 무료 초대</button>"
		ENDIF
		SIF DAY == 1
			左側表示文字列 += @"<font color='#%カラーパレット_HTML("えっちな色")%'>첫날 무료!!</font>"
			左側表示文字列 += "<br>"
	ENDIF
ELSE
	左側表示文字列 += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[2] 스위트룸 무료 초대 (방 만실)</font><br>"
ENDIF

IF 通常キャラ招待:0 || ＤＭキャラ招待
	左側表示文字列 += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[-] 특수 광고를 내다 (광고 실행 완료)</font><br>"
ELSE
	IF STRFIND(表示キー_左, "特殊") > -1
		左側表示文字列 += @"<font color='#%カラーパレット_HTML("黄")%'>[-] 특수 광고를 내다</font><br>"
	ELSE
		左側表示文字列 += "[-] 특수 광고를 내다<br>"
	ENDIF
	CALL 特殊広告一覧表示(左側表示文字列)
ENDIF


右側表示文字列 = 

SELECTCASE 表示キー_左
	CASE "通常の広告"
		右側表示文字列 += "■일반 광고<br><br>"
		右側表示文字列 += "넓고 다양한 지역에 광고를 내는 명령어입니다.<br>"
		右側表示文字列 += "얼마나 루피를 사용하는지에 따라 찾아오는 캐릭터의 수가 변화합니다.<br>"
		右側表示文字列 += "소비하는 루피에 비해 찾아오는 캐릭터가 많아지기 쉽지만, 반대로 특정 캐릭터를 노릴 수는 없습니다.<br><br>"
		右側表示文字列 += "어느 정도 광고를 내시겠습니까?<br><br>"

		IF MONEY < 3000
			右側表示文字列 += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[100] 근처 섬에 광고를 낸다 (루피 부족)</font><br>"
		ELSEIF 表示キー_右 == "近くの島"
			右側表示文字列 += @"<font color='#%カラーパレット_HTML("黄")%'>[100] 근처 섬에 광고를 낸다</font><br>"
		ELSE
			右側表示文字列 += "<button value='100'>[100] 근처 섬에 광고를 낸다</button><br>"
		ENDIF
		右側表示文字列 += @"(필요 루피: 3000루피 집객 예상치:{2 + 招待キャラ数補正算出(), 2}～{3 + 招待キャラ数補正算出(), 2}명)<br><br>"

		IF MONEY < 5000
			右側表示文字列 += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[101] 광범위하게 광고를 낸다 (루피 부족)</font><br>"
		ELSEIF 表示キー_右 == "広範囲"
			右側表示文字列 += @"<font color='#%カラーパレット_HTML("黄")%'>[102] 광범위하게 광고를 낸다</font><br>"
		ELSE
			右側表示文字列 += "<button value='101'>[101] 광범위하게 광고를 낸다</button><br>"
		ENDIF
		右側表示文字列 += @"(필요 루피: 5000루피 집객 예상치:{3 + 招待キャラ数補正算出(), 2}～{5 + 招待キャラ数補正算出(), 2}명)<br><br>"

		IF MONEY < 10000
			右側表示文字列 += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[102] 공역 전체에 광고를 낸다 (루피 부족)</font><br>"
		ELSEIF 表示キー_右 == "空域全体"
			右側表示文字列 += @"<font color='#%カラーパレット_HTML("黄")%'>[102] 공역 전체에 광고를 낸다</font><br>"
		ELSE
			右側表示文字列 += "<button value='102'>[102] 공역 전체에 광고를 낸다</button><br>"
		ENDIF
		右側表示文字列 += @"(필요 루피: 10000루피　　집객 예상치:{5 + 招待キャラ数補正算出(), 2}～{7 + 招待キャラ数補正算出(), 2}명)<br><br>"

		IF 表示キー_右 != ""
			右側表示文字列 += "<button value='103'>[103] 이 설정으로 광고를 내다</button><br>"
		ENDIF
	CASE "スイートルーム"
		右側表示文字列 += "■스위트룸 무료 초대<br><br>"
		右側表示文字列 += "임의의 캐릭터에게 무료 초대를 보냅니다. 20000루피의 비용이 듭니다.<br>"
		SIF DAY == 1
			右側表示文字列 += @"……하지만, 게임 시작 첫날만<font color='#%カラーパレット_HTML("えっちな色")%'>무료</font>입니다.<br>"
		右側表示文字列 += "지정한 캐릭터를 노리는 것이 가능하며, 반드시 방문해 줍니다.<br>"
		右側表示文字列 += "일반적인 체류자와는 별개이므로 보통 방이 가득 차 있어도 부를 수 있습니다.<br><br>"
		右側表示文字列 += "하지만 무료 초대이므로, 하루 종료 시의 매출에는 기여하지 않습니다.<br><br>"

		右側表示文字列 += "■현재 스위트룸 사용 상황<br>"
		FOR LOCAL, 0, スイートルーム部屋数
			右側表示文字列 += @"{LOCAL + 2000}호실:"
			IF スイートルーム部屋割り配列:LOCAL != ""
				VARSET 配列分解文字列
				SPLIT スイートルーム部屋割り配列:LOCAL, "_", 配列分解文字列
				IF STRCOUNT(スイートルーム部屋割り配列:LOCAL, "_") > 2
					右側表示文字列 += @"%CALLNAME:(TOINT(配列分解文字列:1))%외<br>"
				ELSE
					右側表示文字列 += @"%CALLNAME:(TOINT(配列分解文字列:1))%<br>"
				ENDIF
			ELSE
				右側表示文字列 += "체류자 없음<br>"
			ENDIF
		NEXT
		右側表示文字列 += "<br>"
		IF スイートルーム招待モード == 0
			右側表示文字列 += @"<font color='#%カラーパレット_HTML("黄")%'>[동행자를 허가한다]</font> / <button value='101'>[한 명만 초대한다]</button>"
		ELSE
			右側表示文字列 += @"<button value='101'>[동행자를 허가한다]</button> / <font color='#%カラーパレット_HTML("黄")%'>[한 명만 초대한다]</font>"
		ENDIF
		右側表示文字列 += "<br>"
		右側表示文字列 += "<button value='100'>[100] 스위트룸에 초대할 캐릭터를 선택한다</button>"
	CASEELSE
		詳細文文字列受け渡し変数 = 
		TRYCALLFORM %表示キー_左%("右側表示")
		右側表示文字列 += 詳細文文字列受け渡し変数
ENDSELECT

LOCALS = <div rect='81,31,1937,2812' padding='50, 25, 300' border='31' bcolor='#C0C0C0'>%左側表示文字列%</div>
LOCALS += @"<div rect='2050,31,3875,2812' padding='50, 25, 50' border='31' bcolor='#C0C0C0'>%右側表示文字列%</div>"
;下部戻るボタン
LOCALS += "<div rect='131,2262,1937,500'>"
LOCALS += "<button value='998'>[998] 성별 제어 옵션</button><br>"
LOCALS += "<button value='997'>[997] 시스템 설명</button><br>"
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 메인 메뉴로 돌아가기</button><br>"
LOCALS += "</div>"


HTML_PRINT LOCALS
FOR LOCAL, 0, 26
	PRINTL
NEXT


$INPUT_LOOP
IF チュートリアルフラグ:最初の広告 == 0
	RESULT = 997
ELSE
	BINPUT
ENDIF

SELECTCASE RESULT
	CASE 1
		表示キー_左 = 通常の広告
		表示キー_右 = 
		RESTART
	CASE 2
		表示キー_左 = スイートルーム
		表示キー_右 = 
		RESTART
	CASE 999
		表示キー_左 = 
		表示キー_右 = 
		RETURN 0
	CASE 998
		CALL OPTION_招待切り替え
		RESTART
	CASE 997
		DRAWLINE
		PRINTL ［광고: 튜토리얼］
		DRAWLINE
		HTML_PRINT "<img src='顔エラ' height='900' width='900'>"
		PRINTL 
		PRINTL 
		PRINTL 
		PRINTL 
		PRINTL 
		PRINTL 
		PRINTL 
		CALL 広告システムチュートリアル
		DRAWLINE
		RESTART
	CASE IS < 100
		;特殊広告間の切り替え
		表示キー_左 = %特殊広告関数保存:(RESULT - 10)%
		表示キー_右 = 
		RESTART
ENDSELECT

;100~はそれぞれの詳細画面の操作
SELECTCASE 表示キー_左
	CASE "通常の広告"
		SELECTCASE RESULT
			CASE 100
				表示キー_右 = 近くの島
				RESTART
			CASE 101
				表示キー_右 = 広範囲
				RESTART
			CASE 102
				表示キー_右 = 空域全体
				RESTART
			CASE 103
				PRINTL
				PRINTL
				SELECTCASE 表示キー_右
					CASE "近くの島"
						PRINTW 가까운 섬에 광고를 냈습니다.
						MONEY -= 3000
						CALL 人気上昇処理(3000)
						集客人数 = RAND:2 + 2 + 招待キャラ数補正算出()
					CASE "広範囲"
						PRINTW 먼 섬까지 광고를 냈습니다.
						MONEY -= 5000
						CALL 人気上昇処理(5000)
						集客人数 = RAND:3 + 3 + 招待キャラ数補正算出()
					CASE "空域全体"
						PRINTW 큰맘 먹고 공역 전체에 광고를 냈습니다!
						MONEY -= 10000
						CALL 人気上昇処理(10000)
						集客人数 = RAND:3 + 5 + 招待キャラ数補正算出()
				ENDSELECT
				CALL 通常招待_実処理
				表示キー_左 = 
				表示キー_右 = 
				RESTART
		ENDSELECT
	CASE "スイートルーム"
		IF RESULT == 100
			CALL スイートルーム招待_実処理
			IF RESULT == 0
				;0戻りは広告を発注したパターン
				表示キー_左 = 
				表示キー_右 = 
			ENDIF
			RESTART
		ELSEIF RESULT == 101
			INVERTBIT スイートルーム招待モード, 0
			RESTART
		ENDIF
	CASEELSE
		TRYCALLFORM %表示キー_左%("実処理", RESULT)
		IF RESULT == 0
			;0戻りは広告を発注したパターン
			表示キー_左 = 
			表示キー_右 = 
			SIF 通常キャラ招待:0 == 0
				通常キャラ招待:0 = -1
		ENDIF
		RESTART
ENDSELECT

RETURN 1



@通常招待_実処理
#DIM 集客候補, キャラクタ数上限
#DIM 候補人数
VARSET 集客候補

;上限以下にする
CALL ユニーク客人数チェック()
集客人数 = MIN(集客人数, (滞在キャラ上限 - 滞在キャラ数))

LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	;招待不可キャラは排除
	SIF CFLAG:LOCAL:招待不可フラグ
		CONTINUE
	;サプチケで呼ぶ予定のキャラは排除
	SIF LOCAL == サプチケキャラ招待
		CONTINUE
	;オプションによる制御
	SIF 性別招待制御_実処理(LOCAL)
		CONTINUE
	LOCAL:3 = 0
	FOR LOCAL:2, 0, 100
		SIF 解放キャラ招待:(LOCAL:2) == 0
			BREAK
		IF 解放キャラ招待:(LOCAL:2) == LOCAL
			LOCAL:3 = 1
			BREAK
		ELSEIF LOCAL:2 < 20 && スイートルーム招待:(LOCAL:2) == LOCAL
			LOCAL:3 = 1
			BREAK
		ENDIF
	NEXT
	SIF LOCAL:3
		CONTINUE
	;滞在状態ではないキャラをカウント
	IF CFLAG:LOCAL:滞在期間 == -1
		集客候補:(LOCAL:1) = LOCAL
		LOCAL:1 += 1
	ENDIF
NEXT
候補人数 = LOCAL:1

;候補人数が０の場合は追加募集出来ないようにしてそのまま終わる
IF 候補人数 == 0
	通常キャラ招待:0 = -1
	RETURN 0
ENDIF

;候補人数が集客人数より少ない場合は全員登録
IF 候補人数 <= 集客人数
	FOR LOCAL, 0, 候補人数
		通常キャラ招待:LOCAL = 集客候補:LOCAL
	NEXT
ELSE
	LOCAL:2 = 0
	LOCAL = 候補人数 - 1
	;集客候補配列をシャッフル
	WHILE LOCAL > 1
		LOCAL:1 = RAND:(LOCAL - 1)
		SWAP 集客候補:LOCAL, 集客候補:(LOCAL:1)
		LOCAL -= 1
		LOCAL:2 += 1
		;必要分集まったらおしまい
		SIF LOCAL:2 >= 集客人数
			BREAK
	WEND
	;配列の後ろの方にランダムシャッフルされた候補が集まってるはずなので集客人数分取得
	FOR LOCAL, 0, 集客人数
		通常キャラ招待:LOCAL = 集客候補:(候補人数 - 1 - LOCAL)
	NEXT
ENDIF


@スイートルーム招待_実処理
#DIM 表示候補, キャラクタ数上限
#DIM 現在ページ
#DIM 広告対象
#DIMS ソート情報

VARSET 表示候補, -1
現在ページ = 0


;表示キャラを登録
LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	;招待不可キャラは排除
	SIF CFLAG:LOCAL:招待不可フラグ
		CONTINUE
	;DM配達済みは排除
	SIF ABS(ＤＭキャラ招待) == LOCAL
		CONTINUE
	;サプチケ対象は排除
	SIF サプチケキャラ招待 == LOCAL
		CONTINUE
	;滞在済みは出さない
	SIF CFLAG:LOCAL:滞在期間 >= 0
		CONTINUE
	;オプションによる制御はスイートルームには出さない
	; SIF 性別招待制御_実処理(LOCAL)
	; 	CONTINUE
	LOCAL:3 = 0
	FOR LOCAL:2, 0, 100
		SIF 解放キャラ招待:(LOCAL:2) == 0
			BREAK
		IF 解放キャラ招待:(LOCAL:2) == LOCAL
			LOCAL:3 = 1
			BREAK
		ENDIF
	NEXT
	SIF LOCAL:3
		CONTINUE
	表示候補:(LOCAL:1) = LOCAL
	LOCAL:1 += 1
NEXT
CALL キャラ配列フィルタ処理(表示候補, -1)

$表示_LOOP
;広告対象の表示
DRAWLINE
PRINTL 누구를 무료 초대하시겠습니까?
DRAWLINE
LOCAL:1 = 0
;キャラの表示（15人まで）
FOR LOCAL, (現在ページ * 項目表示数), ((現在ページ * 項目表示数) + 項目表示数)
	;登録が終わったら表示終了
	IF 表示候補:LOCAL == -1
		BREAK
	ENDIF
	IF 文字色グループ取得(表示候補:LOCAL) >= 0
		SETCOLOR 同室時_グループ文字色:(文字色グループ取得(表示候補:LOCAL))
	ELSEIF CFLAG:(表示候補:LOCAL):同室文字色
		SETCOLOR CFLAG:(表示候補:LOCAL):同室文字色
	ENDIF
	ソート情報 '= ソート情報表示(表示候補:LOCAL, 1)
	IF ソート情報 == ""
		PRINTBUTTON @"[{LOCAL, 3}] %GET_NAME(表示候補:LOCAL, 1, 50)%", LOCAL
	ELSE
		PRINTBUTTON @"[{LOCAL, 3}] %GET_NAME(表示候補:LOCAL, 1, 40)%", LOCAL
		PRINTFORM 　%ソート情報%　
	ENDIF
	RESETCOLOR
	SIF LOCAL % 2 != 0 && LOCAL
		PRINTL 
NEXT

PRINTL 
DRAWLINE

[SKIPSTART]
IF 現在ページ > 0
	PRINTBUTTONLC "[9000]이전 페이지로", 9000
ELSE
	PRINTLC 　
ENDIF
PRINTFORM - {現在ページ + 1} -
IF 表示候補:((現在ページ * 30) + 30) > 0
	PRINTBUTTONC "[9001]다음 페이지로", 9001
ENDIF
PRINTL
[SKIPEND]
;ページめくり表示
CALL ページ送り共通表示(現在ページ, 項目表示数, 最大表示数, 9000, 9003)
DRAWLINE
CALL フィルタソートボタンセット表示(9020, 9030, 9031, 9040)
DRAWLINE
PRINTL 
PRINTBUTTON "[9999] 돌아가기", 9999

$INPUT_LOOP
BINPUT

;広告対象のINPUT
SELECTCASE RESULT
	CASE 9020, 9030, 9031, 9040
		CALL フィルタソートボタンセット実行部(RESULT, 9020, 9030, 9031, 9040)
		現在ページ = 0
		RESTART
	CASE 9000 TO 9007
		CALL ページ送り共通実行(RESULT, 現在ページ, 項目表示数, 最大表示数, 9000, 9003)
		GOTO 表示_LOOP
;	CASE 9000
;		現在ページ = MAX(現在ページ - 1, 0)
;		GOTO 表示_LOOP
;	CASE 9001
;		SIF LOCAL:1 == 0
;			現在ページ += 1
;		GOTO 表示_LOOP
	CASE 9999
		RETURN -1
	CASE IS >= CHARANUM, IS < 0
		REUSELASTLINE 
		GOTO INPUT_LOOP
	CASEELSE
		IF 表示候補:RESULT < 0
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		広告対象 = 表示候補:RESULT
ENDSELECT

LOCAL:9 = TFLAG:調教モード
TFLAG:調教モード = -2
CALL PRINT_STATE(広告対象, 0)
LOCAL:1 = RESULT
SWAP LOCAL:9, TFLAG:調教モード

SIF RESULT != 1
	GOTO 表示_LOOP

DRAWLINE
PRINTFORMW %CALLNAME:MASTER%은(는) %CALLNAME:広告対象%을(를) 스위트룸에 무료 초대했다.
SIF DAY != 1
	MONEY -= 20000

FOR LOCAL, 0, スイートルーム部屋数
	SIF スイートルーム部屋割り配列:LOCAL != ""
		CONTINUE
	SIF スイートルーム招待:LOCAL > 0
		CONTINUE
	スイートルーム招待:LOCAL = 広告対象
NEXT



@招待キャラ数補正算出()
#FUNCTION
LOCAL = 0

IF イベント完了フラグ確認("大拡張！空艇港") > 0
	LOCAL += 1
ENDIF

RETURNF LOCAL


