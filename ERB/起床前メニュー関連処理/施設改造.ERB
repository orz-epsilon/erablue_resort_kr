;---------------------------------------------------------------
;リゾート施設の改造、増築、開拓関連
;---------------------------------------------------------------

@施設改装メニュー処理
#DIM 画面表示用変数
;100~199の範囲でボタン数値を収めること

LOCALS = <div rect='81,0,1912,344' border='31' bcolor='#C0C0C0'></div>
LOCALS += @"<div rect='156,62,1912,281'>보유 루피：%NUM_FORMAT(MONEY)%<br>보유 ZP　：%ZP所持量全文字列()%</div>"
LOCALS += "<div rect='81,375,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2050,375,3875,2812' border='31' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='156,500,1937,2812'>"
LOCALS += "<br>"
LOCALS += "<button value='100'>[100] 정원 계열을 개장</button>"
LOCALS += "<br>"
LOCALS += "<button value='101'>[101] 유희실 계열을 개장</button>"
LOCALS += "<br>"
LOCALS += "<button value='102'>[102] 수영장 계열을 개장</button>"
LOCALS += "<br>"
IF 温泉作成許可フラグ
	LOCALS += "<button value='103'>[103] 온천 계열을 개장</button>"
	LOCALS += "<br>"
ENDIF
LOCALS += "<button value='104'>[104] 레스토랑 계열을 개장</button>"
LOCALS += "<br>"
LOCALS += "<br>"
IF 施設改造度:20:0
	LOCALS += "<button value='109'>[109] 자택을 개장</button>"
ELSE
	LOCALS += "<button value='109'>[109] 자택을 건설한다</button>"
ENDIF
LOCALS += "<br>"
LOCALS += "<button value='110'>[110] 직원 방을 증축</button>"
LOCALS += "<br>"
IF 鍛冶場作成許可フラグ
	LOCALS += "<button value='120'>[120] 공정항 에리어를 발전시킨다</button>"
	LOCALS += "<br>"
ENDIF
IF MATCH(開拓エリア開発番号, 大規模番号_居住区エリア) > 0
	LOCALS += "<button value='130'>[130] 거주구 에리어를 발전시킨다</button>"
	LOCALS += "<br>"
ENDIF
IF MATCH(開拓エリア開発番号, 大規模番号_海エリア) > 0
	LOCALS += "<button value='131'>[131] 바다 에리어를 발전시킨다</button>"
	LOCALS += "<br>"
ENDIF
LOCALS += "<br>"
LOCALS += "<button value='998'>[998] 시스템 설명</button>"
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 돌아가기</button>"
LOCALS += "<br>"
LOCALS += "</div>"
LOCALS += "<div rect='2125,500,3875,2812'>"
SELECTCASE 画面表示用変数
	CASE 0
		LOCALS += "리조트 시설의 개장·개조를 실시합니다.<br>"
		LOCALS += "개조에는 루피 등의 자원이 필요하지만, <br>"
		LOCALS += "체류 가능 인원이나 리조트의 인기 상한 등이 상승합니다. <br>"
	CASE 100
		LOCALS += 庭系列改造メニュー()
	CASE 101
		LOCALS += 遊戯室系列改造メニュー()
	CASE 102
		LOCALS += プール系列改造メニュー()
	CASE 103
		LOCALS += 風呂場系列改造メニュー()
	CASE 104
		LOCALS += レストラン系列改造メニュー()
	CASE 109
		LOCALS += 自宅改造メニュー()
	CASE 110
		LOCALS += 従業員部屋増築メニュー()
	CASE 120
		LOCALS += 空艇港改造メニュー()
	CASE 130
		LOCALS += 居住区改造メニュー()
	CASE 131
		LOCALS += 海改造メニュー()
ENDSELECT
LOCALS += "</div>"




HTML_PRINT LOCALS, 1
FOR LOCAL, 0, 29
	PRINTL
NEXT
DRAWLINE
IF チュートリアルフラグ:施設改造 == 0
	RESULT = 998
ELSE
	BINPUT
ENDIF
LOCAL = RESULT

SELECTCASE LOCAL
	CASE 100 TO 199
		画面表示用変数 = LOCAL
		RESTART
	CASE 998
		CALL 施設改造チュートリアル()
		RESTART
	CASE 999
		画面表示用変数 = 0
		RETURN
	CASEELSE
		SELECTCASE 画面表示用変数
			CASE 100
				CALL 庭系列改造実行関数(LOCAL)
			CASE 101
				CALL 遊戯室系列改造実行関数(LOCAL)
			CASE 102
				CALL プール系列改造実行関数(LOCAL)
			CASE 103
				CALL 風呂場系列改造実行関数(LOCAL)
			CASE 104
				CALL レストラン系列改造実行関数(LOCAL)
			CASE 109
				CALL 自宅改造実行関数(LOCAL)
			CASE 110
				CALL 従業員部屋増築実行関数(LOCAL)
			CASE 120
				CALL 空艇港改造実行関数(LOCAL)
			CASE 130
				CALL 居住区改造実行関数(LOCAL)
			CASE 131
				CALL 海系列改造実行関数(LOCAL)
		ENDSELECT
		RESTART
ENDSELECT


@従業員部屋増築メニュー()
#FUNCTIONS
;100~199と999は大本のメニュー操作で使うため、使用しないこと

LOCALS = 직원 방을 증축하여, 더욱 직원을 고용할 수 있도록 합니다. <br><br>
IF 従業員部屋_増築数 < 従業員部屋拡張上限
	LOCALS += @"현재 직원 방 수:{従業員部屋数}　　증축 상한:{従業員部屋拡張上限 + (従業員部屋数 - 従業員部屋_増築数)}<br>"
	IF 従業員部屋_増築数 < 18
		LOCALS += @"■<button value='200'>[직원 방 증축]</button> (필요:%NUM_FORMAT((従業員部屋_増築数 + 2) * 5000)%루피)<br>"
	ELSE
		LOCALS += @"■<button value='200'>[직원 방 증축]</button> （필요:%NUM_FORMAT((従業員部屋_増築数 + 2) * 10000)%루피）<br>"
	ENDIF
ELSE
	LOCALS += @"※이미 최대 수까지 증축되었습니다<br>"
ENDIF

RETURNF LOCALS

@従業員部屋増築実行関数(ARG)
#DIM 必要費用

SELECTCASE ARG
	CASE 200
		IF 従業員部屋_増築数 < 18
			必要費用 = (従業員部屋_増築数 + 2) * 5000
		ELSE
			必要費用 = (従業員部屋_増築数 + 2) * 10000
		ENDIF
		IF MONEY < 必要費用
			PRINTW 루피가 부족합니다.
			RETURN 0
		ENDIF
		PRINTW 직원 방을 한 칸 증축했습니다.
		MONEY -= 必要費用
		従業員部屋_増築数 += 1
		CALL 従業員部屋数算出()
ENDSELECT


@従業員部屋数算出()
;数値のブレを防ぐために算出関数を設置

;初期値分
従業員部屋数 = 2

;増築分
従業員部屋数 += 従業員部屋_増築数

;セフレエンド分
SIF エンディングイベント完了フラグ確認("性欲の城") > 0
	従業員部屋数 += 10
