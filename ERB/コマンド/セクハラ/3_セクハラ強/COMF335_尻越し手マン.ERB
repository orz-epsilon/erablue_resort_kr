;===================================================
;尻越し手マン
;===================================================
@COMNAME335
#FUNCTIONS
TSTR:コマンド名受渡 = 엉덩이 너머 핸드잡


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_335
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ強")
弱点コマンドカテゴリ = V


;===================================================
;ツールチップ文章
;===================================================
@COM_TOOLTIP_335
TSTR:ツールチップ受渡 '= "<br>■엉덩이 너머 핸드잡<br>"
TSTR:ツールチップ受渡 += "상대의 등 뒤에서 손을 돌려, 엉덩이 너머로 질에 손가락을 넣어 주무르는 커맨드. <br>"
TSTR:ツールチップ受渡 += "대상이 하반신 뒤쪽에 갑옷을 입고 있는 경우・상하 일체복을 입고 있는 경우는 실행 불가. <br>"
TSTR:ツールチップ受渡 += "데리고 나갈 수 있을 때, 모드화하여 동행 상태가 된다. <br>"
TSTR:ツールチップ受渡 += "COM 타입: 성희롱<br>획득 소스: 접촉・노출・쾌락V・고통<br>"
TSTR:ツールチップ受渡 += "고유 획득 경험: 없음<br>"


;===================================================
;コマンド実行処理
;===================================================
@COM335
#LOCALSIZE 1
#DIM 同行数
;尻越し手マン

CALL セクハラ反応タイプ判定(TARGET)
;RFLAG:コマンド結果受渡し変数
;1:拒絶  2:受け入れ 3:喜び
;RFLAG:コマンド結果受渡し変数２
;1:無知  2:無反応 3:嫌悪  4:拒否  5:余裕  6:羞恥  7:愉悦  8:汎用

;セクハラ強は基本的に喜び以外に入らないはず（合意条件であり、合意に入るにはそれなりに仲良くならなければならないため）

SELECTCASE RFLAG:コマンド結果受渡し変数２
	CASE 1
		;無知
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
	CASE 2
		;無反応
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
	CASE 3
		;嫌悪
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)
	CASE 4
		;拒否
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
	CASE 5
		;余裕
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 120)
	CASE 6
		;羞恥
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 300)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 350)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 150)
	CASE 7
		;愉悦
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 130)
	CASE 8
		;汎用
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
		CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
ENDSELECT
SIF 同性愛性癖判定(PLAYER, TARGET)
	EXP:PLAYER:同性愛経験 += 1
SIF 同性愛性癖判定(TARGET, PLAYER)
	EXP:TARGET:同性愛経験 += 1
;発生する快楽は受け手の状態に依存しない
CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER, 100)
SIF TALENT:PLAYER:器用な指
	CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER, 50)

;苦痛
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 40 * PAIN_CHECK_V(TARGET) / 1000)

;性的陥落済かつ同行が二人未満なら同行化
IF 陥落チェック_性的(TARGET) && (CFLAG:TARGET:デート == 0 || CFLAG:TARGET:デート == PLAYER + 100)
	同行数 = 0
	FOR LOCAL, 1, VARSIZE("TARGET")
		SIF TARGET:LOCAL < 1
			BREAK
		IF CFLAG:(TARGET:LOCAL):同行 
			同行数 ++
		ENDIF
	NEXT
	IF 同行数 < 2
		CALL MODE_CHANGE("尻越し手マン", PLAYER, TARGET)
		CFLAG:TARGET:同行 = 60 + (CFLAG:好感度レベル - 関係閾値:2) * 5
		RFLAG:コマンド結果受渡し変数３ = 1
	ENDIF
ENDIF

TIME += 10
RETURN 1


;===================================================
;実行可能判定
;===================================================
@COM_ABLE335
;尻越し手マン実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(335) && !RFLAG:罰ゲーム中
	RETURN RESULT
;睡眠中は駄目
SIF CFLAG:睡眠
	RETURN 0
;Vがない場合駄目
SIF TALENT:Ｖ感度 == -2
	RETURN 0
SIF GETBIT(TALENT:性別, 0) == 0
	RETURN 0
;処女は駄目
SIF TALENT:処女
	RETURN 0
;ある程度濡れてないとダメ
SIF PALAM:潤滑 < PALAMLV:2
	RETURN 0

;下半身着衣
;後鎧、めくれない上下一体服、上下一体タイツ、前張りがあると無理
SIF TEQUIP:下腹部後鎧あり
	RETURN 0
SIF TEQUIP:上下一体服 && TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:上下一体タイツ && TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0
SIF TEQUIP:下半身下着あり == 2
	RETURN 0

;TARGETの好感度レベル
SIF CFLAG:好感度レベル < 1 + 関係閾値:2
	RETURN 0
RETURN 1


;===================================================
;実行時メッセージ
;===================================================
@MESSAGE_COM335

PRINTFORM %CALLNAME:PLAYER%은(는) %CALLNAME:TARGET%의 등 뒤에서 손을 돌려
IF TEQUIP:下半身服あり
	PRINT 옷 속에 집어넣자
ELSE
	PRINT 하자
ENDIF
PRINTFORM ,\@ TALENT:種族 == 4 ? 작은 # \@엉덩이 너머로
;潤滑LV4以上
SIF PALAM:TARGET:潤滑 >= PALAMLV:4
	PRINT 흠뻑 젖은
PRINTFORML 은밀한 틈에 손가락을 넣어, 주무르기 시작했다…

IF CUP:TARGET:快Ｖ > 1000
	;一定以上の快楽が発生した時にちょっと声が出る
	PRINTFORML 갑작스러운 쾌락에, %CALLNAME:TARGET%은(는) 무심코 작은 신음을 흘리고 말았다…
	PRINTL
ENDIF

IF RFLAG:コマンド結果受渡し変数３
	PRINTL 
	PRINTFORML %CALLNAME:PLAYER%은(는) 그대로 %CALLNAME:TARGET%을(를) 데리고 다니기로 했다
ENDIF

@MESSAGE_COM335_非口上時
;基本喜びだけのはずなので一旦分類せずに直接喜びパターンの文章を書く
SELECTCASE RFLAG:コマンド結果受渡し変数２
	CASE 1
		;無知
PRINTFORML %CALLNAME:TARGET%은(는) 잘 모르겠지만, %CALLNAME:PLAYER%이(가) 즐겁다면 좋다고 기뻐하고 있다
	CASE 2
		;無反応
PRINTFORML %CALLNAME:TARGET%은(는) 태연한 척하지만, 살짝 얼굴을 붉히고 %CALLNAME:PLAYER%에게 몸을 비벼왔다…
	CASE 3
		;嫌悪
PRINTFORML %CALLNAME:TARGET%은(는) 어쩔 수 없는 사람이네, 라는 얼굴로 %CALLNAME:PLAYER%가 하고 싶은 대로 내버려 두고 있다…
	CASE 4
		;拒否
PRINTFORML %CALLNAME:TARGET%은(는) 나쁘지 않은 기분인 듯, %CALLNAME:PLAYER%가 하고 싶은 대로 내버려 두고 있다…
	CASE 5
		;余裕
PRINTFORML %CALLNAME:TARGET%은(는) %CALLNAME:PLAYER%에게, 주위에 들키지 않도록 몰래 만지라고 말했다…
	CASE 6
		;羞恥
PRINTFORML %CALLNAME:TARGET%은(는) 붉어진 얼굴로 %CALLNAME:PLAYER%을(를) 바라보며, 살며시 몸을 기대왔다…
	CASE 7
		;愉悦
PRINTFORML %CALLNAME:TARGET%은(는) 다른 곳도 만질 거냐며, 요염한 표정으로 %CALLNAME:PLAYER%을(를) 도발하고 있다…
	CASE 8
PRINTFORML %CALLNAME:TARGET%은(는) %CALLNAME:PLAYER%에게 만져지는 것을 조금 기쁘게 생각하는 것 같다…
ENDSELECT
