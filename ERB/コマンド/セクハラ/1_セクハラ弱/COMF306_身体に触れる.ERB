
@COMNAME306
#FUNCTIONS
TSTR:コマンド名受渡 = 몸에 닿다

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_306
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ弱")
睡眠中許可 = 1

@COM_TOOLTIP_306
LOCALS = <br>■몸에 닿다<br>
LOCALS += "상대의 몸에 부드럽게 닿는 커맨드. <br>커맨드 실행 시 어디에 닿을지 선택한다."
LOCALS += "<br>COM 타입: 성희롱<br>획득 소스: 접촉・노출・환락(일정 조건 시)・불쾌(일정 조건 시)・반감(일정 조건 시) 등"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM306
#DIM 選択先保存
#DIMS 選択肢リスト, 100
#DIM 選択肢数
#DIM ループ用
;身体に触れる

IF RFLAG:サイドボタン受渡し変数
	;サイドボタンから来た時は選択処理挟まず直接コマンド実行に飛ぶ
	選択先保存 = RFLAG:サイドボタン受渡し変数
ELSE
	;まずは選択肢のリストを作る
	CALL 触れ合い選択肢リスト(選択肢リスト)
	選択肢数 = RESULT

	DRAWLINE
	PRINTL 어디에 닿을까요?
	DRAWLINE
	FOR ループ用, 0, 選択肢数
		CALLFORM 身体に触れる_ABLE_%選択肢リスト:ループ用%
		IF RESULT
			CALLFORMF COMNAME_S306_{RESULT}
			PRINTBUTTON @"[{ループ用}] %TSTR:コマンド名受渡%", ループ用
			PRINTL 
		ENDIF
	NEXT
	PRINTL 
	PRINTBUTTON "[999] 돌아가기", 999
	PRINTL 
	DRAWLINE
	BINPUT
	選択先保存 = RESULT
	SELECTCASE 選択先保存
		CASE 999
			RETURN -1
		CASE 0 TO 99
			CALLFORM 身体に触れる_ABLE_%選択肢リスト:選択先保存%
			選択先保存 = RESULT
	ENDSELECT
ENDIF

;COM実行処理
;弱点コマンド処理の都合上、CALL_COMを経由させる
RSTR:SELECTCOM_S受け渡し = 306_{選択先保存}
SELECTCOM_S = %RSTR:SELECTCOM_S受け渡し%
CALL CALL_COM(TARGET)
RETURN RESULT

;触れ合いの選択肢をソートした状態で渡すための関数
@触れ合い選択肢リスト(選択肢リスト変数)
#DIM CONST 関数接頭辞長さ = STRLENSU("身体に触れる_ABLE_")
#DIMS REF 選択肢リスト変数, 0
#DIM 選択肢数
#DIMS 選択肢名
#DIM 関数総数
#DIM ループ用

#DIMS CONST 選択先順番制御 = "背中に触れる", "耳を触る"

VARSET 選択肢リスト変数
選択肢数 = 0
FOR ループ用, 0, VARSIZE("選択先順番制御")
	SIF EXISTFUNCTION(@"身体に触れる_ABLE_%選択先順番制御:ループ用%") == 1
		選択肢リスト変数:(選択肢数++) '= 選択先順番制御:ループ用
NEXT

VARSET RESULTS
関数総数 = ENUMFUNCBEGINSWITH("身体に触れる_ABLE_")
FOR ループ用, 0, 関数総数
	選択肢名 '= SUBSTRINGU(RESULTS:ループ用, 関数接頭辞長さ, -1)
	SIF MATCH(選択先順番制御, 選択肢名)
		CONTINUE
	選択肢リスト変数:(選択肢数++) '= 選択肢名
NEXT

RETURN 選択肢数

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE306
;実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(306)
	RETURN RESULT
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM306
CALLFORM MESSAGE_COM_S%RSTR:SELECTCOM_S受け渡し%

@MESSAGE_COM306_非口上時
CALLFORM MESSAGE_COM_S%RSTR:SELECTCOM_S受け渡し%_非口上時
