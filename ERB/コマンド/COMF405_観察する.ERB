
@COMNAME405
#FUNCTIONS
TSTR:コマンド名受渡 = 관찰한다


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_405
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("単独可能")

@COM_TOOLTIP_405
LOCALS = <br>■観察する<br>
LOCALS += "주변이나 캐릭터를 잘 관찰하는 커맨드."
LOCALS += "<br>COM 타입: 단독 가능<br>획득 소스: 없음"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%


;-------------------------------------------------
;観察する
;-------------------------------------------------
@COM405
#DIM ループ用

;RFLAG:コマンド結果受渡し変数
;  0 =人の気配はない
;  1 =人の気配がある
; 10 =眠っているキャラの観察
; 11 =ダウンしているキャラの観察

RFLAG:コマンド結果受渡し変数 = 0
FOR ループ用, 1, VARSIZE("TARGET")
	SIF TARGET:ループ用 <= 0
		BREAK
	IF CFLAG:(TARGET:ループ用):隠密
		RFLAG:コマンド結果受渡し変数 = 1
		BREAK
	ENDIF
NEXT

IF TARGET > 0 && CFLAG:睡眠
	;眠った対象がいるとき
	IF CFLAG:体力限界
		;対象がダウンしているとき、気絶キャラの観察になる
		RFLAG:コマンド結果受渡し変数 = 11
		;RFLAG:コマンド結果受渡し変数２にスコア（体力により補正）
		RFLAG:コマンド結果受渡し変数２ = EX:スコア * 9999 / MAXBASE:体力
	ELSE
		RFLAG:コマンド結果受渡し変数 = 10
		;RFLAG:コマンド結果受渡し変数２に起床までの残り時間
		CALL CHARA_ROUTINE_STATUS(TARGET, "起床時刻")
		RFLAG:コマンド結果受渡し変数２ = MAX(RESULT + TCVAR:行動時間ゆらぎ - TIME, 0)
	ENDIF
ENDIF

TIME += 3
RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE405
;観察実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(405) && GLOBAL_COMABLE_TYPE("隠密")
	RETURN RESULT
SIF CFLAG:PLAYER:うふふ
	RETURN 0
RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM405

SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE 0
		PRINTFORML %CALLNAME:PLAYER%는 주위를 둘러보았다
		IF TARGET <= 0
			PRINTL ……사람의 기척은 없는 것 같다
		ELSE
			PRINTL ……평소의 북적거리는 리조트 풍경이다
		ENDIF
	CASE 1
		PRINTFORML %CALLNAME:PLAYER%는 주위를 둘러보았다
		PRINTL ……?
		PRINTL 물건 뒤에 무언가가 숨어있는 것 같다
	CASE 10
		PRINTFORML %CALLNAME:PLAYER%는 잠들어있는 %CALLNAME:TARGET%의 모습을 관찰했다
		;起床までの残り時間
		SELECTCASE RFLAG:コマンド結果受渡し変数２
			CASE IS > 180
				PRINTFORML %CALLNAME:TARGET%는 푹 잠들어 있다. 한동안 깨어날 것 같지 않다…
			CASE IS > 90
				PRINTFORML %CALLNAME:TARGET%는 푹 잠들어 있다. 깨어나려면 좀 더 시간이 걸릴 것 같다…
			CASE IS > 30
				PRINTFORML %CALLNAME:TARGET%가 %CALLNAME:PLAYER%가 낸 희미한 소리에 반응했다. 슬슬 눈을 뜰 것 같다…
			CASEELSE
				PRINTFORML %CALLNAME:TARGET%%TEXTR("는 뒤척이고 있다/는 잠꼬대를 하고 있다/의 몸이 계속 움직이고 있다/의 숨이 얕다")%. 곧 깨어날 것 같다…
		ENDSELECT
	CASE 11
		PRINTFORML %CALLNAME:PLAYER%는 잠들어있는 %CALLNAME:TARGET%의 모습을 관찰했다
		;スコアの多さ
		SELECTCASE RFLAG:コマンド結果受渡し変数２
			CASE IS >= 400
				PRINTFORM %CALLNAME:TARGET%의 몸은 들이부어진 쾌락의 깊이를 말해주듯, 잠들면서도 경련하며
				IF GETBIT(TALENT:性別, 0)
					IF TEQUIP:下半身服あり
						PRINT 음순에서 뿜어져 나온 조수로 잠옷에 얼룩을 만들고
					ELSEIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
						PRINT 음순에서 뿜어져 나온 조수로 타이츠에 얼룩을 만들고
					ELSEIF TEQUIP:下半身下着あり
						PRINT 음순에서 뿜어져 나온 조수로
						IF TEQUIP:下半身下着あり > 0
							PRINT 속옷에 얼룩을 만들고
						ELSE
							IF TEQUIP:下半身下着あり == -1
								PRINT 사타구니를 장식하는 얼마 안 되는 천을
							ELSEIF TEQUIP:下半身下着あり == -2
								PRINT 반짝이는 피어스를
							ELSE
								PRINT 음란하게 적힌 명찰을
							ENDIF
							PRINT 젖어
						ENDIF
					ELSE
						PRINT , 음순에서 조수를 뿜어내며
					ENDIF
				ENDIF
				PRINTL 계속되고 있다…
			CASE IS >= 20
				PRINTFORML %CALLNAME:TARGET%의 몸은 주어진 쾌락의 여운으로, 잠결에도 때때로 경련하고 있다…
			CASEELSE
				PRINTFORML %CALLNAME:TARGET%은 새근새근 숨을 쉬고 있다…
		ENDSELECT
ENDSELECT
