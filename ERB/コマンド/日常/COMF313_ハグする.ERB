

@COMNAME313
#FUNCTIONS
TSTR:コマンド名受渡 = 포옹하다

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_313
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_313
LOCALS = <br>■포옹하다<br>
LOCALS += "상대를 가볍게 안아주는 커맨드. 일정 확률로 대성공, 성공, 실패로 분기."
LOCALS += "<br>COM 타입: 일상<br>획득 소스: 환락・정복・접촉・노출・불쾌(실패 시)"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM313
;スキンシップ
LOCAL = RAND:100
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= 9
	;10％で大成功
	RFLAG:コマンド結果受渡し変数 = 1
ELSEIF LOCAL >= LOCAL:1
	;10～1％で失敗
	RFLAG:コマンド結果受渡し変数 = -1
ELSE
	;残りは成功
	RFLAG:コマンド結果受渡し変数 = 0
ENDIF



;歓楽強度
LOCAL:0 = 200
;征服
LOCAL:1 = 100
IF RFLAG:コマンド結果受渡し変数 == -1
	TIMES LOCAL:0 , 0.10
	TIMES LOCAL:1 , 0.50
ELSEIF RFLAG:コマンド結果受渡し変数 == 0
	TIMES LOCAL:0 , 1.00
	TIMES LOCAL:1 , 1.00
ELSE
	TIMES LOCAL:0 , 2.00
	TIMES LOCAL:1 , 2.00
ENDIF


CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL:0)
CALL SOURCE_CALC_征服(TARGET, PLAYER, LOCAL:1)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 80)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 60)
IF RFLAG:コマンド結果受渡し変数 == -1
	CALL SOURCE_CALC_不快(TARGET, PLAYER, 100)
ELSE
	CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, LOCAL:0 / 3)
	CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, LOCAL:0 / 4)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, LOCAL:0 / 3)
ENDIF
;あなたに胸があり、かつ相手に男要素がある場合、上昇量倍
IF GETBIT(TALENT:PLAYER:性別, 0) && GETBIT(TALENT:TARGET:性別, 1)
	CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, LOCAL:0 / 4)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, LOCAL:0 / 3)
ENDIF

TIME += 5

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE313
;スキンシップ実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(313)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM313

PRINTFORML %CALLNAME:PLAYER%은(는)%CALLNAME:TARGET%을(를) 가볍게 포옹했다.

IF RFLAG:コマンド結果受渡し変数 == 1
	PRINTFORML %CALLNAME:TARGET%은(는) 왠지 기뻐하는 것 같다…
ELSEIF RFLAG:コマンド結果受渡し変数 == 0
	PRINTFORML %CALLNAME:TARGET%도 싫지 않은 모양이다…
ELSEIF RFLAG:コマンド結果受渡し変数 == -1
	PRINTFORML …아무래도, 조금 너무 친한 척한 것 같다…
ENDIF

