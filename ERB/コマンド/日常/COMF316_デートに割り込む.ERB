@COMNAME316
#FUNCTIONS
TSTR:コマンド名受渡 = 데이트에 끼어들다

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_316
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_316
LOCALS = <br>■데이트에 끼어들다<br>
LOCALS += "데이트하고 있는 상대에게 용무가 있다고 말해서 함께 있어달라고 하는 커맨드. <br>몇 분 정도 함께 있어줄지는 소질이나 화술 기능에 따라 변동한다."
LOCALS += "<br>COM 타입: 일상<br>획득 소스: 환락・정복・접촉"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM316
#DIM 基礎時間
#DIM 同行者保存
#DIM 同行数値記録
同行者保存 = 0
同行数値記録 = 0

基礎時間 = ABL:MASTER:話術技能 * 20
;素質補正
基礎時間 += デート取り合い度算出(TARGET, ABS(CFLAG:TARGET:デート) - 100, MASTER)
;0以下の場合は断る
IF 基礎時間 <= 0
	;ここ口上入れたいけどとりあえず汎用のみ
	PRINTFORMW %CALLNAME:TARGET%은(는) 지금 바쁘니까, 라며 %CALLNAME:PLAYER%의 유혹을 거절했다.
	RETURN -1
ENDIF

;何分までOKかを格納
TCVAR:TARGET:デートちょっかい値 = 基礎時間
TCVAR:TARGET:ちょっかい経過時間 = 0

;一度に連れ歩けるのは二人まで,八面六臂があれば無視
IF !あなた特殊能力:八面六臂
	FOR LOCAL,1,CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		;TARGETに関してカウントする必要なし
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:(TARGET:LOCAL):同行
			IF 同行数値記録
				;既に同行者が2人いるパターン
				IF 同行数値記録 > CFLAG:(TARGET:LOCAL):同行
					CALL 同行解除処理(TARGET:LOCAL)
					CFLAG:(TARGET:LOCAL):デート = 0
				ELSE
					CALL 同行解除処理(同行者保存)
					CFLAG:同行者保存:デート = 0
					同行数値記録 = CFLAG:(TARGET:LOCAL):同行
					同行者保存 = TARGET:LOCAL
				ENDIF
			ELSE
				同行数値記録 = CFLAG:(TARGET:LOCAL):同行
				同行者保存 = TARGET:LOCAL
			ENDIF
		ENDIF
	NEXT
ENDIF
SIF CFLAG:TARGET:同行 < 基礎時間
	CFLAG:TARGET:同行 = 基礎時間
TFLAG:TARGET変更用 = TARGET
TIME += 1

;SOURCEまで回さないのでここで口上を表示させる
RESULT = 0
RESULTS =
IF FLAG:口上表示切り替えオプション > 0
	TRYCALLFORM KOJO_COM_{NO:TARGET}_316(TARGET)
ENDIF

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE316

;実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(316)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;デート時のみ
SIF CFLAG:デート == 0 || CFLAG:デート == MASTER + 100
	RETURN 0
;自分がデート中はダメ
SIF CFLAG:PLAYER:デート
	RETURN 0
;一日一回
SIF TCVAR:TARGET:デートちょっかい値 <= -1
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM316
PRINTFORML %CALLNAME:TARGET%은(는) 용무가 있다면, 이라며 데이트를 중단하고 %CALLNAME:PLAYER%의 이야기를 기다리고 있다.
PRINTFORML 대략 {TCVAR:TARGET:デートちょっかい値, 3}분 동안 함께 있어줄 것 같다.

@デートちょっかい終了処理(ARG)
;デートちょっかい値は正の時はちょっかいの許容時間、負の時はすでに今日実行済みであることを示す
TCVAR:ARG:デートちょっかい値 = -1
TCVAR:ARG:ちょっかい経過時間 = 0
RETURN 1
