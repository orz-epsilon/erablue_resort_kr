@MODE_持続快楽_Ａ騎乗位(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号
CALL SOURCE_CALC_奉仕(対象キャラ番号, 実行キャラ番号, 60)
CALL MODE_持続快楽_Ａ挿入系(実行キャラ番号, 対象キャラ番号, 120, 200)

CALL ウエスト変動処理(対象キャラ番号, "愛撫", -1)

@MODETYPE_Ａ騎乗位(部位番号, 処理モード, プレイヤー番号, ターゲット番号)
#DIM 部位番号
#DIM プレイヤー番号
#DIM ターゲット番号
#DIMS 処理モード

IF 処理モード == "写真用文字列_プレイヤー"
	;モード固有の「～～をする」の文章を入れ込む
	詳細文文字列受け渡し変数 = 올라탄%CALLNAME:ターゲット番号%의 항문에 아래에서 페니스를 찔러 넣다
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ELSEIF 処理モード == "写真用文字列_ターゲット"
	;モード固有の「～～をする」の文章を入れ込む
	IF 陥落チェック_性的(ターゲット番号) == 0 && BASE:ターゲット番号:酩酊 > 300
		;未陥落の泥酔姦。ヤってる最中にも酩酊は下がり続けるので、泥酔の500からちょい下げて300までで
		;むしろ未陥落でもないと別に酔ってコトに及んだ情報残してもシコくなさそうなので未陥落のみ
		詳細文文字列受け渡し変数 = %TEXTR("취한 기세로/취기에 휩쓸려/만취하여")%, %CALLNAME:プレイヤー番号% 위에 올라타서 %TEXTR("페니스/자지")%를
		詳細文文字列受け渡し変数 += @"%TEXTR("항문/엉덩이/항문 구멍/엉덩이 구멍")%%TEXTR("에 물고/로 받아들여")%, %TEXTR("허리를 그라인드시키다/허리를 흔들다/허리를 튕기다/허리 흔들기 봉사를 하다/쾌락을 탐하다")%"
	ELSE
		詳細文文字列受け渡し変数 = %CALLNAME:プレイヤー番号%에 올라타서, %TEXTR("페니스/자지")%를
		詳細文文字列受け渡し変数 += @"%TEXTR("항문/엉덩이/항문 구멍/엉덩이 구멍")%%TEXTR("에 물고/로 받아들여서")%%TEXTR("허리를 그라인드시키다/허리를 흔들다/허리를 튕기다/허리 흔들기 봉사를 하다/쾌락을 탐하다")%"
	ENDIF
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ENDIF

;0=ペニス 1=クリ 2=指（手） 3=口 4=胸 5=膣 6=アナル
;挿入で使用するのはペニスと膣で1を返す
SELECTCASE 部位番号
	CASE 部位_ペニス
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_占有
	CASE 部位_口
		IF プレイヤー番号 < 0 || ターゲット番号 < 0
			;キャラ不明の場合は一般論
			RETURN 部位状況_なし
		ELSEIF ABS(体格差計算(プレイヤー番号, ターゲット番号, 1)) >= 4
			;体格差が4サイズになると口は届かない
			;ターゲット側がプレイヤー側に乗ってるので+1補正
			RETURN 部位状況_届かない
		ELSE
			RETURN 部位状況_なし
		ENDIF
	CASE 部位_膣
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
	CASE 部位_アナル
		SIF 処理モード == "ターゲット"
			RETURN 部位状況_占有
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
ENDSELECT


@MODE_絶頂相手_Ａ騎乗位(絶頂部位, 処理モード, 判定結果)
#FUNCTION
#DIM 絶頂部位
#DIMS 処理モード
#DIM REF 判定結果
RETURNF MODE_絶頂相手_Ａ挿入系(絶頂部位, 処理モード, 判定結果)


@MESSAGE_MODE_Ａ騎乗位解除(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

PRINTFORML %CALLNAME:実行キャラ番号%은(는)%CALLNAME:対象キャラ番号%의 항문에서 천천히 음경을 빼냈다
