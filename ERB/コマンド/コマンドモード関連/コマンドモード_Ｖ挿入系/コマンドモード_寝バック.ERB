@MODE_持続快楽_寝バック(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号
CALL MODE_持続快楽_Ｖ挿入系(実行キャラ番号, 対象キャラ番号, 150, 150)


@MODETYPE_寝バック(部位番号, 処理モード, プレイヤー番号, ターゲット番号)
#DIM 部位番号
#DIM プレイヤー番号
#DIM ターゲット番号
#DIMS 処理モード

IF 処理モード == "写真用文字列_プレイヤー"
	;モード固有の「～～をする」の文章を入れ込む
	詳細文文字列受け渡し変数 = 엎드린%CALLNAME:ターゲット番号%에 페니스를 쑤셔 넣는다
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ELSEIF 処理モード == "写真用文字列_ターゲット"
	;モード固有の「～～をする」の文章を入れ込む
	IF 陥落チェック_性的(ターゲット番号) == 0 && BASE:酩酊 > 300
		;未陥落の泥酔姦。ヤってる最中にも酩酊は下がり続けるので、泥酔の500からちょい下げて300までで
		;むしろ未陥落でもないと別に酔ってコトに及んだ情報残してもシコくなさそうなので未陥落のみ
		詳細文文字列受け渡し変数 = %TEXTR("취한 기세로/취기에 휩쓸려/만취하여")%、%CALLNAME:プレイヤー番号%
		IF TALENT:ターゲット番号:恋人持ち > 0
			IF RISKY_DAY(ターゲット番号) >= 1
				詳細文文字列受け渡し変数 += @"와 후배위로 바람%TEXTR("탁란/임신시켜/씨를 뿌려")%%TEXTR("섹스/야한 짓을/교미를")%하고 있다"
			ELSE
				詳細文文字列受け渡し変数 += @"와 후배위로 바람%TEXTR("섹스/야한 짓/교미")%하고 있다"
			ENDIF
		ELSE
			詳細文文字列受け渡し変数 += @"에게%TEXTR("엎드려 깔려/후배위로")% 보지를%TEXTR("사용당하고 있다/허락해 버리고 있다/즐거움을 당하고 있다/범해지고 있다/범해져 뭉개지고 있다/박살나고 있다/쑤셔지고 있다")%"
		ENDIF
	ELSE
		詳細文文字列受け渡し変数 = %TEXTR("엎드려 깔려/후배위로")%、%CALLNAME:プレイヤー番号%
		IF TALENT:ターゲット番号:恋人持ち > 0
			詳細文文字列受け渡し変数 += @"와 바람%TEXTR("섹스/야한 짓/교미")%하고 있다"
		ELSE
			詳細文文字列受け渡し変数 += @"의%TEXTR("페니스/자지")%%TEXTR("를 물고 있다/에게 범해지고 있다/를 푹푹 쑤셔지고 있다")%"
		ENDIF
	ENDIF
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ENDIF

;0=ペニス 1=クリ 2=指（手） 3=口 4=胸 5=膣 6=アナル
;挿入で使用するのはペニスと膣で1を返す
SELECTCASE 部位番号
	CASE 部位_ペニス
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_占有
		SIF 処理モード == "ターゲット"
			RETURN 部位状況_届かない
	CASE 部位_クリトリス
		;クリは届かない
		SIF 処理モード == "ターゲット"
			RETURN 部位状況_届かない
	CASE 部位_口
		;口は届かない
		RETURN 部位状況_届かない
	CASE 部位_胸
		;胸は届かない
		RETURN 部位状況_届かない
	CASE 部位_膣
		SIF 処理モード == "ターゲット"
			RETURN 部位状況_占有
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
	CASE 部位_アナル
		;プレイヤーの尻は届かない
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
ENDSELECT


@MODE_絶頂相手_寝バック(絶頂部位, 処理モード, 判定結果)
#FUNCTION
#DIM 絶頂部位
#DIMS 処理モード
#DIM REF 判定結果
RETURNF MODE_絶頂相手_Ｖ挿入系(絶頂部位, 処理モード, 判定結果)


@MESSAGE_MODE_寝バック解除(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

PRINTFORML %CALLNAME:実行キャラ番号%은(는) %CALLNAME:対象キャラ番号%의 질 안에서 천천히 음경을 빼냈다
SIF TCVAR:対象キャラ番号:膣内精液量
	PRINTFORML %CALLNAME:対象キャラ番号%의 질 입구에서, %CALLNAME:実行キャラ番号%의 정액이 끈적하게 흘러내렸다…
