
@COMNAME_S62_1
#FUNCTIONS
TSTR:コマンド名受渡 = 입구를 공략
;男・ふたなり相手だと前立腺責めになる
SIF GETBIT(TALENT:TARGET:性別, 1)
	TSTR:コマンド名受渡 = 전립선 공략

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_62_1
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("Ａ系")
SIF CFLAG:TARGET:睡眠
	TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ強")
睡眠中許可 = 1
弱点コマンドカテゴリ = A
同時モード許可 = 3
同時モード拡張許可 = 1
オートコマンド許可 = 1
連続オート許可 = 1

@COM_TOOLTIP_S62_1
LOCALS = <br>■入口を責める<br>
LOCALS += "장내 입구 부근에 페니스로 강한 자극을 주는 커맨드. <br>상대가 남자·후타나리일 때, [전립선 공략]으로 변화한다. <br>A삽입계 모드일 때만 실행 가능."
LOCALS += "<br>COM 타입: A계<br>획득 소스: 쾌A·접촉·고통"
LOCALS += "<br>고유 획득 경험: A성교 경험(TARGET)·전립선 개발(TARGET·일정 조건 시)·C성교 경험_A삽입(PLAYER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM_S62_1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

;ペニスがあるなら前立腺もある
IF GETBIT(TALENT:TARGET:性別, 1)
	RCVAR:前立腺開発フラグ = 3
ENDIF
CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, 500)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)
CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 500)

;苦痛
LOCAL = 200 * PAIN_CHECK_A(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:PLAYER:Ｃ性交経験_Ａ挿入 ++
EXP:Ａ性交経験 ++

;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 4
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 4

EXP:愛情経験 += TALENT:恋慕 * 2

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S62_1
;入口を責める実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE_S("62_1")
	RETURN RESULT
;Ａ挿入系モードじゃない時はアウト
SIF !MODE_存在判定_完全一致("Ａ挿入系", PLAYER, TARGET)
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S62_1
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%은

IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT ,
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT 의 입구 부근을 꼼꼼히 문질렀다
	RETURN 0
ENDIF

IF GETBIT(TALENT:TARGET:性別, 1)
	PRINTFORM %CALLNAME:TARGET%의 오돌토돌한 전립선을 집요하게 괴롭혔다
ELSE
	PRINTFORM %CALLNAME:TARGET%의 입구 부근을 꼼꼼히 문질렀다
ENDIF

