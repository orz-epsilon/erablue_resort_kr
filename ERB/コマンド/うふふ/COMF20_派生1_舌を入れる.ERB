
@COMNAME_S20_1
#FUNCTIONS
TSTR:コマンド名受渡 = 혀를 넣는다

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_20_1
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ_被挿入中_セクハラ")
弱点コマンドカテゴリ = S
オートコマンド許可 = 1

@COM_TOOLTIP_S20_1
LOCALS = <br>■舌を入れる<br>
LOCALS += "키스 모드 중, 상대의 입안에 혀를 넣어 얽히게 하는 커맨드. <br>딥 키스 모드로 이행. <br>[혀놀림][고양이혀]에 따라 효과가 증감."
LOCALS += "<br>COM 타입: 우후후or피삽입중or성희롱<br>획득 소스: 노출・접촉・쾌S・불결・고통(일정 조건 시)"
LOCALS += "<br>고유 획득 경험: 키스 경험(PLAYER＆TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM_S20_1
;-------------------------------------------------
;モードの設定
;-------------------------------------------------
CALL MODE_CHANGE("ディープキス", PLAYER, TARGET)
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;奉仕コマンドフラグ
RCVAR:TARGET:奉仕コマンドフラグ = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------


CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 250)
CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 250)
CALL SOURCE_CALC_快Ｓ(PLAYER, TARGET, 250)
CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 150)

CALL SOURCE_CALC_不潔(TARGET, PLAYER, 50)


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
IF TALENT:PLAYER:舌使い
	CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
	CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 150)
ENDIF

IF TALENT:TARGET:舌使い
	CALL SOURCE_CALC_快Ｓ(PLAYER, TARGET, 150)
ENDIF

;猫舌では減少
SIF TALENT:PLAYER:猫舌
	CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 20)


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 3
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 3


SIF TALENT:恋慕
	EXP:愛情経験 += 1

SIF TALENT:TARGET:猫舌
	EXP:嗜虐快楽経験 += 1

EXP:キス経験 ++
EXP:PLAYER:キス経験 ++

CALL 初体験日登録処理(PLAYER, TARGET, "ファーストキス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 1)
IF RESULT
	RCVAR:PLAYER:ファーストキス中フラグ = 1
	CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:TARGET%의 [입술]에 첫 키스를 바쳤다.</font><img src='えっちハート'>","うふふ")
ENDIF
CALL 初体験日登録処理(PLAYER, TARGET, "キス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 0)

CALL 初体験日登録処理(TARGET, PLAYER, "ファーストキス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 1)
IF RESULT
	RCVAR:TARGET:ファーストキス中フラグ = 1
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:PLAYER%의 [입술]에 첫 키스를 바쳤다.</font><img src='えっちハート'>","うふふ")
ENDIF
CALL 初体験日登録処理(TARGET, PLAYER, "キス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 0)

RETURN 1


@CAN_COM_S20_1(ARG)
#DIM CONST 必要実行値 = 20
#DIM 実行値
#DIM ＋表示フラグ

SKIPDISP ARG
PRINTFORML 혀를 넣는다(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:欲望
IF ABL:欲望
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:欲望 * 1
	PRINTFORM 욕망 LV{ABL:欲望}({ABL:欲望 * 1})
	＋表示フラグ = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:奉仕精神 * 4
	PRINTFORM 봉사 정신 LV{ABL:奉仕精神}({ABL:奉仕精神 * 4})
	＋表示フラグ = 1
ENDIF

IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 2
	PRINTFORM 쾌락 각인 LV{MARK:快楽刻印}({MARK:快楽刻印 * 2})
	＋表示フラグ = 1
ENDIF

;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 5)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 5) * 1
	PRINTFORM 욕정 LV{GETPALAMLV(PALAM:欲情, 5)}({GETPALAMLV(PALAM:欲情, 5) * 1})
	＋表示フラグ = 1
ENDIF

;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 1
	PRINT 부끄러움(1)
	＋表示フラグ = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭耐性 > 0
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 1
	PRINT 오취 둔감(1)
	＋表示フラグ = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭耐性 < 0
	PRINT  - 
	実行値 -= 1
	PRINT 오취 민감(1)
	＋表示フラグ = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 6
	PRINT 헌신적(6)
	＋表示フラグ = 1
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感応答 < 0 && !TALENT:淫乱
	PRINT  - 
	実行値 -= 1
	PRINT 쾌감 부정(1)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 사랑(5)
	＋表示フラグ = 1
ENDIF
;親愛
IF TALENT:恋慕 > 1
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 10
	PRINT 친애(10)
	＋表示フラグ = 1
ENDIF
;キス嗜好
IF TALENT:キス嗜好 == 1
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 10
	PRINT 키스 좋아함(10)
	＋表示フラグ = 1
ELSEIF TALENT:キス嗜好 == 2
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 키스 마(999)
	＋表示フラグ = 1
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 당신의 말대로(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(20以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 실행값{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S20_1
;舌を入れる実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE_S("20_1")
	RETURN RESULT
;ボールギャグ使用中は不可
; SIF TEQUIP:ボールギャグ
; 	RETURN 0
;猫舌の調教者の場合は技巧Lv2以上必要
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	RETURN 0
;キスモードじゃない時はアウト
SIF !MODE_存在判定_順不同("キス", PLAYER, TARGET)
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S20_1
PRINTFORM %CALLNAME:PLAYER%
IF TALENT:TARGET:恋慕
	PRINTFORML 가 %CALLNAME:TARGET%의 입술에 혀를 대자,
	PRINTFORML %CALLNAME:TARGET%는 스스로 입을 벌려 혀와 혀를 얽어매었다…
ELSE
	PRINTFORML 는 %CALLNAME:TARGET%의 입술 틈새로 혀를 밀어 넣어, 혀와 혀를 얽어매었다…
ENDIF

SIF TCVAR:TARGET:泥酔眠姦 && TCVAR:TARGET:言いなり
	PRINTFORM 취해서 %CALLNAME:PLAYER%를 연모하는 사람으로 착각하고 있는 %CALLNAME:TARGET%는 %TEXTR("기쁜 듯이/적극적으로")% 혀를 얽어왔다…