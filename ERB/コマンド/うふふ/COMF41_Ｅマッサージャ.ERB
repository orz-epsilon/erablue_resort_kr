;-------------------------------------------------
;Ｅマッサージャ
;道具使用コマンド、レベル2
;-------------------------------------------------
@COMNAME41
#FUNCTIONS

TSTR:コマンド名受渡 = E 마사지기



;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_41
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("道具系")
弱点コマンドカテゴリ = C
オートコマンド許可 = 1


@COM_TOOLTIP_41
LOCALS = <br>■Ｅマッサージャ<br>
LOCALS += "진동하는 휴대용 마사지 기구를 사용하여 상대의 클리토리스나 페니스에 강한 자극을 주는 커맨드."
LOCALS += "<br>COM 타입: 도구계<br>획득 소스: 쾌C・노출・접촉"
LOCALS += "<br>고유 획득 경험: 도구 사용 경험(PLAYER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM41
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER,500)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 40)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 30)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

EXP:PLAYER:道具使用経験 ++
RCVAR:TARGET:道具コマンドフラグ = 1

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE41
;Ｅマッサージャ実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(41)
	RETURN RESULT
;クリがない場合駄目
SIF TALENT:TARGET:Ｃ感度 == -2
	RETURN 0
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:1 == 0 && NOITEM == 0
	RETURN 0

;下半身着衣
;前鎧、めくれない服があると無理
SIF TEQUIP:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0

;使用者が指を何かに使ってるなら無理
SIF TEQUIP:PLAYER:指
	RETURN 0
;クリトリスを何かに使ってる・届かないなら無理
SIF TEQUIP:PLAYER:クリトリス
	RETURN 0
CALL MODE_部位届く判定(部位_クリトリス, PLAYER, TARGET)
SIF RESULT == 0
	RETURN 0

;ベッドルームでないとダメ
SIF !BEDROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	RETURN 0
	
;オートコマンド中の制限
SIF FLAG:オートコマンド判定中フラグ && OPTION変数:玩具プレイ禁止
	RETURN 0
	
;オートコマンド中の制限
SIF FLAG:オートコマンド判定中フラグ && TALENT:TARGET:性別 == 2 &&  OPTION変数:男性への愛撫禁止
	RETURN 0	
	
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM41
PRINTFORM %CALLNAME:PLAYER%은
IF GETBIT(TALENT:TARGET:性別, 1)
	PRINTFORM %CALLNAME:TARGET%의 페니스에
ELSE
	PRINTFORM %CALLNAME:TARGET%의 은밀한 곳에
ENDIF
IF TEQUIP:下半身服あり && TEQUIP:スカートめくり可
	PRINT 옷을 걷어 올리고
ELSEIF TEQUIP:下半身服あり && TEQUIP:ずらし可
	PRINT 옷 위로
ELSEIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	PRINT 타이츠 위로
ELSEIF TEQUIP:下半身下着あり > 0
	PRINT 속옷 위로
ENDIF

PRINTL 진동하는 전기 안마기를 눌러 댔다
PRINTFORM %CALLNAME:TARGET%은
;Ｃ感覚LV3未満＋欲情のPALAMLV3未満
IF ABL:Ｃ感覚 < 3 && PALAM:欲情 < PALAMLV:3
	PRINT 몹시 간지러워하는 듯
;Ｃ感覚LV3未満＋欲情のPALAMLV3以上
ELSEIF ABL:Ｃ感覚 < 3 && PALAM:欲情 >= PALAMLV:3
	PRINT ,…아무 말도 못 하게 된 듯
;恋慕か親愛＋Ｃ感覚LV4以上＋欲情のPALAMLV3未満
ELSEIF TALENT:恋慕 && ABL:Ｃ感覚 >= 4 && PALAM:欲情 < PALAMLV:3 && PLAYER == MASTER
	PRINT 기다렸다는 듯 다리를 벌리고, 받아들이는 듯
;恋慕か親愛＋Ｃ感覚LV4以上＋欲情のPALAMLV3以上＋欲情のPALAMLV5未満
ELSEIF TALENT:恋慕 && ABL:Ｃ感覚 >= 4 && PALAM:欲情 >= PALAMLV:3 && PALAM:欲情 < PALAMLV:5 && PLAYER == MASTER
	PRINTFORM 쾌락에 침을 흘리면서, 몇 번이고 %CALLNAME:PLAYER%의 이름을 부르
;恋慕か親愛＋Ｃ感覚LV4以上＋欲情のPALAMLV5以上
ELSEIF TALENT:恋慕 && ABL:Ｃ感覚 >= 4 && PALAM:欲情 >= PALAMLV:5 && PLAYER == MASTER
	PRINTFORM %CALLNAME:PLAYER%을 끌어안으며 절정의 황홀감에 젖어 있는 듯
;Ｃ感覚LV3以上＋欲情のPALAMLV3未満
ELSEIF ABL:Ｃ感覚 >= 3 && PALAM:欲情 < PALAMLV:3
	PRINT 강렬한 쾌감에 얼굴을 붉히는 듯
;Ｃ感覚LV3以上＋欲情のPALAMLV3以上＋欲情のPALAMLV5未満
ELSEIF ABL:Ｃ感覚 >= 3 && PALAM:欲情 >= PALAMLV:3 && PALAM:欲情 < PALAMLV:5
	PRINT 넋이 나간 눈으로 쾌락을 탐하는 듯
;Ｃ感覚LV3以上＋欲情のPALAMLV5以上
ELSEIF ABL:Ｃ感覚 >= 3 && PALAM:欲情 >= PALAMLV:5
	PRINT 지금이라도 절정에 달할 것 같은 모습
ENDIF
PRINTL 이다…


