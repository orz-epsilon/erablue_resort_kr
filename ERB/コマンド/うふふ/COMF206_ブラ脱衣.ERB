;-------------------------------------------------
;ブラ脱衣
;-------------------------------------------------
@COMNAME206
#FUNCTIONS
IF TEQUIP:上半身下着あり == 2
	TSTR:コマンド名受渡 = 니플 패드를 벗기다
ELSEIF TEQUIP:上半身下着あり == 3
	TSTR:コマンド名受渡 = 가슴 붕대에서 해방하다
ELSEIF TEQUIP:上半身下着あり == 4
	TSTR:コマンド名受渡 = 가슴 리본을 풀다
ELSEIF TEQUIP:上半身下着あり == -3
	TSTR:コマンド名受渡 = 유두 명찰을 떼다
ELSEIF TEQUIP:上半身下着あり == -2
	TSTR:コマンド名受渡 = 니플 피어싱을 빼다
ELSE
	TSTR:コマンド名受渡 = 브래지어를 벗기다
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_206
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
SIF CFLAG:TARGET:睡眠
	TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ強")
睡眠中許可 = 1
オートコマンド許可 = 3

@COM_TOOLTIP_206
LOCALS = <br>■브래지어를 벗기다<br>
LOCALS += "상대의 상반신 속옷을 벗기는 커맨드.<br>상반신 속옷의 종류에 따라 명칭이 변화.<br>상반신 속옷 제거 가능 시, 상반신 옷이 있어도 실행할 수 있다."
LOCALS += "<br>COM 타입: 우후후<br>획득 소스: 없음"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM206
;口上出力のため実際の脱衣処理はEVENTCOMENDで

;オートコマンドだけここでやる
IF TFLAG:オートコマンドフラグ
	RFLAG:オートコマンド脱衣用フラグ = 1
	TEQUIP:上半身下着あり = 0
ENDIF
;野外
IF BEDROOM(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		RFLAG:コマンド結果受渡し変数 = 1
		RCVAR:TARGET:露出コマンドフラグ = 1
	ENDIF
ENDIF

RETURN 1


@CAN_COM206(ARG)
#DIM CONST 必要実行値 = 20
#DIM 実行値
#DIM ＋表示フラグ

SIF RFLAG:指示モード対象あなたフラグ
	RETURN 1

SKIPDISP ARG
PRINTFORML 상반신 속옷 탈의(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:露出癖
IF ABL:露出癖
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:露出癖 * 10
	PRINTFORM 노출벽LV{ABL:露出癖}({ABL:露出癖 * 10})
	＋表示フラグ = 1
ENDIF
IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 쾌락 각인LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 10)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 10) * 5
	PRINTFORM 욕정LV{GETPALAMLV(PALAM:欲情, 10)}({GETPALAMLV(PALAM:欲情, 10) * 5})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 1
	PRINT 부끄러움(1)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 5
	PRINT 남자 혐오(5)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 연모(5)
	＋表示フラグ = 1
ENDIF
;セフレ
IF TALENT:セフレ
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 3
	PRINT 섹프(3)
	＋表示フラグ = 1
ENDIF
;野外
IF BEDROOM(CFLAG:現在マップ種別, CFLAG:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		PRINT  - 
		実行値 -= 5
		PRINT 야외 노출(5)
		＋表示フラグ = 1
	ENDIF
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 시키는 대로(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(20以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 실행값{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE206
;上半身下着脱衣
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(206)
	RETURN RESULT
;脱衣不可
SIF TALENT:脱衣不可
	RETURN 0
;既に着ていない
SIF !TEQUIP:上半身下着あり
	RETURN 0
;鎧があるとダメ
SIF TEQUIP:上半身鎧あり
	RETURN 0
;抜き取れない服があるとダメ
IF TEQUIP:上半身服あり && !TEQUIP:上半身下着抜き取り可
	;ニプレスなら差し込みorはだけ可でOK
	SIF TEQUIP:上半身下着あり != 2 || (!TEQUIP:手差し込み可 && !TEQUIP:はだけ可)
		RETURN 0
ENDIF
;タイツもダメ
SIF TEQUIP:上タイツあり
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------

@MESSAGE_COM206
PRINTFORM %CALLNAME:PLAYER%는
IF TEQUIP:上半身下着あり == 2
	PRINTFORML %CALLNAME:TARGET%의 니플 패드를 벗겼다
ELSEIF TEQUIP:上半身下着あり == 3
	PRINTFORML %CALLNAME:TARGET%의 가슴을 조이던 사라시를 풀었다
ELSEIF TEQUIP:上半身下着あり == 4
	PRINTFORML %CALLNAME:TARGET%의 가슴을 장식하고 있던 리본을 풀었다
ELSEIF TEQUIP:上半身下着あり == -3
	PRINTFORML %CALLNAME:TARGET%의 젖꼭지를 꿰뚫고 있던 명찰 달린 안전핀을 뺐다
ELSEIF TEQUIP:上半身下着あり == -2
	PRINTFORML %CALLNAME:TARGET%의 젖꼭지를 장식하는 니플 피어싱을 뺐다
ELSE
	PRINTFORML %CALLNAME:TARGET%의 브래지어를 벗겼다
ENDIF
IF TALENT:TARGET:バストサイズ == 3
	PRINT 출렁!
	PRINT_IMG "えっちハート"
	PRINT 하고, 풍만한 유방이 크게 흔들리면서 튀어나왔다
ELSEIF TALENT:TARGET:バストサイズ == 2
	PRINT 출렁, 하고 큰 유방이 튀어나왔다
ENDIF


IF RFLAG:コマンド結果受渡し変数
	PRINTFORML 다른 사람들이 보는 앞에서 벗은 탓에, %CALLNAME:TARGET%는 어딘가 불안해하는 것 같다…
ENDIF


