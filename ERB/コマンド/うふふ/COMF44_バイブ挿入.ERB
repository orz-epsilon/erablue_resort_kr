;-------------------------------------------------
;バイブ
;道具使用コマンド、レベル2
;-------------------------------------------------
@COMNAME44
#FUNCTIONS
TSTR:コマンド名受渡 = 바이브 삽입

;弱点コマンド欄に専用の文字列を表示したい場合、これを設定すること
弱点コマンド欄表示名 = バイブ挿入


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_44
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("道具系&Ｖ系")
弱点コマンドカテゴリ = V

同時モード許可 = 999


@COM_TOOLTIP_44
LOCALS = <br>■バイブ<br>
LOCALS += "전동 바이브를 삽입하여 질 내부를 자극하는 커맨드. <br>바이브 장비 상태가 되어, 빼거나 성행위를 종료할 때까지 지속적으로 쾌락이 발생한다. <br>[처녀]를 잃는다."
LOCALS += "<br>COM 타입: 도구계<br>획득 소스: 쾌V・노출・접촉・고통"
LOCALS += "<br>고유 획득 경험: 도구 사용 경험(PLAYER)・V 경험(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM44
#DIM 強さ初期値記録

;処女
IF TALENT:TARGET:処女
	DRAWLINE
	PRINTL 바이브로 처녀성을 잃게 됩니다만, 괜찮으시겠습니까?
	PRINTBUTTONLC "[0] 네", 0
	PRINTBUTTONLC "[1] 아니요", 1
	PRINTL
	BINPUT
	IF RESULT == 1
		RETURN -1
	ENDIF
ENDIF

IF OPTION変数:バイブ強さ初期値 == -1
	$選択LOOP
	PRINTL
	PRINTL
	PRINTL
	DRAWLINE
	PRINTL ■강도의 초기값을 선택해 주세요
	PRINT 　　바이브 강도:
	PRINTBUTTON "-"
	FOR LOCAL, 0, 6
		IF 強さ初期値記録 >= LOCAL
			PRINTBUTTON "■"
		ELSE
			PRINTBUTTON "□"
		ENDIF
	NEXT
	PRINTBUTTON "[+]", "6"
	PRINTL
	PRINTL
	PRINTBUTTONLC "[900] 바이브를 장착한다", 900
	PRINTBUTTONLC "[999] 돌아간다", 999

	BINPUT
	LOCAL = RESULT
	SELECTCASE LOCAL
		CASE 0 TO 5
			強さ初期値記録 = LOCAL
			GOTO 選択LOOP
		CASE -1
			強さ初期値記録 = MAX(強さ初期値記録 - 1, 0)
			GOTO 選択LOOP
		CASE 6
			強さ初期値記録 = MIN(強さ初期値記録 + 1, 5)
			GOTO 選択LOOP
		CASE 900
			;着脱の処理
			CALL MODE_CHANGE("バイブ", PLAYER, TARGET, , , 強さ初期値記録)
		CASE 999
			RETURN -1
	ENDSELECT
ELSE
	;着脱の処理
	CALL MODE_CHANGE("バイブ", PLAYER, TARGET, , , OPTION変数:バイブ強さ初期値)
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER,250)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 20)

;苦痛
LOCAL = 100 * PAIN_CHECK_V(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)

;処女喪失
CALL LOST_VIRGIN(TARGET, PLAYER, 2)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------

;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

EXP:PLAYER:道具使用経験 ++
RCVAR:TARGET:道具コマンドフラグ = 1

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE44
;バイブ実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(44)
	RETURN RESULT
;すでに使ってるとだめ
;SIF MODE_存在判定_ターゲット側("バイブ", TARGET)
;	RETURN 0
;Vがない場合駄目
SIF TALENT:TARGET:Ｖ感度 == -2
	RETURN 0
SIF GETBIT(TALENT:TARGET:性別, 0) == 0
	RETURN 0
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:4 == 0 && NOITEM == 0
	RETURN 0

;下半身着衣
;前鎧、めくれない服、タイツ、前張りがあると無理
SIF TEQUIP:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0
SIF TEQUIP:下半身下着あり == 2
	RETURN 0

;装着者が指を何かに使ってるなら無理
SIF TEQUIP:PLAYER:指
	RETURN 0
;部位使ってると駄目
SIF TEQUIP:TARGET:膣 && !TFLAG:コマンド条件制御用
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM44
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%은
IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT ,
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT 모두에게 바이브 끝을 대고 삽입했다
	RETURN 0
ENDIF

SIF ABL:Ｖ感覚 >= 4 && TALENT:淫乱
	PRINT 허리를 들어 올려 애원하고 있다
PRINTFORM %CALLNAME:TARGET%의
SIF TEQUIP:下半身下着あり > 0
	PRINTFORM 속옷을 내리고,
;潤滑LV5以上でさらに追加
SIF PALAM:潤滑 >= PALAMLV:5
	PRINT 흥건하게
;潤滑LV3以上でさらに追加
SIF PALAM:潤滑 >= PALAMLV:3
	PRINT 점액을 머금은
;小柄体型
IF TALENT:種族 < 0
	PRINT 어린 보지에
ELSE
	PRINT 은밀한 틈새에
ENDIF
PRINTL 바이브 끝을 대고 익숙하게 만들어간다…

PRINTFORM %CALLNAME:TARGET%는
;処女＋淫乱か娼婦
IF TALENT:処女 == 1 && TALENT:淫乱
	PRINT 자신이 처녀라는 것을 잊은 듯한 미소를 보이며
;処女＋恋慕か親愛
ELSEIF TALENT:処女 == 1 && TALENT:恋慕 && PLAYER == MASTER
	PRINTFORM 조금 쓸쓸해 보이는 눈으로 %CALLNAME:PLAYER%를 보며
;処女＋淫乱
ELSEIF TALENT:処女 == 1 && TALENT:淫乱
	PRINT 처녀라는 것도 상관없이 허리를 흔들며 애원하고
;処女＋一線越えない
ELSEIF TALENT:処女 == 1 && TALENT:一線越えない
	PRINT 무언가를 포기한 듯한 표정으로 눈에 희미하게 눈물을 글썽이며
;処女
ELSEIF TALENT:処女 == 1
	PRINT 덜덜 떨며, 상실의 공포에 질려

;Ｖ経験200以上＋淫乱か娼婦
ELSEIF EXP:Ｖ経験 > 200 && TALENT:淫乱
	PRINT 삽입에 대한 기대만으로 덜덜 허리를 떨며 갈 것 같아졌고
;Ｖ経験150以上＋淫乱か娼婦
ELSEIF EXP:Ｖ経験 > 150 && TALENT:淫乱
	PRINTFORM 녹아내린 눈으로 %CALLNAME:PLAYER%를 응시하며, 스스로 음순을 비벼대고
;Ｖ経験120以上＋淫乱か娼婦
ELSEIF EXP:Ｖ経験 > 120 && TALENT:淫乱
	PRINT 황홀한 표정으로 참지 못하겠다는 듯 허리를 비틀며
;Ｖ経験70以上＋淫乱か娼婦
ELSEIF EXP:Ｖ経験 > 70 && TALENT:淫乱
	PRINTFORM 무언가를 갈구하듯 눈을 글썽이며 %CALLNAME:PLAYER% 쪽을 바라보며
;Ｖ経験40以上
ELSEIF EXP:Ｖ経験 > 40
	PRINT 바이브가 스치는 감촉에 도취된 숨을 내쉬고
;Ｖ経験20以上
ELSEIF EXP:Ｖ経験 > 20
	PRINT 바이브가 스치는 감촉에 몸을 비틀며 애액을 배어 나오게 하고
;それ以外
ELSE
	PRINT 바이브가 스치는 감촉에 살 주름을 실룩거리게 하며
ENDIF
PRINTL 있다…
PRINTFORM %CALLNAME:TARGET%의
;潤滑LV4以上
SIF PALAM:潤滑 >= PALAMLV:4
	PRINT 흠뻑 젖은
PRINTFORML 성기에 바이브를 삽입했다

