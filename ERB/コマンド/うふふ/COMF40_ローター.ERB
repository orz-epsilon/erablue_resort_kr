;-------------------------------------------------
;ローター
;道具使用コマンド、レベル1
;-------------------------------------------------
@COMNAME40
#FUNCTIONS

	TSTR:コマンド名受渡 = 로터




;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_40
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("道具系")
弱点コマンドカテゴリ = C
オートコマンド許可 = 1



@COM_TOOLTIP_40
LOCALS = <br>■ローター<br>
LOCALS += "로터를 사용하여 상대의 클리토리스나 페니스에 자극을 주는 커맨드."
LOCALS += "<br>COM 타입: 도구계<br>획득 소스: 쾌C・노출・접촉"
LOCALS += "<br>고유 획득 경험: 도구 사용 경험(PLAYER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM40
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER,250)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 60)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

EXP:PLAYER:道具使用経験 ++
RCVAR:TARGET:道具コマンドフラグ = 1
RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE40
;ローター実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(40)
	RETURN RESULT
;クリがない場合駄目
SIF TALENT:TARGET:Ｃ感度 == -2
	RETURN 0
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:0 == 0 && NOITEM == 0
	RETURN 0

;下半身着衣
;前鎧、めくれない服があると無理
SIF TEQUIP:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0

;使用者が指を何かに使ってるなら無理
SIF TEQUIP:PLAYER:指
	RETURN 0
;クリトリスを何かに使ってる・届かないなら無理
SIF TEQUIP:PLAYER:クリトリス
	RETURN 0
CALL MODE_部位届く判定(部位_クリトリス, PLAYER, TARGET)
SIF RESULT == 0
	RETURN 0
	
;オートコマンド中の制限
SIF FLAG:オートコマンド判定中フラグ && OPTION変数:玩具プレイ禁止
	RETURN 0
	
;オートコマンド中の制限
SIF FLAG:オートコマンド判定中フラグ && TALENT:TARGET:性別 == 2 &&  OPTION変数:男性への愛撫禁止
	RETURN 0	
	
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM40
PRINTFORM %CALLNAME:PLAYER%은/는
IF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	PRINTFORM %CALLNAME:TARGET%의
	PRINTL 하반신에 타이츠 위로 떨리는 로터를 갖다 댔다…
	RETURN 0
ENDIF
IF TEQUIP:下半身下着あり > 0
	IF TALENT:恋慕
		PRINTFORM 스스로 속옷을 내리고 준비하고 있다
	ELSE
		PRINTFORM 속옷을 내려 노출시켰다
	ENDIF
ELSEIF TEQUIP:下半身服あり && TEQUIP:スカートめくり可
	IF TALENT:恋慕
		PRINTFORM 스스로 옷을 걷어 올리고 준비하고 있다
	ELSE
		PRINTFORM 옷을 걷어 올려 노출시켰다
	ENDIF
ELSEIF TEQUIP:下半身服あり && TEQUIP:ずらし可
	IF TALENT:恋慕
		PRINTFORM 스스로 옷을 내려 준비하고 있다
	ELSE
		PRINTFORM 옷을 내려 노출시켰다
	ENDIF
ENDIF
PRINTFORM %CALLNAME:TARGET%의
;オトコ＋欲情のPALAMLV3以上
IF !(TALENT:性別 & 1) && (PALAM:欲情 >= PALAMLV:3)
	PRINTL 단단하게 솟아오른 페니스에 떨리는 로터를 갖다 댔다…
;オトコ
ELSEIF !(TALENT:性別 & 1)
	PRINTL 페니스에 떨리는 로터를 갖다 댔다…
;ふたなり＋欲情のPALAMLV3以上
ELSEIF (TALENT:性別 & 3) == 3 && PALAM:欲情 >= PALAMLV:3
	PRINTL 축축한 은밀한 입술에 떨리는 로터를 갖다 댔다…
;ふたなり
ELSEIF (TALENT:性別 & 3) == 3
	PRINTL 은밀한 입술에 떨리는 로터를 갖다 댔다…
;欲情のPALAMLV3以上
ELSEIF PALAM:欲情 >= PALAMLV:3
	PRINTL 단단하고 뾰족한 클리토리스에 떨리는 로터를 갖다 댔다…
ELSE
	PRINTL 클리토리스에 떨리는 로터를 갖다 댔다…
ENDIF

