;-------------------------------------------------
;フェラチオ
;奉仕系コマンド、レベル2
;-------------------------------------------------
@COMNAME81
#FUNCTIONS

IF MODE_存在判定_完全一致("シックスナイン可能", PLAYER, TARGET) && GETBIT(FLAG:モード管理, モードビット_同時) == 0
	TSTR:コマンド名受渡 = 식스나인
ELSEIF MODE_存在判定_完全一致("パイズリ", TARGET, PLAYER)
	TSTR:コマンド名受渡 = 파이즈리 펠라
ELSE
	TSTR:コマンド名受渡 = 펠라치오
ENDIF
SIF 派生名フラグ
	TSTR:コマンド名受渡 = 펠라치오

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_81
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ&奉仕")
同時モード許可 = 3
同時モード拡張許可 = 0
オートコマンド許可 = 3
反転オート許可 = 3


@COM_TOOLTIP_81
IF RFLAG:奉仕逆転時ツールチップフラグ
	LOCALS = <br>■フェラチオする<br>
	LOCALS += "자신이 입으로 상대의 페니스를 애무하는 커맨드.<br>펠라치오 모드가 되어, 모드를 변경하거나 성행위를 종료할 때까지 지속적으로 특정 소스가 발생한다.<br>파이즈리 모드 시, [파이즈리 펠라]로 변화."
ELSE
	LOCALS = <br>■フェラチオ<br>
	LOCALS += "상대에게 입으로 자신의 페니스를 애무하게 하는 커맨드.<br>펠라치오 모드가 되어, 모드를 변경하거나 성행위를 종료할 때까지 지속적으로 특정 소스가 발생한다.<br>파이즈리 모드 시, [파이즈리 펠라]로 변화."
ENDIF
LOCALS += "<br>대상자가 [혀놀림][고양이 혀]를 가지고 있을 경우, 실행자에게의 효과가 증감한다."
LOCALS += "<br>COM 타입: 우후후<br>획득 소스: 노출・접촉・봉사・불결"
LOCALS += "<br>고유 획득 경험: 구강성교 경험(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM81
;-------------------------------------------------
;モードの設定
;-------------------------------------------------
SIF !MODE_シックスナイン中_完全一致(PLAYER, TARGET) && MODE_存在判定_完全一致("シックスナイン可能", PLAYER, TARGET) && GETBIT(FLAG:モード管理, モードビット_同時) == 0
	RFLAG:コマンド結果受渡し変数 = 1
IF MODE_存在判定_完全一致("パイズリ", TARGET, PLAYER)
	RFLAG:同時モード体位変化フラグ = 1
	CALL MODE_DELETE_プレイヤー側("パイズリ", TARGET)
	CALL MODE_CHANGE("パイズリフェラ", TARGET, PLAYER)
ELSEIF RFLAG:同時モード体位変化フラグ
	CALL MODE_CHANGE("パイズリフェラ", TARGET, PLAYER)
ELSE
	CALL MODE_CHANGE("フェラチオ", TARGET, PLAYER)
ENDIF
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------

;奉仕快楽
RCVAR:TARGET:奉仕コマンドフラグ = 1
RCVAR:TARGET:汎用喘ぎ_文節低減フラグ = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 150)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 30)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 120)
CALL SOURCE_CALC_奉仕(TARGET, PLAYER, 300)

CALL SOURCE_CALC_不潔(TARGET, PLAYER, 250)

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------

;舌使いによって、快感のソース増加
IF TALENT:TARGET:舌使い
	CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 150)
ENDIF

;猫舌では苦痛ちょいある
SIF TALENT:TARGET:猫舌
	CALL SOURCE_CALC_苦痛(PLAYER, TARGET, 20)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;フェラ経験
EXP:口淫経験 += 1

;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 7
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 7

;愛情経験
SIF TALENT:恋慕
	EXP:愛情経験 += 1

;奴隷が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
SIF TALENT:TARGET:猫舌
	EXP:嗜虐快楽経験 += 1

CALL 初体験日登録処理(TARGET, PLAYER, "ファーストキス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "陰茎", "", 1)
IF RESULT
	RCVAR:TARGET:ファーストキス中フラグ = 1
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:PLAYER%의 [음경]에 첫 키스를 바쳤다.</font><img src='えっちハート'>","うふふ")
ENDIF

RETURN 1


@CAN_COM81(ARG)
#DIM CONST 必要実行値 = 24
#DIM 実行値
#DIM ＋表示フラグ

SKIPDISP ARG
PRINTFORML 펠라치오(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:欲望
IF ABL:欲望
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:欲望 * 1
	PRINTFORM 욕망LV{ABL:欲望}({ABL:欲望 * 1})
	＋表示フラグ = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:奉仕精神 * 4
	PRINTFORM 봉사정신LV{ABL:奉仕精神}({ABL:奉仕精神 * 4})
	＋表示フラグ = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:精液中毒 * 3
	PRINTFORM 정액중독LV{ABL:精液中毒}({ABL:精液中毒 * 3})
	＋表示フラグ = 1
ENDIF

IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 쾌락각인LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 5)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 5) * 1
	PRINTFORM 욕정LV{GETPALAMLV(PALAM:欲情, 5)}({GETPALAMLV(PALAM:欲情, 5) * 1})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 1
	PRINT 수줍음(1)
	＋表示フラグ = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭耐性 > 0
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 1
	PRINT 악취둔감(1)
	＋表示フラグ = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭耐性 < 0
	PRINT  - 
	実行値 -= 3
	PRINT 악취민감(3)
	＋表示フラグ = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 6
	PRINT 헌신적(6)
	＋表示フラグ = 1
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感応答 < 0 && !TALENT:淫乱
	PRINT  - 
	実行値 -= 1
	PRINT 쾌감의 부정(1)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 12
	PRINT 남자 싫음(12)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 연모(5)
	＋表示フラグ = 1
ENDIF
;親愛
IF TALENT:恋慕 > 1
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 친애(5)
	＋表示フラグ = 1
ENDIF

;調教者がふたなり
IF (TALENT:PLAYER:性別 & 3) == 3
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 8
	PRINT 후타나리(8)
	＋表示フラグ = 1
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 당신의 말대로(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(24以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 실행 {必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE81
;フェラチオ実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
SIF GLOBAL_COMABLE(81)
	RETURN RESULT
;調教者がオトコか半人半妖かふたなりじゃないとダメ
SIF !(TALENT:PLAYER:性別 & 2)
	RETURN 0
;ちんこがない場合駄目
SIF TALENT:PLAYER:Ｃ感度 == -2
	RETURN 0
;下半身着衣
;前鎧、タイツ、前張りがあると無理
SIF TEQUIP:PLAYER:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:PLAYER:下タイツあり && !TEQUIP:PLAYER:下タイツ破れ
	RETURN 0
SIF TEQUIP:PLAYER:下半身下着あり == 2
	RETURN 0
;猫舌の奴隷の場合は技巧Lv2以上必要
SIF TALENT:猫舌 && ABL:技巧 < 2
	RETURN 0
;ボールギャグ使用中は不可
; SIF TEQUIP:ボールギャグ
; 	RETURN 0
IF !MODE_存在判定_完全一致("パイズリ", TARGET, PLAYER)
	;ペニスを何かに使ってるなら無理
	SIF TEQUIP:PLAYER:ペニス
		RETURN 0
ENDIF
;口を何かに使ってるなら無理
SIF TEQUIP:TARGET:口
	RETURN 0

;オートコマンド中の制限
IF FLAG:オートコマンド判定中フラグ
	SIF 初体験済みチェック("ファーストキス", PLAYER) == 0
		RETURN 0
ENDIF

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM81
#DIM 同時キャラ番号
IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT ,
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	IF MODE_存在判定_完全一致("パイズリフェラ", TARGET, PLAYER)
		PRINTFORML 는 유방을 맞대면서 %CALLNAME:PLAYER%의 페니스에 혀를 핥았다…
	ELSE
		PRINTFORML 는 분담해서 %CALLNAME:PLAYER%의 페니스에 혀를 핥고 있다…
	ENDIF
	RETURN 0
ENDIF

;VARSET LOCAL

;;お掃除
;IF LOCAL && (TCVAR:LOCAL:103 == PLAYER || TCVAR:LOCAL:105 == PLAYER)
;		PRINTFORML %CALLNAME:LOCAL%の中にたっぷりと射精してからペニスを抜き、
;		PRINTFORML ペニスに付着した精液を%CALLNAME:TARGET%に舐め取らせた…
;		SIF TFLAG:8
;			PRINTFORML %CALLNAME:TARGET%は尿道に残った精液まで残らずきれいにしてから\@ ABL:精液中毒 > 2 ? 名残を惜しむかのように # \@ペニスから唇を離した…
;ELSEIF NOWEX:MASTER:11
;	IF LOCAL == TARGET
;		PRINTFORML 射精の直前に\@ MASTER_POSE(6,1,1) ? ヴァギナ # アナル \@からペニスを抜き%CALLNAME:TARGET%に咥えさせた…
;	ELSEIF LOCAL
;		PRINTFORML 射精の直前に%CALLNAME:LOCAL%からペニスを抜き%CALLNAME:TARGET%に咥えさせた…
;	ELSE
;		PRINTFORML 射精寸前のペニスを%CALLNAME:TARGET%に咥えさせた…
;	ENDIF
;	IF ABL:技巧 > 2
;		IF ABL:精液中毒 > 2
;			PRINTFORML %CALLNAME:TARGET%は唇で亀頭を包んでから高速で舌を動かし%CALLNAME:PLAYER%に射精を促している…
;		ELSE
;			PRINTFORML %CALLNAME:TARGET%はペニスの敏感な部分に舌をはわせ%CALLNAME:PLAYER%に射精を促している…
;		ENDIF
;	ELSE
;		PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の反応を伺いながら懸命に舌を動かしている…
;	ENDIF
;ELSE

IF MODE_存在判定_完全一致("パイズリフェラ", TARGET, PLAYER)
	PRINTFORM %CALLNAME:TARGET%는
	;恋慕か親愛
	IF TALENT:恋慕 && PLAYER == MASTER
		PRINT 자애로운 표정으로,
		;淫乱か娼婦
		SIF TALENT:淫乱
			PRINT 스스로
	ELSEIF 知識素質:性知識 < 0
		PRINTFORM %CALLNAME:PLAYER%에게 부탁받아,
	ENDIF
	PRINTFORML 파이즈리하고 있던 %CALLNAME:PLAYER%의 페니스를 물었다…
ELSE
	PRINTFORM %CALLNAME:TARGET%는
	;シックスナインに派生
	SIF RFLAG:コマンド結果受渡し変数
		PRINTFORM %CALLNAME:PLAYER%와 머리 위치가 서로 엇갈리도록 자세를 바꾸자,
	;恋慕か親愛
	IF TALENT:恋慕 && PLAYER == MASTER
		PRINT 자애로운 표정으로,
		;淫乱か娼婦
		SIF TALENT:淫乱
			PRINT 스스로
	ELSEIF 知識素質:性知識 < 0
		PRINTFORM %CALLNAME:PLAYER%에게 말한 대로,
	ENDIF
	;シックスナイン体勢中
	SIF !RFLAG:コマンド結果受渡し変数 && MODE_シックスナイン中_完全一致(TARGET, PLAYER)
		PRINT 눈앞에 있는
	PRINTFORML %CALLNAME:PLAYER%의 페니스에 혀를 훑고 있다…
ENDIF
;ENDIF

