;-------------------------------------------------
;アナルバイブ
;道具使用コマンド、レベル2
;-------------------------------------------------
@COMNAME45
#FUNCTIONS
TSTR:コマンド名受渡 = 애널 바이브 삽입

;弱点コマンド欄に専用の文字列を表示したい場合、これを設定すること
弱点コマンド欄表示名 = アナルバイブ挿入



;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_45
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("道具系&Ａ系")
弱点コマンドカテゴリ = A
同時モード許可 = 999


@COM_TOOLTIP_45
LOCALS = <br>■アナルバイブ<br>
LOCALS += "전동 애널 바이브를 삽입하여 장내를 자극하는 커맨드. <br>애널 바이브 장비 상태가 되어, 빼거나 성행위를 종료할 때까지 지속적으로 쾌락이 발생한다."
LOCALS += "<br>COM 타입: 도구계<br>획득 소스: 쾌A·노출·접촉·고통"
LOCALS += "<br>고유 획득 경험: 도구 사용 경험(PLAYER)·A 경험(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM45
#DIM 強さ初期値記録

IF OPTION変数:アナルバイブ強さ初期値 == -1
	$選択LOOP
	PRINTL
	PRINTL
	PRINTL
	DRAWLINE
	PRINTL ■강도 초기값을 선택해 주세요
	PRINT 　　애널 바이브 강도:
	PRINTBUTTON "[-]", -1
	FOR LOCAL, 0, 6
		IF 強さ初期値記録 >= LOCAL
			PRINTBUTTON "■", LOCAL
		ELSE
			PRINTBUTTON "□", LOCAL
		ENDIF
	NEXT
	PRINTBUTTON "[+]", "6"
	PRINTL
	PRINTL
	PRINTBUTTONLC "[900] 애널 바이브를 장착한다", 900
	PRINTBUTTONLC "[999] 돌아간다", 999

	BINPUT
	LOCAL = RESULT
	SELECTCASE LOCAL
		CASE 0 TO 5
			強さ初期値記録 = LOCAL
			GOTO 選択LOOP
		CASE -1
			強さ初期値記録 = MAX(強さ初期値記録 - 1, 0)
			GOTO 選択LOOP
		CASE 6
			強さ初期値記録 = MIN(強さ初期値記録 + 1, 5)
			GOTO 選択LOOP
		CASE 900
			;着脱の処理
			CALL MODE_CHANGE("アナルバイブ", PLAYER, TARGET, , , 強さ初期値記録)
		CASE 999
			RETURN -1
	ENDSELECT
ELSE
	;着脱の処理
	CALL MODE_CHANGE("アナルバイブ", PLAYER, TARGET, , , OPTION変数:アナルバイブ強さ初期値)
ENDIF
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_快Ａ(TARGET, PLAYER,250)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 20)

LOCAL = 250 * PAIN_CHECK_A(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------

;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

EXP:PLAYER:道具使用経験 ++
RCVAR:TARGET:道具コマンドフラグ = 1


RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE45
;アナルバイブ実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(45)
	RETURN RESULT
;すでに使ってるとだめ
;SIF MODE_存在判定_ターゲット側("アナルバイブ", TARGET)
;	RETURN 0
;Aがない場合駄目
SIF TALENT:TARGET:Ａ感度 == -2
	RETURN 0
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:5 == 0 && NOITEM == 0
	RETURN 0

;下半身着衣
;後鎧、めくれない服、タイツがあると無理
SIF TEQUIP:下腹部後鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0

;装着者が指を何かに使ってるなら無理
SIF TEQUIP:PLAYER:指
	RETURN 0
;部位使ってると駄目
SIF TEQUIP:TARGET:アナル && !TFLAG:コマンド条件制御用
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM45
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%은
IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT ,
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT 모두에게 애널 바이브 끝을 대고 삽입했다
	RETURN 0
ENDIF

PRINTFORM %CALLNAME:TARGET%의
SIF TEQUIP:下半身下着あり > 0
	PRINTFORM 속옷을 내리고,
PRINTFORML 국화 구멍에 애널 바이브를 댔다…
PRINTFORM %CALLNAME:TARGET%은
;Ａ経験180以上＋淫乱か娼婦
IF EXP:Ａ経験 > 180 && TALENT:淫乱
	PRINT 녹아내린 표정으로, 스스로 엉덩이를 벌리고
;Ａ経験140以上＋淫乱か娼婦
ELSEIF EXP:Ａ経験 > 140 && TALENT:淫乱
	PRINT 삽입을 애타게 기다리는 듯 허리를 비틀며
;Ａ経験100以上＋淫乱か娼婦
ELSEIF EXP:Ａ経験 > 100 && TALENT:淫乱
	PRINT 뺨을 붉히고, 기대감에 엉덩이를 흔들며
;Ａ経験70以上＋淫乱か娼婦
ELSEIF EXP:Ａ経験 > 70 && TALENT:淫乱
	PRINT 움츠러든 곳에 가해지는 바이브의 자극에 몸을 떨며
;Ａ経験40以上
ELSEIF EXP:Ａ経験 > 40
	PRINT 기대와 부정이 뒤섞인 듯한 복잡한 표정을 지으며
;Ａ経験20以上
ELSEIF EXP:Ａ経験 > 20
	PRINT 수치심으로 물든 얼굴을 숙이고
;それ以外
ELSE
	PRINT 부정한 구멍을 침범당하는 치욕과 굴욕감에 물든 표정을 짓고
ENDIF
PRINTL 있다…
PRINTFORM %CALLNAME:TARGET%의
;潤滑LV4以上
SIF PALAM:潤滑 >= PALAMLV:4
	PRINT 점액으로 뒤덮인
PRINTFORML 애널에 바이브를 삽입했다

