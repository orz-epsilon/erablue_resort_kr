;-------------------------------------------------
;ボテ腹ズリ
;奉仕系コマンド
;-------------------------------------------------
@COMNAME87
#FUNCTIONS
TSTR:コマンド名受渡 = 보테배 문지르기
SIF GETBIT(FLAG:モード管理, モードビット_同時)
	TSTR:コマンド名受渡 = 더블 보테배 문지르기


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_87
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ&奉仕")
同時モード許可 = 2
同時モード拡張許可 = 0
オートコマンド許可 = 3

@COM_TOOLTIP_87
IF RFLAG:奉仕逆転時ツールチップフラグ
	LOCALS = <br>■불룩한 배를 문지르다<br>
	LOCALS += "자신의 임신하여 커진 복부로 상대의 페니스를 애무하는 커맨드.<br>불룩한 배 문지르기 모드가 되어, 모드를 변경하거나 성행위를 종료할 때까지 지속적으로 특정 소스가 발생한다."
ELSE
	LOCALS = <br>■불룩한 배 문지르기<br>
	LOCALS += "상대에게 임신하여 커진 복부로 자신의 페니스를 애무하게 하는 커맨드.<br>불룩한 배 문지르기 모드가 되어, 모드를 변경하거나 성행위를 종료할 때까지 지속적으로 특정 소스가 발생한다."
ENDIF
LOCALS += "<br>COM 타입: 우후후<br>획득 소스: 쾌S・노출・접촉・봉사・불결"
TSTR:ツールチップ受渡 = %LOCALS%

@COM87
;-------------------------------------------------
;モードの設定
;-------------------------------------------------
CALL MODE_CHANGE("ボテ腹ズリ", TARGET, PLAYER)
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;奉仕快楽
RCVAR:TARGET:奉仕コマンドフラグ = 1
RCVAR:TARGET:汎用喘ぎ_文節低減フラグ = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------




CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 100)
CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 50)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)
CALL SOURCE_CALC_奉仕(TARGET, PLAYER, 400)

CALL SOURCE_CALC_不潔(TARGET, PLAYER, 150)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 7
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 7

;愛情経験
SIF TALENT:恋慕
	EXP:愛情経験 += 2
RETURN 1

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
@CAN_COM87(ARG)
#DIM CONST 必要実行値 = 25
#DIM 実行値
#DIM ＋表示フラグ

SKIPDISP ARG
PRINTFORML 보테배즈리(%CALLNAME:TARGET%)

実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:欲望
IF ABL:欲望
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:欲望 * 1
	PRINTFORM 욕망LV{ABL:欲望}({ABL:欲望 * 1})
	＋表示フラグ = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:奉仕精神 * 4
	PRINTFORM 봉사 정신LV{ABL:奉仕精神}({ABL:奉仕精神 * 4})
	＋表示フラグ = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:精液中毒 * 3
	PRINTFORM 정액 중독LV{ABL:精液中毒}({ABL:精液中毒 * 3})
	＋表示フラグ = 1
ENDIF

IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 쾌락 각인LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 5)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 5) * 1
	PRINTFORM 욕정LV{GETPALAMLV(PALAM:欲情, 5)}({GETPALAMLV(PALAM:欲情, 5) * 1})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 4
	PRINT 수치심(4)
	＋表示フラグ = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭耐性 > 0
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 1
	PRINT 악취 둔감(1)
	＋表示フラグ = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭耐性 < 0
	PRINT  - 
	実行値 -= 2
	PRINT 악취 민감(2)
	＋表示フラグ = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 8
	PRINT 헌신적(8)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 12
	PRINT 남성 혐오(12)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 연모(5)
	＋表示フラグ = 1
ENDIF
;親愛
IF TALENT:恋慕 > 1
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 친애(5)
	＋表示フラグ = 1
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 당신의 말대로(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(25以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 실행값{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE87
;ボテ腹ズリ実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
SIF GLOBAL_COMABLE(87)
	RETURN RESULT
;ちんこがない場合駄目
SIF TALENT:PLAYER:Ｃ感度 == -2
	RETURN 0
;ある程度の妊娠進行度が必要
SIF PREGNANCY_SIZE(TARGET) < 2
	RETURN 0
;調教者がオトコか半人半妖かふたなりじゃないとダメ
SIF !(TALENT:PLAYER:性別 & 2)
	RETURN 0
;縄使用中は不可
; SIF TEQUIP:縄
; 	RETURN 0
;上半身着衣(TARGET)
;鎧、差し込めないorはだけない服があると無理
SIF TEQUIP:上半身鎧あり
	RETURN 0
SIF TEQUIP:上半身服あり && (!TEQUIP:はだけ可)
	RETURN 0
;下半身着衣(PLAYER)
;前鎧、タイツ、前張りがあると無理
SIF TEQUIP:PLAYER:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:PLAYER:下タイツあり && !TEQUIP:PLAYER:下タイツ破れ
	RETURN 0
SIF TEQUIP:PLAYER:下半身下着あり == 2
	RETURN 0
;ペニスを何かに使ってるなら無理
SIF TEQUIP:PLAYER:ペニス
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM87
#DIMS 腹部描写
#DIM 同時キャラ番号

IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT ,
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINTFORML 는 서로의 복부를 바싹 붙이고, %CALLNAME:PLAYER%의 페니스를 불룩한 배로 다루고 있다…
	RETURN 0
ENDIF

SELECTCASE PREGNANCY_SIZE(TARGET)
	CASE IS >= 5
		腹部描写 = 巨大にせり出したボテ腹
	CASE 4
		腹部描写 = 大きく膨らんだボテ腹
	CASEELSE
		腹部描写 = 山なりに盛り上がった腹部
ENDSELECT
IF GETBIT(FLAG:モード管理, モードビット_同時) == 0
	PRINTFORML %CALLNAME:TARGET%는
	IF 知識素質:性知識 < 0
		PRINTFORM %CALLNAME:PLAYER%에게 시키는 대로
	ENDIF
	PRINTFORM %腹部描写%를 들이대고, 짓누르듯이 %CALLNAME:PLAYER%의 페니스에 복부를 비비고 있다…
ENDIF
