;-------------------------------------------------
;上半身タイツ脱衣
;------------------------------------------------
@COMNAME202
#FUNCTIONS
IF TEQUIP:上半身服あり || (EQUIP:上下一体タイツ && (TEQUIP:下腹部後鎧あり || TEQUIP:下腹部前鎧あり || (TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可)))
	LOCALS = 찢다
ELSE
	LOCALS = 벗기다
ENDIF

IF TEQUIP:上下一体タイツ
	TSTR:コマンド名受渡 = 타이츠를%LOCALS%
ELSE
	TSTR:コマンド名受渡 = 이너를%LOCALS%
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_202
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
SIF CFLAG:TARGET:睡眠
	TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ")
睡眠中許可 = 1
オートコマンド許可 = 3

@COM_TOOLTIP_202
IF TEQUIP:上半身服あり || (EQUIP:上下一体タイツ && (TEQUIP:下腹部後鎧あり || TEQUIP:下腹部前鎧あり || (TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可)))
	LOCALS:1 = 찢다
	LOCALS:2 = 찢다
ELSE
	LOCALS:1 = 벗기다
	LOCALS:2 = 벗기다
ENDIF
LOCALS = <br>■이너를%LOCALS:1%<br>
LOCALS += @"상대의 상반신 이너를%LOCALS:2% 명령. <br>상하 일체형 타이츠의 경우, [타이츠를%LOCALS:1%]에 합류한다."
LOCALS += "<br>COM 타입: 우후후<br>획득 소스: 없음"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM202
;口上出力のため実際の脱衣処理はEVENTCOMENDで

;オートコマンドだけここでやる
IF TFLAG:オートコマンドフラグ
	RFLAG:オートコマンド脱衣用フラグ = 1
	IF TEQUIP:上半身服あり || (EQUIP:上下一体タイツ && (TEQUIP:下腹部後鎧あり || TEQUIP:下腹部前鎧あり || (TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可)))
		TEQUIP:上タイツ破れ = 1
		SIF TEQUIP:上下一体タイツ
			TEQUIP:下タイツ破れ = 1
	ELSE
		TEQUIP:上タイツあり = 0
		TEQUIP:上タイツ破れ = 0
		;上下一体の場合ここで下も脱がす
		IF TEQUIP:上下一体タイツ
			TEQUIP:下タイツあり = 0
			TEQUIP:下タイツ破れ = 0
			TEQUIP:上下一体タイツ = 0
		ENDIF
	ENDIF
ENDIF
;野外
IF BEDROOM(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		RFLAG:コマンド結果受渡し変数 = 1
		RCVAR:TARGET:露出コマンドフラグ = 1
	ENDIF
ENDIF

RETURN 1


@CAN_COM202(ARG)
#DIM CONST 必要実行値 = 18
#DIM 実行値
#DIM ＋表示フラグ

SIF RFLAG:指示モード対象あなたフラグ
	RETURN 1

SKIPDISP ARG
IF TEQUIP:上半身服あり || (EQUIP:上下一体タイツ && (TEQUIP:下腹部後鎧あり || TEQUIP:下腹部前鎧あり || (TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可)))
	LOCALS = 찢기
ELSE
	LOCALS = 탈의
ENDIF
IF TEQUIP:上下一体タイツ
	PRINTFORML 타이츠%LOCALS%(%CALLNAME:TARGET%)
ELSE
	PRINTFORML 이너%LOCALS%(%CALLNAME:TARGET%)
ENDIF
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:露出癖
IF ABL:露出癖
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:露出癖 * 10
	PRINTFORM 노출증LV{ABL:露出癖}({ABL:露出癖 * 10})
	＋表示フラグ = 1
ENDIF
IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 쾌락 각인LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 10)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 10) * 10
	PRINTFORM 발정LV{GETPALAMLV(PALAM:欲情, 10)}({GETPALAMLV(PALAM:欲情, 10) * 10})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 1
	PRINT 수줍음(1)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 5
	PRINT 남자 기피(5)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 연모(5)
	＋表示フラグ = 1
ENDIF
;セフレ
IF TALENT:セフレ
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 3
	PRINT 섹프(3)
	＋表示フラグ = 1
ENDIF
;野外
IF BEDROOM(CFLAG:現在マップ種別, CFLAG:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		PRINT  - 
		実行値 -= 5
		PRINT 야외 노출(5)
		＋表示フラグ = 1
	ENDIF
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 당신 뜻대로(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(18以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 실행값{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE202
#DIM 下半身側脱衣フラグ, 1

;上半身タイツ脱衣
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(202)
	RETURN RESULT
;脱衣不可
SIF TALENT:脱衣不可
	RETURN 0
;既に着ていない
SIF !TEQUIP:上タイツあり
	RETURN 0
;鎧があるとダメ
SIF TEQUIP:上半身鎧あり
	RETURN 0

下半身側脱衣フラグ = 0
IF TEQUIP:上下一体タイツ
	TFLAG:コマンド条件制御用 = 1
	CALL COM_ABLE205
	TFLAG:コマンド条件制御用 = 0
	下半身側脱衣フラグ = RESULT
ENDIF

;破く判定
IF TEQUIP:上半身服あり
	SIF (!TEQUIP:手差し込み可 && !TEQUIP:はだけ可 && !TEQUIP:パイズリ可 && !下半身側脱衣フラグ) || TEQUIP:上タイツ破れ
		RETURN 0
;脱がせる判定
ELSE
	;指を何かに使ってるなら無理…と思ったけど腕部分ありなしどちらもあるかもなのでスルーに
	;SIF TEQUIP:TARGET:指２
	;	RETURN 0
	;胸をニプルキャップ以外で使ってると駄目
	SIF TEQUIP:TARGET:胸 && !MODE_存在判定_ターゲット側("ニプルキャップ", TARGET)
		RETURN 0
	;上下一体だと下半身も見る
	IF TEQUIP:上下一体タイツ
		SIF !下半身側脱衣フラグ
			RETURN 0
	ELSE
		;口を何かに使ってるなら無理
		SIF TEQUIP:TARGET:口
			RETURN 0
	ENDIF
ENDIF
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM202
IF TEQUIP:上半身服あり || (EQUIP:上下一体タイツ && (TEQUIP:下腹部後鎧あり || TEQUIP:下腹部前鎧あり || (TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可)))
	LOCALS = 찢었다
	LOCALS:1 = 찢겼다
ELSE
	LOCALS = 벗겼다
	LOCALS:1 = 벗었다
ENDIF
PRINTFORM %CALLNAME:PLAYER%는
IF TEQUIP:上下一体タイツ
	PRINTFORML %CALLNAME:TARGET%의 타이츠를 %LOCALS%…
ELSE
	PRINTFORML %CALLNAME:TARGET%의 속옷을 %LOCALS%…
ENDIF

IF RFLAG:コマンド結果受渡し変数
	PRINTFORML 다른 사람의 눈이 있는 곳에서 %LOCALS:1%한 탓인지, %CALLNAME:TARGET%는 어딘가 불안해 보인다…
ENDIF


