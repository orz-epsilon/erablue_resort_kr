;-------------------------------------------------
;キス
;コミュニケーション系コマンド、レベル1
;-------------------------------------------------
@COMNAME20
#FUNCTIONS
TSTR:コマンド名受渡 = 키스
IF GETBIT(FLAG:モード管理, モードビット_同時)
	TSTR:コマンド名受渡 = 번갈아 키스
ELSEIF TCVAR:PLAYER:前ターン被射精場所 == 12 || TCVAR:TARGET:前ターン被射精場所 == 12
	TSTR:コマンド名受渡 = 정액 키스
ENDIF


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_20
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ_被挿入中_セクハラ")
;弱点コマンドカテゴリ = S
同時モード許可 = 3
同時モード拡張許可 = 1
睡眠中許可 = 1
SIF TALENT:PLAYER:キス嗜好 || TALENT:TARGET:キス嗜好
	連続オート許可 = 1



@COM_TOOLTIP_20
LOCALS = <br>■키스<br>
LOCALS += "상대와 입술을 맞대는 커맨드. 키스 모드로 이행. <br>[혀놀림][고양이혀]에 따라 효과가 증감."
LOCALS += "<br>COM 타입: 우후후or피삽입중or성희롱<br>획득 소스: 노출・접촉・쾌S・불결・고통(일정 조건 시)"
LOCALS += "<br>고유 획득 경험: 키스 경험(PLAYER＆TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM20
;-------------------------------------------------
;モードの設定
;-------------------------------------------------
CALL MODE_CHANGE("キス", PLAYER, TARGET)
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;奉仕コマンドフラグ
RCVAR:TARGET:奉仕コマンドフラグ = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

IF !CFLAG:睡眠
	CALL SOURCE_CALC_露出(TARGET, PLAYER, 30)
	CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)
	CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 100)
	CALL SOURCE_CALC_不潔(TARGET, PLAYER, 50)

	IF TCVAR:PLAYER:前ターン被射精場所 == 12 && TCVAR:TARGET:前ターン被射精場所 == 12
	ELSEIF TCVAR:PLAYER:前ターン被射精場所 == 12
		CALL SOURCE_CALC_不潔(TARGET, PLAYER, 100)
	ELSEIF TCVAR:TARGET:前ターン被射精場所 == 12
		CALL SOURCE_CALC_不潔(PLAYER, TARGET, 100)
	ENDIF

	;舌を使う調教では、調教者の舌使いによって、快感のソース増加
	IF TALENT:PLAYER:舌使い
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
	ENDIF

	IF TALENT:TARGET:舌使い
		CALL SOURCE_CALC_快Ｓ(PLAYER, TARGET, 150)
	ENDIF

	;猫舌では減少
	SIF TALENT:PLAYER:猫舌
		CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 20)

	;同性愛
	SIF 同性愛性癖判定(TARGET,PLAYER)
		EXP:TARGET:同性愛経験 += 3
	SIF 同性愛性癖判定(PLAYER,TARGET)
		EXP:PLAYER:同性愛経験 += 3

	SIF TALENT:恋慕
		EXP:愛情経験 += 1

	SIF TALENT:TARGET:猫舌
		EXP:嗜虐快楽経験 += 1
ENDIF
CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 150)
CALL SOURCE_CALC_快Ｓ(PLAYER, TARGET, 150)
IF TALENT:PLAYER:舌使い
	CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 150)
ENDIF


;-------------------------------------------------
;経験の処理
;-------------------------------------------------

EXP:キス経験 ++
EXP:PLAYER:キス経験 ++

CALL 初体験日登録処理(PLAYER, TARGET, "ファーストキス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 1)
IF RESULT
	RCVAR:PLAYER:ファーストキス中フラグ = 1
	CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:TARGET%의 [입술]에 첫 키스를 바쳤다.</font><img src='えっちハート'>","うふふ")
ENDIF
CALL 初体験日登録処理(PLAYER, TARGET, "キス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 0)

CALL 初体験日登録処理(TARGET, PLAYER, "ファーストキス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 1)
IF RESULT
	RCVAR:TARGET:ファーストキス中フラグ = 1
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:PLAYER%의 [입술]에 첫 키스를 바쳤다.</font><img src='えっちハート'>","うふふ")
ENDIF
CALL 初体験日登録処理(TARGET, PLAYER, "キス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 0)


RETURN 1

@CAN_COM20(ARG)
#DIM CONST 必要実行値 = 15
#DIM 実行値
#DIM ＋表示フラグ

SKIPDISP ARG
PRINTFORML 키스한다(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:欲望
IF ABL:欲望
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:欲望 * 1
	PRINTFORM 욕망LV{ABL:欲望}({ABL:欲望 * 1})
	＋表示フラグ = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:奉仕精神 * 4
	PRINTFORM 봉사정신LV{ABL:奉仕精神}({ABL:奉仕精神 * 4})
	＋表示フラグ = 1
ENDIF

IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 2
	PRINTFORM 쾌락각인LV{MARK:快楽刻印}({MARK:快楽刻印 * 2})
	＋表示フラグ = 1
ENDIF

;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 5)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 5) * 1
	PRINTFORM 욕정LV{GETPALAMLV(PALAM:欲情, 5)}({GETPALAMLV(PALAM:欲情, 5) * 1})
	＋表示フラグ = 1
ENDIF

;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 1
	PRINT 부끄러움(1)
	＋表示フラグ = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭耐性 > 0
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 1
	PRINT 악취 둔감(1)
	＋表示フラグ = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭耐性 < 0
	PRINT  - 
	実行値 -= 1
	PRINT 악취 민감(1)
	＋表示フラグ = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 6
	PRINT 헌신적(6)
	＋表示フラグ = 1
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感応答 < 0 && !TALENT:淫乱
	PRINT  - 
	実行値 -= 1
	PRINT 쾌감 부정(1)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  +
	実行値 += 5
	PRINT 연모(5)
	＋表示フラグ = 1
ENDIF
;親愛
IF TALENT:恋慕 > 1
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 10
	PRINT 친애(10)
	＋表示フラグ = 1
ENDIF
;キス嗜好
IF TALENT:キス嗜好 == 1
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 10
	PRINT 키스 선호(10)
	＋表示フラグ = 1
ELSEIF TALENT:キス嗜好 == 2
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 키스 마니아(999)
	＋表示フラグ = 1
ENDIF
;睡眠中
IF CFLAG:睡眠
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 수면 중(999)
	＋表示フラグ = 1
ENDIF

;初吻
IF 知識素質:性知識 > 0 && 初体験済みチェック("ファーストキス",TARGET) == 0
	SIF ＋表示フラグ
 		PRINT  - 
 	実行値 -= 25
 	PRINT 첫 키스(25)
 ＋表示フラグ = 1
ENDIF
;初吻
IF 知識素質:性知識 == 0 && 初体験済みチェック("ファーストキス",TARGET) == 0
SIF ＋表示フラグ
 PRINT  - 
 実行値 -= 20
 PRINT 첫 키스(20)
 ＋表示フラグ = 1
ENDIF
;初吻
IF 知識素質:性知識 < 0 && 初体験済みチェック("ファーストキス",TARGET) == 0
SIF ＋表示フラグ
 PRINT  - 
 実行値 -= 10
 PRINT 첫 키스(10)
 ＋表示フラグ = 1
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 당신 말대로(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(15以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 실행값{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE20
;キスする実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(20)
	RETURN RESULT
;ボールギャグ使用中は不可
; SIF TEQUIP:ボールギャグ
; 	RETURN 0
;口を何かに使ってるなら無理
IF TEQUIP:PLAYER:口
	RETURN 0
ENDIF
SIF TEQUIP:TARGET:口
	RETURN 0
;キス魔は猫舌無視
SIF TALENT:TARGET:キス嗜好 == 2
	RETURN 1
;猫舌の調教者の場合は技巧Lv2以上必要
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	RETURN 0

;オートコマンド中の制限
IF FLAG:オートコマンド判定中フラグ
	SIF 初体験済みチェック("ファーストキス", PLAYER) == 0
		RETURN 0
ENDIF

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM20

#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%는
IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT ,
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT 와 번갈아 입술을 포갰다…
	RETURN 0
ELSEIF TCVAR:PLAYER:前ターン被射精場所 == 12
	PRINTFORM 자신의 입에 정액을 남긴 채
ELSEIF TCVAR:TARGET:前ターン被射精場所 == 12
	PRINTFORM 상대방의 입에 남은 정액도 신경 쓰지 않고
ENDIF
IF CFLAG:睡眠
	PRINT 잠들어 있는
ENDIF

PRINTFORML %CALLNAME:TARGET%와 입술을 포갰다…


SIF TCVAR:TARGET:泥酔眠姦 && TCVAR:TARGET:言いなり
	PRINTFORM 만취해서 %CALLNAME:PLAYER%를 연인으로 착각하고 있는 %CALLNAME:TARGET%는, %TEXTR("기쁜 듯이/적극적으로")%%CALLNAME:PLAYER%의 입술을 원하고 있다…

