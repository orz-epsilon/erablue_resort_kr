;-------------------------------------------------
;パンツ脱衣
;-------------------------------------------------
@COMNAME207
#FUNCTIONS
IF TEQUIP:下半身下着あり == 2
	TSTR:コマンド名受渡 = 앞치마를 벗기다
ELSEIF TEQUIP:下半身下着あり == 3
	TSTR:コマンド名受渡 = 훈도시를 벗기다
ELSEIF TEQUIP:下半身下着あり == 4
	TSTR:コマンド名受渡 = 리본을 풀다
ELSEIF TEQUIP:下半身下着あり == -2
	TSTR:コマンド名受渡 = 성기 피어스를 빼다
ELSEIF TEQUIP:下半身下着あり == -3
	TSTR:コマンド名受渡 = 클리토리스에서 명찰을 떼다
ELSE
	TSTR:コマンド名受渡 = 속옷을 벗기다
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_207
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
SIF CFLAG:TARGET:睡眠
	TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ強")
睡眠中許可 = 1
オートコマンド許可 = 3

@COM_TOOLTIP_207
LOCALS = <br>■속옷을 벗기다<br>
LOCALS += "상대의 하반신 속옷을 벗기는 커맨드.<br>하반신 속옷이 앞치마인 경우, [앞치마를 벗기다]로 변화.<br>하반신 속옷이 훈도시인 경우, [훈도시를 벗기다]로 변화.<br>하반신 속옷 빼내기 가능인 경우, 하반신 옷이 있어도 실행할 수 있다."
LOCALS += "<br>COM 타입: 우후후<br>획득 소스: 없음"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM207
;口上出力のため実際の脱衣処理はEVENTCOMENDで

;オートコマンドだけここでやる
IF TFLAG:オートコマンドフラグ
	RFLAG:オートコマンド脱衣用フラグ = 1
	TEQUIP:下半身下着あり = 0
ENDIF

;野外
IF BEDROOM(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		RFLAG:コマンド結果受渡し変数 = 1
		RCVAR:TARGET:露出コマンドフラグ = 1
	ENDIF
ENDIF

RETURN 1


@CAN_COM207(ARG)
#DIM CONST 必要実行値 = 20
#DIM 実行値
#DIM ＋表示フラグ

SIF RFLAG:指示モード対象あなたフラグ
	RETURN 1

SKIPDISP ARG
PRINTFORML 하반신 속옷 탈의(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:露出癖
IF ABL:露出癖
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:露出癖 * 10
	PRINTFORM 노출벽 LV{ABL:露出癖}({ABL:露出癖 * 10})
	＋表示フラグ = 1
ENDIF
IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 쾌락 각인 LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 10)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 10) * 5
	PRINTFORM 욕정 LV{GETPALAMLV(PALAM:欲情, 10)}({GETPALAMLV(PALAM:欲情, 10) * 5})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 1
	PRINT 수줍음(1)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 5
	PRINT 남성 혐오(5)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 연모(5)
	＋表示フラグ = 1
ENDIF
;セフレ
IF TALENT:セフレ
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 3
	PRINT 섹스 프렌드(3)
	＋表示フラグ = 1
ENDIF
;野外
IF BEDROOM(CFLAG:現在マップ種別, CFLAG:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		PRINT  - 
		実行値 -= 10
		PRINT 야외 노출(10)
		＋表示フラグ = 1
	ENDIF
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 당신이 시키는 대로(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(20以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 실행값{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE207
;下半身下着脱衣
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(207)
	RETURN RESULT
;脱衣不可
SIF TALENT:脱衣不可
	RETURN 0
;既に着ていない
SIF !TEQUIP:下半身下着あり
	RETURN 0
;服がある
IF TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可
	;前張りならめくりorずらし可でOK
	SIF TEQUIP:下半身下着あり != 2 || (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
		RETURN 0
ENDIF
;タイツがある
SIF TEQUIP:下タイツあり
	RETURN 0
;バイブ以外で膣を使ってるなら無理
SIF TEQUIP:TARGET:膣 && !MODE_存在判定_ターゲット側("バイブ", TARGET)
	RETURN 0
;前張り以外
IF TEQUIP:下半身下着あり != 2
	;バイブとビーズ以外でアナルを使ってるなら無理
	SIF TEQUIP:TARGET:アナル && !MODE_存在判定_ターゲット側("アナルバイブ", TARGET) && !MODE_存在判定_ターゲット側("アナルビーズ", TARGET)
		RETURN 0
	;ペニスを使ってると駄目
	SIF TEQUIP:TARGET:ペニス
		RETURN 0
	;足コキ中は駄目
	SIF MODE_存在判定_プレイヤー側("足コキ", TARGET)
		RETURN 0
ENDIF

RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM207
PRINTFORM %CALLNAME:PLAYER%는
IF TEQUIP:下半身下着あり == 2
	PRINTFORML %CALLNAME:TARGET%의 앞가리개를 벗겼다…
ELSEIF TEQUIP:下半身下着あり == 3
	PRINTFORML %CALLNAME:TARGET%의 훈도시를 벗겼다…
ELSEIF TEQUIP:下半身下着あり == 4
	PRINTFORML %CALLNAME:TARGET%의 리본 래핑을 풀었다…
ELSEIF TEQUIP:下半身下着あり == -2
	PRINTFORML %CALLNAME:TARGET%의 성기 장식 피어스를 빼냈다…
ELSEIF TEQUIP:下半身下着あり == -3
	PRINTFORML %CALLNAME:TARGET%의 클리토리스에서 음란한 표기가 적힌 명찰을 떼어냈다…
ELSE
	PRINTFORML %CALLNAME:TARGET%의 속옷을 벗겼다…
ENDIF

IF RFLAG:コマンド結果受渡し変数
	PRINTFORML 남들의 시선이 있는 곳에서 벗은 탓에, %CALLNAME:TARGET%은/는 어딘가 불안해하는 듯하다…
ENDIF

