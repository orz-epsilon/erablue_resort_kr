;-------------------------------------------------
;自慰
;愛撫系コマンド、レベル3
;-------------------------------------------------
@COMNAME9
#FUNCTIONS
TSTR:コマンド名受渡 = 자위

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_9
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")

@COM_TOOLTIP_9
LOCALS = <br>■자위<br>
LOCALS += "상대에게 그 자리에서 자위를 시키는 커맨드. <br>자위 모드로 이행하여, 파생 커맨드로 자위 상세 지시를 내릴 수 있다."
LOCALS += "<br>COM 타입: 우후후<br>획득 소스: 선택지에 따라 다름"
LOCALS += "<br>고유 획득 경험: 자위 경험・조교 자위 경험<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM9
#DIM 選択先保存
#DIMS 選択肢リスト, 100
#DIM 選択肢数
#DIM ループ用
;自慰

IF RFLAG:サイドボタン受渡し変数
	;サイドボタンから来た時は選択処理挟まず直接コマンド実行に飛ぶ
	選択先保存 = RFLAG:サイドボタン受渡し変数
ELSE
	;まずは選択肢のリストを作る
	CALL 自慰選択肢リスト(選択肢リスト)
	選択肢数 = RESULT

	DRAWLINE
	PRINTL 어떤 자위를 할까요?
	DRAWLINE
	PRINTFORML ■%CALLNAME:PLAYER%
	自慰ターゲット保存 = PLAYER
	FOR ループ用, 0, 選択肢数
		CALLFORM 自慰_ABLE_%選択肢リスト:ループ用%
		IF RESULT
			PRINTBUTTON @"[{ループ用}] %選択肢リスト:ループ用%", ループ用
			PRINTL 
		ENDIF
	NEXT
	PRINTL 
	PRINTFORML ■%CALLNAME:TARGET%
	自慰ターゲット保存 = TARGET
	FOR ループ用, 0, 選択肢数
		CALLFORM 自慰_ABLE_%選択肢リスト:ループ用%
		IF RESULT
			PRINTBUTTON @"[{ループ用 + 200}] %選択肢リスト:ループ用%", ループ用 + 200
			PRINTL 
		ENDIF
	NEXT
	PRINTL 
	PRINTBUTTON "[999] 돌아가기", 999
	PRINTL 
	DRAWLINE
	BINPUT
	選択先保存 = RESULT
	SELECTCASE 選択先保存
		CASE 999
			RETURN -1
		CASE 0 TO 99
			自慰ターゲット保存 = PLAYER
			CALLFORM 自慰_ABLE_%選択肢リスト:選択先保存%
			選択先保存 = RESULT
		CASE 200 TO 299
			自慰ターゲット保存 = TARGET
			CALLFORM 自慰_ABLE_%選択肢リスト:(選択先保存 - 200)%
			選択先保存 = RESULT
	ENDSELECT
ENDIF

;COM実行処理
;弱点コマンド処理の都合上、CALL_COMを経由させる
RSTR:SELECTCOM_S受け渡し = 9_{選択先保存}
SELECTCOM_S = %RSTR:SELECTCOM_S受け渡し%
CALL CALL_COM(自慰ターゲット保存)
RETURN RESULT

;自慰の選択肢をソートした状態で渡すための関数
@自慰選択肢リスト(選択肢リスト変数)
#DIM CONST 関数接頭辞長さ = STRLENSU("自慰_ABLE_")
#DIMS REF 選択肢リスト変数, 0
#DIM 選択肢数
#DIMS 選択肢名
#DIM 関数総数
#DIM ループ用

#DIMS CONST 選択先順番制御 = "クリオナ"

VARSET 選択肢リスト変数
選択肢数 = 0
FOR ループ用, 0, VARSIZE("選択先順番制御")
	SIF EXISTFUNCTION(@"自慰_ABLE_%選択先順番制御:ループ用%") == 1
		選択肢リスト変数:(選択肢数++) '= 選択先順番制御:ループ用
NEXT

VARSET RESULTS
関数総数 = ENUMFUNCBEGINSWITH("自慰_ABLE_")
FOR ループ用, 0, 関数総数
	選択肢名 '= SUBSTRINGU(RESULTS:ループ用, 関数接頭辞長さ, -1)
	SIF MATCH(選択先順番制御, 選択肢名)
		CONTINUE
	選択肢リスト変数:(選択肢数++) '= 選択肢名
NEXT

RETURN 選択肢数

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE9
;自慰実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(9)
	RETURN RESULT
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM9
CALLFORM MESSAGE_COM_S%RSTR:SELECTCOM_S受け渡し%

@MESSAGE_COM9_非口上時
CALLFORM MESSAGE_COM_S%RSTR:SELECTCOM_S受け渡し%_非口上時
