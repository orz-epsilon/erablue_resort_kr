;-------------------------------------------------
;乳首責め
;愛撫系コマンド、レベル1
;-------------------------------------------------
@COMNAME7
#FUNCTIONS
IF TALENT:陥没乳首 && !TCVAR:陥没乳首露出フラグ
	TSTR:コマンド名受渡 = 젖꼭지 파기
	SIF GETBIT(FLAG:モード管理, モードビット_同時)
		TSTR:コマンド名受渡 = 동시에 젖꼭지 파기
ELSE
	TSTR:コマンド名受渡 = 젖꼭지 괴롭히기
	SIF GETBIT(FLAG:モード管理, モードビット_同時)
		TSTR:コマンド名受渡 = 동시에 젖꼭지 괴롭히기
ENDIF



;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_7
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ_被挿入中")
弱点コマンドカテゴリ = B
同時モード許可 = 2
同時モード拡張許可 = 1
オートコマンド許可 = 1
連続オート許可 = 1

@COM_TOOLTIP_7
LOCALS = <br>■유두 괴롭힘<br>
LOCALS += "상대의 젖꼭지를 괴롭히고 애무하는 커맨드."
LOCALS += "<br>COM타입: 우후후or피삽입중<br>획득 소스: 쾌B・노출・접촉・고통"
LOCALS += "<br>고유 획득 경험: 젖꼭지 개발(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM7
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

RCVAR:乳首開発フラグ = 2

CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 120)

;勃起直後であるパターンの時、フラグを確保
SIF TCVAR:陥没乳首露出フラグ == 10
	RFLAG:コマンド結果受渡し変数 = 1

SIF TALENT:PLAYER:器用な指
	CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 50)

CALL SOURCE_CALC_露出(TARGET, PLAYER, 60)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 60)
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 20)
;もうひとつの実行キャラの指と対象キャラの胸が空いていたなら両手で愛撫した扱いで快Ｂにボーナス
SIF (TEQUIP:PLAYER:指 || TEQUIP:PLAYER:指２) && (TEQUIP:TARGET:胸 || TEQUIP:TARGET:胸２)
	CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 40)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 5
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 5
;手淫経験
EXP:PLAYER:手淫経験 += 1

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE7
;乳首責め
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(7)
	RETURN RESULT
;Bがない場合駄目
SIF TALENT:TARGET:Ｂ感度 == -2
	RETURN 0
	
;上半身着衣
;鎧、差し込めないorはだけない服、タイツ、ブラ等があると無理
SIF TEQUIP:上半身鎧あり
	RETURN 0
SIF TEQUIP:上半身服あり && (!TEQUIP:手差し込み可 && !TEQUIP:はだけ可)
	RETURN 0
SIF TEQUIP:上タイツあり && !TEQUIP:上タイツ破れ
	RETURN 0
SIF TEQUIP:上半身下着あり > 0
	RETURN 0
IF !MODE_存在判定_完全一致("授乳手コキ", TARGET, PLAYER) && !MODE_存在判定_完全一致("乳首吸い", PLAYER, TARGET)
	;指を何かに使ってるなら無理
	SIF TEQUIP:PLAYER:指
		RETURN 0
	;胸を何かに使ってるなら無理（パイズリ中は許可）
	IF !MODE_存在判定_プレイヤー側("パイズリ系", TARGET)
		SIF TEQUIP:TARGET:胸
			RETURN 0
	ENDIF
	;胸に届かないなら無理
	CALL MODE_部位届く判定(部位_胸, PLAYER, TARGET)
	SIF RESULT == 0
		RETURN 0
ENDIF

;オートコマンド中の制限
SIF FLAG:オートコマンド判定中フラグ && TALENT:TARGET:性別 == 2 &&  OPTION変数:男性への愛撫禁止
	RETURN 0	

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM7
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%는
IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT ,
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT 의 끝부분의 돌기를 손가락으로 굴렸다…
	RETURN 0
ENDIF

IF MODE_存在判定_順不同("ＶＡ挿入系", PLAYER, TARGET)
	PRINTFORM %CALLNAME:TARGET%와 이어진 채 %OPPAI_DESCRIPTION(TARGET)%
ELSE
	PRINTFORM %CALLNAME:TARGET%의 %OPPAI_DESCRIPTION(TARGET)%
ENDIF
SELECTCASE TALENT:陥没乳首
	CASE 0
		;欲情のPALAMLV3以上
		IF PALAM:欲情 >= PALAMLV:3
			PRINT 의 끝부분의, 완전히 단단해진 돌기를
			IF MODE_存在判定_完全一致("授乳手コキ", TARGET, PLAYER) || MODE_存在判定_完全一致("乳首吸い", PLAYER, TARGET)
				PRINTL 혀로 굴렸다…
			ELSEIF TEQUIP:上半身下着あり == -2 && RAND:3
				PRINTL 장식된 피어스째로 쥐어짜 올렸다…
			ELSEIF TEQUIP:上半身下着あり == -3 && RAND:3
				PRINTL 장식된 명찰을 잡아당겨, 괴롭혔다…
			ELSE
				PRINTL 집어 올렸다…
			ENDIF
		ELSE
			PRINT 의 끝부분의, 아직 부드러운 돌기를
			IF MODE_存在判定_完全一致("授乳手コキ", TARGET, PLAYER) || MODE_存在判定_完全一致("乳首吸い", PLAYER, TARGET)
				PRINTL 혀로 굴렸다…
			ELSEIF TEQUIP:上半身下着あり == -2 && RAND:3
				PRINTL 장식된 피어스째로 쥐어짜 올렸다…
			ELSEIF TEQUIP:上半身下着あり == -3 && RAND:3
				PRINTL 장식된 명찰을 잡아당겨, 괴롭혔다…
			ELSE
				PRINTL 집어 올렸다…
			ENDIF
		ENDIF
	CASE 1
		IF !TCVAR:陥没乳首露出フラグ
			PRINT 의 움푹 들어간 곳에 손가락을 넣어, 숨겨진 유두를
			SIF MODE_存在判定_完全一致("授乳手コキ", TARGET, PLAYER) || MODE_存在判定_完全一致("乳首吸い", PLAYER, TARGET)
				PRINT 혀로
			PRINTL 후벼 파고, 주물렀다…
		ELSEIF RFLAG:コマンド結果受渡し変数
			;勃起直後パターン
			PRINT 의 움푹 들어간 곳에
			IF MODE_存在判定_完全一致("授乳手コキ", TARGET, PLAYER) || MODE_存在判定_完全一致("乳首吸い", PLAYER, TARGET)
				PRINT 혀
			ELSE
				PRINT 손가락
			ENDIF
			PRINTL 을 넣어 휘젓자,
			PRINTFORML 딱딱하게 굳은 %CALLNAME:TARGET%의 젖꼭지가 밖으로 모습을 드러냈다
		ELSE
			PRINT 끝에 달린, 완전히 딱딱해진 돌기를
			IF MODE_存在判定_完全一致("授乳手コキ", TARGET, PLAYER) || MODE_存在判定_完全一致("乳首吸い", PLAYER, TARGET)
				PRINTL 혀로 굴렸다…
			ELSEIF TEQUIP:上半身下着あり == -2 && RAND:3
				PRINTL 장식된 피어스째로 쥐어짜 올렸다…
			ELSEIF TEQUIP:上半身下着あり == -3 && RAND:3
				PRINTL 꾸며진 명찰을 잡아당겨, 괴롭혀 주었다…
			ELSE
				PRINTL 집어 올렸다…
			ENDIF
		ENDIF
ENDSELECT

