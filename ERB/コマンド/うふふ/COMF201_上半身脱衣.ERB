;-------------------------------------------------
;上半身脱衣
;-------------------------------------------------
@COMNAME201
#FUNCTIONS
IF TEQUIP:上下一体服
	TSTR:コマンド名受渡 = 옷을 벗기다
ELSE
	TSTR:コマンド名受渡 = 상의를 벗기다
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_201
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
SIF CFLAG:TARGET:睡眠
	TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ")
睡眠中許可 = 1
オートコマンド許可 = 3

@COM_TOOLTIP_201
LOCALS = <br>■上の服を脱がす<br>
LOCALS += "상대의 상반신 옷을 벗기는 커맨드。<br>상하 일체형 옷의 경우, [옷을 벗기다]로 변화한다。"
LOCALS += "<br>COM 타입: 우후후<br>획득 소스: 없음"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM201
;口上出力のため実際の脱衣処理はEVENTCOMENDで


;オートコマンドだけここでやる
IF TFLAG:オートコマンドフラグ
	RFLAG:オートコマンド脱衣用フラグ = 1
	TEQUIP:上半身服あり = 0
	TEQUIP:脇出し = 0
	TEQUIP:へそ出し = 0
	TEQUIP:はだけ可 = 0
	TEQUIP:手差し込み可 = 0
	TEQUIP:パイズリ可 = 0
	TEQUIP:上半身下着抜き取り可 = 0
	;上下一体の場合ここで下も脱がす
	IF TEQUIP:上下一体服
		TEQUIP:下半身服あり = 0
		TEQUIP:スカートめくり可 = 0
		TEQUIP:下半身下着抜き取り可 = 0
		TEQUIP:ずらし可 = 0
		TEQUIP:上下一体服 = 0
	ENDIF
ENDIF
;野外
IF BEDROOM(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		RFLAG:コマンド結果受渡し変数 = 1
		RCVAR:TARGET:露出コマンドフラグ = 1
	ENDIF
ENDIF

RETURN 1


@CAN_COM201(ARG)
#DIM CONST 必要実行値 = 20
#DIM 実行値
#DIM ＋表示フラグ

SIF RFLAG:指示モード対象あなたフラグ
	RETURN 1

SKIPDISP ARG
IF TEQUIP:上下一体服
	PRINTFORML 탈의(%CALLNAME:TARGET%)
ELSE
	PRINTFORML 상반신 탈의(%CALLNAME:TARGET%)
ENDIF
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:露出癖
IF ABL:露出癖
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:露出癖 * 10
	PRINTFORM 노출벽LV{ABL:露出癖}({ABL:露出癖 * 10})
	＋表示フラグ = 1
ENDIF
IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 쾌락 각인LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 10)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 10) * 10
	PRINTFORM 욕정LV{GETPALAMLV(PALAM:欲情, 10)}({GETPALAMLV(PALAM:欲情, 10) * 10})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 1
	PRINT 수줍음(1)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 5
	PRINT 남성 혐오(5)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 연모(5)
	＋表示フラグ = 1
ENDIF
;セフレ
IF TALENT:セフレ
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 3
	PRINT 섹스 프렌드(3)
	＋表示フラグ = 1
ENDIF
;野外
IF BEDROOM(CFLAG:現在マップ種別, CFLAG:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		PRINT  - 
		実行値 -= 10
		PRINT 야외 노출(10)
		＋表示フラグ = 1
	ENDIF
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 당신의 뜻대로(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(20以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 실행값{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE201
;上半身上着脱衣
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(201)
	RETURN RESULT
;脱衣不可
SIF TALENT:脱衣不可
	RETURN 0
;既に着ていない
SIF !TEQUIP:上半身服あり
	RETURN 0
;鎧があるとダメ
SIF TEQUIP:上半身鎧あり
	RETURN 0
;上下一体の場合は下鎧があってもダメ
;IF TEQUIP:上下一体服
;	SIF TEQUIP:下腹部後鎧あり
;		RETURN 0
;	SIF TEQUIP:下腹部前鎧あり
;		RETURN 0
;ENDIF

;指を何かに使ってるなら無理
SIF TEQUIP:TARGET:指２
	RETURN 0
IF !TEQUIP:はだけ可
	;口を何かに使ってるなら無理(下から脱がす上下一体服ならOK)
	SIF TEQUIP:TARGET:口 && (!TEQUIP:上下一体服 || (TEQUIP:上下一体服 && TEQUIP:スカートめくり可))
		RETURN 0
	;胸をニプルキャップ以外で使ってると駄目
	SIF TEQUIP:TARGET:胸 && !MODE_存在判定_ターゲット側("ニプルキャップ", TARGET)
		RETURN 0
ENDIF
;上下一体の場合は下半身も見る
IF TEQUIP:上下一体服
	;めくれる＝ワンピース型なら上から脱がせる？
	IF TEQUIP:スカートめくり可
		SIF TEQUIP:下腹部後鎧あり
			RETURN 0
		SIF TEQUIP:下腹部前鎧あり
			RETURN 0
	ELSE
		TFLAG:コマンド条件制御用 = 1
		CALL COM_ABLE204
		TFLAG:コマンド条件制御用 = 0
		SIF !RESULT
			RETURN 0
	ENDIF
ENDIF
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM201
PRINTFORM %CALLNAME:PLAYER%는
IF TEQUIP:上下一体服
	PRINTFORML %CALLNAME:TARGET%의 옷을 벗겼다…
ELSE
	PRINTFORML %CALLNAME:TARGET%의 상의를 벗겼다…
ENDIF
IF GROUPMATCH(TEQUIP:上半身下着あり, 1, 3, -1)
	IF TALENT:TARGET:バストサイズ == 3
		PRINT 꿀렁!
		PRINT_IMG "えっちハート"
		PRINT 하고, 풍만한 유방이 크게 흔들리며 튀어나왔다
	ELSEIF TALENT:TARGET:バストサイズ == 2
		PRINT 철렁, 하고 커다란 유방이 튀어나왔다
	ENDIF
ENDIF


IF RFLAG:コマンド結果受渡し変数
	PRINTFORML 사람들 눈이 있는 곳에서 벗은 탓에, %CALLNAME:TARGET%은/는 어딘가 불안해하는 듯하다…
ENDIF



