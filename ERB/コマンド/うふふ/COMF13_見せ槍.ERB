;-------------------------------------------------
;見せ槍
;-------------------------------------------------
@COMNAME13
#FUNCTIONS
TSTR:コマンド名受渡 = 성기 노출

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_13
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
オートコマンド許可 = 1

@COM_TOOLTIP_13
LOCALS = <br>■전시용 창<br>
LOCALS += "상대에게 음경을 보여주어 성욕을 부추기는 커맨드."
LOCALS += "<br>COM 타입: 우후후<br>획득 소스: 욕정・굴종・정복"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM13
#DIM 比較値
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

;無知の場合、何も起こらない
IF 知識素質:TARGET:性知識 == -2
	RETURN 1
ENDIF

;基礎
CALL SOURCE_CALC_欲情(TARGET, PLAYER, 100)
CALL SOURCE_CALC_屈従(TARGET, PLAYER, 60)

IF GETBIT(TALENT:TARGET:性別, 1)
	比較値 = TALENT:PLAYER:男性器サイズ - TALENT:TARGET:男性器サイズ
	;-2:こどもちんこ、-1:短小、0:平凡、1:巨根、2:規格外
	IF 比較値 > 0
		CALL SOURCE_CALC_屈従(TARGET, PLAYER, 100)
	ELSEIF 比較値 < 0
		CALL SOURCE_CALC_征服(TARGET, PLAYER, 100)
		CALL SOURCE_CALC_欲情(TARGET, PLAYER, 50)
	ELSE
		CALL SOURCE_CALC_欲情(TARGET, PLAYER, 100)
	ENDIF
ELSE
	比較値 = TALENT:PLAYER:男性器サイズ - MAX(TALENT:TARGET:体格, -2)
	;-3:ハーヴィン並、-2:子供並、-1:小柄、0:普通、1:長身、2:男ドラフ並
	IF 比較値 > 0
		CALL SOURCE_CALC_屈従(TARGET, PLAYER, 100)
	ELSEIF 比較値 < 0
		CALL SOURCE_CALC_征服(TARGET, PLAYER, 100)
		CALL SOURCE_CALC_欲情(TARGET, PLAYER, 50)
	ELSE
		CALL SOURCE_CALC_欲情(TARGET, PLAYER, 100)
	ENDIF
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE13
;見せ槍
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(13)
	RETURN RESULT
;ちんこがない場合駄目
SIF GETBIT(TALENT:PLAYER:性別, 1) == 0 || TALENT:PLAYER:Ｃ感度 == -2
	RETURN 0
;ちんこを何かに使ってるなら無理
SIF TEQUIP:PLAYER:ペニス
	RETURN 0
;勃起必須
SIF BASE:PLAYER:勃起 < 1000
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM13
PRINTFORM %CALLNAME:PLAYER%는 %CALLNAME:TARGET%의 얼굴에 발기한
SELECTCASE TALENT:PLAYER:男性器サイズ
	CASE -2
		PRINT 사랑스러운, 어린아이 같은
	CASE -1
		PRINT 작은 편의
	CASE 0
	CASE 1
		PRINT 남들보다 큰
	CASE 2
		PRINT 규격 외로 거대한
ENDSELECT

PRINTL 음경을 가까이 대고 보여주었다……

SIF 知識素質:性知識 < 0
	PRINTFORM %CALLNAME:TARGET%는 %CALLNAME:PLAYER%가 무엇을 하는지 몰라 어리둥절한 표정을 짓고 있다
