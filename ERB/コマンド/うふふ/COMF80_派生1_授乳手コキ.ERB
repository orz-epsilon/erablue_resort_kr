
@COMNAME_S80_1
#FUNCTIONS
TSTR:コマンド名受渡 = 수유 수음


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_80_1
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
弱点コマンドカテゴリ = B
オートコマンド許可 = 3
反転オート許可 = 3


@COM_TOOLTIP_S80_1
IF RFLAG:奉仕逆転時ツールチップフラグ
	LOCALS = <br>■수유 핸드잡을 하는<br>
	LOCALS += "상대에게 자신의 유두를 빨게 하면서, 상대의 페니스를 손으로 애무하는 커맨드.<br>수유 수음 모드가 되어, 모드를 변경하거나 성행위를 종료할 때까지 지속적으로 특정 소스가 발생한다."
ELSE
	LOCALS = <br>■수유 핸드잡<br>
	LOCALS += "상대의 유두에 달라붙어 빨면서, 상대에게 손으로 자신의 페니스를 애무받는 커맨드.<br>수유 수음 모드가 되어, 모드를 변경하거나 성행위를 종료할 때까지 지속적으로 특정 소스가 발생한다."
ENDIF
LOCALS += "<br>COM 타입: 우후후<br>획득 소스: 쾌B・노출・접촉・봉사・불결"
LOCALS += "<br>고유 획득 경험: 수음 경험(TARGET)・유두 개발(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM_S80_1
;-------------------------------------------------
;モードの設定
;-------------------------------------------------
CALL MODE_DELETE_ターゲット側("手コキ", PLAYER)
SIF MODE_存在判定_完全一致("乳首吸い", PLAYER, TARGET)
	CALL MODE_DELETE_プレイヤー側("乳首吸い", PLAYER)
CALL MODE_CHANGE("授乳手コキ", TARGET, PLAYER)
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;奉仕快楽
RCVAR:TARGET:奉仕コマンドフラグ = 1
RCVAR:TARGET:汎用喘ぎ_文節低減フラグ = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------



RCVAR:乳首開発フラグ = 2

CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 200)
SIF TALENT:TARGET:器用な指
	CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 50)

CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 200)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 20)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
CALL SOURCE_CALC_奉仕(TARGET, PLAYER, 250)


CALL SOURCE_CALC_不潔(TARGET, PLAYER, 100)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;手淫経験
EXP:手淫経験 ++

;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 4
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 4

RETURN 1



@CAN_COM_S80_1(ARG)
#DIM CONST 必要実行値 = 20
#DIM 実行値
#DIM ＋表示フラグ

SKIPDISP ARG
PRINTFORML 수유 핸드잡(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:欲望
IF ABL:欲望
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:欲望 * 1
	PRINTFORM 욕망LV{ABL:欲望}({ABL:欲望 * 1})
	＋表示フラグ = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:奉仕精神 * 4
	PRINTFORM 봉사정신LV{ABL:奉仕精神}({ABL:奉仕精神 * 4})
	＋表示フラグ = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:精液中毒 * 1
	PRINTFORM 정액 중독LV{ABL:精液中毒}({ABL:精液中毒 * 1})
	＋表示フラグ = 1
ENDIF

IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 쾌락 각인LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 5)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 5) * 1
	PRINTFORM 욕정LV{GETPALAMLV(PALAM:欲情, 5)}({GETPALAMLV(PALAM:欲情, 5) * 1})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 1
	PRINT 부끄러움(1)
	＋表示フラグ = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭耐性 > 0
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 1
	PRINT 악취 둔감(1)
	＋表示フラグ = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭耐性 < 0
	PRINT  - 
	実行値 -= 3
	PRINT 악취 민감(3)
	＋表示フラグ = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 6
	PRINT 헌신적(6)
	＋表示フラグ = 1
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感応答 < 0 && !TALENT:淫乱
	PRINT  - 
	実行値 -= 1
	PRINT 쾌감의 부정(1)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 5
	PRINT 남자 혐오(5)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 연모(5)
	＋表示フラグ = 1
ENDIF
;親愛
IF TALENT:恋慕 > 1
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 친애(5)
	＋表示フラグ = 1
ENDIF

;調教者がふたなり
IF (TALENT:PLAYER:性別 & 3) == 3
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 8
	PRINT 후타나리(8)
	＋表示フラグ = 1
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 당신의 뜻대로(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(20以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 실행값{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S80_1
;授乳手コキ実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
SIF GLOBAL_COMABLE_S("80_1")
	RETURN RESULT
;手コキモードじゃない時はアウト
SIF !MODE_存在判定_完全一致("手コキ", TARGET, PLAYER)
	RETURN 0
;Bがない場合駄目
SIF TALENT:TARGET:Ｂ感度 == -2
	RETURN 0
;陥没乳首は乳首露出させてからでないと不可
SIF TALENT:陥没乳首 && !TCVAR:陥没乳首露出フラグ
	RETURN 0
IF !MODE_存在判定_完全一致("乳首吸い", PLAYER, TARGET)
	;口を何かに使ってるなら無理
	SIF TEQUIP:PLAYER:口
		RETURN 0
	;胸を何かに使ってるなら無理
	SIF TEQUIP:TARGET:胸
		RETURN 0
ENDIF
;上半身着衣
;鎧、差し込めないorはだけない服、タイツ、ブラ等があると無理
SIF TEQUIP:上半身鎧あり
	RETURN 0
SIF TEQUIP:上半身服あり && (!TEQUIP:手差し込み可 && !TEQUIP:はだけ可)
	RETURN 0
SIF TEQUIP:上タイツあり && !TEQUIP:上タイツ破れ
	RETURN 0
SIF TEQUIP:上半身下着あり > 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S80_1
PRINTFORM %CALLNAME:TARGET%는
SIF 知識素質:性知識 < 0
	PRINTFORM %CALLNAME:PLAYER%가 시키는 대로,

PRINTFORM %CALLNAME:PLAYER%
IF RAND:2 == 0
	PRINT 를 무릎에 눕히고,
ELSE
	PRINT 몸을 기대어 쓰러지며,
ENDIF
SELECTCASE TALENT:TARGET:バストサイズ
	CASE -2
		PRINT 희미하게 부푼 가슴에 톡 박혀 있는
	CASE -1
		PRINT 작게 부푼
	CASE 1
		PRINT 커다란 가슴의
	CASE 2
		PRINT 풍만하고 부드러운 가슴의
	CASE 3
		PRINT 얼굴이 파묻힐 정도로 커다란 가슴의
ENDSELECT
PRINTL 젖꼭지를 빨게 하면서 페니스에 손가락을 얽어 비벼 올리고 있다…


