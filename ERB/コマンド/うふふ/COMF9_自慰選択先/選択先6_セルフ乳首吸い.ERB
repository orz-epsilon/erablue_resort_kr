
@COMNAME_S9_6
#FUNCTIONS
TSTR:コマンド名受渡 = 셀프 유두 빨기

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_9_6
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
弱点コマンドカテゴリ = B_女
;自慰系オートコマンドは個別に禁止設定を行うため2を入れる
オートコマンド許可 = 2


@COM_TOOLTIP_S9_6
LOCALS = <br>■셀프 유두 빨기<br>
LOCALS += "상대가 보는 앞에서 유두를 입에 머금고 자위하는 커맨드."
LOCALS += "<br>COM 타입: 우후후<br>획득 소스: 노출・쾌B・환락(일정 조건 시)・반감(일정 조건 시)"
LOCALS += "<br>고유 획득 경험: 자위 경험(자위 실행자), 조교 자위 경험(자위 실행자)<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM_S9_6
CALL SOURCE_CALC_快Ｂ(自慰ターゲット保存, -1, 150)
SIF TALENT:自慰ターゲット保存:舌使い
	CALL SOURCE_CALC_快Ｂ(自慰ターゲット保存, -1, 100)
CALL SOURCE_CALC_露出(自慰ターゲット保存, MASTER, 150)

SELECTCASE EXP:自慰ターゲット保存:調教自慰経験
	CASE IS > 50
		CALL SOURCE_CALC_歓楽(自慰ターゲット保存, MASTER, 100)
	CASEELSE
		;回数が少ないと反感
		SIF 自慰ターゲット保存 != MASTER
			CALL SOURCE_CALC_反感(自慰ターゲット保存, MASTER, 200)
ENDSELECT

;露出扱い
RCVAR:自慰ターゲット保存:露出コマンドフラグ = 1
EXP:自慰ターゲット保存:自慰経験 += 1
EXP:自慰ターゲット保存:調教自慰経験 += 1
;モード遷移
CALL MODE_CHANGE("セルフ乳首吸い", MASTER, (自慰ターゲット保存))
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S9_6

IF FLAG:オートコマンド判定中フラグ
	;オート時のみ実行判定を行う
	CALL 自慰_ABLE_セルフ乳首吸い
	RETURN RESULT
ELSE
	;選択から飛んでくるのでABLEは常に0
	RETURN 0
ENDIF

@自慰_ABLE_セルフ乳首吸い
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE_S("9_6")
	RETURN RESULT
;無知
SIF 知識素質:(自慰ターゲット保存):性知識 < 0 && EXP:(自慰ターゲット保存):自慰経験 < 1
	RETURN 0
;爆乳以上
SIF TALENT:(自慰ターゲット保存):バストサイズ < 2
	RETURN 0
;鎧があると駄目
SIF TEQUIP:(自慰ターゲット保存):上半身鎧あり
	RETURN 0
;服駄目
SIF TEQUIP:(自慰ターゲット保存):上半身服あり && !TEQUIP:(自慰ターゲット保存):はだけ可
	RETURN 0
;タイツ駄目
SIF TEQUIP:(自慰ターゲット保存):上タイツあり && !TEQUIP:(自慰ターゲット保存):上タイツ破れ
	RETURN 0
;下着駄目
SIF TEQUIP:(自慰ターゲット保存):上半身下着あり > 0
	RETURN 0
;存在しない場合駄目
SIF TALENT:(自慰ターゲット保存):Ｂ感度 == -2
	RETURN 0
;男の場合は駄目
SIF GETBIT(TALENT:(自慰ターゲット保存):性別, 0) == 0
	RETURN 0
;使用中
SIF TEQUIP:(自慰ターゲット保存):胸
	RETURN 0
SIF TEQUIP:(自慰ターゲット保存):口
	RETURN 0
SIF TEQUIP:(自慰ターゲット保存):指
	RETURN 0
;関数実行のため、派生番号を返す
RETURN 6

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S9_6

PRINTFORM %CALLNAME:(自慰ターゲット保存)%은(는)
IF TEQUIP:(自慰ターゲット保存):上半身服あり
	PRINT 옷에서 가슴을 노출시키고
ENDIF
PRINTL 젖꼭지를 입에 머금고 혀로 자극하기 시작했다…


@MESSAGE_COM_S9_6_非口上時
SIF (自慰ターゲット保存)== MASTER
	RETURN 0
SIF TFLAG:オートコマンドフラグ > 0
	RETURN 0

IF EXP:(自慰ターゲット保存):調教自慰経験 <= 10
	PRINTFORML 누군가에게 보여지면서 하는 자위에는 익숙하지 않은 듯,%CALLNAME:(自慰ターゲット保存)%은(는) 얼굴을 붉히고 %CALLNAME:MASTER%을(를) 노려보고 있다…
ELSEIF EXP:(自慰ターゲット保存):調教自慰経験 <= 50
	PRINTFORML 조금 익숙해진 듯하지만, 아직%CALLNAME:MASTER%의 시선이 신경 쓰이는 듯하다…
ELSE
	PRINTFORML %CALLNAME:(自慰ターゲット保存)%은(는) %CALLNAME:MASTER%에게 보여지면서 하는 자위에, 묘한 고양감을 느끼고 있다…
ENDIF
