
@COMNAME_S9_2
#FUNCTIONS
TSTR:コマンド名受渡 = 클리오나

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_9_2
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
弱点コマンドカテゴリ = C_女
;自慰系オートコマンドは個別に禁止設定を行うため2を入れる
オートコマンド許可 = 2


@COM_TOOLTIP_S9_2
LOCALS = <br>■클리오나<br>
LOCALS += "상대가 보는 앞에서 클리토리스를 손가락으로 문질러 자위하는 커맨드."
LOCALS += "<br>COM 타입：우후후<br>획득 소스：노출・쾌C・환락(일정 조건 시)・반감(일정 조건 시)"
LOCALS += "<br>고유 획득 경험：자위 경험(자위 실행자)、조교 자위 경험(자위 실행자)<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM_S9_2
;共通処理
CALL Ｃ自慰系共通処理
;モード遷移
CALL MODE_CHANGE("クリオナ", MASTER, (自慰ターゲット保存))
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S9_2

IF FLAG:オートコマンド判定中フラグ
	;オート時のみ実行判定を行う
	CALL 自慰_ABLE_クリオナ
	RETURN RESULT
ELSE
	;選択から飛んでくるのでABLEは常に0
	RETURN 0
ENDIF

@自慰_ABLE_クリオナ
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE_S("9_2")
	RETURN RESULT
;無知
SIF 知識素質:(自慰ターゲット保存):性知識 < 0 && EXP:(自慰ターゲット保存):自慰経験 < 1
	RETURN 0
;鎧があると駄目
SIF TEQUIP:(自慰ターゲット保存):下腹部前鎧あり
	RETURN 0
;存在しない場合駄目
SIF TALENT:(自慰ターゲット保存):Ｃ感度 == -2
	RETURN 0
;ペニスがあるとダメ
SIF GETBIT(TALENT:(自慰ターゲット保存):性別, 1)
	RETURN 0
;使用中
SIF TEQUIP:(自慰ターゲット保存):クリトリス
	RETURN 0
SIF TEQUIP:(自慰ターゲット保存):指
	RETURN 0
;関数実行のため、派生番号を返す
RETURN 2

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S9_2

PRINTFORM %CALLNAME:(自慰ターゲット保存)%은(는) 자신의 클리토리스를
IF TEQUIP:(自慰ターゲット保存):下半身服あり && !TEQUIP:(自慰ターゲット保存):スカートめくり可
	PRINT 옷 위로
ELSEIF TEQUIP:(自慰ターゲット保存):下タイツあり && !TEQUIP:(自慰ターゲット保存):下タイツ破れ
	PRINT 타이츠 위로
ELSEIF TEQUIP:(自慰ターゲット保存):下半身下着あり == 2
	PRINT 앞 가림막 위로
ELSEIF TEQUIP:(自慰ターゲット保存):下半身下着あり > 0
	PRINT 속옷 위로
ENDIF
PRINTL 손가락으로 자극하기 시작했다…


@MESSAGE_COM_S9_2_非口上時
SIF (自慰ターゲット保存)== MASTER
	RETURN 0
SIF TFLAG:オートコマンドフラグ > 0
	RETURN 0

IF EXP:(自慰ターゲット保存):調教自慰経験 <= 10
	PRINTFORML 누군가에게 보여지면서 하는 자위에는 익숙하지 않은 듯, %CALLNAME:(自慰ターゲット保存)%은(는) 얼굴을 붉히고 %CALLNAME:MASTER%을(를) 노려보고 있다…
ELSEIF EXP:(自慰ターゲット保存):調教自慰経験 <= 50
	PRINTFORML 조금 익숙해진 듯하지만, 아직 %CALLNAME:MASTER%의 시선이 신경 쓰이는 듯하다…
ELSE
	PRINTFORML %CALLNAME:(自慰ターゲット保存)%은(는) %CALLNAME:MASTER%에게 보여지면서 하는 자위에, 묘한 고양감을 느끼고 있다…
ENDIF
