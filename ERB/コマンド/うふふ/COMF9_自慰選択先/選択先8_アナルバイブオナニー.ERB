
@COMNAME_S9_8
#FUNCTIONS
TSTR:コマンド名受渡 = 아날 바이브 자위

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_9_8
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
;自慰系オートコマンドは個別に禁止設定を行うため2を入れる
オートコマンド許可 = 2
オートコマンド許可 = 12

@COM_TOOLTIP_S9_8
LOCALS = <br>■아날 바이브 자위<br>
LOCALS += "상대가 보는 앞에서 아날 바이브를 삽입하여 자위를 하는 커맨드."
LOCALS += "<br>COM 타입: 우후후<br>획득 소스: 노출・쾌B・환락(일정 조건 시)・반감(일정 조건 시)"
LOCALS += "<br>고유 획득 경험: 자위 경험(자위 실행자), 조교 자위 경험(자위 실행자)<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM_S9_8
;弱点コマンドをバイブと共通させる
LOCAL = 弱点番号算出(PLAYER, "45")
IF LOCAL >= 0 || 追加弱点番号算出(PLAYER, "45") >= 0
	CALL 弱点の欠片取得処理("45")
	TCVAR:PLAYER:弱点コマンドフラグ = 1
	SIF 弱点看破:PLAYER:LOCAL == 0
		弱点看破:PLAYER:LOCAL = 1
ENDIF

;共通処理
CALL Ａ自慰系共通処理
;モード遷移
CALL MODE_CHANGE("アナルバイブ", MASTER, 自慰ターゲット保存)
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S9_8

IF FLAG:オートコマンド判定中フラグ
	;オート時のみ実行判定を行う
	CALL 自慰_ABLE_アナルバイブオナニー
	RETURN RESULT
ELSE
	;選択から飛んでくるのでABLEは常に0
	RETURN 0
ENDIF

@自慰_ABLE_アナルバイブオナニー
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE_S("9_8")
	RETURN RESULT
;無知
SIF 知識素質:(自慰ターゲット保存):性知識 < 0 && EXP:(自慰ターゲット保存):自慰経験 < 1
	RETURN 0
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:5 == 0 && NOITEM == 0
	RETURN 0
;鎧があると駄目
SIF TEQUIP:(自慰ターゲット保存):下腹部後鎧あり
	RETURN 0
;服駄目
SIF TEQUIP:(自慰ターゲット保存):下半身服あり && !TEQUIP:(自慰ターゲット保存):スカートめくり可 && !TEQUIP:(自慰ターゲット保存):ずらし可
	RETURN 0
;タイツ駄目
SIF TEQUIP:(自慰ターゲット保存):下タイツあり && !TEQUIP:(自慰ターゲット保存):下タイツ破れ
	RETURN 0
;存在しない場合駄目
SIF TALENT:(自慰ターゲット保存):Ａ感度 == -2
	RETURN 0
;アナル感覚
SIF ABL:(自慰ターゲット保存):Ａ感覚 < 3
	RETURN 0
;使用中
SIF TEQUIP:(自慰ターゲット保存):アナル
	RETURN 0
SIF TEQUIP:(自慰ターゲット保存):指
	RETURN 0
	
;オートコマンド中の制限
IF FLAG:オートコマンド判定中フラグ
	SIF OPTION変数:玩具プレイ禁止
		RETURN 0
	SIF TALENT:(自慰ターゲット保存):性別 == 2 && OPTION変数:男性への愛撫禁止
		RETURN 0
ENDIF

;関数実行のため、派生番号を返す
RETURN 8

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S9_8

PRINTFORM %CALLNAME:(自慰ターゲット保存)%은(는)
IF TEQUIP:(自慰ターゲット保存):下半身服あり && TEQUIP:(自慰ターゲット保存):ずらし可
	PRINT 옷을 밀어내고
ELSEIF TEQUIP:(自慰ターゲット保存):下半身下着あり > 0
	PRINT 속옷을 밀어내고
ENDIF
PRINTL 애널 바이브를 항문에 넣고, 스위치를 켰다…


@MESSAGE_COM_S9_8_非口上時
SIF (自慰ターゲット保存)== MASTER
	RETURN 0
SIF TFLAG:オートコマンドフラグ > 0
	RETURN 0

IF EXP:(自慰ターゲット保存):調教自慰経験 <= 10
	PRINTFORML 누군가에게 보여지는 자위에는 익숙하지 않은 듯, %CALLNAME:(自慰ターゲット保存)%은(는) 얼굴을 붉히고 %CALLNAME:MASTER%를 노려보고 있다…
ELSEIF EXP:(自慰ターゲット保存):調教自慰経験 <= 50
	PRINTFORML 조금 익숙해진 듯하지만, 아직 %CALLNAME:MASTER%의 시선이 신경 쓰이는 듯하다…
ELSE
	PRINTFORML %CALLNAME:(自慰ターゲット保存)%은(는) %CALLNAME:MASTER%에게 보여지는 자위에, 묘한 고양감을 느끼고 있다…
ENDIF
