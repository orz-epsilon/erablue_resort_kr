;-------------------------------------------------------------------------------------------
;射精先の選択肢
;-------------------------------------------------------------------------------------------
@オート射精先設定
;射精先をいちいち選ばなくてもいいように設定する
DRAWLINE
PRINTL 사정 시, 선택 없이 사정하는 설정을 합니다.
PRINTL 설정된 사정처가 선택 가능한 경우, 자동으로 그 사정처를 선택합니다.
PRINTL 여러 개의 선택 가능한 사정처가 설정된 경우, 우선도 숫자가 작은 쪽이 우선적으로 선택됩니다.
DRAWLINE

FOR LOCAL, 0, 31
	IF オート射精先オプション:LOCAL == -1
		PRINTBUTTON @"[{LOCAL}] 자동 사정처를 설정한다", LOCAL
		PRINTL
		BREAK
	ELSE
		TSTR:コマンド名受渡 = 
		TRYCALLFORM SHOOT_NAME_{オート射精先オプション:LOCAL}(-1)
		SIF TSTR:コマンド名受渡 == ""
			CONTINUE

		PRINTFORM 우선도{LOCAL}：%TSTR:コマンド名受渡, 20, LEFT%　　
		IF LOCAL > 0
			PRINTBUTTON "[↑]", LOCAL + 100
		ELSE
			PRINT     
		ENDIF
		IF LOCAL < 30 && オート射精先オプション:(LOCAL + 1) > -1
			PRINTBUTTON "[↓]", LOCAL + 200
		ELSE
			PRINT     
		ENDIF
		PRINT 　　
		PRINTBUTTON "[변경]", LOCAL
		PRINT   
		PRINTBUTTON "[해제]", LOCAL + 300
		PRINTL
	ENDIF
NEXT
PRINTL
PRINTBUTTON @"[999] 돌아간다", 999
PRINTL
DRAWLINE
BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 999
		RETURN 0
	CASE IS < 100
		DRAWLINE
		FOR LOCAL:1, 0, 31
			TSTR:コマンド名受渡 = 
			TRYCALLFORM SHOOT_NAME_{LOCAL:1}(-1)
			SIF TSTR:コマンド名受渡 == ""
				CONTINUE
			PRINTBUTTON @"[{LOCAL:1}] %TSTR:コマンド名受渡%", LOCAL:1
			PRINTL
		NEXT
		PRINTL
		PRINTBUTTON @"[999] 돌아간다", 999
		PRINTL
		DRAWLINE
		BINPUT
		IF RESULT != 999
			オート射精先オプション:LOCAL = RESULT
		ENDIF
	CASE IS < 200
		;上に移動
		SWAP オート射精先オプション:(LOCAL - 100), オート射精先オプション:(LOCAL - 101)
	CASE IS < 300
		;下に移動
		SWAP オート射精先オプション:(LOCAL - 200), オート射精先オプション:(LOCAL - 199)
	CASEELSE
		ARRAYSHIFT オート射精先オプション, -1, -1, LOCAL - 300
ENDSELECT

RESTART


;-------------------------------------------------------------------------------------------
;SHOOT_0:膣内
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_0(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 膣内射精
RETURN 1


@SHOOT_PALAM_0(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

EXP:(対象キャラ):Ｖ経験 += 5
EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ

;履歴登録
RFLAG:履歴用数値保存 = 1
CALL 履歴登録チェック(対象キャラ, "膣内射精回数")
履歴用実績:対象キャラ:膣内射精回数 += 1

;Q.なんでフラグを別に作成してるの？
;A.将来的にコンドームとかに対応する用
RCVAR:(対象キャラ):膣内射精フラグ = 1

CALL SOURCE_CALC_快Ｖ(対象キャラ, 実行キャラ, 200 * (RCVAR:対象キャラ:被精液濃縮 / 4 + 1))
CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 400)

;妊娠処理用に膣内射精された精液を計上
CALL TAKE_SPERM(対象キャラ, 実行キャラ, RCVAR:実行キャラ:射精量)


@SHOOT_ABLE_0(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ
;選択肢に出る条件
;Ｖ挿入系モードじゃない時はアウト
{
SIF !MODE_存在判定_完全一致("Ｖ挿入系", 実行キャラ, 対象キャラ)
	&& !MODE_存在判定_完全一致("ハーヴィンオナホＶ", 実行キャラ, 対象キャラ)
	&& !MODE_存在判定_完全一致("挿入歩きＶ", 実行キャラ, 対象キャラ)
	&& !MODE_存在判定_完全一致("ハーヴィンディルドＶ", 対象キャラ, 実行キャラ)
}
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_0(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ
;PRINTFORM %CALLNAME:実行キャラ%は%CALLNAME:(対象キャラ)%の膣奥で
;以上元部分 以下加筆部分
PRINTFORM %CALLNAME:実行キャラ%는
IF CFLAG:対象キャラ:睡眠
	PRINTFORM 잠들었다
ELSEIF TCVAR:対象キャラ:泥酔眠姦
	PRINTFORM 취해 쓰러졌다
ENDIF
PRINTFORM %CALLNAME:(対象キャラ)%의
PRINTDATA
	DATA 질 안쪽
;	DATA 最奥
;	DATA 肉穴の最奥
;教えの最奥がチラつく
	DATA 질의 가장 안쪽
	DATA 가장 깊은 곳
	DATA 제일 깊은 곳
ENDDATA
SELECTCASE RAND:2
	CASE 0
		PRINT 에
		IF RAND:2 == 0
			PRINTDATA
				DATA 귀두를
				DATA 페니스의 끝을
			ENDDATA
			PRINTDATA
				DATA 끈적하게
				DATA 딱 붙여
				DATA 꾹꾹
				DATA 뱅글뱅글
				DATA 
			ENDDATA
			PRINT 밀어붙이며
		ENDIF
	CASEELSE
		PRINT 로
ENDSELECT
IF RFLAG:フィニッシュフラグ > 1
	PRINTDATA
		DATA 대량의
		DATA 농후한
		DATA 엄청난 양의
	ENDDATA
ENDIF
PRINTDATA
	DATA 정액
	DATA 씨앗
;	DATA 種汁
;オラッ！APとBP満タンにしてやる！
	DATA 백탁
ENDDATA
PRINT 을
PRINTDATA
	DATA 토해냈다
	DATA 흩뿌렸다
	DATA 뿜어냈다
ENDDATA
PRINT …
PRINTL 
IF 実行キャラ == MASTER
	CALL 挿入アニメ表示(対象キャラ, "_射精")
ENDIF
TCVAR:(対象キャラ):被射精位置 = 1

;-------------------------------------------------------------------------------------------
;SHOOT_1:腸内
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_1(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 腸内射精
RETURN 1


@SHOOT_PALAM_1(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

EXP:(対象キャラ):Ａ経験 += 5
EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ

;履歴登録
RFLAG:履歴用数値保存 = 1
CALL 履歴登録チェック(対象キャラ, "腸内射精回数")
履歴用実績:対象キャラ:腸内射精回数 += 1

;Q.なんでフラグを別に作成してるの？
;A.将来的にコンドームとかに対応する用
RCVAR:(対象キャラ):腸内射精フラグ = 1

CALL SOURCE_CALC_快Ａ(対象キャラ, 実行キャラ, 200 * (RCVAR:対象キャラ:被精液濃縮 / 4 + 1))
CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 400)

;腸内射精された精液を計上
CALL TAKE_SPERM_A(対象キャラ, 実行キャラ, RCVAR:実行キャラ:射精量)

@SHOOT_ABLE_1(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;A挿入系モードじゃない時はアウト
{
SIF !MODE_存在判定_完全一致("Ａ挿入系", 実行キャラ, 対象キャラ)
	&& !MODE_存在判定_完全一致("ハーヴィンオナホＡ", 実行キャラ, 対象キャラ)
	&& !MODE_存在判定_完全一致("挿入歩きＡ", 実行キャラ, 対象キャラ) 
	&& !MODE_存在判定_完全一致("ハーヴィンディルドＡ", 対象キャラ, 実行キャラ)
}
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_1(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

PRINTFORM %CALLNAME:実行キャラ%는%CALLNAME:(対象キャラ)%의 장내에
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 대량의
PRINTL 정액을 토해냈다…
IF 実行キャラ == MASTER
	CALL 挿入アニメ表示(対象キャラ, "_射精")
ENDIF
TCVAR:(対象キャラ):被射精位置 = 2

;-------------------------------------------------------------------------------------------
;SHOOT_2:口
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_2(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 口内射精
RETURN 1


@SHOOT_PALAM_2(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

EXP:(対象キャラ):Ｓ経験 += 5
EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ

;履歴登録
RFLAG:履歴用数値保存 = 1
CALL 履歴登録チェック(対象キャラ, "口内射精回数")
履歴用実績:対象キャラ:口内射精回数 += 1

;ここで精飲するかどうかをRCVAR:(対象キャラ):精飲フラグで渡す
;喜んで飲む=2 なんとか飲み込む=1 吐き出す=0
RCVAR:(対象キャラ):精飲フラグ = 精飲フラグ(対象キャラ)
IF RCVAR:(対象キャラ):精飲フラグ > 0
	EXP:(対象キャラ):精飲経験 += RFLAG:フィニッシュフラグ
	;ミルク濃厚率を上げる
	;濃厚率補正は30％まで
	SIF TCVAR:対象キャラ:ミルク濃厚率補正 < 30
		TCVAR:対象キャラ:ミルク濃厚率補正 += 10
ENDIF

;精飲時、絶頂していると精飲快楽経験取得
IF RCVAR:(対象キャラ):絶頂の強度 && RCVAR:(対象キャラ):精飲フラグ
	EXP:(対象キャラ):精飲快楽経験 += RCVAR:(対象キャラ):絶頂の強度
ENDIF

CALL SOURCE_CALC_快Ｓ(対象キャラ, 実行キャラ, 100 * (RCVAR:対象キャラ:被精液濃縮 / 7 + 1))
CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 350)


@SHOOT_ABLE_2(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;フェラチオ系モードじゃない時はアウト
SIF MODE_存在判定_完全一致("フェラチオ系", 対象キャラ, 実行キャラ)
	RETURN 1
;パイズリ系モードなら技巧3以上かつ飲んでくれる場合のみOK
SIF MODE_存在判定_完全一致("パイズリ系", 対象キャラ, 実行キャラ) && 精飲フラグ(対象キャラ) && ABL:(対象キャラ):技巧 > 2 && !MODE_存在判定_完全一致("縦パイズリ", 対象キャラ, 実行キャラ)
	RETURN 1
RETURN 0


@MESSAGE_SHOOT_2(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

IF TCVAR:対象キャラ:泥酔眠姦
	PRINTFORM %CALLNAME:実行キャラ%는 취해 쓰러진 %CALLNAME:(対象キャラ)%의 입에 페니스를 밀어 넣고,
ELSEIF MODE_存在判定_完全一致("パイズリ系", 対象キャラ, 実行キャラ)
	PRINTFORML %CALLNAME:(対象キャラ)%는 사정의 징후를 감지하자마자 %CALLNAME:実行キャラ%의 페니스를 물었다
	PRINTFORM %CALLNAME:実行キャラ%는 그대로 %CALLNAME:(対象キャラ)%의 입에
ELSE
	PRINTFORM %CALLNAME:実行キャラ%는 %CALLNAME:(対象キャラ)%의 입에
ENDIF
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 대량의
PRINTL 정액을 토해냈다…
SELECTCASE RCVAR:(対象キャラ):精飲フラグ
	CASE 0
		IF ABL:対象キャラ:技巧 > 2
			IF TFLAG:マッサージ_鬼灯の香使用時間 > 0 && TFLAG:マッサージ_鬼灯の香使用時間 + 20 < DAY * 1440 + TIME
				PRINTFORML 몽롱해하는 %CALLNAME:対象キャラ%는, 입안에 나온 정액을 그대로 토해버렸다
			ELSE
				PRINTFORML %CALLNAME:対象キャラ%는 익숙한 모습으로 입안의 정액을 휴지에 토해냈다
			ENDIF
		ELSE
			PRINTFORML %CALLNAME:対象キャラ%는 기침하며, 입안에 나온 정액을 토해버렸다
		ENDIF
	CASE 1
		PRINTFORML %CALLNAME:対象キャラ%는 익숙지 않은 모습으로 입안의 정액을 조금씩 삼켰다
	CASE 2
		IF 知識素質:対象キャラ:性知識 > 1
			PRINTFORM %CALLNAME:対象キャラ%는 입에 나온 정액을 삼켰다는 증거를 보여주듯이,
			PRINTL 입을 열어 전부 삼킨 입안을 보여주었다
		ELSE
			PRINTFORML %CALLNAME:対象キャラ%는 목을 울리며 입안의 정액을 삼켰다
		ENDIF
ENDSELECT
TCVAR:(対象キャラ):被射精位置 = 3

;-------------------------------------------------------------------------------------------
;SHOOT_3:乳内
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_3(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 乳内射精
SIF 対象キャラ < 0 || TALENT:対象キャラ:バストサイズ < 1
	TSTR:コマンド名受渡 = 胸に射精
RETURN 1


@SHOOT_PALAM_3(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_快Ｂ(対象キャラ, 実行キャラ, 10 * (RCVAR:対象キャラ:被精液濃縮 / 7 + 1))
CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 350)

EXP:(対象キャラ):Ｂ経験 += 1
EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ

;履歴登録
RFLAG:履歴用数値保存 = 1
CALL 履歴登録チェック(対象キャラ, "乳内射精回数")
履歴用実績:対象キャラ:乳内射精回数 += 1

@SHOOT_ABLE_3(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;パイズリ系モードじゃない時はアウト
SIF !MODE_存在判定_完全一致("パイズリ系", 対象キャラ, 実行キャラ)
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_3(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

PRINTFORM %CALLNAME:実行キャラ%는%CALLNAME:(対象キャラ)%의
SELECTCASE TALENT:(対象キャラ):バストサイズ
	CASE IS < 1
		PRINT 가슴에
	CASE 1
		PRINT 가슴골에
	CASE 2
		PRINT 커다란 가슴골에
	CASE 3
		PRINT 매우 풍만하게 자란 가슴골에
ENDSELECT
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 대량의
PRINTL 정액을 토해냈다…
TCVAR:(対象キャラ):被射精位置 = 4

;-------------------------------------------------------------------------------------------
;SHOOT_5:顔射
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_5(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 顔射
RETURN 1


@SHOOT_PALAM_5(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 200)

EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ

;履歴登録
RFLAG:履歴用数値保存 = 1
CALL 履歴登録チェック(対象キャラ, "顔射回数")
履歴用実績:対象キャラ:顔射回数 += 1


@SHOOT_ABLE_5(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;パイズリ系モードでも口戯系モードでもない時はアウト
SIF !MODE_存在判定_完全一致("パイズリ系", 対象キャラ, 実行キャラ) && !MODE_存在判定_完全一致("口戯系", 対象キャラ, 実行キャラ)
	RETURN 0
;縦パイズリは駄目
SIF MODE_存在判定_完全一致("縦パイズリ", 対象キャラ, 実行キャラ)
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_5(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

PRINTFORM %CALLNAME:実行キャラ%는 %CALLNAME:(対象キャラ)%의 얼굴을 향해
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 대량의
PRINTL 정액을 토해냈다…
TCVAR:(対象キャラ):被射精位置 = 5

;-------------------------------------------------------------------------------------------
;SHOOT_6:腹射精
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_6(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = お腹に出す
RETURN 1


@SHOOT_PALAM_6(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 200)

EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ


@SHOOT_ABLE_6(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;仰向けの体位時
LOCAL = 0
SIF MODE_存在判定_完全一致("正常位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("対面座位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("側位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("屈曲位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("駅弁", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("対面立位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ正常位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ対面座位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ側位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ屈曲位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ駅弁", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ対面立位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("疑似性交", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_順不同("兜合わせ", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("ボテ腹ズリ", 対象キャラ, 実行キャラ)
	LOCAL += 1
SIF LOCAL == 0
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_6(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

PRINTFORM %CALLNAME:実行キャラ%는 %CALLNAME:(対象キャラ)%의\@PREGNANCY_SIZE(対象キャラ) >= 3?불룩한 배#배꼽\@을 향해
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 대량의
PRINTL 정액을 토해냈다…
TCVAR:(対象キャラ):被射精位置 = 6

;-------------------------------------------------------------------------------------------
;SHOOT_7:背中射精
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_7(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 背中に出す
RETURN 1


@SHOOT_PALAM_7(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 200)

EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ


@SHOOT_ABLE_7(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;うつぶせの体位時
LOCAL = 0
SIF MODE_存在判定_完全一致("後背位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("寝バック", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("側位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("背面座位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("背面駅弁", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("立ちバック", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ後背位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ寝バック", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ背面座位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ側位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ背面駅弁", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ立ちバック", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF LOCAL == 0
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_7(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

PRINTFORM %CALLNAME:実行キャラ%는 %CALLNAME:(対象キャラ)%의 등에 대고
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 대량의
PRINTL 정액을 토해냈다…
TCVAR:(対象キャラ):被射精位置 = 7

;-------------------------------------------------------------------------------------------
;SHOOT_8:太ももに射精
;-------------------------------------------------------------------------------------------

@SHOOT_NAME_8(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 太ももに射精
SIF 対象キャラ >= 0 && 下半身体型(対象キャラ) >= 2
	TSTR:コマンド名受渡 = 太ももに中出し
RETURN 1


@SHOOT_PALAM_8(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 350)

EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ


@SHOOT_ABLE_8(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;素股、疑似性交と足が閉じてる体位
LOCAL = 0
SIF MODE_存在判定_完全一致("寝バック", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("側位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("屈曲位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ寝バック", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ側位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ屈曲位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("素股", 対象キャラ, 実行キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("疑似性交", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF LOCAL == 0
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_8(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

PRINTFORM %CALLNAME:実行キャラ%는 %CALLNAME:対象キャラ%의
IF 対象キャラ >= 0 && 下半身体型(対象キャラ) >= 2
	PRINT 통통한 허벅지를 딱 붙이게 하자, 그 안에
ELSE
	PRINT 허벅지를 향해
ENDIF
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 대량의
PRINTL 정액을 토해냈다…
TCVAR:(対象キャラ):被射精位置 = 8

;-------------------------------------------------------------------------------------------
;SHOOT_9:手のひらに出す
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_9(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 手のひらに出す
RETURN 1


@SHOOT_PALAM_9(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 200)

EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ


@SHOOT_ABLE_9(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;手コキ系モードじゃない時はアウト
LOCAL = 0
SIF MODE_存在判定_完全一致("手コキ系", 対象キャラ, 実行キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ｃ愛撫", 対象キャラ, 実行キャラ) && GETBIT(TALENT:実行キャラ:性別, 1)
	LOCAL += 1
SIF LOCAL == 0
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_9(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

IF MODE_存在判定_完全一致("手コキ系", 対象キャラ, 実行キャラ) && ABL:対象キャラ:技巧 > 2 && TCVAR:対象キャラ:泥酔眠姦 != 1
	PRINTFORM %CALLNAME:(対象キャラ)%는 사정의 징후를 감지하자, %CALLNAME:実行キャラ%의
	PRINTDATA
		DATA 귀두를
		DATA 페니스 끝을
	ENDDATA
	PRINTL 손바닥으로 감쌌다
	PRINTFORM %CALLNAME:実行キャラ%는 그대로 %CALLNAME:(対象キャラ)%의 손바닥 안에
ELSE
	PRINTFORM %CALLNAME:実行キャラ%는 %CALLNAME:(対象キャラ)%의 손바닥에
ENDIF
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 대량의
PRINTL 정액을 토해냈다…

IF !CFLAG:対象キャラ:睡眠 && 対象キャラ != MASTER
	PRINTFORM %CALLNAME:対象キャラ%는 손바닥을 펼치자,
	IF RFLAG:フィニッシュフラグ >= 3
		PRINT 손에서 넘쳐흐를 정도로 엄청난 양의
	ELSEIF RFLAG:フィニッシュフラグ > 1
		PRINT 끈적하게 들러붙은
	ELSE
		PRINT 퍼진
	ENDIF
	IF TCVAR:対象キャラ:泥酔眠姦 == 1
		PRINT 정액을 초점 없는 눈으로 멍하니 바라보고 있다…
	ELSEIF RFLAG:フィニッシュフラグ > 1 && ABL:対象キャラ:サド性感 > 2
		PRINTFORML 정액을 즐거운 듯이 %CALLNAME:実行キャラ%에게 보여주었다
	ELSEIF RFLAG:フィニッシュフラグ > 1 && ABL:対象キャラ:奉仕精神 > 5
		PRINTFORML 정액을 자랑스럽게 %CALLNAME:実行キャラ%에게 보여주었다
	ELSEIF CFLAG:対象キャラ:発情期フラグ < 0
		IF 知識素質:対象キャラ:性知識 == -2
			PRINT 정액에서 본능적인 무언가를 느낀 것인지,
		ELSE
			PRINT 정액을
		ENDIF
		PRINTL 열띤 표정으로 바라보고 있다
	ELSEIF 実行キャラ == MASTER && TALENT:対象キャラ:恋慕
		PRINTL 정액을 사랑스럽게 바라보고 있다
	ELSEIF (TALENT:対象キャラ:性的興味 > 0 && 知識素質:対象キャラ:性知識 == 0) || 知識素質:対象キャラ:性知識 == -2
		PRINTL 정액을 흥미진진한 모습으로 만지고 있다
	ELSE
		PRINT 정액을 휴지로
		IF TALENT:対象キャラ:汚臭耐性 < 0
			PRINTDATA
				DATA 몇 번이고
				DATA 정성스럽게
				DATA 깨끗하게
			ENDDATA
		ENDIF
		PRINTL 닦았다
	ENDIF
ENDIF
TCVAR:(対象キャラ):被射精位置 = 9

;-------------------------------------------------------------------------------------------
;SHOOT_10:足に出す
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_10(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 足に出す
RETURN 1


@SHOOT_PALAM_10(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 200)

EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ


@SHOOT_ABLE_10(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;足コキモードじゃない時はアウト
SIF !MODE_存在判定_完全一致("足コキ", 対象キャラ, 実行キャラ)
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_10(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ
#DIM フィニッシュ反応

フィニッシュ反応 = 0
IF MODE_存在判定_完全一致("足コキ", 対象キャラ, 実行キャラ) && ABL:対象キャラ:技巧 > 2 && TCVAR:対象キャラ:泥酔眠姦 != 1
	IF ABL:対象キャラ:サド性感 > 2
		フィニッシュ反応 = 2
		PRINTFORML %CALLNAME:対象キャラ%은 사정의 징후를 감지하자, %CALLNAME:実行キャラ%의 페니스를 힘껏 밟았다
	ELSEIF ABL:対象キャラ:奉仕精神 > 5
		フィニッシュ反応 = 1
		PRINTFORML %CALLNAME:対象キャラ%은 사정의 징후를 감지하자, %CALLNAME:実行キャラ%의 페니스를 양발로 감쌌다
	ENDIF
ENDIF
PRINTFORM %CALLNAME:実行キャラ%은 \@ フィニッシュ反応 ? 그대로 # \@%CALLNAME:対象キャラ%의 발에
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 대량의
PRINTL 정액을 뿜어냈다…

IF !CFLAG:対象キャラ:睡眠 && 対象キャラ != MASTER && TCVAR:対象キャラ:泥酔眠姦 != 1
	IF フィニッシュ反応 == 1
		PRINTFORM %CALLNAME:対象キャラ%은 양발을 페니스에서 떼어내고, 그 사이에
		IF RFLAG:フィニッシュフラグ > 1
			PRINT 끈적하게 하얀 다리를 놓는 정액을 자랑스럽게
		ELSE
			PRINT 퍼진 정액을
		ENDIF
		PRINTFORML %CALLNAME:実行キャラ%에게 보여주었다
	ELSEIF フィニッシュ反応 != 2 && 実行キャラ == MASTER && TALENT:対象キャラ:恋慕
		PRINTFORM %CALLNAME:実行キャラ%이 사정하는 동안, %CALLNAME:対象キャラ%은 발에
		SIF RFLAG:フィニッシュフラグ > 1
			PRINT 대량의
		PRINTL 정액이 쏟아지는 감촉을 즐기고 있다
	ELSE
		PRINTFORM %CALLNAME:対象キャラ%은 발을 페니스에서 떼어내고,
		IF RFLAG:フィニッシュフラグ > 1
			PRINT 끈적하게 들러붙은
		ELSE
			PRINT 묻은
		ENDIF
		IF フィニッシュ反応 == 2
			PRINTFORML 정액을 심술궂게 웃으며 %CALLNAME:実行キャラ%에게 보여주었다
		ELSEIF (TALENT:対象キャラ:性的興味 > 0 && 知識素質:対象キャラ:性知識 == 0) || 知識素質:対象キャラ:性知識 == -2
			PRINTL 정액을 흥미진진한 모습으로 양발로 가지고 놀고 있다
		ELSE
			PRINT 정액을 티슈로
			IF TALENT:対象キャラ:汚臭耐性 < 0
				PRINTDATA
					DATA 몇 번이고
					DATA 정성껏
					DATA 깨끗하게
				ENDDATA
			ENDIF
			PRINTL 닦았다
		ENDIF
	ENDIF
ENDIF
TCVAR:(対象キャラ):被射精位置 = 10

;-------------------------------------------------------------------------------------------
;SHOOT_30:他にどこにも射精せない時用
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_30(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 何もない場所に射精
RETURN 1


@SHOOT_PALAM_30(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ


@SHOOT_ABLE_30(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ
RETURN 1


@MESSAGE_SHOOT_30(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

IF 対象キャラ == -1
	;非うふふ時射精
	IF RFLAG:コマンド結果受渡し変数 == 23 && 実行キャラ == MASTER
		;モブ逆レイプ
		PRINTFORML %CALLNAME:実行キャラ%은 누구인지 모르는 상대의 질에 정액을 뿜어내며 절정했다…
	ELSEIF RFLAG:コマンド結果受渡し変数 == 24 && 実行キャラ == MASTER
		;モブA逆レイプ
		PRINTFORML %CALLNAME:実行キャラ%은 누구인지 모르는 상대의 항문에 정액을 뿜어내며 절정했다…
	ELSEIF TEQUIP:実行キャラ:下半身下着あり > 0
		PRINTFORML %CALLNAME:実行キャラ%은 속옷 안에 정액을 뿜어내며 절정했다…
	ELSEIF TEQUIP:実行キャラ:下半身服あり
		PRINTFORML %CALLNAME:実行キャラ%은 옷 안에 정액을 뿜어내며 절정했다…
	ELSE
		PRINTFORML %CALLNAME:実行キャラ%은 음경에서 정액을 뿜어내며 절정했다…
	ENDIF
ELSE
	PRINTFORML %CALLNAME:実行キャラ%은 음경에서 정액을 뿜어내며 절정했다…
ENDIF
