;------------------------------------------------------------------------------
;通常時、遊べる施設で遊びに誘ってくる
;------------------------------------------------------------------------------
@AUTOCOM3
CALL SOURCE_CALC_歓楽(AUTOCOM_実行番号保存, PLAYER, 500)
CALL SOURCE_CALC_好感度要素_友情度UP(AUTOCOM_実行番号保存, PLAYER, 250)

;プール系、及び爆乳以上だとぽろりチャンス
LOCAL = RAND:5
IF SWIMROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置) && TALENT:AUTOCOM_実行番号保存:バストサイズ > 1 && LOCAL == 0
	CALL SOURCE_CALC_露出_反感無し(AUTOCOM_実行番号保存, PLAYER, 100)
	RFLAG:コマンド結果受渡し変数 = 1
ENDIF

TIME_遊びに誘う:AUTOCOM_実行番号保存:0 = TIME

RETURN 1

@AUTOCOM_ABLE3(キャラ番号)
#DIM キャラ番号
;あなたが必要
SIF GETBIT(AUTOCOM_相手判別関数(キャラ番号), 0) == 0
	RETURN 0
;友人以上
SIF CFLAG:キャラ番号:好感度レベル < 関係閾値:2
	RETURN 0
;４つの好感度要素で友情度が最も高い
LOCAL = 要素最大値算出(キャラ番号)
SIF LOCAL != 0 && LOCAL != 5
	RETURN 0
;遊ぶ系施設
SIF !ENJOYROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	RETURN 0
;２時間に１回のみ発動
SIF TIME - TIME_遊びに誘う:キャラ番号:0 < 120
	RETURN 0
;うふふ中ではない
SIF CFLAG:キャラ番号:うふふ
	RETURN 0
RETURN 1

@MESSAGE_AUTOCOM3
PRINTFORML %CALLNAME:AUTOCOM_実行番号保存%는 %CALLNAME:PLAYER%를 유혹해서, 함께 %GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)%에서 즐겁게 놀았다

;プール系、及び爆乳以上だとぽろりチャンス
IF RFLAG:コマンド結果受渡し変数
	PRINTW 
	PRINTFORML ……맙소사! 놀다 보니, %CALLNAME:AUTOCOM_実行番号保存%의 수영복이 흘러내려 가슴이 노출되어 버렸다!
	PRINTFORM %CALLNAME:AUTOCOM_実行番号保存%는
	SELECTCASE TALENT:AUTOCOM_実行番号保存:羞恥心
		CASE -1
			PRINT 웃으면서
		CASE 0
			PRINT 서둘러
		CASE 1
			PRINT 아주 부끄러운 듯이
	ENDSELECT
	PRINTL 수영복을 정리하고 있다…
ENDIF


