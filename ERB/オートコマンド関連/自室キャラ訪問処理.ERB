@自室キャラ訪問処理
#DIM 訪問候補者, キャラクタ数上限
#DIM キャラ番号
#DIM 候補番号
#DIMS 場所候補, 10
#DIM 目的候補, 10
#DIM 候補数
#DIM 飲み会人数
;休憩時、キャラが自室に遊びに来る処理

;OPTION制御
SIF OPTION変数:休憩制御
	RETURN -1

;休憩時に誰か来るのは1/2の確率
SIF RAND:2
	RETURN -1

;候補者リスト作成
候補番号 = 0
FOR キャラ番号, 1, CHARANUM
	;もう今日は遊びに来た
	SIF TCVAR:キャラ番号:自室訪問フラグ
		CONTINUE
	;島にいる
	SIF CFLAG:キャラ番号:滞在期間 < 1
		CONTINUE
	;寝てる
	SIF CFLAG:キャラ番号:睡眠
		CONTINUE
	;何らかの原因で隠密
	SIF CFLAG:キャラ番号:隠密
		CONTINUE
	;デート中
	SIF CFLAG:キャラ番号:デート
		CONTINUE
	;体力限界
	SIF CFLAG:キャラ番号:体力限界
		CONTINUE
	;好感度が一定以上か、従業員のどちらか
	SIF CFLAG:キャラ番号:好感度レベル < 関係閾値:2 && !TALENT:キャラ番号:従業員
		CONTINUE
	訪問候補者:候補番号 = キャラ番号
	候補番号 += 1
	IF TALENT:キャラ番号:恋慕
		;恋慕は確率３倍
		訪問候補者:候補番号 = キャラ番号
		候補番号 += 1
		訪問候補者:候補番号 = キャラ番号
		候補番号 += 1
	ELSEIF TALENT:キャラ番号:セフレ
		;セフレは確率２倍
		訪問候補者:候補番号 = キャラ番号
		候補番号 += 1
	ENDIF
NEXT

;一人も候補がいないなら戻る
IF 候補番号 == 0
	;-1を返すことで口上などの処理をスキップ
	RETURN -1
ENDIF

;訪問キャラ決定
AUTOCOM_実行番号保存 = 訪問候補者:(RAND:候補番号)
TCVAR:AUTOCOM_実行番号保存:自室訪問フラグ = 1
AUTOCOM_対象番号保存 = MASTER

;訪問目的決定
VARSET 目的候補
候補番号 = 0
;0:遊びに誘いに来る、1:自室で遊ぶ、2:仕事の用事で来る 3:うふふお誘い
;遊びは無条件
目的候補:(候補番号++) = 0
;自室は仲良いor同性
SIF CFLAG:AUTOCOM_実行番号保存:好感度レベル >= 関係閾値:3 || 同性愛性癖判定(AUTOCOM_実行番号保存, PLAYER)
	目的候補:(候補番号++) = 1
;仕事は従業員のみ
SIF TALENT:AUTOCOM_実行番号保存:従業員
	目的候補:(候補番号++) = 2
;飲み会は酒好きor飲酒経験一定以上（対応場所がない場合はなし）
IF 施設改造度:5:0 || 施設改造度:3:0 || 施設改造度:20:0
	IF TALENT:AUTOCOM_実行番号保存:飲酒拒否 < 1
		SIF TALENT:AUTOCOM_実行番号保存:飲兵衛 > 0 || (TALENT:AUTOCOM_実行番号保存:飲兵衛 == 0 && EXP:AUTOCOM_実行番号保存:飲酒経験 >= 50)
			目的候補:(候補番号++) = 4
	ENDIF
ENDIF

訪問目的 = 目的候補:(RAND:候補番号)
;陥落済み、かつ性欲が溜まっているとうふふお誘い。他すべてに優先する
IF 陥落チェック_性的(AUTOCOM_実行番号保存) && BASE:AUTOCOM_実行番号保存:性欲 > 1500 && 初体験日取得("初うふふ", AUTOCOM_実行番号保存, MASTER)
	訪問目的 = 3
ENDIF
;口上用に目的を保存
RFLAG:コマンド結果受渡し変数 = 訪問目的

;遊ぶ場所の決定
IF 訪問目的 == 0
	VARSET 場所候補
	候補番号 = 0
	場所候補:(候補番号++) = 遊戯室
	場所候補:(候補番号++) = プール
	SIF 施設改造度:3:0
		場所候補:(候補番号++) = キャンプ場
	SIF MATCH(開拓エリア開発番号, 大規模番号_海エリア) > 0
		場所候補:(候補番号++) = 海
	RSTR:コマンド結果受渡し文字列 = %場所候補:(RAND:候補番号)%
ELSEIF 訪問目的 == 4
	VARSET 場所候補
	候補番号 = 0
	SIF 施設改造度:5:0
		場所候補:(候補番号++) = バーエリア
	SIF 施設改造度:3:0
		場所候補:(候補番号++) = バーベキューエリア
	SIF 施設改造度:20:0
		場所候補:(候補番号++) = %CALLNAME:MASTER%の自宅

	RSTR:コマンド結果受渡し文字列 = %場所候補:(RAND:候補番号)%
ENDIF

;訪問処理
$訪問ラベル
DRAWLINE
PRINTFORML %CALLNAME:PLAYER%가 자기 방에서 쉬고 있는데, %CALLNAME:AUTOCOM_実行番号保存%가 찾아왔다.
TCVAR:AUTOCOM_実行番号保存:挨拶フラグ = 1
SELECTCASE 訪問目的
	CASE 0
		PRINTFORML %RSTR:コマンド結果受渡し文字列%로 같이 놀지 않겠냐는 권유 같은데…… 어쩌지?
		DRAWLINE
		PRINTBUTTON "[0] 놀러 간다", 0
		PRINTL
		PRINTBUTTON "[1] 놀러 간다 (간이판)", 1
		PRINTL
		PRINTBUTTON "[2] 거절한다", 2
		PRINTL
		PRINTL
		PRINTBUTTON "[100] 선택지에 대해 설명", 100
	CASE 1
		PRINTFORML 방으로 놀러 왔다고 하는데…… 어쩌지?
		DRAWLINE
		PRINTBUTTON "[0] 방으로 들인다", 0
		PRINTL
		PRINTBUTTON "[1] 방으로 들인다 (간이판)", 1
		PRINTL
		PRINTBUTTON "[2] 거절한다", 2
		PRINTL
		PRINTL
		PRINTBUTTON "[100] 선택지에 대해 설명", 100
	CASE 2
		PRINTFORML 일과 관련해서 묻고 싶은 게 있는 모양이다.
		WAIT
		PRINTFORML %CALLNAME:PLAYER%는 %CALLNAME:AUTOCOM_実行番号保存%의 질문에 답하며 적절한 지시를 내렸다.
		PRINTFORMW %CALLNAME:AUTOCOM_実行番号保存%는 납득한 듯한 모습으로 일터로 돌아갔다.

		CALL SOURCE_CALC_好感度要素_信頼度UP(AUTOCOM_実行番号保存, PLAYER, 250)
		TARGET = AUTOCOM_実行番号保存
		RETURN 0
	CASE 3
		IF 知識素質:AUTOCOM_実行番号保存:性知識 > 1
			IF 月算出() == "冬の月" && EXIST画像FILE("resources/" + CSTR:AUTOCOM_実行番号保存:画像フォルダ + "/短冊_裸リボン")
				PRINTFORML %CALLNAME:AUTOCOM_実行番号保存%는 당신을 기쁘게 하려고 자신의 육체를 선물 삼아 리본으로 장식하고 있다…
				CALL PRINT_STRL(@"IMAGEPATH_%CSTR:AUTOCOM_実行番号保存:画像フォルダ%/短冊_裸リボン")
				RSTR:コマンド結果受渡し文字列 = 裸リボン
				;初視聴で衣装ゲット
				IF STRCOUNT(所持エロ衣装文字列, @"エロ/裸リボン_") == 0
					CALL エロ衣装登録処理("裸リボン")
				ENDIF
			ELSE
				PRINTFORML %CALLNAME:AUTOCOM_実行番号保存%는 당신을 기쁘게 하려고 정욕을 부추기는 속옷을 입고 왔다…
				IF EXIST画像FILE("resources/" + CSTR:AUTOCOM_実行番号保存:画像フォルダ + "/短冊_セクシーランジェリー")
					CALL PRINT_STRL(@"IMAGEPATH_%CSTR:AUTOCOM_実行番号保存:画像フォルダ%/短冊_セクシーランジェリー")
				ENDIF
				RSTR:コマンド結果受渡し文字列 = セクシーランジェリー
			ENDIF
		ELSE
			PRINTFORML %CALLNAME:AUTOCOM_実行番号保存%는 농염한 눈빛으로 %CALLNAME:PLAYER%를 응시하고 있다……
			RSTR:コマンド結果受渡し文字列 = 通常
		ENDIF
		PRINTL 아무래도 몸을 겹치자는 권유인 듯한데, 어쩌지?
		DRAWLINE
		PRINTBUTTON "[0] 방으로 들인다", 0
		PRINTL
		PRINTBUTTON "[2] 거절한다", 2
		PRINTL
		PRINTL
		PRINTBUTTON "[100] 선택지에 대한 설명", 100
	CASE 4
		PRINTFORML %RSTR:コマンド結果受渡し文字列%에서 회식하지 않겠냐는 초대인 것 같은데…… 어떡할까?
		DRAWLINE
		PRINTBUTTON "[0] 회식한다", 0
		PRINTL
		PRINTBUTTON "[1] 회식한다 (간략 버전)", 1
		PRINTL
		PRINTBUTTON "[2] 거절한다", 2
		PRINTL
		PRINTL
		PRINTBUTTON "[100] 선택지에 대한 설명", 100
ENDSELECT

BINPUT
;口上用に選択肢を記録
RFLAG:コマンド結果受渡し変数２ = RESULT

SELECTCASE 訪問目的
	CASE 0
		SELECTCASE RFLAG:コマンド結果受渡し変数２
			CASE 0
				CFLAG:PLAYER:現在マップ種別 = 0
				CFLAG:AUTOCOM_実行番号保存:現在マップ種別 = 0
				CFLAG:AUTOCOM_実行番号保存:同室 = 1
				PRINTFORML %CALLNAME:PLAYER%는 %CALLNAME:AUTOCOM_実行番号保存%와 함께 %RSTR:コマンド結果受渡し文字列%에서 놀기로 했다.
				;一時間持続だが、休憩段階で60分経つのでその分を加算して代入
				CFLAG:AUTOCOM_実行番号保存:一緒に遊ぶ = 120
				TFLAG:TARGET変更用 = AUTOCOM_実行番号保存
				SELECTCASE RSTR:コマンド結果受渡し文字列
					CASE "遊戯室"
						IF 施設改造度:5:0 == 1
							LOCAL = RAND(503, 505)
							訪問先場所番号 = LOCAL
							CFLAG:AUTOCOM_実行番号保存:現在位置 = 訪問先場所番号
						ELSE
							訪問先場所番号 = 5
							CFLAG:AUTOCOM_実行番号保存:現在位置 = 訪問先場所番号
						ENDIF
					CASE "キャンプ場"
						LOCAL = RAND(303, 307)
						訪問先場所番号 = LOCAL
						CFLAG:AUTOCOM_実行番号保存:現在位置 = 訪問先場所番号
					CASE "プール"
						IF 施設改造度:15:0 == 1
							LOCAL = RAND(1503, 1506)
							訪問先場所番号 = LOCAL
							CFLAG:AUTOCOM_実行番号保存:現在位置 = 訪問先場所番号
						ELSE
							訪問先場所番号 = 15
							CFLAG:AUTOCOM_実行番号保存:現在位置 = 訪問先場所番号
						ENDIF
					CASE "海"
						LOCAL = RAND(2, 3)
						CFLAG:PLAYER:現在マップ種別 = 3
						CFLAG:AUTOCOM_実行番号保存:現在マップ種別 = 3
						訪問先場所番号 = LOCAL
						CFLAG:AUTOCOM_実行番号保存:現在位置 = 訪問先場所番号
				ENDSELECT
				TFLAG:マップ種別 = 派生マップ番号取得(CFLAG:AUTOCOM_実行番号保存:現在マップ種別, CFLAG:AUTOCOM_実行番号保存:現在位置)
			CASE 1
				TIME += 60
				TARGET = AUTOCOM_実行番号保存
				PRINTFORML %CALLNAME:PLAYER%는 %CALLNAME:AUTOCOM_実行番号保存%와 함께 %RSTR:コマンド結果受渡し文字列%에서 즐겁게 놀았다.
				PRINTFORMW 만족한 %CALLNAME:AUTOCOM_実行番号保存%와 헤어지고, %CALLNAME:PLAYER%는 자기 방으로 돌아왔다.

				CALL SOURCE_CALC_歓楽(AUTOCOM_実行番号保存, PLAYER, 500)
				CALL SOURCE_CALC_好感度要素_友情度UP(AUTOCOM_実行番号保存, PLAYER, 250)
			CASE 2
				PRINTFORMW 약속이 있다고 말하며, %CALLNAME:PLAYER%는 %CALLNAME:AUTOCOM_実行番号保存%의 초대를 거절했다……
				RETURN 1
			CASE 100
				DRAWLINE
				PRINTL 놀러 간다: 초대한 캐릭터와 함께 장소를 이동합니다.
				PRINTL 　　　　　　그 후의 행동에 대해서는 제한이 없습니다.
				PRINTL
				PRINT 놀러 간다 (간략 버전):
				PRINTL 간단한 묘사와 일정량의 호감도 변화가 실행됩니다.
				PRINTL 　　　　　　　　　　　장소 이동 등은 일어나지 않으며, 캐릭터는 방문 전에 있던 장소로 돌아갑니다.
				PRINTL 
				PRINTL 거절한다: 거절합니다. 캐릭터는 방문 전에 있던 장소로 돌아가고, 호감도 변화는 일어나지 않습니다.
				PRINTL 
				WAIT
				GOTO 訪問ラベル
		ENDSELECT
	CASE 1
		SELECTCASE RFLAG:コマンド結果受渡し変数２
			CASE 0
				CFLAG:PLAYER:現在マップ種別 = 0
				CFLAG:AUTOCOM_実行番号保存:現在マップ種別 = 0
				CFLAG:AUTOCOM_実行番号保存:同室 = 1
				PRINTFORML %CALLNAME:PLAYER%는 %CALLNAME:AUTOCOM_実行番号保存%와 함께 자기 방에서 놀기로 했다.
				;一時間持続だが、休憩段階で60分経つのでその分を加算して代入
				TFLAG:TARGET変更用 = AUTOCOM_実行番号保存
				CFLAG:AUTOCOM_実行番号保存:一緒に遊ぶ = 120
				CFLAG:AUTOCOM_実行番号保存:現在位置 = CFLAG:PLAYER:現在位置
			CASE 1
				TIME += 60
				TARGET = AUTOCOM_実行番号保存
				PRINTFORML %CALLNAME:PLAYER%는 %CALLNAME:AUTOCOM_実行番号保存%와 함께 자기 방에서 즐겁게 놀았다.
				PRINTFORMW 만족한 %CALLNAME:AUTOCOM_実行番号保存%는 리조트로 돌아갔다.

				CALL SOURCE_CALC_歓楽(AUTOCOM_実行番号保存, PLAYER, 500)
				CALL SOURCE_CALC_好感度要素_友情度UP(AUTOCOM_実行番号保存, PLAYER, 250)
			CASE 2
				PRINTFORMW 일정이 있다고 말하며, %CALLNAME:PLAYER%는 %CALLNAME:AUTOCOM_実行番号保存%의 유혹을 거절했다……
				RETURN 1
			CASE 100
				DRAWLINE
				PRINTL 방으로 초대하기: 찾아온 캐릭터를 자신의 방으로 초대합니다.
				PRINTL 　　　　　　이때, 분위기에 보정이 걸려 일부 명령어의 발생 조건이 완화됩니다.
				PRINTL 　　　　　　그 이후의 행동에 대한 제한은 없습니다.
				PRINTL
				PRINT 방으로 초대하기 (간이 버전):
				PRINTL 간단한 묘사와 일정한 호감도 변화가 실행됩니다.
				PRINTL 　　　　　　　　　　　장소 이동 등은 일어나지 않으며, 캐릭터는 방문 전에 있던 장소로 돌아갑니다.
				PRINTL 
				PRINTL 거절하기: 거절합니다. 캐릭터는 방문 전에 있던 장소로 돌아가며, 호감도 변화는 일어나지 않습니다.
				PRINTL %CALLNAME:PLAYER%는 %CALLNAME:AUTOCOM_実行番号保存%를 방으로 초대하여, 부드럽게 안아주었다.
				WAIT
				GOTO 訪問ラベル
		ENDSELECT
	CASE 2
		;選択肢なし
	CASE 3
		SELECTCASE RFLAG:コマンド結果受渡し変数２
			CASE 0
				CFLAG:PLAYER:現在マップ種別 = 0
				CFLAG:AUTOCOM_実行番号保存:現在マップ種別 = 0
				CFLAG:AUTOCOM_実行番号保存:同室 = 1
				PRINTFORML 일정이 있다고 말하며, %CALLNAME:PLAYER%는 %CALLNAME:AUTOCOM_実行番号保存%의 유혹을 거절했다……
				TFLAG:TARGET変更用 = AUTOCOM_実行番号保存
				TARGET:(GET_TARGETNUM() + 1) = AUTOCOM_実行番号保存
				CFLAG:AUTOCOM_実行番号保存:一緒に遊ぶ = 120
				CFLAG:AUTOCOM_実行番号保存:現在位置 = CFLAG:PLAYER:現在位置
				IF RSTR:コマンド結果受渡し文字列 != "通常"
					SIF CSTR:AUTOCOM_実行番号保存:コスプレ前の服 == "" && CSTR:AUTOCOM_実行番号保存:表示グラフィックカテゴリ != ""
						CSTR:AUTOCOM_実行番号保存:コスプレ前の服 '= CSTR:AUTOCOM_実行番号保存:表示グラフィックカテゴリ
					LOCALS '= CSTR:AUTOCOM_実行番号保存:脱ぐ前の服
					CALL CLOTHES_CHANGE(AUTOCOM_実行番号保存, RSTR:コマンド結果受渡し文字列)
					CSTR:AUTOCOM_実行番号保存:脱ぐ前の服 '= LOCALS
				ENDIF
				;3Pモード管理フラグを初期化
				FLAG:モード管理 = 0
				CALL うふふ開始キャラ処理(AUTOCOM_実行番号保存, "キャラからお誘い")
				CFLAG:MASTER:うふふ = 1
				TFLAG:マップ種別 = 派生マップ番号取得(CFLAG:AUTOCOM_実行番号保存:現在マップ種別, CFLAG:AUTOCOM_実行番号保存:現在位置)
			CASE 2
				PRINTFORMW 방으로 초대하기: 찾아온 캐릭터를 자신의 방으로 초대하여, 쓰러뜨립니다.
				RETURN 1
			CASE 100
				DRAWLINE
				PRINTL 거절하기: 거절합니다. 캐릭터는 방문 전에 있던 장소로 돌아가며, 호감도 변화는 일어나지 않습니다.
				PRINTL
				PRINTL 断る：断ります。キャラクターは訪問前にいた場所に戻り、好感度変化は起こりません。
				PRINTL %CALLNAME:PLAYER%는 %CALLNAME:AUTOCOM_実行番号保存%와(과) 함께 %RSTR:コマンド結果受渡し文字列%에서 술자리를 갖기로 했다.
				WAIT
				GOTO 訪問ラベル
		ENDSELECT
	CASE 4
		SELECTCASE RFLAG:コマンド結果受渡し変数２
			CASE 0
				CFLAG:PLAYER:現在マップ種別 = 0
				CFLAG:AUTOCOM_実行番号保存:現在マップ種別 = 0
				CFLAG:AUTOCOM_実行番号保存:同室 = 1
				PRINTFORML %CALLNAME:PLAYER%는 %CALLNAME:AUTOCOM_実行番号保存%와(과) 함께 %RSTR:コマンド結果受渡し文字列%에서 즐겁게 술자리를 가졌다.
				;一時間持続だが、休憩段階で60分経つのでその分を加算して代入
				CFLAG:AUTOCOM_実行番号保存:一緒に遊ぶ = 120
				TFLAG:TARGET変更用 = AUTOCOM_実行番号保存
				SELECTCASE RSTR:コマンド結果受渡し文字列
					CASE "バーエリア"
						訪問先場所番号 = 503
						CFLAG:AUTOCOM_実行番号保存:現在位置 = 訪問先場所番号
					CASE "バーベキューエリア"
						訪問先場所番号 = 304
						CFLAG:AUTOCOM_実行番号保存:現在位置 = 訪問先場所番号
					CASE @"%CALLNAME:MASTER%の自宅"
						訪問先場所番号 = 2005
						CFLAG:AUTOCOM_実行番号保存:現在位置 = 訪問先場所番号
				ENDSELECT
				TFLAG:マップ種別 = 派生マップ番号取得(CFLAG:AUTOCOM_実行番号保存:現在マップ種別, CFLAG:AUTOCOM_実行番号保存:現在位置)
				
				;先にここでモードを変えておかないとTARGETが上手く格納されない
				TFLAG:調教モード = 3
				CFLAG:MASTER:飲み会 = 1
				あなた追跡キャラ = 0
				CFLAG:AUTOCOM_実行番号保存:飲み会 = 1
				TFLAG:TARGET変更用 = AUTOCOM_実行番号保存
				オートコマンド_飲み会開始フラグ = 1

			CASE 1
				TIME += 60
				TARGET = AUTOCOM_実行番号保存
				PRINTFORML 만족한 %CALLNAME:AUTOCOM_実行番号保存%와(과) 헤어지고, %CALLNAME:PLAYER%는 자신의 방으로 돌아왔다.
				PRINTFORMW 일정이 있다고 말하며, %CALLNAME:PLAYER%는 %CALLNAME:AUTOCOM_実行番号保存%의 유혹을 거절했다……

				
				IF TALENT:AUTOCOM_実行番号保存:酒に強い == 2
					BASE:AUTOCOM_実行番号保存:酩酊 = 100
				ELSEIF TALENT:AUTOCOM_実行番号保存:酒に強い == 1
					BASE:AUTOCOM_実行番号保存:酩酊 = 300
				ELSEIF TALENT:AUTOCOM_実行番号保存:酒に強い == 0
					BASE:AUTOCOM_実行番号保存:酩酊 = 600
				ELSEIF TALENT:AUTOCOM_実行番号保存:酒に強い == -1
					BASE:AUTOCOM_実行番号保存:酩酊 = 900
				ENDIF
				IF TALENT:PLAYER:酒に強い == 2
					BASE:PLAYER:酩酊 = 100
				ELSEIF TALENT:PLAYER:酒に強い == 1
					BASE:PLAYER:酩酊 = 300
				ELSEIF TALENT:PLAYER:酒に強い == 0
					BASE:PLAYER:酩酊 = 600
				ELSEIF TALENT:PLAYER:酒に強い == -1
					BASE:PLAYER:酩酊 = 900
				ENDIF
				CALL SOURCE_CALC_歓楽(AUTOCOM_実行番号保存, PLAYER, 500)
				CALL SOURCE_CALC_好感度要素_友情度UP(AUTOCOM_実行番号保存, PLAYER, 250)
			CASE 2
				PRINTFORMW 予定があると言って、%CALLNAME:PLAYER%は%CALLNAME:AUTOCOM_実行番号保存%の誘いを断った……
				RETURN 1
			CASE 100
				DRAWLINE
				PRINTL 술자리 갖기: 권유하러 온 캐릭터와 함께 장소를 이동하여, 술자리 모드로 전환합니다.
				PRINTL 　　　　　　　그 후의 행동에 대해서는 제한이 없습니다.
				PRINTL
				PRINT 술자리 갖기 (간이판):
				PRINTL 간단한 묘사와 일정한 호감도 변화가 실행됩니다.
				PRINTL 　　　　　　　　　　　　장소 이동 등은 일어나지 않으며, 캐릭터는 방문 전 있던 장소로 돌아갑니다.
				PRINTL 
				PRINTL 거절하기: 거절합니다. 캐릭터는 방문 전 있던 장소로 돌아가며, 호감도 변화는 일어나지 않습니다.
				PRINTL 
				WAIT
				GOTO 訪問ラベル
		ENDSELECT
ENDSELECT

