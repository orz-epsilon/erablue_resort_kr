@経験値増加アイテム処理画面(キャラ番号)
#DIM キャラ番号
#DIM 配列番号
#DIM 星晶系所持数, 3
#DIM 星晶系使用数, 3
#DIM 増加経験値量
#DIM 予測レベル


CALL 戦闘能力表示画面(キャラ番号)
DRAWLINE
PRINTL 아이템을 사용하여, 캐릭터의 경험치를 증가시킬 수 있습니다.
PRINTFORML ※존재하는 캐릭터 중, 가장 높은 레벨이 상한.
PRINTFORML 현재 한계: Lv{最大レベル保存, 2}
DRAWLINE
星晶系所持数:0 = 素材アイテム数量算出("星晶の破片")
星晶系所持数:1 = 素材アイテム数量算出("星晶の欠片")
星晶系所持数:2 = 素材アイテム数量算出("星晶塊")

増加経験値量 = 星晶系使用数:0 * 500 + 星晶系使用数:1 * 3000 + 星晶系使用数:2 * 15000
FOR LOCAL, 0, 80
	IF 戦闘経験値テーブル:LOCAL >= 基礎BATTLE_STATE:キャラ番号:蓄積EXP + 増加経験値量
		予測レベル = LOCAL + 1
		BREAK
	ENDIF
NEXT

IF SUMARRAY(星晶系所持数) > 0
	IF 星晶系所持数:0 > 0
		PRINTFORM ■성정의 파편　　소지 수:{星晶系所持数:0, 3}　　사용 수:{星晶系使用数:0, 3}　　
		PRINTBUTTON "[-10]", 1
		PRINTBUTTON "[-1]", 2
		IF 予測レベル >= 最大レベル保存
			SETCOLOR カラーパレット("グレーアウト")
			PRINTPLAIN [+1]
			PRINTPLAIN [+10]
			RESETCOLOR
		ELSE
			PRINTBUTTON "[+1]", 3
			PRINTBUTTON "[+10]", 4
		ENDIF
		PRINTL
	ENDIF
	IF 星晶系所持数:1 > 0
		PRINTFORM ■성정의 조각　　소지 수:{星晶系所持数:1, 3}　　사용 수:{星晶系使用数:1, 3}　　
		PRINTBUTTON "[-10]", 101
		PRINTBUTTON "[-1]", 102
		IF 予測レベル >= 最大レベル保存
			SETCOLOR カラーパレット("グレーアウト")
			PRINTPLAIN [+1]
			PRINTPLAIN [+10]
			RESETCOLOR
		ELSE
			PRINTBUTTON "[+1]", 103
			PRINTBUTTON "[+10]", 104
		ENDIF
		PRINTL
	ENDIF
	IF 星晶系所持数:2 > 0
		PRINTFORM ■성정괴　　　소지 수:{星晶系所持数:2, 3}　　사용 수:{星晶系使用数:2, 3}　　
		PRINTBUTTON "[-10]", 201
		PRINTBUTTON "[-1]", 202
		IF 予測レベル >= 最大レベル保存
			SETCOLOR カラーパレット("グレーアウト")
			PRINTPLAIN [+1]
			PRINTPLAIN [+10]
			RESETCOLOR
		ELSE
			PRINTBUTTON "[+1]", 203
			PRINTBUTTON "[+10]", 204
		ENDIF
		PRINTL
	ENDIF
	PRINTFORML 예측 성장: Lv{基礎BATTLE_STATE:キャラ番号:Lv, 2} > Lv{予測レベル, 2}
	PRINTL
	IF SUMARRAY(星晶系使用数) > 0
		PRINTBUTTON "[998] 이상의 수를 사용한다", 998
		PRINTL
	ELSE
		SETCOLOR カラーパレット("グレーアウト")
		PRINTPLAIN [998] 이상의 수를 사용한다
		RESETCOLOR
		PRINTL
	ENDIF
ELSE
	PRINTL ※대상 아이템을 소지하고 있지 않습니다.
ENDIF
[IF_DEBUG]
	PRINTBUTTON "[997] 지정 레벨까지 올린다", 997
	PRINTL
[ENDIF]
DRAWLINE
PRINTBUTTON "[999] 돌아간다", 999
PRINTL

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 998
		IF 星晶系使用数:0 > 0
			PRINTFORML 성정의 파편을 {星晶系使用数:0, 3}개 사용했다!
			CALL 素材消費処理("星晶の破片", 星晶系使用数:0)
		ENDIF
		IF 星晶系使用数:1 > 0
			PRINTFORML 성정의 조각을 {星晶系使用数:1, 3}개 사용했다!
			CALL 素材消費処理("星晶の欠片", 星晶系使用数:1)
		ENDIF
		IF 星晶系使用数:2 > 0
			PRINTFORML 성정괴를 {星晶系使用数:2, 3}개 사용했다!
			CALL 素材消費処理("星晶塊", 星晶系使用数:2)
		ENDIF
		PRINTFORMW 경험치를{増加経験値量, 6}획득!
		基礎BATTLE_STATE:キャラ番号:蓄積EXP += 増加経験値量
		CALL 口上変数初期化
		CALL レベルアップ処理(キャラ番号)
		SIF RESULT
			CALL 口上変数表示(キャラ番号, 0)
		VARSET 星晶系使用数
		RESTART
	CASE 999
		VARSET 星晶系使用数
		RETURN 0
	CASE 997
		PRINTL 설정하고 싶은 레벨을 입력해 주세요(최대 80, 레벨 다운은 할 수 없습니다)
		INPUT
		IF INRANGE(RESULT, 基礎BATTLE_STATE:キャラ番号:Lv+2, 80)
			基礎BATTLE_STATE:キャラ番号:蓄積EXP = 戦闘経験値テーブル:(RESULT - 1)
			CALL レベルアップ処理(キャラ番号)
		ENDIF
		RESTART
	CASEELSE
		SELECTCASE LOCAL % 100
			CASE 1
				星晶系使用数:(LOCAL / 100) = MAX(0, 星晶系使用数:(LOCAL / 100) - 10)
			CASE 2
				星晶系使用数:(LOCAL / 100) = MAX(0, 星晶系使用数:(LOCAL / 100) - 1)
			CASE 3
				星晶系使用数:(LOCAL / 100) = MIN(星晶系所持数:(LOCAL / 100), 星晶系使用数:(LOCAL / 100) + 1)
			CASE 4
				FOR 配列番号, 0, 10
					星晶系使用数:(LOCAL / 100) = MIN(星晶系所持数:(LOCAL / 100), 星晶系使用数:(LOCAL / 100) + 1)
					増加経験値量 = 星晶系使用数:0 * 500 + 星晶系使用数:1 * 3000 + 星晶系使用数:2 * 15000

					FOR LOCAL:1, 0, 80
						IF 戦闘経験値テーブル:(LOCAL:1) >= 基礎BATTLE_STATE:キャラ番号:蓄積EXP + 増加経験値量
							予測レベル = (LOCAL:1) + 1
							BREAK
						ENDIF
					NEXT
					IF 予測レベル >= 最大レベル保存
						BREAK
					ENDIF
				NEXT
		ENDSELECT
		RESTART
ENDSELECT

