@証関数_レンジャーの証(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード

SELECTCASE 処理モード
	CASE "ジョブ証種別"
		証関連文字列受け渡し = 일반 직업
	CASE "証名"
		TSTR:コマンド名受渡 = 레인저의 증표
	CASE "クラス名"
		SIF 対象キャラ <= 0
			RETURN 0
		SELECTCASE GET_JOBSKILL_LEVEL(対象キャラ)
			CASE IS >= 3
				RESULTS = サイドワインダー
				RESULTS:1 = Class.III
				RETURN 1
			CASE 2
				RESULTS = マークスマン
				RESULTS:1 = Class.II
				RETURN 1
			CASEELSE
				RESULTS = レンジャー
				RESULTS:1 = Class.I
				RETURN 1
		ENDSELECT
	CASE "フレーバー"
		詳細文文字列受け渡し変数 '= "민첩한 움직임으로 공격을 피하고 적을 노리는 사수의 증표.<br>"
		詳細文文字列受け渡し変数 += "공격을 회피하거나 약해진 적에게 추격타를 날리는 것을 특기로 한다.<br>"
		詳細文文字列受け渡し変数 += "따라서 적의 약화에 능숙한 동료와의 궁합이 좋다.<br>"
ENDSELECT

@証出現率_レンジャーの証(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 2
	RETURN 0
SELECTCASE 入手タイプ
	CASE "輸入"
		RETURN 装備レアリティ_Ｎ
ENDSELECT

@証アビ関数_レンジャーの証_1(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 1
#DIMS CONST アビ名 = "ライトウェイト"
#DIM CONST 消費ＭＰ = 100
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 70
	CASE 2
		効果数値:0 = 0
	CASEELSE
		効果数値:0 = 0
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　소비 MP:{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　효과: 자신의 회피율을 다음 행동 시까지 [+1000%/고유 칸] 증가시킨다.<br>"
		詳細文文字列受け渡し変数 += "　　　　자신에게 다음 행동 시까지 약화 무효 효과를 부여한다."
		IF 効果数値:0 > 0
			詳細文文字列受け渡し変数 += "<br>"
			詳細文文字列受け渡し変数 += @"　　　　자신의 어빌리티 대미지를 다음 행동 시까지 [{効果数値:0}％/C 칸] 증가시킨다."
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = ライトウェイト_回避率
		アビ汎用文字列:バフ・デバフ対象能力 = 회피율
		アビ汎用変数:効果量 = 1000
		アビ汎用変数:持続行動回数 = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 약화 무효
		アビ汎用文字列:バフ・デバフ対象能力 = 약화 무효
		アビ汎用変数:持続行動回数 = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		IF 効果数値:0 > 0
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = アビリティダメージ補正C
			アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_アビリティダメージ
			アビ汎用変数:効果割合 = 効果数値:0
			アビ汎用変数:持続行動回数 = 2
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@証アビ関数_レンジャーの証_2(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 2
#DIMS CONST アビ名 = "アローレイン"
#DIM CONST 消費ＭＰ = 20
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 得意武器判定(対象キャラ, "弓")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("攻撃")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 150
		効果数値:1 = 20
	CASE 2
		効果数値:0 = 125
		効果数値:1 = 15
	CASEELSE
		効果数値:0 = 100
		効果数値:1 = 0
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%(장비자가 활 특기or공격 타입일 때)<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　소비 MP:{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　효과: 적 전체에 자신의 속성 ［공격력✕{効果数値:0}％］ 데미지를 준다."
		IF 効果数値:1 > 0
			詳細文文字列受け渡し変数 += "<br>"
			詳細文文字列受け渡し変数 += @"　　　　대상의 공격력을 3턴 ［-{効果数値:1}％/B칸］ 감소시킨다. 기본 성공률 80％."
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値:0
		CALL アビテンプレート_ダメージ処理_自属性

		IF 効果数値:1 > 0
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 攻撃力B
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:効果割合 = 効果数値:1
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 80
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT

@証アビ関数_レンジャーの証_3(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 2
#DIMS CONST アビ名 = "ハードライン"
#DIM CONST 消費ＭＰ = 15
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 得意武器判定(対象キャラ, "銃")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("バランス")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 250
		効果数値:1 = 50
	CASEELSE
		効果数値:0 = 250
		効果数値:1 = 0
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%(장비자가 총 특기or밸런스 타입일 때)<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　소비 MP:{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　효과: 적 단일 대상에게 자신의 속성 ［공격력✕100％］ 데미지를 준다. <br>"
		詳細文文字列受け渡し変数 += @"　　　　대상이 디버프 상태일 경우, 데미지 배율이 ［{効果数値:0}％］가 된다."
		IF 効果数値:1 > 0
			詳細文文字列受け渡し変数 += "<br>"
			詳細文文字列受け渡し変数 += @"　　　　대상의 디버프 효과를 ［1턴］ 연장한다. 기본 성공률 {効果数値:1}％."
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		SIF アビテンプレート用_キャンセルフラグ
			RETURN
		IF 弱体存在判定(アビテンプレート用_対象保存:0)
			アビ汎用変数:効果割合 = 効果数値:0
		ELSE
			アビ汎用変数:効果割合 = 100
		ENDIF
		CALL アビテンプレート_ダメージ処理_自属性

		IF 効果数値:1 > 0
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = ALL
			アビ汎用変数:効果量 = 1
			アビ汎用変数:基礎成功確率 = 効果数値:1
			CALL アビテンプレート_不利効果_デバフ効果延長_持続ターン
		ENDIF
ENDSELECT

@証アビ関数_レンジャーの証_4(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 1
#DIMS CONST アビ名 = "キルストリーク"
#DIM CONST 消費ＭＰ = 50
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ) - 2
ELSE
	LEVEL = 0
ENDIF
SELECTCASE LEVEL
	CASEELSE
		効果数値:0 = 100
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　소비ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　효과：적 하나에 자신의 속성 [공격력✕200%] 데미지를 준다. <br>"
		詳細文文字列受け渡し変数 += @"　　　　대상 디버프 효과 1개당, 데미지 배율이 [{効果数値:0}%] 상승한다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		SIF アビテンプレート用_キャンセルフラグ
			RETURN
		アビ汎用変数:効果割合 = 200 + 効果数値:0 * 弱体数算出(アビテンプレート用_対象保存:0)
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT

@証アビ関数_レンジャーの証_10(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIMS CONST アビ名 = "スタディエイム"
#DIM CONST 消費ＭＰ = 10
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 得意武器判定(対象キャラ, "弓")
	ELSEIF 得意武器判定(対象キャラ, "銃")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0 && LEVEL <= 1
			詳細文文字列受け渡し変数 '= @"■%アビ名% (장비자가 활 특기or총 특기일 때)<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　소비ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　효과：적 하나에 3턴간, 약체 상태로 취급되는 스터디 에임 효과를 부여한다. <br>"
		詳細文文字列受け渡し変数 += "　　　　자신의 오의 게이지를 [+20%] 한다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用文字列:バフ・デバフ枠 = 스터디 에임
		アビ汎用文字列:バフ・デバフ対象能力 = 스터디 에임
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:弱体耐性貫通オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 20
		CALL アビテンプレート_有利効果_奥義ゲージUP
ENDSELECT
