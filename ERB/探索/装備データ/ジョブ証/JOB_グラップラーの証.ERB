@証関数_グラップラーの証(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード

SELECTCASE 処理モード
	CASE "ジョブ証種別"
		証関連文字列受け渡し = 일반 직업
	CASE "証名"
		TSTR:コマンド名受渡 = 그래플러의 증표
	CASE "クラス名"
		SIF 対象キャラ <= 0
			RETURN 0
		SELECTCASE GET_JOBSKILL_LEVEL(対象キャラ)
			CASE IS >= 3
				RESULTS = オーガ
				RESULTS:1 = Class.III
				RETURN 1
			CASE 2
				RESULTS = クンフー
				RESULTS:1 = Class.II
				RETURN 1
			CASEELSE
				RESULTS = グラップラー
				RESULTS:1 = Class.I
				RETURN 1
		ENDSELECT
	CASE "フレーバー"
		詳細文文字列受け渡し変数 '= "무기를 버리고 자신의 육체만을 단련하는 격투가의 증표。<br>"
		詳細文文字列受け渡し変数 += "그 길은 길고 험난하기 때문에, 뜻을 품는 자는 매우 적다。<br>"
		詳細文文字列受け渡し変数 += "장비하면, 무도의 마음에 닿을 수 있을 것 같은 기분이 든다。<br>"
ENDSELECT

@証出現率_グラップラーの証(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 2
	RETURN 0
SELECTCASE 入手タイプ
	CASE "輸入"
		RETURN 装備レアリティ_Ｎ
ENDSELECT

@証アビ関数_グラップラーの証_1(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 1
#DIMS CONST アビ名 = "捨身の型"
#DIM CONST 消費ＭＰ = 20
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 65
	CASE 2
		効果数値:0 = 60
	CASEELSE
		効果数値:0 = 50
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　소비 MP:{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　효과: 자신의 공격력을 5턴간 [{効果数値:0}％/A범위] 증가시킨다。<br>"
		詳細文文字列受け渡し変数 += "　　　　자신의 방어력을 5턴간 [20％/A범위] 감소시킨다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 공격력A
		アビ汎用文字列:バフ・デバフ対象能力 = 공격력
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 방어력A
		アビ汎用文字列:バフ・デバフ対象能力 = 방어력
		アビ汎用変数:効果割合 = 20
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:弱体耐性貫通オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@証アビ関数_グラップラーの証_2(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 3
#DIMS CONST アビ名 = "カウンター"
#DIM CONST 消費ＭＰ = 20
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 得意武器判定(対象キャラ, "格闘")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("回避")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 250
		効果数値:1 = 4
		効果数値:2 = 100
	CASE 2
		効果数値:0 = 150
		効果数値:1 = 3
		効果数値:2 = 0
	CASEELSE
		効果数値:0 = 100
		効果数値:1 = 3
		効果数値:2 = 0
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%(장비자가 격투 특기or회피 타입일 때)<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　소비 MP：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　효과：자신의 회피율을 공격을{効果数値:1}회 받을 때까지［+100％/A범위］한다. <br>"
		詳細文文字列受け渡し変数 += @"　　　　자신에게{効果数値:1}회 회피 시 반격［공격력✕{効果数値:0}％］효과를 부여한다."
		IF 効果数値:2 > 0
			詳細文文字列受け渡し変数 += "<br>"
			詳細文文字列受け渡し変数 += @"　　　　자신에 대한 적대심을 다음 행동 시까지［+{効果数値:2}/C범위］한다."
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 回避率A
		アビ汎用文字列:バフ・デバフ対象能力 = 回避率
		アビ汎用変数:効果量 = 100
		アビ汎用変数:持続ターン = 2
		アビ汎用変数:持続回数 = 効果数値:1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 回避反撃
		アビ汎用文字列:バフ・デバフ対象能力 = 回避反撃
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:持続ターン = 2
		アビ汎用変数:持続回数 = 効果数値:1
		CALL アビテンプレート_有利効果_バフ効果セット

		IF 効果数値:2 > 0
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 敵対心C
			アビ汎用文字列:バフ・デバフ対象能力 = 敵対心
			アビ汎用変数:効果割合 = 効果数値:2
			アビ汎用変数:持続行動回数 = 2
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@証アビ関数_グラップラーの証_3(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 2
#DIMS CONST アビ名 = "練気"
#DIM CONST 消費ＭＰ = 15
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 得意武器判定(対象キャラ, "格闘")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("連撃")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 17
		効果数値:1 = 50
	CASE 2
		効果数値:0 = 13
		効果数値:1 = 50
	CASEELSE
		効果数値:0 = 10
		効果数値:1 = 50
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%（장비자가 격투 특기or연격 타입일 때）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　소비 MP：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　효과：자신의 공격력을 5턴［{効果数値:0}％/고유 범위］증가시킨다. <br>"
		詳細文文字列受け渡し変数 += @"　　　　이 공격력 증가는［{効果数値:1}％］까지 누적할 수 있다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 練気_攻撃力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:累積上限_割合 = 効果数値:1
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_有利効果_累積バフ効果セット
ENDSELECT

@証アビ関数_グラップラーの証_4(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 2
#DIMS CONST アビ名 = "闘気"
#DIM CONST 消費ＭＰ = 15
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ) - 2
ELSE
	LEVEL = 0
ENDIF
SELECTCASE LEVEL
	CASEELSE
		効果数値:0 = 200
		効果数値:1 = 20
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　소비 MP:{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　효과: 적 하나에 자신의 속성 [공격력✕{効果数値:0}%] 데미지를 준다.<br>"
		詳細文文字列受け渡し変数 += "　　　　이 어빌리티는 상대의 방어력을 무시한다.<br>"
		詳細文文字列受け渡し変数 += "　　　　자신의 오의 게이지를［+{効果数値:1}％］한다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:防御無視オプション_割合 = 100
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 効果数値:1
		CALL アビテンプレート_有利効果_奥義ゲージUP
ENDSELECT
