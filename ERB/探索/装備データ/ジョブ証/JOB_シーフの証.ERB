@証関数_シーフの証(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード

SELECTCASE 処理モード
	CASE "ジョブ証種別"
		証関連文字列受け渡し = 일반 직업
	CASE "証名"
		TSTR:コマンド名受渡 = 도적의 증표
	CASE "クラス名"
		SIF 対象キャラ <= 0
			RETURN 0
		SELECTCASE GET_JOBSKILL_LEVEL(対象キャラ)
			CASE IS >= 3
				RESULTS = ホークアイ
				RESULTS:1 = Class.III
				RETURN 1
			CASE 2
				RESULTS = レイダー
				RESULTS:1 = Class.II
				RETURN 1
			CASEELSE
				RESULTS = シーフ
				RESULTS:1 = Class.I
				RETURN 1
		ENDSELECT
	CASE "フレーバー"
		詳細文文字列受け渡し変数 '= "교활한 어빌리티와 독자적인 움직임으로 싸우는 증표。<br>"
		詳細文文字列受け渡し変数 += "특정 조건에서 큰 화력을 내거나, 아이템을 얻기 쉬워지는 기술을 다룬다。<br>"
		詳細文文字列受け渡し変数 += "장착하면 손재주가 좋아진 기분이 든다。<br>"
ENDSELECT

@証出現率_シーフの証(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 2
	RETURN 0
SELECTCASE 入手タイプ
	CASE "輸入"
		RETURN 装備レアリティ_Ｎ
ENDSELECT

@証アビ関数_シーフの証_1(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM バフID
#DIM LEVEL
#DIM 消費ＭＰ
#DIM 効果数値, 1
#DIMS CONST アビ名 = "ブレイクアサシン"
#DIMS CONST 数値フォーマット = "#,.0##"
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		消費ＭＰ = 50
		効果数値:0 = 2750
	CASE 2
		消費ＭＰ = 50
		効果数値:0 = 2500
	CASEELSE
		消費ＭＰ = 100
		効果数値:0 = 2500
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　소비 MP：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　효과: 일반 공격을 한다。<br>"
		詳細文文字列受け渡し変数 += @"　　　　대상의 CT가 0일 때, 이 일반 공격의 데미지는%TOSTR(効果数値:0, 数値フォーマット)%배가 된다。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 브레이크 어쌔신
		アビ汎用文字列:バフ・デバフ対象能力 = 브레이크 어쌔신
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:持続行動回数 = 1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = -1
		CALL アビテンプレート_有利効果_バフ効果セット
		;ダメージ計算や攻撃への反応でバフが増減したときのためにIDで保持する
		バフID = DT_CELL_GET("戦闘効果データベース", バフ番号取得_枠("ブレイクアサシン", 戦闘行動キャラ), "id")
		; 選択時メッセージより前にメッセージを出さないための処理
		CALL アビテンプレ変数リセット

		TRYCCALLFORM 固有通常攻撃_{NO:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号)}
		CATCH
			アビ汎用文字列:選択時メッセージ１ = 누구를 공격하시겠습니까?
			CALL アビ対象選択テンプレート_敵単体
			アビ汎用変数:効果割合 = 100
			CALL アビテンプレート_通常攻撃処理
		ENDCATCH

		CALL 戦闘効果削除(バフID, 1)
ENDSELECT

@証アビ関数_シーフの証_2(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM ループ用
#DIM LEVEL
#DIM 効果数値, 3
#DIMS CONST アビ名 = "トレジャーハント"
#DIM CONST 消費ＭＰ = 15
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 得意武器判定(対象キャラ, "短剣")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("サポート")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 3
		効果数値:1 = 4
		効果数値:2 = 5
	CASE 2
		効果数値:0 = 3
		効果数値:1 = 3
		効果数値:2 = 4
	CASEELSE
		効果数値:0 = 3
		効果数値:1 = 3
		効果数値:2 = 3
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%（장비자가 단검 특기or서포트 타입일 때）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　소비 MP：{消費ＭＰ}<br>"
		IF 効果数値:0 == 効果数値:2
			詳細文文字列受け渡し変数 += @"　효과: 적 단일 대상에게 드롭률 UP 효과를 {効果数値:0}회 부여한다."
		ELSE
			詳細文文字列受け渡し変数 += @"　효과: 적 단일 대상에게 드롭률 UP 효과를 {効果数値:0}～{効果数値:2}회 부여한다."
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		FOR ループ用, 0, 効果数値:RAND(3)
			CALL アビテンプレート_不利効果_トレハン
		NEXT
ENDSELECT

@証アビ関数_シーフの証_3(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 2
#DIMS CONST アビ名 = "デッドリーエイム"
#DIM CONST 消費ＭＰ = 20
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 得意武器判定(対象キャラ, "銃")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("連撃")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 4
		効果数値:1 = 60
	CASE 2
		効果数値:0 = 3
		効果数値:1 = 55
	CASEELSE
		効果数値:0 = 3
		効果数値:1 = 50
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0 && LEVEL != 2
			詳細文文字列受け渡し変数 '= @"■%アビ名%（장비자가 총 특기or연격 타입일 때）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　소비 MP：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　효과: 자신의 TA율을 {効果数値:0}턴간 [+{効果数値:1}％/A범위] 한다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = TA율A
		アビ汎用文字列:バフ・デバフ対象能力 = TA율
		アビ汎用変数:効果量 = 効果数値:1
		アビ汎用変数:持続ターン = 効果数値:0
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@証アビ関数_シーフの証_4(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM トレハン行数
#DIM トレハンLv
#DIM LEVEL
#DIM 効果数値, 2
#DIMS CONST アビ名 = "ブレイクキープ"
#DIM CONST 消費ＭＰ = 25
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ) - 1
ELSE
	LEVEL = 0
ENDIF
SELECTCASE LEVEL
	CASE IS >= 2
		効果数値:0 = 30
		効果数値:1 = 10
	CASEELSE
		効果数値:0 = 20
		効果数値:1 = 10
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　소비 MP：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　효과: CT가 0인 적 단일 대상에게 1턴간 공포 효과를 부여한다. 기본 성공률 {効果数値:0}％。<br>"
		詳細文文字列受け渡し変数 += "　　　　대상의 트레저 헌트 Lv이 높을수록 성공률이 높아지며, 효과 턴도 길어진다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		IF アビテンプレート用_キャンセルフラグ
			RETURN
		ELSEIF 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 10):チャージターン > 0
			CALL 口上変数初期化
			WSTR:(K++) = 대상의 차지 턴이 0이 아닙니다
			CALL メッセージ欄表示用関数(,,,0)
			アビテンプレート用_キャンセルフラグ = 1
			RETURN
		ENDIF
		トレハン行数 = デバフ番号取得("ドロップ率UP", アビテンプレート用_対象保存:0)

		アビ汎用文字列:バフ・デバフ枠 = 공포
		アビ汎用文字列:バフ・デバフ対象能力 = 공포
		アビ汎用変数:持続ターン = 1
		アビ汎用変数:基礎成功確率 = 効果数値:0
		IF トレハン行数 >= 0
			トレハンLv = DT_CELL_GET("戦闘効果データベース", トレハン行数, "効果量_固定値")
			アビ汎用変数:持続ターン += MAX(1, トレハンLv / 2)
			アビ汎用変数:基礎成功確率 += 効果数値:1 * トレハンLv
		ENDIF
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@証アビ関数_シーフの証_10(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIMS CONST アビ名 = "不意打ち"
#DIM CONST 消費ＭＰ = 0

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　소비 MP：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　효과：적 하나에게 자속성의 [행동 속도✕100%] 데미지를 준다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 100
		アビ汎用文字列:攻撃側使用能力値 = 행동속도
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT
