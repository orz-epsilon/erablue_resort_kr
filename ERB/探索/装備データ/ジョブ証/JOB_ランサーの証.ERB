@証関数_ランサーの証(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード

SELECTCASE 処理モード
	CASE "ジョブ証種別"
		証関連文字列受け渡し = 通常ジョブ
	CASE "証名"
		TSTR:コマンド名受渡 = 랜서의 증표
	CASE "クラス名"
		SIF 対象キャラ <= 0
			RETURN 0
		SELECTCASE GET_JOBSKILL_LEVEL(対象キャラ)
			CASE IS >= 3
				RESULTS = ヴァルキュリア（未実装）
				RESULTS:1 = Class.III
				RETURN 1
			CASE 2
				RESULTS = ドラグーン（未実装）
				RESULTS:1 = Class.II
				RETURN 1
			CASEELSE
				RESULTS = ランサー
				RESULTS:1 = Class.I
				RETURN 1
		ENDSELECT
	CASE "フレーバー"
		詳細文文字列受け渡し変数 '= "창이나 도끼를 능숙하게 휘두르며 유연하게 싸우는 창술사의 증표. <br>"
		詳細文文字列受け渡し変数 += "특기 무기에 따라 효과가 변화하는 어빌리티를 가진다. <br>"
		詳細文文字列受け渡し変数 += "손에 넣으면 한 알로 두 번 맛있는 기분을 느낄 수 있다. <br>"
ENDSELECT

@証出現率_ランサーの証(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 2
	RETURN 0
SELECTCASE 入手タイプ
	CASE "輸入"
		RETURN 装備レアリティ_Ｎ
ENDSELECT

@証アビ関数_ランサーの証_1(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 武器フラグ
#DIM 効果数値, 2
#DIMS CONST アビ名 = "リードドライブ"
#DIM CONST 消費ＭＰ = 25
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	武器フラグ = 0
	SIF 得意武器判定(対象キャラ, "槍")
		SETBIT 武器フラグ, 0
	SIF 得意武器判定(対象キャラ, "斧")
		SETBIT 武器フラグ, 1
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 250
		効果数値:1 = 3
	CASE 2
		効果数値:0 = 200
		効果数値:1 = 5
	CASEELSE
		効果数値:0 = 150
		効果数値:1 = 6
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　소비 MP:{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　효과: 적 단일 대상에게 자신의 속성 [공격력✕{効果数値:0}％] 데미지를 준다. <br>"
		IF 対象キャラ <= 0 || !GETBIT(武器フラグ, 0)
			詳細文文字列受け渡し変数 += @"　　　　자신의 버프 효과 수가{効果数値:1}이상이고"
		ELSE
			詳細文文字列受け渡し変数 += "　　　　"
		ENDIF
		詳細文文字列受け渡し変数 += "대상의 CT가 0일 경우, 대상에게 1회의 공포 효과를 부여한다. 기본 성공률 100％.<br>"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 += "　　　　(창이 특기 무기일 경우, 항상 발동)<br>"
		ENDIF
		IF 対象キャラ <= 0 || !GETBIT(武器フラグ, 1)
			詳細文文字列受け渡し変数 += @"　　　　대상의 디버프 효과 수가{効果数値:1}이상일 경우,"
		ELSE
			詳細文文字列受け渡し変数 += "　　　　"
		ENDIF
		詳細文文字列受け渡し変数 += "대상에게 슬로우 [2] 효과를 부여한다. 기본 성공률 60％."
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 += "<br>"
			詳細文文字列受け渡し変数 += "　　　　(도끼가 특기 무기인 경우, 항상 발동)"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値:0
		CALL アビテンプレート_ダメージ処理_自属性

		SIF アビテンプレート用_キャンセルフラグ
			RETURN
		SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
			RETURN
		IF (GETBIT(武器フラグ, 0) || 強化数算出(戦闘行動キャラ) >= 効果数値:1) && 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 10):チャージターン == 0
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 공포
			アビ汎用文字列:バフ・デバフ対象能力 = 공포
			アビ汎用変数:持続回数 = 1
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
		IF GETBIT(武器フラグ, 1) || 弱体数算出(アビテンプレート用_対象保存:0) >= 効果数値:1
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 2
			アビ汎用変数:基礎成功確率 = 60
			CALL アビテンプレート_不利効果_スロウ
		ENDIF
ENDSELECT

@証アビ関数_ランサーの証_2(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM 敵番号
#DIM 対象数
#DIM LEVEL
#DIM 武器フラグ
#DIMS 能力名_自己
#DIMS 能力名_追加
#DIM 効果数値, 1
#DIMS CONST アビ名 = "デュアルインパルス"
#DIM CONST 消費ＭＰ = 15
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	武器フラグ = 0
	IF 得意武器判定(対象キャラ, "槍")
		SETBIT 武器フラグ, 0
		SIF 得意武器判定(対象キャラ, "斧")
			SETBIT 武器フラグ, 1
	ELSEIF 得意武器判定(対象キャラ, "斧")
		SETBIT 武器フラグ, 1
		SIF 得意武器判定(対象キャラ, "槍")
			SETBIT 武器フラグ, 0
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("連撃")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		能力名_自己 '= "TA율"
		能力名_追加 '= "連続攻撃率"
		効果数値:0 = 15
	CASE 2
		能力名_自己 '= "DA율"
		能力名_追加 '= "DA율"
		効果数値:0 = 25
	CASEELSE
		能力名_自己 '= "DA율"
		能力名_追加 '= "DA율"
		効果数値:0 = 20
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%(장비자가 창 특기or도끼 특기or연격 타입일 때)<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　소비 MP:{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　효과: 자신의%能力名_自己%을(를) 2턴 [ +100％/C 범위] 한다. <br>"
		IF 対象キャラ <= 0 || !GETBIT(武器フラグ, 0)
			詳細文文字列受け渡し変数 += "　　　　자신의 버프 효과 수가 4 이상인 경우,"
		ELSE
			詳細文文字列受け渡し変数 += "　　　　"
		ENDIF
		詳細文文字列受け渡し変数 += @"아군 전체의%能力名_追加%을(를) 3턴 [ +{効果数値:0}％/A 범위] 한다. <br>"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 += "　　　　(창이 특기 무기인 경우, 항상 발동)<br>"
		ENDIF
		IF 対象キャラ <= 0 || !GETBIT(武器フラグ, 1)
			詳細文文字列受け渡し変数 += "　　　　디버프 효과 수가 4 이상인 적의"
		ELSE
			詳細文文字列受け渡し変数 += "　　　　적 전체의"
		ENDIF
		詳細文文字列受け渡し変数 += @"%能力名_追加%을(를) 3턴 [ -{効果数値:0}％/B 범위] 한다. 기본 성공률 90％. "
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 += "<br>"
			詳細文文字列受け渡し変数 += "　　　　(도끼가 특기 무기인 경우, 항상 발동)"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = %能力名_自己%C
		アビ汎用文字列:バフ・デバフ対象能力 = %能力名_自己%
		アビ汎用変数:効果量 = 100
		アビ汎用変数:持続ターン = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		IF GETBIT(武器フラグ, 0) || 強化数算出(戦闘行動キャラ) >= 4
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用文字列:バフ・デバフ枠 = %能力名_追加%A
			アビ汎用文字列:バフ・デバフ対象能力 = %能力名_追加%
			アビ汎用変数:効果量 = 効果数値:0
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF

		CALL アビ汎用変数文字列リセット
		対象数 = 0
		FOR 敵番号, 10, 20
			SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
				CONTINUE
			SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
				CONTINUE
			IF GETBIT(武器フラグ, 1) || 弱体数算出(敵番号) >= 4
				CALL アビ対象選択テンプレート_指定(敵番号, 対象数)
				対象数 += 1
			ENDIF
		NEXT
		IF 対象数 > 0
			アビ汎用文字列:バフ・デバフ枠 = %能力名_追加%B
			アビ汎用文字列:バフ・デバフ対象能力 = %能力名_追加%
			アビ汎用変数:効果量 = 効果数値:0
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 90
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT

@証アビ関数_ランサーの証_3(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 1
#DIMS CONST アビ名 = "ランドグリース"
#DIM CONST 消費ＭＰ = 20
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("攻撃")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("奥義")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 25
	CASE 2
		効果数値:0 = 22
	CASEELSE
		効果数値:0 = 20
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= 어빌리티 이름
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%（장비자가 공격 타입or오의 타입일 때）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　소비 MP:{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　효과: 적 전체의 방어력을 3턴［{効果数値:0}％/B枠］감소시킨다. 기본 성공률 80％."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 방어력B
		アビ汎用文字列:バフ・デバフ対象能力 = 방어력
		アビ汎用変数:効果量 = 効果数値:0
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 80
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@証アビ関数_ランサーの証_4(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 3
#DIMS CONST アビ名 = "レゾナンスサージ"
#DIM CONST 消費ＭＰ = 25
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ) - 2
ELSE
	LEVEL = 0
ENDIF
SELECTCASE LEVEL
	CASEELSE
		効果数値:0 = 15
		効果数値:1 = 135
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= 어빌리티 이름
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　소비 MP:{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　효과: 적 전체에 자속성의［공격력✕100％］대미지를 준다. <br>"
		詳細文文字列受け渡し変数 += @"　　　　자신의 어빌리티 대미지를 6턴［{効果数値:0}％/고유 칸］증가시킨다. <br>"
		詳細文文字列受け渡し変数 += @"　　　　이 어빌리티 대미지 증가는［{効果数値:1}％］까지 누적할 수 있다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:基礎成功確率 = 100
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 레조넌스 서지_어빌리티 대미지 보정
		アビ汎用文字列:バフ・デバフ対象能力 = 대미지 보정_어빌리티 대미지
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:累積上限_割合 = 効果数値:1
		アビ汎用変数:持続ターン = 6
		CALL アビテンプレート_有利効果_累積バフ効果セット
ENDSELECT
