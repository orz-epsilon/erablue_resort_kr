
@指輪個別文章表示_白秋の指輪
LOCALS = 
LOCALS += @"■백추의 반지<br><br>"
LOCALS += "어떤 나라의 무녀가 금행의 기운을 담아 만든 반지. <br>"
LOCALS += "가을에 장비함으로써, 전위형 성능을 발휘한다. <br>"
LOCALS += "인생이 안정되고 깊이가 생기는 중년기 인물의 장비에도 적합할 것 같다. <br>"
LOCALS += "<br>"

詳細文文字列受け渡し変数 = %LOCALS%
基礎販売額 = 50000

@指輪基礎効果文章表示_白秋の指輪(指輪番号, 効果番号)
#DIM 効果番号
#DIM 指輪番号
LOCALS = 
LOCALS += "【기초 효과】<br>"
LOCALS += "최대HP+400 최대MP+30 공격력+200 방어력+300 회복력+150 ※조건 있음<br>"

LOCALS:1 = %DT_CELL_GETS("所持指輪データベース", 指輪番号, "オプションステータス補正", 1)%
IF LOCALS:1 != ""
	LOCALS += @"<br>【옵션 효과】<br>%オプション効果表示文字列作成(LOCALS:1)%"
ENDIF

詳細文文字列受け渡し変数 = %LOCALS%

@指輪出現率_白秋の指輪(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 3
	RETURN 0
SELECTCASE 入手タイプ
	CASE "古ぼけた装備"
		RETURN 装備レアリティ_ＳＲ
ENDSELECT

@指輪基礎効果適用判定_白秋の指輪(対象キャラ)
#DIM 対象キャラ
;秋なら無条件でOK
SIF (DAY / 30) % 4 == 2
	RETURN 1
;耳飾りとのセット効果
SIF GROUPMATCH(DT_CELL_GETS("所持耳飾りデータベース", 装備ステータス補正保存:対象キャラ:装備耳飾り番号, "耳飾り名", 1), "桜花の耳飾り", "朝顔の耳飾り", "紅葉の耳飾り", "水仙の耳飾り", "蓮華の耳飾り", "菊花の耳飾り") > 0
	RETURN 1
;壮年・中年
RETURN (TALENT:対象キャラ:年齢層 == 年齢層_壮年 || TALENT:対象キャラ:年齢層 == 年齢層_中年)

@指輪ステータス決定_白秋の指輪(オプション数, 情報取得フラグ = 0, 効果指定 = -1)
#DIM 効果指定
#DIM オプション数
#DIM 情報取得フラグ
#DIMS 基本補正文字列

基本補正文字列 = 최대HP_400_최대MP_30_공격력_200_방어력_300_회복력_150

IF 情報取得フラグ
	詳細文文字列受け渡し変数 = %基本補正文字列%
	;オプション固定用にオプションランクを返す
	RETURN 2
ENDIF

;オプション数に応じてオプション効果をランダムで付加
;基礎指輪なのでLv2のオプションしか付かない
;例外作りたいときは例外の関数作ってもろて

オプション補正文字列 = 
IF オプション数
	FOR LOCAL, 0, オプション数
		CALL オプション効果決定(2)
	NEXT
ENDIF

LOCAL = DT_ROW_ADD("所持指輪データベース", "指輪名", "白秋の指輪", "基礎ステータス補正", 基本補正文字列, "オプションステータス補正", オプション補正文字列)
