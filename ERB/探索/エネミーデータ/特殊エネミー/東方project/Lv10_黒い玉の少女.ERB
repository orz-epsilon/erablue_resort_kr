
@敵BATTLE_STATE_Lv10_黒い球の少女(ARG)
敵BATTLE_STATE:ARG:Lv = 10
敵BATTLE_STATE:ARG:最大ＨＰ = 12000
敵BATTLE_STATE:ARG:ＨＰ = 12000
敵BATTLE_STATE:ARG:攻撃力 = 480
敵BATTLE_STATE:ARG:防御力 = 450
敵BATTLE_STATE:ARG:回復力 = 0
敵BATTLE_STATE:ARG:行動速度 = 400
敵BATTLE_STATE:ARG:DA율 = 0
敵BATTLE_STATE:ARG:TA率 = 0
敵BATTLE_STATE:ARG:命中率 = 70
敵BATTLE_STATE:ARG:回避率 = 0
敵BATTLE_STATE:ARG:属性 = 5
敵BATTLE_STATE:ARG:所持EXP = 650
敵BATTLE_STATE:ARG:所持ルピ = 0
敵BATTLE_STATE:ARG:最大チャージターン = 2

敵BATTLE_STATE:ARG:ボスフラグ = 1
敵BATTLE_STATE:ARG:ランダム不許可フラグ = 1

敵BATTLE_STATE_STR:ARG:種族 = 인간형_요괴

SIF EXIST画像FILE(@"%CSTR:(キャラ検索("[宵闇の妖怪]ルーミア")):画像フォルダ%/顔_デフォルト")
	敵BATTLE_STATE_STR:ARG:ボス画像 = %CSTR:(キャラ検索("[宵闇の妖怪]ルーミア")):画像フォルダ%/顔_デフォルト

@敵討伐後処理_Lv10_黒い球の少女(隊列番号)
#DIM 隊列番号
#DIM ドロップ確率
#DIMS ドロップアイテム
#DIM データベース行数
#DIM トレハンLv

トレハンLv = 0
データベース行数 = デバフ番号取得("ドロップ率UP")
SIF データベース行数 > -1
	トレハンLv = DT_CELL_GET("戦闘効果データベース", データベース行数, "効果量_固定値")


ドロップ確率 = 10 + トレハンLv * 5
IF RAND:100 < ドロップ確率
	リザルト後特殊処理_処理キー = ルーミア招待フラグ解放
ENDIF

@リザルト後特殊処理_ルーミア招待フラグ解放()
#DIMS 立ち絵_ルーミア
#DIM 対象キャラ

対象キャラ = キャラ検索("[宵闇の妖怪]ルーミア")
;既に解放済みなら戻す
SIF CFLAG:対象キャラ:招待不可フラグ == 0
	RETURN 0

;立ち絵リソース記録に必要な画像のリソースを生成する
CALLF 顔グラ表示用文字列関数(対象キャラ,100,,,1,1)
立ち絵_ルーミア = %立ち絵リソース記録%

;会話部分
CALL 口上変数初期化
WSTR:(K++) = ……이런! 검은 구슬의 소녀가 일어나, 적의 없는 눈으로 이쪽을 보고 있다!
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「아야야야…… 너는 잡아먹을 수 있는 인류가 아닌 것 같네.
WSTR:(K++) = 　끔찍한 일을 당했어, 산지는 확인해야겠네」
CALL メッセージ欄表示用関数(立ち絵_ルーミア,"黒い球の少女",,0)
CALL 口上変数初期化
WSTR:(K++) = 산지라니. 이쪽 세계의 인간이 아닌 것은 확실하지만.
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「이세계에서 왔어? 재미있겠네, 나도 그쪽으로 가보고 싶어.
WSTR:(K++) = 　그쪽 세계는 캄캄하지 않으면 좋겠는데」
CALL メッセージ欄表示用関数(立ち絵_ルーミア,"黒い球の少女",,0)
CALL 口上変数初期化
WSTR:(K++) = %CALLNAME:PLAYER%는 인간을 먹지 않는 것과 대가를 지불하는 것을 지킨다면, 리조트를 소개해 줘도 좋다고 말한다.
WSTR:(K++) = 그리고 캄캄한 것은 스스로 어둠을 내고 있기 때문이 아니냐고도 말한다.
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「걱정 마세요. 이세계산 인간은 이제 질렸어요.
WSTR:(K++) = 　그리고, 대가는 돈이겠죠. 인간이 가끔 가지고 있는 빛나는 것이라면 가지고 있는 게 있어요」
CALL メッセージ欄表示用関数(立ち絵_ルーミア,"黒い球の少女",,0)
CALL 口上変数初期化
WSTR:(K++) = 「그런데 이세계 인간이 비식용이라면, 무슨 용도의 인간이야? 애완용?」
CALL メッセージ欄表示用関数(立ち絵_ルーミア,"黒い球の少女",,0)
CALL 口上変数初期化
WSTR:(K++) = 사이좋게 노는 용도일까.
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「헤에, 그런가ー」
CALL メッセージ欄表示用関数(立ち絵_ルーミア,"黒い球の少女",,0)
CALL 口上変数初期化
WSTR:(K++) = [어둠의 요괴]루미아가 리조트를 방문하게 되었다!
CALL メッセージ欄表示用関数(,,,2)
CALL 口上変数初期化
CFLAG:対象キャラ:招待不可フラグ = 0


@敵トリガー行動_Lv10_黒い球の少女
#DIM 攻撃回数
#DIMS 立ち絵_ルーミア

;立ち絵リソース記録に必要な画像のリソースを生成する
CALLF 顔グラ表示用文字列関数(キャラ検索("[宵闇の妖怪]ルーミア"),100,,,1,1)
立ち絵_ルーミア = %立ち絵リソース記録%


IF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ * 2 / 3 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0) == 0

	;顔グラ込みのセリフを出す
	WSTR:(K++) = 「%TEXTR("당신은 먹어도 되는 인간?/스펠 카드, 간다ー/어둠에 삼켜져서…… 뭐였더라?")%」
	CALL メッセージ欄表示用関数(立ち絵_ルーミア,"黒い球の少女",,0)
	CALL 口上変数初期化

	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%의 스펠 카드 선언!
	アビ汎用文字列:実行時メッセージ２ = ──어둠 부적「디머케이숀」──

	CALL アビ対象選択テンプレート_味方全体_生者のみ()
	アビ汎用変数:効果割合 = 150
	CALL アビテンプレート_ダメージ処理_自属性

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 어둠
	アビ汎用文字列:バフ・デバフ対象能力 = 어둠
	アビ汎用変数:効果割合 = 30
	アビ汎用変数:持続ターン = 3
	アビ汎用変数:基礎成功確率 = 60
	CALL アビテンプレート_不利効果_デバフ効果セット

	;トリガーフラグをセット
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0
	;チャージリセット
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0
	
ELSEIF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ * 1 / 3 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1) == 0

	;顔グラ込みのセリフを出す
	WSTR:(K++) = 「%TEXTR("당신은 먹어도 되는 인간?/스펠 카드, 간다ー/어둠에 삼켜져서…… 뭐였더라?")%」
	CALL メッセージ欄表示用関数(立ち絵_ルーミア,"黒い球の少女",,0)
	CALL 口上変数初期化

	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%의 스펠 카드 선언!
	アビ汎用文字列:実行時メッセージ２ = ──밤 부적「나이트 버드」──

	FOR 攻撃回数, 0, 3
		CALL アビ対象選択テンプレート_味方ランダムＸ体(2, 1)
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 어둠
		アビ汎用文字列:バフ・デバフ対象能力 = 어둠
		アビ汎用変数:効果割合 = 30
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 40
		CALL アビテンプレート_不利効果_デバフ効果セット
	NEXT

	;トリガーフラグをセット
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1
	;チャージリセット
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0
ELSE
	;通常攻撃は複数回攻撃
	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%의 통상 탄막!

	FOR 攻撃回数, 0, RAND:2 + 2
		CALL アビ対象選択テンプレート_味方ランダムＸ体(1)
		アビ汎用変数:効果割合 = 80
		アビ汎用文字列:ダメージ種類 = 통상 공격 데미지
		CALL アビテンプレート_ダメージ処理_自属性
	NEXT
	IF デバフ番号取得("恐怖", 戦闘行動キャラ) < 0
		敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン += 1
	ELSE
		;回数消費
		CALL デバフ回数経過処理(戦闘行動キャラ, "恐怖")
	ENDIF
ENDIF

RETURN 1

@敵チャージ行動_Lv10_黒い球の少女
アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%의 스펠 카드 선언!
アビ汎用文字列:実行時メッセージ２ = ──어둠 부적「디머케이숀」──

CALL アビ対象選択テンプレート_味方全体_生者のみ()
アビ汎用変数:効果割合 = 150
CALL アビテンプレート_ダメージ処理_自属性

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 어둠
アビ汎用文字列:バフ・デバフ対象能力 = 어둠
アビ汎用変数:効果割合 = 30
アビ汎用変数:持続ターン = 3
アビ汎用変数:基礎成功確率 = 60
CALL アビテンプレート_不利効果_デバフ効果セット

