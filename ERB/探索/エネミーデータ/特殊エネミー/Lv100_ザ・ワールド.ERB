
@敵BATTLE_STATE_Lv100_ザ・ワールド(ARG)
敵BATTLE_STATE:ARG:Lv = 100
敵BATTLE_STATE:ARG:最大ＨＰ = 360000
敵BATTLE_STATE:ARG:ＨＰ = 360000
敵BATTLE_STATE:ARG:攻撃力 = 1000
敵BATTLE_STATE:ARG:防御力 = 1000
敵BATTLE_STATE:ARG:回復力 = 0
敵BATTLE_STATE:ARG:行動速度 = 1000
敵BATTLE_STATE:ARG:DA率 = 50
敵BATTLE_STATE:ARG:TA率 = 25
敵BATTLE_STATE:ARG:命中率 = 120
敵BATTLE_STATE:ARG:回避率 = 20
敵BATTLE_STATE:ARG:属性 = 6
敵BATTLE_STATE:ARG:所持EXP = 45000
敵BATTLE_STATE:ARG:所持ルピ = 90000
敵BATTLE_STATE:ARG:最大チャージターン = 2

敵BATTLE_STATE:ARG:ボスフラグ = 1
敵BATTLE_STATE:ARG:ランダム不許可フラグ = 1

敵BATTLE_STATE_STR:ARG:種族 = 성정수

@敵討伐後処理_Lv100_ザ・ワールド(隊列番号)
#DIM 隊列番号
#DIM ドロップ確率
#DIMS ドロップアイテム
#DIM データベース行数
#DIM トレハンLv

トレハンLv = 0
データベース行数 = デバフ番号取得("ドロップ率UP")
SIF データベース行数 > -1
	トレハンLv = DT_CELL_GET("戦闘効果データベース", データベース行数, "効果量_固定値")

ドロップ確率 = 100
ドロップアイテム = 世界のイデア
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 50 + トレハンLv * 2
ドロップアイテム = 世界のイデア
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)



@敵トリガー行動_Lv100_ザ・ワールド
#DIM 味方番号
#DIM ダメージ量

IF バフ番号取得_枠("ヴァース・クライシス", 戦闘行動キャラ) > -1
	IF 戦闘中カウント:10:被ダメ合計 > 20000
		WSTR:(K++) = ヴァース・クライシス가 해제되었다!
		
		CALL バフ削除_枠(戦闘行動キャラ, "ヴァース・クライシス")
		RETURN 1
	ELSE
		;『ヴァース・クライシス』：味方全体に現在HP70％分のダメージ/強化効果を全て消去・奥義ゲージDOWN効果
		WSTR:(K++) = 「창조는… 이 손으로 이루어진다!」
		WSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%의 ヴァース・クライシス!
		
		FOR 味方番号, 0, 4
			SIF BATTLE_STATE:味方番号:キャラ登録番号 < 1
				CONTINUE
			SIF BATTLE_STATE:味方番号:ＨＰ < 1
				CONTINUE
			
			ダメージ量 = (BATTLE_STATE:味方番号:ＨＰ / 10) * 7
			
			BATTLE_STATE:味方番号:ＨＰ = MAX(BATTLE_STATE:味方番号:ＨＰ - ダメージ量, 0)
			WSTR:(K++) = %CALLNAME:(BATTLE_STATE:味方番号:キャラ登録番号)%은(는) {ダメージ量}의 피해를 입었다!
			CALL メッセージ欄表示用関数(,,,0)
		NEXT
		
		CALL 口上変数初期化
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		FOR LOCAL, 0, 9
			CALL アビテンプレート_不利効果_バフ消去()
		NEXT
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 100
		CALL アビテンプレート_不利効果_奥義ゲージDOWN
		
		敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0

		行動種別フラグ:戦闘行動キャラ:大技発動 = 1
		CALL バフ削除_枠(戦闘行動キャラ, "ヴァース・クライシス")
		RETURN 1
	ENDIF
ELSEIF バフ番号取得_枠("スターリー・ノヴァ", 戦闘行動キャラ) > -1
	IF 戦闘中カウント:10:被ダメ合計 > 10000
		WSTR:(K++) = スターリー・ノヴァ가 해제되었다!
		
		CALL バフ削除_枠(戦闘行動キャラ, "スターリー・ノヴァ")
		RETURN 1
	ELSE
		;『スターリー・ノヴァ』：味方全体にランダム属性4倍ダメージ/ザ・ワールドに与ダメ増加効果・TA率UP効果
		SELECTCASE RAND:2
			CASE 0
				WSTR:(K++) = 「새로운 이치다…!」
			CASE 1
				WSTR:(K++) = 「어리석은 자에게…… 행복한 죽음을!」
		ENDSELECT
		
		アビテンプレート用_アビ名 = スターリー・ノヴァ
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 400
		SELECTCASE RAND:6
			CASE 0
				CALL アビテンプレート_ダメージ処理_火属性
			CASE 1
				CALL アビテンプレート_ダメージ処理_水属性
			CASE 2
				CALL アビテンプレート_ダメージ処理_土属性
			CASE 3
				CALL アビテンプレート_ダメージ処理_風属性
			CASE 4
				CALL アビテンプレート_ダメージ処理_光属性
			CASE 5
				CALL アビテンプレート_ダメージ処理_闇属性
		ENDSELECT
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 100
		アビ汎用文字列:バフ・デバフ枠 = 주는 피해 증가
		アビ汎用文字列:バフ・デバフ対象能力 = 주는 피해
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 50
		アビ汎用文字列:バフ・デバフ枠 = TA 확률
		アビ汎用文字列:バフ・デバフ対象能力 = TA 확률
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット
		
		敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0
		
		行動種別フラグ:戦闘行動キャラ:大技発動 = 1
		CALL バフ削除_枠(戦闘行動キャラ, "スターリー・ノヴァ")
		RETURN 1
	ENDIF
ELSEIF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < (敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ / 4) * 3 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0) == 0
	;75%で真の力を解放/弱体効果を回復
	WSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%이 진정한 힘을 해방했다!
	
	アビテンプレート用_アビ名 = 真の力解放
	
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	FOR LOCAL, 0, DT_ROW_LENGTH("戦闘効果データベース")
		SIF DT_CELL_GETS("戦闘効果データベース", LOCAL, "適用範囲") == "10"
			CALL アビテンプレート_有利効果_デバフ消去()
	NEXT

	;HPトリガー使い終わったのでフラグを立てる
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0
	RETURN 1
ELSEIF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ / 2 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1) == 0
	;50%で『ヴァース・クライシス』を構え始める
	WSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%의 손바닥에 창조의 빛이 수렴한다…!
	
	アビテンプレート用_アビ名 = 大技待機

	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:特殊表示オプション = 1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = ヴァース・クライシス
	アビ汎用文字列:バフ・デバフ対象能力 = 버스 크라이시스
	アビ汎用変数:持続ターン = -1
	CALL アビテンプレート_有利効果_バフ効果セット
	
	戦闘中カウント:10:被ダメ合計 = 0
	
	;HPトリガー使い終わったのでフラグを立てる
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1
	RETURN 1
ELSEIF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ / 5 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 2) == 0
	;25%で『スターリー・ノヴァ』を構え始める
	WSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%이 별의 힘을 모으기 시작했다…
	
	アビテンプレート用_アビ名 = 大技待機
	
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:特殊表示オプション = 1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = 스탈리 노바
	アビ汎用文字列:バフ・デバフ対象能力 = 스탈리 노바
	アビ汎用変数:持続ターン = -1
	CALL アビテンプレート_有利効果_バフ効果セット
	
	戦闘中カウント:10:被ダメ合計 = 0
	
	;HPトリガー使い終わったのでフラグを立てる
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 2
	RETURN 1
ENDIF



@敵チャージ行動_Lv100_ザ・ワールド
IF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < (敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ / 4) * 3
	SELECTCASE RAND:2
		CASE 0
			SELECTCASE RAND:2
				CASE 0
					WSTR:(K++) = 「새로운 이치다…!」
				CASE 1
					WSTR:(K++) = 「어리석은 자에게……행복한 죽음을!」
			ENDSELECT
			
			アビテンプレート用_アビ名 = スターリー・ノヴァ
			
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用変数:効果割合 = 400
			SELECTCASE RAND:6
				CASE 0
					CALL アビテンプレート_ダメージ処理_火属性
				CASE 1
					CALL アビテンプレート_ダメージ処理_水属性
				CASE 2
					CALL アビテンプレート_ダメージ処理_土属性
				CASE 3
					CALL アビテンプレート_ダメージ処理_風属性
				CASE 4
					CALL アビテンプレート_ダメージ処理_光属性
				CASE 5
					CALL アビテンプレート_ダメージ処理_闇属性
			ENDSELECT
			
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:効果量 = 100
			アビ汎用文字列:バフ・デバフ枠 = 주는 피해 증가
			アビ汎用文字列:バフ・デバフ対象能力 = 주는 피해
			アビ汎用変数:持続ターン = 4
			CALL アビテンプレート_有利効果_バフ効果セット
			
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 50
			アビ汎用文字列:バフ・デバフ枠 = TA율
			アビ汎用文字列:バフ・デバフ対象能力 = TA율
			アビ汎用変数:持続ターン = 4
			CALL アビテンプレート_有利効果_バフ効果セット
		CASE 1
			SELECTCASE RAND:2
				CASE 0
					WSTR:(K++) = 「내가아! 신이다아아아아!」
				CASE 1
					WSTR:(K++) = 「종언의 시간이다아…!」
			ENDSELECT
			
			アビテンプレート用_アビ名 = メビ・コメット
			
			CALL アビ汎用変数文字列リセット
			
			FOR LOCAL, 0, 19
				CALL アビ対象選択テンプレート_味方ランダムＸ体(1)
				アビ汎用変数:効果割合 = 70
				SELECTCASE RAND:6
					CASE 0
						CALL アビテンプレート_ダメージ処理_火属性
					CASE 1
						CALL アビテンプレート_ダメージ処理_水属性
					CASE 2
						CALL アビテンプレート_ダメージ処理_土属性
					CASE 3
						CALL アビテンプレート_ダメージ処理_風属性
					CASE 4
						CALL アビテンプレート_ダメージ処理_光属性
					CASE 5
						CALL アビテンプレート_ダメージ処理_闇属性
				ENDSELECT
			NEXT
			
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用変数:効果量 = 0
			アビ汎用文字列:バフ・デバフ枠 = 어빌리티 봉인
			アビ汎用文字列:バフ・デバフ対象能力 = 어빌리티 봉인
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_デバフ効果セット
			
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 0
			アビ汎用文字列:バフ・デバフ枠 = 오의 봉인
			アビ汎用文字列:バフ・デバフ対象能力 = 오의 봉인
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_デバフ効果セット
	ENDSELECT
ELSE
	SELECTCASE RAND:2
		CASE 0
			WSTR:(K++) = 「새로운 신의 탄생이다…… 깨달아라!」

			アビテンプレート用_アビ名 = セレスティアル・スフィア
			
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用変数:効果割合 = 300
			SELECTCASE RAND:6
				CASE 0
					CALL アビテンプレート_ダメージ処理_火属性
				CASE 1
					CALL アビテンプレート_ダメージ処理_水属性
				CASE 2
					CALL アビテンプレート_ダメージ処理_土属性
				CASE 3
					CALL アビテンプレート_ダメージ処理_風属性
				CASE 4
					CALL アビテンプレート_ダメージ処理_光属性
				CASE 5
					CALL アビテンプレート_ダメージ処理_闇属性
			ENDSELECT
			
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 0
			アビ汎用文字列:バフ・デバフ枠 = 강압
			アビ汎用文字列:バフ・デバフ対象能力 = 강압
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_デバフ効果セット
			
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 50
			アビ汎用文字列:バフ・デバフ枠 = 어둠
			アビ汎用文字列:バフ・デバフ対象能力 = 어둠
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_デバフ効果セット
		CASE 1
			CALL アビ汎用変数文字列リセット
			WSTR:(K++) = 「진리는… 내 손에 있다!」

			アビテンプレート用_アビ名 = ワールズアンサー
			
			FOR LOCAL, 0, 41
				CALL アビ対象選択テンプレート_味方ランダムＸ体(1)
				アビ汎用変数:効果割合 = 30
				SELECTCASE RAND:6
					CASE 0
						CALL アビテンプレート_ダメージ処理_火属性
					CASE 1
						CALL アビテンプレート_ダメージ処理_水属性
					CASE 2
						CALL アビテンプレート_ダメージ処理_土属性
					CASE 3
						CALL アビテンプレート_ダメージ処理_風属性
					CASE 4
						CALL アビテンプレート_ダメージ処理_光属性
					CASE 5
						CALL アビテンプレート_ダメージ処理_闇属性
				ENDSELECT
			NEXT
			
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用変数:効果量 = 50
			アビ汎用文字列:バフ・デバフ枠 = DA율
			アビ汎用文字列:バフ・デバフ対象能力 = DA율
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_デバフ効果セット
			
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 50
			アビ汎用文字列:バフ・デバフ枠 = TA율
			アビ汎用文字列:バフ・デバフ対象能力 = TA율
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_デバフ効果セット
	ENDSELECT
ENDIF



@固有戦闘開始時処理_エネミー_Lv100_ザ・ワールド()
SELECTCASE RAND:2
	CASE 0
		WSTR:(K++) = 「시련의 끝에야말로……!」
	CASE 1
		WSTR:(K++) = 「신세계의 초석이 되어라……!」
ENDSELECT
	
アビテンプレート用_アビ名 = プロジェクション・ワールド

CALL アビ対象選択テンプレート_自己のみ
アビ汎用変数:特殊表示オプション = 1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:効果量 = 0
アビ汎用文字列:バフ・デバフ枠 = プロジェクション・ワールド
アビ汎用文字列:バフ・デバフ対象能力 = プロジェクション・ワールド
アビ汎用変数:持続ターン = 2
CALL アビテンプレート_有利効果_バフ効果セット



@バフ・デバフ特殊表示_プロジェクション・ワールド(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

SETCOLOR カラーパレット("紫")
PRINTFORML ⚠예조 발생 중!⚠
RESETCOLOR
PRINTFORML 프로젝션 월드가 발동 중인 상태 [2턴/삭제 불가]
PRINTFORML ◆공격 행동을 하지 않지만, 턴 종료 시 더 월드의 공격력과 방어력이 누적 상승 [영구/삭제 불가]
SETCOLOR カラーパレット("紫")
PRINTFORML ⚔25000 데미지 or 효과 턴 종료로 해제
RESETCOLOR



@固有行動開始時処理_バフ_プロジェクション・ワールド(バフ・デバフ行数)
#DIM バフ・デバフ行数

IF 戦闘中カウント:10:被ダメ合計 > 500
	WSTR:(K++) = 프로젝션 월드가 해제되었다!
	
	CALL バフ削除_枠(戦闘行動キャラ, "プロジェクション・ワールド")
ELSE
	WSTR:(K++) = 더 월드는 창공을 신세계로 칠하고 있다…
	行動不能フラグ = 1
ENDIF



@固有ターン終了時処理_エネミー_Lv100_ザ・ワールド()
#DIM プロジェクション・ワールド

プロジェクション・ワールド = バフ番号取得_枠("プロジェクション・ワールド", 10)
IF プロジェクション・ワールド > -1
	IF 戦闘中カウント:10:被ダメ合計 > 500
		WSTR:(K++) = 프로젝션 월드가 해제되었다!
		
		CALL バフ削除_枠(戦闘行動キャラ, "プロジェクション・ワールド")
	ELSE
		アビテンプレート用_アビ名 = プロジェクション・ワールド
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果割合 = 50
		アビ汎用変数:累積上限_割合 = 100
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力_P・W
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		CALL アビテンプレート_有利効果_累積バフ効果セット
		
		アビ汎用文字列:バフ・デバフ枠 = 防御力_P・W
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		CALL アビテンプレート_有利効果_累積バフ効果セット
	ENDIF
ENDIF



@バフ・デバフ特殊表示_ヴァース・クライシス(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

SETCOLOR カラーパレット("黄")
PRINTFORML ⚠예조 발생 중!⚠
RESETCOLOR
PRINTFORML 버스 크라이시스를 겨누고 있는 상태 [삭제 불가]
PRINTFORML ◆아군 전체에게 현재 HP 70%만큼의 데미지/강화 효과를 모두 삭제・오의 게이지 DOWN 효과
SETCOLOR カラーパレット("黄")
PRINTFORML ⚔20000 데미지로 해제
RESETCOLOR



@バフ・デバフ特殊表示_スターリー・ノヴァ(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

SETCOLOR カラーパレット("紫")
PRINTFORML ⚠예조 발생 중!⚠
RESETCOLOR
PRINTFORML 스타리 노바를 외우고 있는 상태[삭제 불가]
PRINTFORML ◆아군 전체에게 랜덤 속성 4배 데미지/더 월드에게 주는 데미지 증가 효과・TA율 UP 효과
SETCOLOR カラーパレット("紫")
PRINTFORML ⚔10000 데미지로 해제
RESETCOLOR



@戦闘画面表示_Lv100_ザ・ワールド(レイヤー番号, 隊列番号)
#DIM レイヤー番号
#DIM 隊列番号
#DIM プロジェクション・ワールド
#DIM ヴァース・クライシス
#DIM スターリー・ノヴァ

プロジェクション・ワールド = バフ番号取得_枠("プロジェクション・ワールド", 10)
IF プロジェクション・ワールド > -1
	GSETBRUSH レイヤー番号, 0xFFC070C0
	GDRAWTEXT レイヤー番号, "⚠プロジェクション・ワールド", 10, 70
ENDIF

ヴァース・クライシス = バフ番号取得_枠("ヴァース・クライシス", 10)
IF ヴァース・クライシス > -1
	GSETBRUSH レイヤー番号, 0xFFFFD900
	GDRAWTEXT レイヤー番号, "⚠ヴァース・クライシス", 10, 70
ENDIF

スターリー・ノヴァ = バフ番号取得_枠("スターリー・ノヴァ", 10)
IF スターリー・ノヴァ > -1
	GSETBRUSH レイヤー番号, 0xFFC070C0
	GDRAWTEXT レイヤー番号, "⚠スターリー・ノヴァ", 10, 70
ENDIF

