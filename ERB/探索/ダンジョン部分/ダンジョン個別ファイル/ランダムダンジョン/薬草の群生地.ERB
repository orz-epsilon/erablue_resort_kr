
@ダンジョン初期処理_薬草の群生地
;ダンジョン構造の読み込み
;0でない時、そのマスは存在する
;
#DIM 深度
#DIM 分岐
#DIMS 接続先配列,2

VARSET ダンジョン構造配列_薬草の群生地
VARSET 分割ブロックリスト
VARSET ランダムダンジョン配列
ランダムダンジョン中フラグ = 1
CALL ブロック生成(4)
CALL ブロック内部屋生成(8)
CALL 部屋タイプ割り当て_薬草の群生地


ダンジョンサブタイトル = FOREST OF HERBS
ダンジョンマス画像URL_伏せ状態 = 壁_森
ダンジョン現在位置:0 = ランダムダンジョン初期位置:0
ダンジョン現在位置:1 = ランダムダンジョン初期位置:1
ダンジョン背景画像 = 背景_森

;スタート地点は必ず存在
;初期からオープン済みなので3を入れる
;ビット0：存在するか　ビット1：移動できるか　ビット2：そこから通路を伸ばすか　ビット3：入れないが表示する場合に使用
;ビット4以降：自由枠フラグ

IF GETVAR(@"ダンジョン構造配列_%ダンジョン名%:{ランダムダンジョン初期位置:0}:{ランダムダンジョン初期位置:1}") == 0
	SETVAR @"ダンジョン構造配列_%ダンジョン名%:{ランダムダンジョン初期位置:0}:{ランダムダンジョン初期位置:1}", 3
ENDIF


FOR 深度, 1, 100
	LOCAL = 0
	FOR 分岐, 0, 9
		IF ダンジョン構造配列_薬草の群生地:深度:分岐
			;既にマス情報が入ってるなら飛ばす
			LOCAL = 1
			CONTINUE
		ELSEIF ENUMFUNCBEGINSWITH(@"ダンジョンマス情報_薬草の群生地_{深度}_{分岐}")
			LOCAL = 1
			ダンジョン構造配列_薬草の群生地:深度:分岐 = 1
		ENDIF
	NEXT
	IF LOCAL
		ダンジョン最大深度 = 深度
	ENDIF
NEXT

@ダンジョン解説文_薬草の群生地

詳細文文字列受け渡し変数 = 
詳細文文字列受け渡し変数 += "리조트 섬에서 발견된 생명력 넘치는 숲. <br>"
詳細文文字列受け渡し変数 += "들어설 때마다 모습을 바꾸는 모습은 마치 던전과 같다. <br>"
詳細文文字列受け渡し変数 += "대형 생물은 서식하지 않아, 차분하게 채집을 할 수 있다. <br>"
詳細文文字列受け渡し変数 += "들어갈 때마다 구조가 변화한다. <br>"

ランダムダンジョンフラグ = 1

;推奨レベルを返す
RETURN 1

@敵討伐後処理_ダンジョン固有_薬草の群生地(討伐エネミー数)
;ダンジョン内で戦闘した後に呼ばれる関数、主にダンジョンごとのドロップアイテム取得に使用
;まだ敵データはリセットしていないので敵BATTLE_STATEで色々情報は取得可能
#DIM 討伐エネミー数
#DIM ドロップ確率
#DIMS ドロップアイテム

ドロップ確率 = 20 * 討伐エネミー数
ドロップアイテム = 薬草
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 10 * 討伐エネミー数
ドロップアイテム = 普通の木材
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 10 * 討伐エネミー数
ドロップアイテム = 普通の野菜
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 10 * 討伐エネミー数
ドロップアイテム = 雑穀
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 5 * 討伐エネミー数
ドロップアイテム = 星晶の破片
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 1 * 討伐エネミー数
ドロップアイテム = 赤銅の古紋
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

;----------
;部屋の内容を割り当てる
;----------
@部屋タイプ割り当て_薬草の群生地
;部屋の属性を調べて対応する条件の部屋からランダムに割り当てる
;ランダムダンジョン配列:深度:分岐:イベント名 = イベントの名前
#DIM 深度
#DIM 分岐

FOR 深度 ,0 ,10
	FOR 分岐 ,0 ,10
		SIF ランダムダンジョン配列:深度:分岐:イベント状態 == ""
			CONTINUE
		IF 深度 == ランダムダンジョン初期位置:0 && 分岐 == ランダムダンジョン初期位置:1
			ランダムダンジョン配列:深度:分岐:イベント名 = 初期マス
		;初期位置
			CONTINUE
		ELSEIF ランダムダンジョン配列:深度:分岐:接続先2 != ""
			ランダムダンジョン配列:深度:分岐:イベント名 = 隠し通路発見
			CONTINUE
		ELSEIF ランダムダンジョン配列:深度:分岐:接続先1 == ""
		;行き止まり
			LOCAL = RAND:100
			SELECTCASE LOCAL
			;50%で群生地
				CASE IS < 50
					ランダムダンジョン配列:深度:分岐:イベント名 = 群生地
					ランダムダンジョン配列:深度:分岐:マス画像名 = 宝箱
				CASEELSE
					ランダムダンジョン配列:深度:分岐:イベント名 = 行き止まり
			ENDSELECT
		ELSE
			ランダムダンジョン配列:深度:分岐:イベント名 = 何もなし
		ENDIF
	NEXT
NEXT


;----------
;マスタイプ
;----------
@マスタイプ_薬草の群生地(深度,分岐)
#DIM 深度
#DIM 分岐
SELECTCASE ランダムダンジョン配列:深度:分岐:マス画像名
	;マスタイプで分岐
	CASE IS == "宝箱"
		ダンジョンマス画像URL = 床_草原
		IF ランダムダンジョン配列:深度:分岐:イベント状態 == "箱空け"
			ダンジョンマス画像URL_重ね表示 = 宝箱アイコン_木箱_開
		ELSE
			ダンジョンマス画像URL_重ね表示 = 宝箱アイコン_木箱
		ENDIF
	CASE IS == ""
		ダンジョンマス画像URL = 床_草原
	CASEELSE
		ダンジョンマス画像URL = 床_草原
ENDSELECT



;----------
;イベント内容
;----------
@イベント_薬草の群生地_初期マス
ダンジョンクリアフラグ_薬草の群生地 = 1
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = 이 근처에는 채집할 만한 것이 없는 것 같다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	KSTR:(K++) = 숲에 들어서자마자 얼마 지나지 않아 식생이 변했다.
	KSTR:(K++) = 이 근처에서 목표로 하는 약초를 얻을 수 있을지도 모른다.
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画


@イベント_薬草の群生地_群生地
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = 이 근처에서 채집할 만한 약초는 더 이상 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 완만하게 경사진 곳으로 나왔다.
	KSTR:(K++) = ...사방이 약초 군락지다!!
	KSTR:(K++) = %CALLNAME:PLAYER%은(는) 약초 10개를 발견했다!
	CALL メッセージ欄表示用関数()
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = 箱空け
	CALL 画面再描画
	CALL 口上変数初期化
	CALL アイテム増減汎用処理("薬草", 10)
ENDIF


@イベント_薬草の群生地_行き止まり
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = 이 근처에는 채집할 만한 것이 없는 것 같다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	KSTR:(K++) = 시야가 트인 곳으로 나왔다.
	KSTR:(K++) = 숲이라고 부를 수 있는 곳은 이 근처까지일 것이다.
	CALL メッセージ欄表示用関数()
	CALL 口上変数初期化
ENDIF
CALL 画面再描画


@イベント_薬草の群生地_何もなし
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = 이 근처에는 채집할 만한 것이 없는 것 같다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	KSTR:(K++) = 숲속을 탐색하다 보니, 조금 트인 곳으로 나왔다.
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画


@イベント_薬草の群生地_隠し通路発見
#DIMS 接続先配列,2

IF 初来訪マス判定用フラグ == 0
;二回目の時
	IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "隠し通路"
	;隠し通路発見イベント
		KSTR:(K++) = 주의 깊게 주변을 둘러보니, 빠져나갈 수 있을 것 같은 틈이 있었다!
		CALL メッセージ欄表示用関数()
		CALL 口上変数初期化
		ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = 隠し通路済
		CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
		;すべての通路を発見
	ELSE
	;隠し通路を発見済み
			KSTR:(K++) = 이 근처에는 채취할 만한 것이 없는 것 같다.
		CALL メッセージ欄表示用関数()
		CALL 口上変数初期化
		;なにもない・・・
	ENDIF
ELSE
;初訪問の時
	KSTR:(K++) = 숲속을 탐색하다 보니, 조금 트인 곳으로 나왔다.
	CALL メッセージ欄表示用関数()
	CALL 口上変数初期化
	;普通のイベント
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = 隠し通路
	FOR LOCAL, 4, 100
		;一つ通路を隠して通路開放
		IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):LOCAL == ""
			BREAK
		ENDIF
			SPLIT ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):LOCAL, "_", 接続先配列
			SETBIT ダンジョン構造配列_薬草の群生地:(TOINT(接続先配列:0)):(TOINT(接続先配列:1)), 1
		NEXT
ENDIF

