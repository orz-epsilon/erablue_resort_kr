
@イベント_リゾート島開拓作業_ランダム_調合知識イベ１
CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 점은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 이상한 모양의 잎을 단 관목 군락지를 발견했다.
	CALL 知識技能所持者検索("調合知識", 1)
	LOCAL = RESULT
	IF LOCAL > -1
		WSTR:(K++) = %CALLNAME:LOCAL%는 그 잎이 포션에 사용되는 약초가 된다는 것을 깨달았다.
		WSTR:(K++) = 이곳에서 조제하면 즉석 포션으로 사용할 수 있을 것이다.
		WSTR:(K++) = 오래 보관할 수 없는 약초이므로 이 탐색 중에만 사용할 수 있지만, 충분히 도움이 될 것이다.
		LOCAL:1 = RAND:3 + 1
		KSTR:(K++) = %CALLNAME:PLAYER%들은 「즉석 올 포션」을 {LOCAL:1}개 얻었다!
		CALL アイテム増減汎用処理("即席オールポーション", LOCAL:1)
		ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	ELSE
		WSTR:(K++) = %CALLNAME:PLAYER%들은 그 이상은 알 수 없었다.
		KSTR:(K++) = 조합 지식이 있었다면 어떤 나무인지 알 수 있었을 텐데……。
	ENDIF
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_調合知識イベ２
CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 점은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 이상한 모양의 잎을 단 관목 군락지를 발견했다.
	CALL 知識技能所持者検索("調合知識", 1)
	LOCAL = RESULT
	IF LOCAL > -1
		WSTR:(K++) = %CALLNAME:LOCAL%는 그 나무가 마력을 저장하는 열매를 맺고 있다는 것을 깨달았다.
		WSTR:(K++) = 수확하면 바로 마력이 흩어져 버리지만, 이곳에서 회복하는 데는 문제없다.
		LOCAL:1 = RAND:3 + 1
		KSTR:(K++) = %CALLNAME:PLAYER%들의 MP가 {LOCAL:1 * 50} 회복했다!
		CALL 探索中全体回復_ＭＰ(LOCAL:1 * 50)
		ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	ELSE
		WSTR:(K++) = %CALLNAME:PLAYER%들은 그 이상은 알 수 없었다.
		KSTR:(K++) = 조합 지식이 있었다면 어떤 나무인지 알 수 있었을 텐데……。
	ENDIF
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_調合知識イベ３
CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 점은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 묘한 형태의 잎을 단 관목 군락지를 발견했다.
	CALL 知識技能所持者検索("調合知識", 1)
	LOCAL = RESULT
	IF LOCAL > -1
		WSTR:(K++) = %CALLNAME:LOCAL%은(는) 그 잎이 일시적인 마력 향상 약이 된다는 것을 깨달았다.
		WSTR:(K++) = 조합한 약을 마시자, 너희들의 몸에 힘이 넘쳐흐른다!
		KSTR:(K++) = %CALLNAME:PLAYER%들의 오의가 즉시 발동 가능해졌다!
		CALL 探索中全体回復_奥義ゲージ(100)
		ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	ELSE
		WSTR:(K++) = %CALLNAME:PLAYER%들에게는 그 이상의 것은 알 수 없었다.
		KSTR:(K++) = 조합 지식이 있었다면 어떤 수목인지 알았을지도 모르지만……
	ENDIF
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_魔法知識イベ１
CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 점은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 바위 그림자에서 마력을 느끼는 돌을 발견했다.
	KSTR:(K++) = 주워볼까요?
	KSTR:(K++) = BUTTON_[예]
	KSTR:(K++) = BUTTON_[아니요]
	CALL メッセージ欄表示用関数(,,,0)
	CALL 口上変数初期化
	$LOOP_MESSAGE
	BINPUTS
	SELECTCASE RESULTS
		CASE "[예]"
			CALL 知識技能所持者検索("魔法知識", 1)
			LOCAL = RESULT
			IF LOCAL > -1
				WSTR:(K++) = %CALLNAME:LOCAL%이(가) 감정한 결과, 아무래도 기공정의 동력이 되는 바람의 마석인 것 같다.
				WSTR:(K++) = 가져가서 팔면 꽤 괜찮은 돈이 될 것이다!
				KSTR:(K++) = %CALLNAME:PLAYER%들은 5만 루피를 손에 넣었다.
				MONEY += 50000
			ELSE
				WSTR:(K++) = 무엇인지는 모르겠지만, 아름다운 마석이다.
				WSTR:(K++) = 가져가서 팔면 현금화할 수 있을지도 모른다.
				KSTR:(K++) = %CALLNAME:PLAYER%들은 1만 루피를 손에 넣었다.
				MONEY += 10000
			ENDIF
			ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
		CASE "[아니요]"
			WSTR:(K++) = 섣불리 만지면 무슨 일이 일어날지 몰라 위험하다. 내버려 두자.
			KSTR:(K++) = 마법 지식이 있었다면 이용할 수 있었을지도 모르지만…… .
		CASEELSE
			CLEARLINE 1
			REUSELASTLINE
			GOTO LOOP_MESSAGE
	ENDSELECT
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_魔法知識イベ２
#DIM ダメージ量

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 모습은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 바위 그늘에서 마력을 느끼는 돌을 발견했다.
	KSTR:(K++) = 주워 볼까요?
	KSTR:(K++) = BUTTON_[예]
	KSTR:(K++) = BUTTON_[아니요]
	CALL メッセージ欄表示用関数(,,,0)
	CALL 口上変数初期化
	$LOOP_MESSAGE
	BINPUTS
	SELECTCASE RESULTS
		CASE "[예]"
			CALL 知識技能所持者検索("魔法知識", 1)
			LOCAL = RESULT
			IF LOCAL > -1
				WSTR:(K++) = 가까이 가보니, %CALLNAME:LOCAL%는 그것이 불의 마석이라는 것을 깨달았다.
				WSTR:(K++) = 무심코 만졌다면 불꽃이 뿜어져 나와 다쳤을 것이다.
				WSTR:(K++) = %CALLNAME:LOCAL%는 마석에서 불필요한 마력을 제거하고, 마석을 간단한 도구로 가공했다.
				LOCAL:1 = RAND:3 + 1
				KSTR:(K++) = %CALLNAME:PLAYER%들은 「불의 마석」을 {LOCAL:1}개 손에 넣었다!
				CALL アイテム増減汎用処理("火の魔石", LOCAL:1)
			ELSE
				WSTR:(K++) = 가까이 가보니, 갑자기 마석에서 불꽃이 뿜어져 나왔다!!
				ダメージ量 = ＰＴ内最大レベル算出() * 20
KSTR:(K++) = %CALLNAME:PLAYER%일행 전원의 HP에{ダメージ量}의 데미지!

				CALL メッセージ欄表示用関数()
				CALL 口上変数初期化
				CALL 探索中全体ダメージ_ＨＰ_致死(ダメージ量)

				WSTR:(K++) = 아무래도 불의 마석에 마력이 너무 많이 쌓여있었던 것 같다.
				WSTR:(K++) = 마력을 다 써버린 마석은 그저 돌멩이가 되어버렸다……!
				KSTR:(K++) = 마법 지식이 있었다면 막을 수 있었을지도 모르지만…… 마음을 다잡고 너희는 앞으로 나아간다.
			ENDIF
			ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
		CASE "[아니요]"
			WSTR:(K++) = 섣불리 만지면 무슨 일이 일어날지 몰라 위험하다. 내버려 두자.
			KSTR:(K++) = 마법 지식이 있었다면 이용할 수 있었을지도 모르지만……!
		CASEELSE
			CLEARLINE 1
			REUSELASTLINE  
			GOTO LOOP_MESSAGE
	ENDSELECT
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_魔法知識イベ３
CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 딱히 특이한 점은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 바위 그늘에서 마력을 느끼는 돌을 발견했다.
	KSTR:(K++) = 주워볼까요?
	KSTR:(K++) = BUTTON_[예]
	KSTR:(K++) = BUTTON_[아니요]
	CALL メッセージ欄表示用関数(,,,0)
	CALL 口上変数初期化
	$LOOP_MESSAGE
	BINPUTS
	SELECTCASE RESULTS
		CASE "[예]"
			CALL 知識技能所持者検索("魔法知識", 1)
			LOCAL = RESULT
			IF LOCAL > -1
				WSTR:(K++) = 가까이 다가가 보니, %CALLNAME:LOCAL%은(는) 그것이 빛의 마석이라는 것을 깨달았다.
				WSTR:(K++) = 생명을 관장하는 마력이 치유의 힘을 크게 증가시킨다……!
				WSTR:(K++) = %CALLNAME:LOCAL%은(는) 마석의 마력을 사용하여, 전투 불능을 포함한 전원의 HP를 100 회복했다!
				CALL 探索中全体回復_ＨＰ_蘇生(100)
			ELSE
				WSTR:(K++) = 가까이 다가가 보니, 갑자기 마석에서 따뜻한 빛이 솟아났다.
				WSTR:(K++) = 빛에 휩싸인 너희들의 상처가 치유되어 간다……。
				KSTR:(K++) = %CALLNAME:PLAYER%들의 HP가 100 회복되었다!
				CALL メッセージ欄表示用関数()
				CALL 探索中全体回復_ＨＰ(100)
				CALL 口上変数初期化
				WSTR:(K++) = 아무래도 빛의 마석에 마력이 너무 많이 쌓여있었던 것 같다.
				WSTR:(K++) = 마력을 다 써버린 마석은 그저 돌멩이가 되어버렸다……。
				KSTR:(K++) = 마법 지식이 있었다면 더 유용하게 쓸 수 있었을지도 모르지만…… 마음을 다잡고 너희들은 앞으로 나아간다.
			ENDIF
			ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
		CASE "[아니요]"
			WSTR:(K++) = 섣불리 건드리면 무슨 일이 일어날지 몰라 위험하다. 내버려 두자.
			KSTR:(K++) = 마법 지식이 있었다면 이용할 수 있었을지도 모르지만……。
		CASEELSE
			CLEARLINE 1
			REUSELASTLINE  
			GOTO LOOP_MESSAGE
	ENDSELECT
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_機械知識イベ１
#DIMS ドロップアイテム

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 점은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 땅에 쓰러져 썩어가는 아이언 골렘을 발견했다.
	KSTR:(K++) = 가까이 가 보시겠습니까?
	KSTR:(K++) = BUTTON_[예]
	KSTR:(K++) = BUTTON_[아니요]
	CALL 知識技能所持者検索("機械知識", 1)
	LOCAL = RESULT
	IF LOCAL > -1
		KSTR:(K++) = BUTTON_[%CALLNAME:LOCAL%이(가) 멀리서 관찰해 본다]
	ELSE
		LOCAL = 0
	ENDIF
	CALL メッセージ欄表示用関数(,,,0)
	CALL 口上変数初期化
	$LOOP_MESSAGE
	BINPUTS
	SELECTCASE RESULTS
		CASE "[예]"
			IF RAND:2
				WSTR:(K++) = 가까이 가서 자세히 보니, 썩은 것은 표면뿐이고 내부의 기구는 아직 사용할 수 있을 것 같다.
				WSTR:(K++) = 분해해서 상태가 좋은 부품을 회수하자.
				ドロップアイテム = %ランダム素材名("ダンジョン素材", RAND:3)%
				LOCAL = RAND:3 + 2
				WSTR:(K++) = %CALLNAME:PLAYER%은(는) %ドロップアイテム%을(를) {LOCAL}개 얻었다!
				CALL 素材入手処理(ドロップアイテム, LOCAL)
			ELSE
				WSTR:(K++) = 가까이 다가가자, 갑자기 골렘의 기체가 이상한 소리를 내며 폭발했다!
				KSTR:(K++) = %CALLNAME:PLAYER%들의 HP가 {ダンジョン階層記録 * 50} 감소했다!
				CALL メッセージ欄表示用関数()
				CALL 探索中全体ダメージ_ＨＰ_非致死(ダンジョン階層記録 * 50)
				CALL 口上変数初期化
				WSTR:(K++) = 아무래도 썩어 보이는 것 같았지만, 동력부만은 살아있었던 모양이다.
				WSTR:(K++) = 부품 종류도 날아가 버렸고, 회수할 만한 부품은 남아있지 않다.
				KSTR:(K++) = 수확이 없었던 것을 아쉬워하며, 마음을 다잡고 너희는 앞으로 나아간다.
			ENDIF
			ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
		CASE "[아니요]"
			WSTR:(K++) = 섣불리 건드리면 무슨 일이 일어날지 몰라 위험하다. 내버려 두자.
		CASE @"[%CALLNAME:LOCAL%이(가) 멀리서 관찰해 본다]"
			IF RAND:2
				WSTR:(K++) = 노출된 기계 부분은 빛을 잃었고, 귀를 기울여 봐도 구동음은 들리지 않는다…
				WSTR:(K++) = %CALLNAME:LOCAL%은(는) 저 골렘은 안전하다고 판단했다.
				WSTR:(K++) = 다시 가까이 다가가자, 썩은 것은 표면뿐이고 내부 기구는 아직 사용할 수 있을 것 같다.
				WSTR:(K++) = 분해해서 상태가 좋은 부품을 회수하자.
				ドロップアイテム = %ランダム素材名("ダンジョン素材", RAND:3)%
				LOCAL = RAND:3 + 2
				WSTR:(K++) = %CALLNAME:PLAYER%은(는) %ドロップアイテム%을(를) {LOCAL}개 얻었다!
				CALL 素材入手処理(ドロップアイテム, LOCAL)
			ELSE
				WSTR:(K++) = 노출된 기계 부분은 빛을 잃었지만, 귀를 기울여 보면 희미하게 구동음이 들린다…
				WSTR:(K++) = %CALLNAME:LOCAL%은(는) 저 골렘은 위험하다고 판단했다.
				KSTR:(K++) = 멀리서 돌을 던져 자극해 보니, 갑자기 골렘의 기체가 이상한 소리를 내며 폭발했다!_PAGE
				WSTR:(K++) = 아무래도 썩어 있는 것처럼 보였지만, 동력부만은 살아있었던 것 같다.
				WSTR:(K++) = 부품 종류도 날아가 버렸고, 회수할 만한 부품은 남아있지 않다.
				KSTR:(K++) = 수확이 없었던 것은 아쉽지만, 안전하게 처리할 수 있었음에 안도하며 너희는 앞으로 나아간다.
			ENDIF
			ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
		CASEELSE
			CLEARLINE 1
			REUSELASTLINE  
			GOTO LOOP_MESSAGE
	ENDSELECT
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_機械知識イベ２
#DIMS ドロップアイテム

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 점은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:PLAYER%들은 야생의 짐승형 골렘과 조우했다.
	WSTR:(K++) = 순간적으로 전투 태세를 취하지만, 골렘은 움직이지 않는다. 아무래도 이쪽을 향한 적의는 없는 것 같다.
	KSTR:(K++) = 가까이 다가가 보시겠습니까?
	KSTR:(K++) = BUTTON_[예]
	KSTR:(K++) = BUTTON_[아니요]
	CALL 知識技能所持者検索("機械知識", 1)
	LOCAL = RESULT
	CALL メッセージ欄表示用関数(,,,0)
	CALL 口上変数初期化
	$LOOP_MESSAGE
	BINPUTS
	SELECTCASE RESULTS
		CASE "[예]"
			WSTR:(K++) = 가까이 다가가 자세히 보니, 램프가 규칙적으로 깜빡이는 것을 알 수 있다.
			IF LOCAL > -1
				WSTR:(K++) = %CALLNAME:LOCAL%가 점멸하는 패턴을 세어보니, 기공정에서 자주 사용되는 통신 신호와 일치하는 것을 알 수 있었다.
				WSTR:(K++) = 해독해 보니…… 「여기 파라 멍멍」이라고 말하고 있는 것 같다…… .
				ドロップアイテム = %TEXTR("渦琥珀/オルディネシュタイン/宝晶原石/星晶の欠片")%
				WSTR:(K++) = 시험 삼아 땅을 파보니, 땅속에서 %ドロップアイテム%가 나왔다!
				KSTR:(K++) = %CALLNAME:LOCAL%들은 %ドロップアイテム%을(를) 1개 손에 넣었다!_PAGE
				CALL 素材入手処理(ドロップアイテム, 1)
				WSTR:(K++) = 골렘은 만족했는지 램프를 끄고, 그 자리에 드러누워 휴식을 취하기 시작했다.
				KSTR:(K++) = 손에 넣은 보석을 감사히 받고, 너희는 골렘에게서 떨어져 앞으로 나아간다.
			ELSE
				WSTR:(K++) = 무언가를 전하려는 것 같지만, %CALLNAME:PLAYER%들에게는 알 수 없었다……
				WSTR:(K++) = 기계 지식이 있는 자라면 알 수 있을지도 모르지만, 지금은 해독할 방법이 없다.
				KSTR:(K++) = 어쩔 수 없이 골렘과 헤어져, 너희는 앞으로 나아간다.
			ENDIF
		CASE "[아니요]"
			WSTR:(K++) = 적대하지 않는다면 굳이 관여할 필요도 없을 것이다.
			KSTR:(K++) = 너희는 골렘에게서 떨어져, 앞으로 나아간다.
		CASEELSE
			CLEARLINE 1
			REUSELASTLINE  
			GOTO LOOP_MESSAGE
	ENDSELECT
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_錬金知識イベ１
#DIMS ドロップアイテム
#DIM 知識２
#DIM 知識１
#DIM 薬効果
#DIM 飲むキャラ

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 모습은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:PLAYER%들이 탐색하고 있자, 숲에서 의문의 노파가 모습을 드러냈다.
	CALL メッセージ欄表示用関数(,,,0)
	CALL 口上変数初期化
	WSTR:(K++) = 「흐흐흐흐…… 좋은 약이 있단다. 마셔볼 테냐?」
	;薬の効果をここで決定しておく
	薬効果 = RAND:4
	;0:ダメージ　1:何も起こらない　2:HP回復　3:MP回復
	飲むキャラ = 味方ランダム選出()
	CALL メッセージ欄表示用関数(,"怪しい老婆",,0)
	CALL 口上変数初期化
	WSTR:(K++) = 노파가 내민 약은 부글부글 거품을 내고 있어, 아무리 봐도 수상하다……
	$選択ループ
	KSTR:(K++) = 약을 마시겠습니까?
	KSTR:(K++) = BUTTON_[예]
	KSTR:(K++) = BUTTON_[아니요]
	CALL 知識技能所持者検索("錬金知識", 2)
	知識２ = RESULT
	CALL 知識技能所持者検索("錬金知識", 1)
	知識１ = RESULT
	IF 知識２ >= 0
		KSTR:(K++) = BUTTON_[약의 효과를 감정한다]
	ELSEIF 知識１ >= 0
		KSTR:(K++) = BUTTON_[약의 효과를 추리한다]
	ENDIF
	CALL メッセージ欄表示用関数(,,,0)
	CALL 口上変数初期化
	$LOOP_MESSAGE
	BINPUTS
	SELECTCASE RESULTS
		CASE "[예]"
			WSTR:(K++) = 「그럼, 꿀꺽 마셔. 한 번에. 휏휏휏……」
			CALL メッセージ欄表示用関数(,"怪しい老婆",,0)
			CALL 口上変数初期化
			SELECTCASE 薬効果
				CASE 0
					WSTR:(K++) = %CALLNAME:(BATTLE_STATE:飲むキャラ:0)%가 꿀꺽 약을 마시자, 씁쓸한 맛과 쓴맛이 입안을 휘젓는다……!
					WSTR:(K++) = %CALLNAME:(BATTLE_STATE:飲むキャラ:0)%의 HP가 1이 되었다!
					CALL メッセージ欄表示用関数(,,,0)
					CALL 口上変数初期化
					BATTLE_STATE:飲むキャラ:ＨＰ = 1
					WSTR:(K++) = %CALLNAME:PLAYER%들이 할머니 쪽을 돌아보자, 어느새인가 어딘가로 사라져 버렸다.
					KSTR:(K++) = 수상한 것은 함부로 마시지 않는 것이 좋다……그런 교훈을 얻고, %CALLNAME:PLAYER%들은 탐색으로 돌아간다.
				CASE 1
					WSTR:(K++) = %CALLNAME:(BATTLE_STATE:飲むキャラ:0)%가 꿀꺽 약을 마시자, 특히 아무 맛도 나지 않는다…….
					WSTR:(K++) = 아니, 그저 색깔이 있는 물 같다.
					CALL メッセージ欄表示用関数(,,,0)
					CALL 口上変数初期化
					WSTR:(K++) = %CALLNAME:PLAYER%들이 할머니 쪽을 돌아보자, 어느새인가 어딘가로 사라져 버렸다.
					KSTR:(K++) = 무엇이었을까……하고 신기하게 생각하며, %CALLNAME:PLAYER%들은 탐색으로 돌아간다.
				CASE 2
					WSTR:(K++) = %CALLNAME:(BATTLE_STATE:飲むキャラ:0)%가 꿀꺽 약을 마시자, 몸의 피로와 통증이 사라져 간다…….
					WSTR:(K++) = %CALLNAME:(BATTLE_STATE:飲むキャラ:0)%의 HP가 완전히 회복되었다!
					CALL メッセージ欄表示用関数(,,,0)
					CALL 口上変数初期化
					BATTLE_STATE:飲むキャラ:ＨＰ = BATTLE_STATE:飲むキャラ:최대HP
					WSTR:(K++) = %CALLNAME:PLAYER%들이 할머니 쪽을 돌아보자, 어느새인가 어딘가로 사라져 버렸다.
					KSTR:(K++) = 감사 인사를 할 틈도 없었던 것을 아쉬워하며, %CALLNAME:PLAYER%들은 탐색으로 돌아간다.
				CASE 3
					WSTR:(K++) = %CALLNAME:(BATTLE_STATE:飲むキャラ:0)%가 꿀꺽 약을 마시자, 온몸에 힘이 솟아오른다!
					WSTR:(K++) = %CALLNAME:(BATTLE_STATE:飲むキャラ:0)%의 오의 게이지가 최대로 되었다!
					CALL メッセージ欄表示用関数(,,,0)
					CALL 口上変数初期化
					BATTLE_STATE:飲むキャラ:奥義ゲージ = BATTLE_STATE:飲むキャラ:게이지최대값
					WSTR:(K++) = %CALLNAME:PLAYER%들이 노파 쪽을 돌아보자, 어느새인가 어딘가로 사라져 버렸다.
					KSTR:(K++) = 감사 인사를 할 틈도 없었던 것을 아쉬워하며, %CALLNAME:PLAYER%들은 탐색으로 돌아간다.
			ENDSELECT
		CASE "[아니요]"
			WSTR:(K++) = 무엇인지 알 수 없는 약을 마실 정도로 %CALLNAME:PLAYER%들은 무모하지 않다.
			KSTR:(K++) = 노파의 제안을 거절하고, 서둘러 그 자리를 떠났다.
		CASE "[약의 효과를 감정한다]"
			SELECTCASE 薬効果
				CASE 0
					WSTR:(K++) = %CALLNAME:知識２%이 약을 자세히 관찰해 본다……
					WSTR:(K++) = 약을 마시면 체력이 크게 깎일 것 같은 기분이 든다……고 %CALLNAME:知識２%은(는) 판단한 것 같다
				CASE 1
					WSTR:(K++) = %CALLNAME:知識２%이 약을 자세히 관찰해 본다……
					WSTR:(K++) = 이것은 약이 아니라 그저 물이 아닌가……라고 %CALLNAME:知識２%은(는) 판단한 것 같다
				CASE 2
					WSTR:(K++) = %CALLNAME:知識２%이 약을 자세히 관찰해 본다……
					WSTR:(K++) = 체력을 크게 회복하는 약인 것 같은 기분이 든다……고 %CALLNAME:知識２%은(는) 판단한 것 같다
				CASE 3
					WSTR:(K++) = %CALLNAME:知識２%이 약을 자세히 관찰해 본다……
					WSTR:(K++) = 오의 게이지를 크게 회복하는 약인 것 같은 기분이 든다……고 %CALLNAME:知識２%은(는) 판단한 것 같다
			ENDSELECT
			CALL メッセージ欄表示用関数(,,,0)
			CALL 口上変数初期化
			GOTO 選択ループ
		CASE "[약의 효과를 추리한다]"
			SELECTCASE 薬効果
				CASE 0, 1
					WSTR:(K++) = %CALLNAME:知識１%이 약을 자세히 관찰해 본다……
					WSTR:(K++) = 별로 좋은 효과는 아닐 것 같은 기분이 든다……고 %CALLNAME:知識１%은(는) 판단한 것 같다
				CASE 2, 3
					WSTR:(K++) = %CALLNAME:知識１%이 약을 자세히 관찰해 본다……
					WSTR:(K++) = 왠지 좋은 효과인 것 같은 기분이 든다……%CALLNAME:知識１%은(는) 판단한 것 같다
			ENDSELECT
			CALL メッセージ欄表示用関数(,,,0)
			CALL 口上変数初期化
			GOTO 選択ループ
		CASEELSE
			CLEARLINE 1
			REUSELASTLINE  
			GOTO LOOP_MESSAGE
	ENDSELECT
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_錬金知識イベ２
#DIMS ドロップアイテム
#DIM 知識１
#DIM 薬効果

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 모습은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:PLAYER%들이 탐색하고 있는데, 갑자기 주위가 짙은 안개에 휩싸여 간다……
	WSTR:(K++) = 날씨가 변한 것일까, 눈 깜짝할 사이에 한 발짝 앞도 보이지 않을 정도로 되어버렸다.
	$選択ループ
	KSTR:(K++) = 어떻게 해야 할까?
	KSTR:(K++) = BUTTON_[일단 나아가 본다]
	KSTR:(K++) = BUTTON_[안개가 갤 때까지 여기서 기다린다]
	CALL 知識技能所持者検索("錬金知識", 1)
	知識１ = RESULT
	IF 知識１ >= 0
		KSTR:(K++) = BUTTON_[연금술로 안개를 걷어낸다]
	ENDIF
	CALL メッセージ欄表示用関数(,,,0)
	CALL 口上変数初期化
	$LOOP_MESSAGE
	BINPUTS
	SELECTCASE RESULTS
		CASE "[일단 나아가 본다]"
			WSTR:(K++) = 앞으로 나아가면 언젠가 안개에서 벗어날 수 있을 것이다. 일단 나아가 본다.
			WSTR:(K++) = …………
			SELECTCASE RAND:3
				CASE 0, 1
					WSTR:(K++) = …………안 돼! 안개 속에서 벗어날 수 없을 것 같다.
					KSTR:(K++) = %CALLNAME:PLAYER%들은 피로로 HP가 100 감소했다.
					CALL メッセージ欄表示用関数()
					CALL 口上変数初期化
					
					CALL 探索中全体ダメージ_ＨＰ_非致死(100)
					GOTO 選択ループ
				CASE 2
					WSTR:(K++) = …………어떻게든 안개 속에서 벗어나는 데 성공한 것 같다.
					KSTR:(K++) = 시야가 좋아진 길과 현재 위치를 지도로 확인하고, %CALLNAME:PLAYER%들은 탐색으로 돌아간다.
			ENDSELECT
		CASE "[안개가 갤 때까지 여기서 기다린다]"
			WSTR:(K++) = 어설프게 돌아다니기보다는, 시간이 걸리더라도 여기서 기다리는 편이 나을 것이다.
			WSTR:(K++) = %CALLNAME:PLAYER%들은 몸을 맞대고, 안개가 갤 때까지 묵묵히 기다린다.
			WSTR:(K++) = …………
			KSTR:(K++) = …………_PAGE
			WSTR:(K++) = …………드디어 안개가 옅어진 것 같다.
			WSTR:(K++) = %CALLNAME:PLAYER%들은 정신적으로 피폐해져, MP가 100 감소했다.
			KSTR:(K++) = 시야가 좋아진 길과 현재 위치를 지도로 확인하고, %CALLNAME:PLAYER%들은 탐색으로 돌아간다.
			CALL メッセージ欄表示用関数()
			CALL 口上変数初期化
			CALL 探索中全体ダメージ_ＭＰ(100)
		CASE "[연금술로 안개를 걷어낸다]"
			WSTR:(K++) = %CALLNAME:知識１%은(는) 주변의 흔한 재료를 사용하여, 안개를 걷어내는 가루약을 정제했다.
			WSTR:(K++) = 만든 가루를 주위에 뿌리자, 바람을 탄 채 공기 중의 수분을 계속 흡착하여 안개가 옅어진다…….`
			WSTR:(K++) = …………탐색에 지장이 없을 정도로 안개가 옅어졌다!
			KSTR:(K++) = 시야가 좋아진 길과 현재 위치를 지도로 확인하고, %CALLNAME:PLAYER%들은 탐색으로 돌아간다.
		CASEELSE
			CLEARLINE 1
			REUSELASTLINE  
			GOTO LOOP_MESSAGE
	ENDSELECT
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_魔物知識イベ１
#DIMS ドロップアイテム
#DIM 知識１
#DIM 薬効果

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 점은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:PLAYER%들이 탐색하던 중, 주변의 나무와 바위에서 큰 상처를 발견했다.
	WSTR:(K++) = 영역을 표시하기 위해 마물이 낸 상처처럼 보이지만, 범위가 넓다…….`
	WSTR:(K++) = 이 근처에 어울리지 않는 강함을 가진 마물이 배회하고 있는 것 같다.
	$選択ループ
	KSTR:(K++) = 어떻게 해야 할까?
	KSTR:(K++) = BUTTON_[방치하면 위험하다. 마물을 찾아 토벌한다]
	KSTR:(K++) = BUTTON_[신중을 기해 일단 이곳에서 벗어난다]
	CALL 知識技能所持者検索("魔物知識", 1)
	知識１ = RESULT
	IF 知識１ >= 0
		KSTR:(K++) = BUTTON_[마물을 매복하여 구제한다]
	ENDIF
	CALL メッセージ欄表示用関数(,,,0)
	CALL 口上変数初期化
	$LOOP_MESSAGE
	BINPUTS
	SELECTCASE RESULTS
		CASE "[방치하면 위험하다. 마물을 찾아 토벌한다]"
			WSTR:(K++) = 방치해서 리조트 쪽으로 나오면 곤란하다.
			WSTR:(K++) = %CALLNAME:PLAYER%들은 주위를 수색하여, 목표로 하는 마물을 찾아냈다.
			KSTR:(K++) = 그대로 흉포한 마물을 상대로 싸움을 건다!
			CALL メッセージ欄表示用関数()

			CALL ランダムエネミーセット関数(2, ダンジョン階層記録 + 5)
			SIF ランダムダンジョン中加護 != ""
				戦闘時_特殊処理フラグ '= ランダムダンジョン中加護
			CALL 雑魚敵遭遇汎用処理
			IF RESULT == -1
				;逃走時
				CALL 口上変数初期化
				CALL 画面再描画
				RETURN 0
			ELSEIF RESULT == 0
				;勝利時
				WSTR:(K++) = %CALLNAME:PLAYER%들은 무사히 마물을 토벌했다.
				KSTR:(K++) = 이것으로 어느 정도 주위는 안전해졌을 것이다. %CALLNAME:PLAYER%들은 탐색으로 돌아갔다.
			ENDIF
		CASE "[신중을 기해 일단 이곳에서 벗어난다]"
			WSTR:(K++) = 아니, 우선 준비를 하자.
			KSTR:(K++) = %CALLNAME:PLAYER%들은 일단 이 장소를 떠나기로 했다.
			CALL メッセージ欄表示用関数()
			CALL 口上変数初期化
			CALL 画面再描画
			RETURN 0
		CASE "[마물을 매복하여 구제한다]"
			WSTR:(K++) = %CALLNAME:知識１%는 주위의 모습을 관찰하여, 마물이 이동하는 루트를 알아냈다.
			WSTR:(K++) = %CALLNAME:PLAYER%들은 그 루트 근처에서 매복……
			KSTR:(K++) = …………마물이 지나가는 순간에 일제 공격을 가했다!_PAGE
			WSTR:(K++) = 마물은 속수무책으로 쓰러져, 구제하는 데 성공했다.
			KSTR:(K++) = 이것으로 어느 정도 주위는 안전해졌을 것이다. %CALLNAME:PLAYER%들은 탐색으로 돌아갔다.
		CASEELSE
			CLEARLINE 1
			REUSELASTLINE  
			GOTO LOOP_MESSAGE
	ENDSELECT
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_魔物知識イベ２
#DIMS ドロップアイテム
#DIM 知識１
#DIM 弱点
#DIM 成功フラグ
#DIM レベルアップフラグ

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 모습은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	弱点 = RAND:2
	WSTR:(K++) = %CALLNAME:PLAYER%들이 탐색하고 있는데, 특징적인 빨강과 파랑의 갑각을 가진 마물이 나타났다!
	WSTR:(K++) = 이 마물은 불, 혹은 물에 약하지만……약점을 잘못 짚으면 강력한 반격을 해오는 습성이 있다.
	$選択ループ
	KSTR:(K++) = 어느 쪽으로 공격해야 할까?
	KSTR:(K++) = BUTTON_[불로 공격이다!]
	KSTR:(K++) = BUTTON_[물로 공격이다!]
	CALL 知識技能所持者検索("魔物知識", 1)
	知識１ = RESULT
	IF 知識１ >= 0
		KSTR:(K++) = BUTTON_[마물 지식으로 약점을 간파한다]
	ENDIF
	CALL メッセージ欄表示用関数(,,,0)
	CALL 口上変数初期化
	$LOOP_MESSAGE
	BINPUTS
	SELECTCASE RESULTS
		CASE "[불로 공격이다!]", "[물로 공격이다!]"
			成功フラグ = 0
			IF RESULTS == "[불로 공격이다!]"
				WSTR:(K++) = %CALLNAME:PLAYER%들은 불 속성으로 공격했다!
				SIF 弱点 == 0
					成功フラグ = 1
			ELSE
				WSTR:(K++) = %CALLNAME:PLAYER%들은 물 속성으로 공격했다!
				SIF 弱点 == 1
					成功フラグ = 1
			ENDIF
			IF 成功フラグ
				WSTR:(K++) = 마물은 큰 비명을 지르며 천천히 그 자리에 쓰러졌다.
				WSTR:(K++) = 아무래도 올바른 약점을 찌를 수 있었던 것 같다.
				KSTR:(K++) = 마물을 쓰러뜨린%CALLNAME:PLAYER%들은 경험치를{ダンジョン階層記録 * 200}얻었다!
				CALL メッセージ欄表示用関数()
				CALL 口上変数初期化
				レベルアップフラグ = 0
				FOR LOCAL, 0, 14
					SIF LOCAL > 3 && LOCAL < 10
						CONTINUE
					SIF BATTLE_STATE:LOCAL:キャラ登録番号 < 1
						CONTINUE
					基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ登録番号):蓄積EXP += ダンジョン階層記録 * 200
					CALL レベルアップ処理(BATTLE_STATE:LOCAL:キャラ登録番号)
					SIF レベルアップフラグ < RESULT
						レベルアップフラグ = RESULT
				NEXT
				IF レベルアップフラグ
					;ステータス代入
					CALL 戦闘初期処理_味方
					CALL メッセージ欄表示用関数(,,,0)
					CALL 口上変数初期化
				ENDIF
				KSTR:(K++) = 이것으로 주변은 어느 정도 안전해졌을 것이다.%CALLNAME:PLAYER%들은 탐색으로 돌아갔다.
			ELSE
				WSTR:(K++) = 마물은 큰 비명을 지르며 흥분한 모습으로%CALLNAME:PLAYER%들을 덮쳐왔다!
				KSTR:(K++) = %CALLNAME:PLAYER%들의 HP가{ダンジョン階層記録 * 30, 4}감소했다!
				CALL メッセージ欄表示用関数()
				CALL 口上変数初期化
				
				CALL 探索中全体ダメージ_ＨＰ_非致死(ダンジョン階層記録 * 30)
				WSTR:(K++) = 그 후, 어떻게든 다른 속성을 사용하여 마물을 쓰러뜨리는 데 성공했다.
				KSTR:(K++) = 뜻밖의 피해를 입고 말았다…… 다음에는 조심하자.%CALLNAME:PLAYER%들은 마음을 다잡고 탐색으로 돌아갔다.
			ENDIF
		CASE "[마물 지식으로 약점을 간파한다]"
			WSTR:(K++) = %CALLNAME:知識１%은(는) 마물의 움직임을 관찰하고, %属性数値文字列変換(弱点)%속성이 약점임을 간파했다!
			WSTR:(K++) = %CALLNAME:PLAYER%들이 올바른 속성으로 공격하자, 마물은 큰 비명을 지르며 천천히 그 자리에 쓰러진다.
			WSTR:(K++) = 마물은 속수무책으로 쓰러져, 구제하는 데 성공했다.
			KSTR:(K++) = 이것으로 주변은 어느 정도 안전해졌을 것이다. %CALLNAME:PLAYER%들은 탐색으로 돌아갔다.
		CASEELSE
			CLEARLINE 1
			REUSELASTLINE  
			GOTO LOOP_MESSAGE
	ENDSELECT
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画


@イベント_リゾート島開拓作業_ランダム_釣り好き１
#DIMS 素材名
#DIM 知識１

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 모습은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:MASTER%들이 탐색하던 중, 맑고 깨끗한 강을 발견했다.
	WSTR:(K++) = 강 속에는 몇 마리의 물고기가 헤엄치고 있는 모습이 보인다. 통통하고 힘차게 살찐 물고기다.
	CALL 知識技能所持者検索("釣り好き", 1)
	知識１ = RESULT
	IF 知識１ >= 0
		WSTR:(K++) = %CALLNAME:知識１%이 가져온 낚싯대를 사용하여, %CALLNAME:MASTER%들은 잠시 동안 낚시를 즐겼다.
		WSTR:(K++) = …………
		WSTR:(K++) = %CALLNAME:知識１%은 충분한 수의 물고기를 낚을 수 있었다!
		素材名 = %ランダム素材名("食物素材", , , "魚")%
		WSTR:(K++) = %素材名%을(를) 3개 손에 넣었다!
		WSTR:(K++) = %CALLNAME:MASTER%들은 물고기를 손질하여 썩지 않도록 보관한 후, 탐색으로 돌아갔다.
		CALL 素材入手処理(素材名, 3)
	ELSE
		WSTR:(K++) = 하지만 오늘은 아무도 낚시 도구를 가져오지 않았다.
		WSTR:(K++) = 낚시를 즐기는 것은 다음 기회로 미루자.
	ENDIF
	CALL メッセージ欄表示用関数(,,,0)
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_料理人１
#DIM 知識１
#DIM 回復量

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 모습은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:MASTER%들이 탐색하던 중, 희귀한 버섯을 발견했다.
	WSTR:(K++) = 맛있다고 평판이 자자하지만, 조리법을 잘못하면 독을 내뿜는다고 한다.
	CALL 知識技能所持者検索("料理人", 2)
	知識１ = RESULT
	IF 知識１ >= 0
		WSTR:(K++) = %CALLNAME:知識１%은(는) 마침, 밑처리 지식이 있는 것 같다.
		WSTR:(K++) = 야외용 조리 기구를 몇 가지 사용하여, 버섯 조리를 시작한다.
		WSTR:(K++) = …………
		WSTR:(K++) = %CALLNAME:知識１%이(가) 만든 버섯 요리에, 모두가 감탄했다!
		回復量 = ダンジョン階層記録 * 50
		WSTR:(K++) = 전원의 HP가 {回復量, 4} 회복되었다!
		CALL メッセージ欄表示用関数(,,,0)
		CALL 口上変数初期化
		CALL 探索中全体回復_ＨＰ(回復量)
		WSTR:(K++) = %CALLNAME:MASTER%들은 허기를 채우고 만족하며, 탐색으로 돌아갔다.
	ELSE
		WSTR:(K++) = 아쉽게도, 아무도 조리법에 대해서는 자세히 모르는 것 같다.
		WSTR:(K++) = 독에 걸려도 곤란하다. 아무것도 하지 않고 탐색으로 돌아가자.
	ENDIF
	CALL メッセージ欄表示用関数(,,,0)
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_高貴１
#DIM 知識１
#DIMS 素材名

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 모습은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:MASTER%들이 탐색하던 중, 로열한 분위기를 풍기는 새와 만났다.
	WSTR:(K++) = 이 새는 마물의 일종으로, 다양한 색으로 빛나는 꼬리 깃털을 펼쳐 로열함을 겨루는 습성이 있는 것 같다.
	KSTR:(K++) = 로열한 새는 %CALLNAME:MASTER%들의 모습을 힐끗 본다._PAGE
	CALL 知識技能所持者検索("高貴", 2)
	知識１ = RESULT
	IF 知識１ >= 0
		WSTR:(K++) = …………새의 시선이 %CALLNAME:知識１%에서 멈춘다.
		WSTR:(K++) = 잠시 빤히 쳐다본 후, 로열한 새는 패배를 인정하듯이 %CALLNAME:知識１%에게 고개를 숙였다.
		WSTR:(K++) = 아무래도 로열함에서 승리한 것 같다.
		WSTR:(K++) = 로열한 새는 승자를 칭찬하듯이, 품속에서 선물을 내밀었다.
		素材名 = %ランダム素材名("贈答アイテム", , , "アクセサリ")%
		CALL 素材入手処理(素材名, 1)
KSTR:(K++) = %CALLNAME:知識１%은(는)%素材名%을(를) 손에 넣었다！_PAGE
		WSTR:(K++) = %CALLNAME:MASTER%들은 떠나가는 로열한 새를 배웅하고, 탐색으로 돌아간다.
	ELSE
		WSTR:(K++) = …………아무래도 로열한 새는 자신이 가장 로열하다는 결론에 도달한 것 같다.
		WSTR:(K++) = %CALLNAME:MASTER%들을 코웃음 치며, 우아하게 어딘가로 떠나간다.
		WSTR:(K++) = 왠지 석연치 않은 기분이 들면서도, %CALLNAME:MASTER%들은 탐색으로 돌아간다.
	ENDIF
	CALL メッセージ欄表示用関数(,,,0)
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_踊り子１
#DIM 知識１
#DIMS 素材名

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 모습은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:MASTER%들이 탐색하고 있는데, 갑자기 숲에서 고블린들이 나타났다!
	WSTR:(K++) = 고블린들은 북을 울리고 피리를 불며, 정열적인 춤으로 미친 듯이 춤춘다!
	KSTR:(K++) = 한바탕 춤을 춘 고블린들은 결정 포즈를 취하며, %CALLNAME:MASTER%들을 도발적으로 바라보았다。_PAGE
	CALL 知識技能所持者検索("踊り子", 2)
	知識１ = RESULT
	IF 知識１ >= 0
		WSTR:(K++) = 대항하여 %CALLNAME:知識１%이(가) 발로 리듬을 타며, 멋진 춤을 선보인다!
		WSTR:(K++) = 고블린들은 %CALLNAME:知識１%의 춤에 맞추듯이 악기를 울리며 함께 춤춘다!
		WSTR:(K++) = …………한바탕 춤을 마친 후, 고블린들은 선전을 칭찬하듯이 선물을 내밀었다.
		素材名 = %ランダム素材名("贈答アイテム", , , "酒")%
		CALL 素材入手処理(素材名, 1)
		KSTR:(K++) = %CALLNAME:知識１%은(는)%素材名%을(를) 손에 넣었다！_PAGE
		WSTR:(K++) = 고블린들은 좋은 미소를 지은 채 숲으로 돌아갔다. 무엇이었을까.
		WSTR:(K++) = %CALLNAME:MASTER%들은 당황하면서도 고블린들을 배웅하고, 탐색으로 돌아간다.
	ELSE
		WSTR:(K++) = …………%CALLNAME:MASTER%들이 움직이지 못하고 있자, 고블린들은 노골적으로 실망한 듯하다.
		WSTR:(K++) = 한숨을 쉬며 어깨를 늘어뜨리고, 터덜터덜 숲속으로 돌아갔다.
		WSTR:(K++) = 왠지 미안한 마음이 들면서, %CALLNAME:MASTER%들은 탐색으로 돌아간다.
	ENDIF
	CALL メッセージ欄表示用関数(,,,0)
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_歴史知識１
#DIM 知識１
#DIMS 素材名

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 모습은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:MASTER%들이 탐색하고 있자, 절벽처럼 되어 있는 지층에서 이상한 돌을 발견했다.
	WSTR:(K++) = 무언가 문양 같은 것이 새겨져 있지만, 마력 등은 느껴지지 않는다.
	KSTR:(K++) = 우연히 만들어진 돌멩이일까?_PAGE
	CALL 知識技能所持者検索("歴史知識", 2)
	知識１ = RESULT
	IF 知識１ >= 0
		WSTR:(K++) = %CALLNAME:知識１%이 돌멩이를 가만히 응시하자……이 문양은 패공 전쟁 시대의 것이라고 간파했다.
		WSTR:(K++) = 당시의 역사를 암호로 기록한 일족이 남긴, 전설의 단편일 것이다.
		WSTR:(K++) = 하늘 세계에서는 모든 곳에서 출토되는 것이지만, 그 성질상, 예지의 전당이 상당한 가격으로 매입하고 있다.
		KSTR:(K++) = 매각하면 용돈벌이 정도의 자금은 될 것이다_PAGE
		WSTR:(K++) = %CALLNAME:MASTER%들은 나중에 매각해 두자, 라고 돌을 품에 넣었다.
		WSTR:(K++) = %CALLNAME:MASTER%은(는) {ダンジョン階層記録 * 150}루피를 손에 넣었다!
		MONEY += ダンジョン階層記録 * 150
	ELSE
		WSTR:(K++) = %CALLNAME:MASTER%들은 특별히 신경 쓰지 않고, 돌멩이를 내던지고 탐색으로 돌아갔다.
	ENDIF
	CALL メッセージ欄表示用関数(,,,0)
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_絵師１
#DIM 知識１
#DIM ダメージ量

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 모습은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:MASTER%들이 탐색하고 있는데, 이리저리 복잡하게 얽힌 지형에 길을 잃고 말았다.
	WSTR:(K++) = 매핑한 지도와 주변을 비교해 보지만, 아무래도 잘못 그린 것 같은 기분이 든다.
	KSTR:(K++) = 다시 한번 주변을 탐색하는 것이 좋을까……?_PAGE
	CALL 知識技能所持者検索("絵師", 2)
	知識１ = RESULT
	IF 知識１ > 0
		WSTR:(K++) = 고민하는 %CALLNAME:MASTER%의 옆에서 %CALLNAME:知識１%이 지도를 보니, 길을 잘못 그렸다는 것을 깨달은 것 같다.
		WSTR:(K++) = %CALLNAME:知識１%은(는) 땅에 스르륵 본보기를 그려, 올바른 지도를 보여주었다.
		WSTR:(K++) = 확인해 보니, 확실히 %CALLNAME:知識１%이(가) 그린 본보기가 맞는 것 같다.
		WSTR:(K++) = %CALLNAME:MASTER%은(는) %CALLNAME:知識１%에게 감사를 표하고, 복잡하게 얽힌 지형을 빠져나와 탐색으로 돌아간다.
	ELSEIF 知識１ == MASTER
		WSTR:(K++) = 다시 한번 %CALLNAME:MASTER%이(가) 지도를 확인하니, 이어지는 길을 잘못 그렸다는 것이 판명되었다.
		WSTR:(K++) = %CALLNAME:MASTER%은(는) 땅에 스르륵 시험 삼아 그려보니, 역시 이쪽이 맞는 것 같다.
		WSTR:(K++) = 다시 지도를 올바르게 고쳐 그리고, %CALLNAME:MASTER%들은 복잡하게 얽힌 지형을 빠져나와 탐색으로 돌아간다.
	ELSE
		WSTR:(K++) = 이대로 나아가다 길을 잃고 조난이라도 당하면 눈 뜨고 볼 수 없다.
		WSTR:(K++) = %CALLNAME:MASTER%들은 주변을 다시 한번 한 걸음 한 걸음 탐색했다……
		ダメージ量 = ダンジョン階層記録 * 10
		KSTR:(K++) = 복잡한 탐색으로 시간을 낭비하여, 전원의 HP가 {ダメージ量, 4} 감소했다.
		;HPバーの表示タイミングを調整するためここで一旦メッセージ表示
		CALL メッセージ欄表示用関数()
		CALL 口上変数初期化
		
		CALL 探索中全体ダメージ_ＨＰ_非致死(ダメージ量)
		WSTR:(K++) = %CALLNAME:MASTER%들은 드디어 완성된 지도를 정리하고, 탐색으로 돌아간다.
	ENDIF
	CALL メッセージ欄表示用関数(,,,0)
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_歌唱技能１
#DIM 知識１
#DIM 知識２
#DIMS 素材名

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 점은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:MASTER%들이 탐색하고 있자, 숲 속 깊은 곳에서 아름다운 노랫소리가 들려왔다.
	WSTR:(K++) = 지금까지 들어본 적 없는 아름답고 매혹적인 노래다.
	KSTR:(K++) = %CALLNAME:MASTER%들은 흥미를 느껴, 노랫소리가 들리는 쪽으로 걸어간다……_PAGE
	CALL 知識技能所持者検索("歌唱技能", 2)
	知識１ = RESULT
	CALL 知識技能所持者検索("歌唱技能", -1)
	知識２ = RESULT
	IF 知識１ >= 0
		WSTR:(K++) = %CALLNAME:知識１%은(는) 위화감을 느끼고, 들려오는 소리에 맞춰 노래하기 시작한다.
		WSTR:(K++) = 그러자 들려오던 노랫소리는 멈추고, 대신 쿡쿡 즐거운 웃음소리가 들려온다.
		素材名 = %ランダム素材名("贈答アイテム", , , "花")%
		CALL 素材入手処理(素材名, 1)
		WSTR:(K++) = 그리고 %CALLNAME:知識１% 위에, 살포시 %素材名%이(가) 떨어졌다.
		KSTR:(K++) = %CALLNAME:知識１%은(는) %素材名%을(를) 손에 넣었다!_PAGE
		WSTR:(K++) = 그 노랫소리는 아마 요정들이 놀고 있는 소리였을 것이다.
		WSTR:(K++) = 이 선물은 함께 노래해 준 것에 대한 보답인 듯하다.
		WSTR:(K++) = %CALLNAME:MASTER%들은 어딘가에 있는 요정들에게 감사를 표하고, 탐색으로 돌아간다.
	ELSEIF 知識２ >= 0
		WSTR:(K++) = %CALLNAME:知識２%은(는) 위화감을 느끼고, 들려오는 소리에 맞춰 노래하기 시작한다.
		WSTR:(K++) = 그러자 들려오던 노랫소리는 멈추고, 대신 고통스러워하는 듯한 비명이 들려왔다!
		WSTR:(K++) = 그 노랫소리는 아마 요정들이 놀고 있는 소리였을 테지만……
		WSTR:(K++) = %CALLNAME:知識２%의 노랫소리를 견디지 못하고 도망쳐 버린 듯하다.
		WSTR:(K++) = %CALLNAME:MASTER%은(는) 어딘가에 있는 요정들에게 마음속으로 사과하고, 탐색으로 돌아간다.
	ELSE
		WSTR:(K++) = 하지만, 아무리 걸어도 노랫소리의 주인은 찾을 수 없다.
		WSTR:(K++) = 그 대신, 어딘가에서 쿡쿡거리는 즐거운 웃음소리가 들려온다.
		WSTR:(K++) = 그 노랫소리는 아마 요정들이 놀고 있는 소리로, 모습을 숨기고 인간을 놀리고 있는 것이리라.
		WSTR:(K++) = %CALLNAME:MASTER%들은 요정이라면 어쩔 수 없다고 포기하고, 탐색으로 돌아간다.
	ENDIF
	CALL メッセージ欄表示用関数(,,,0)
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_経営知識１
#DIM 知識１
#DIMS 素材名
#DIM 品番号

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 모습은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:MASTER%들이 탐색하고 있을 때, 섬 개척 의뢰를 받은 기공사와 만났다.
	WSTR:(K++) = 기공사는 상인을 본업으로 하고 있으며, 의뢰를 받으면서 섬들을 여행하며 상품을 사고팔고 있다고 한다.
	WSTR:(K++) = 관심이 있다면, 비싸지기는 하지만 가지고 있는 약 등을 팔아준다고 한다.
	KSTR:(K++) = 어떻게 할까?
	KSTR:(K++) = BUTTON_[약을 사다]
	KSTR:(K++) = BUTTON_[지금은 필요 없다]
	CALL メッセージ欄表示用関数(,,,0)
	CALL 口上変数初期化
	$LOOP_MESSAGE
	BINPUTS
	SELECTCASE RESULTS
		CASE "[약을 사다]"
			;道具屋処理
			CALL 知識技能所持者検索("経営知識", 2)
			知識１ = RESULT
			IF 知識１ >= 0
				WSTR:(K++) = 상품을 사기 전에, %CALLNAME:知識１%가 상인과 협상을 시작했다.
				WSTR:(K++) = 몇 번의 거래를 한 후, 상품을 깎는 데 성공한 것 같다.
				WSTR:(K++) = 어쩔 수 없다는 표정을 지은 상인은 약을 늘어놓는다.
				CALL メッセージ欄表示用関数(,,,0)
				CALL 口上変数初期化
				品番号 = 0
				汎用アイテム購入用_序文 = ■ダンジョンの商人<br>　「まいど！　たくさん買うてってや！」
				汎用アイテム購入用_品揃え登録:(品番号++) = ポーション
				汎用アイテム購入用_品揃え登録:(品番号++) = マナポーション
				汎用アイテム購入用_品揃え登録:(品番号++) = 胆力の薬
				汎用アイテム購入用_品揃え登録:(品番号++) = 蘇生薬
				汎用アイテム購入用_品揃え登録:(品番号++) = 解毒薬
				CALL 汎用アイテム購入画面処理(0)
			ELSE
				品番号 = 0
				汎用アイテム購入用_序文 = ■ダンジョンの商人<br>　「まいど！　たくさん買うてってや！」
				汎用アイテム購入用_品揃え登録:(品番号++) = ポーション,1600,ルピ
				汎用アイテム購入用_品揃え登録:(品番号++) = マナポーション,12000,ルピ
				汎用アイテム購入用_品揃え登録:(品番号++) = 胆力の薬,1000,ルピ
				汎用アイテム購入用_品揃え登録:(品番号++) = 蘇生薬,6000,ルピ
				汎用アイテム購入用_品揃え登録:(品番号++) = 解毒薬,800,ルピ
				CALL 汎用アイテム購入画面処理(0)
			ENDIF

			WSTR:(K++) = 필요한 물건은 이 정도일까.
			WSTR:(K++) = %CALLNAME:PLAYER%일행은 감사를 표하는 상인과 헤어져 탐색으로 돌아갔다.
		CASE "[지금은 필요 없다]"
			WSTR:(K++) = 지금은 물자에 곤란하지 않다.
			WSTR:(K++) = %CALLNAME:PLAYER%일행은 상인과 헤어져 탐색으로 돌아갔다.
		CASEELSE
			CLEARLINE 1
			REUSELASTLINE  
			GOTO LOOP_MESSAGE
	ENDSELECT
	CALL メッセージ欄表示用関数(,,,0)
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_水泳技能１
#DIM 知識１
#DIMS 素材名

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 모습은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:MASTER%일행이 탐색하던 중, 호수 중앙에 떠 있는 작은 섬을 발견했다.
	WSTR:(K++) = 작은 섬에서는 왠지 모르게 묘한 마력을 느끼는 것 같다.
	KSTR:(K++) = 가능하다면 조사를 해보고 싶지만……_PAGE
	CALL 知識技能所持者検索("水泳技能", 1)
	知識１ = RESULT
	IF 知識１ >= 0
		WSTR:(K++) = %CALLNAME:知識１%은(는) 가벼운 차림이 되어 물속을 휙휙 헤엄쳐 나간다.
		WSTR:(K++) = 잠시 후 작은 섬에 도착하여 주변을 살펴보니…… 특징적인 마력이 담긴 광석을 발견했다.
		WSTR:(K++) = 광석은 약간만 가공하면 귀걸이로 실용할 수 있을 것이다.
		CALL 古ぼけた耳飾り抽選取得処理(ダンジョン階層記録 / 10 + 1)
		KSTR:(K++) = %CALLNAME:知識１%은(는) %RESULTS%을(를) 손에 넣었다！_PAGE
		WSTR:(K++) = 다시 호수를 헤엄쳐, %CALLNAME:知識１%은(는) 강가로 돌아왔다.
		IF 知識１ == MASTER
			WSTR:(K++) = 동료들은 헤엄쳐 준%CALLNAME:知識１%을(를) 위로하고, 탐색으로 돌아간다.
		ELSE
			WSTR:(K++) = %CALLNAME:MASTER%일행은 헤엄쳐 준%CALLNAME:知識１%을(를) 위로하고, 탐색으로 돌아간다.
		ENDIF
	ELSE
		WSTR:(K++) = 하지만 보트나 다리 같은 것은 찾을 수 없다.
		WSTR:(K++) = 아쉽게도 섬으로 건너갈 방법은 없는 것 같다.
		WSTR:(K++) = %CALLNAME:MASTER%들은 작은 섬이 신경 쓰이면서도 포기하고 탐색으로 돌아간다.
	ENDIF
	CALL メッセージ欄表示用関数(,,,0)
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_農業知識１
#DIM 知識１
#DIMS 素材名

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 특별히 달라진 모습은 없다.
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:MASTER%들이 탐색하던 중, 드라이어드의 유체를 발견했다.
	WSTR:(K++) = 드라이어드는 생육 환경에 따라 마물이 되거나, 숲을 지키는 정령이 되기도 한다고 한다.
	KSTR:(K++) = 지금 상태로는 어느 쪽으로 자랄지는 알 수 없지만……。_PAGE
	$選択ループ
	KSTR:(K++) = 드라이어드를 어떻게 해야 할까?
	KSTR:(K++) = BUTTON_[지금 당장 구제한다]
	KSTR:(K++) = BUTTON_[방치한다]
	CALL 知識技能所持者検索("農業知識", 1)
	知識１ = RESULT
	IF 知識１ >= 0
		KSTR:(K++) = BUTTON_[정령이 되도록 환경을 조성한다]
	ENDIF
	CALL メッセージ欄表示用関数(,,,0)
	CALL 口上変数初期化
	$LOOP_MESSAGE
	BINPUTS
	SELECTCASE RESULTS
		CASE "[지금 당장 구제한다]"
			WSTR:(K++) = 마물이 되어 사람을 해치는 존재가 되면 위험하다.
			WSTR:(K++) = %CALLNAME:PLAYER%들은 드라이어드의 유체를 구제하고 탐색으로 돌아갔다.
		CASE "[방치한다]"
			WSTR:(K++) = 아직 아무것도 하지 않은 상대를 도륙하는 것은 어떨까.
			WSTR:(K++) = %CALLNAME:PLAYER%들은 드라이어드의 유체를 그대로 두고 탐색으로 돌아갔다。
		CASE "[정령이 되도록 환경을 조성한다]"
			WSTR:(K++) = %CALLNAME:知識１%는 드라이어드의 정령화에 대해 지식이 있는 것 같다.
			WSTR:(K++) = 아무래도 햇볕이 잘 드는 곳에서, 생육기에 물의 마력을 접종하면 정령이 되기 쉬운 것 같다.
			WSTR:(K++) = %CALLNAME:PLAYER%들은 주위의 나뭇가지들을 치고, 마력을 담은 물을 뿌려준다.
			素材名 = %ランダム素材名("食物素材", , , "野菜")%
			CALL 素材入手処理(素材名, 3)
			WSTR:(K++) = 환경을 정돈할 때, 우연히 자생하고 있는%素材名%을(를) 발견했다. 질도 나쁘지 않은 것 같으니, 가져가자.
			KSTR:(K++) = %CALLNAME:PLAYER%들은 %素材名%을(를) 3개 손에 넣었다!_PAGE
			WSTR:(K++) = 환경을 정돈하니, 왠지 모르게 드라이어드의 유체도 활기차진 것처럼 보인다.
			WSTR:(K++) = 언젠가 정령이 되기를 바라면서, %CALLNAME:PLAYER%들은 탐색으로 돌아갔다.
		CASEELSE
			CLEARLINE 1
			REUSELASTLINE  
			GOTO LOOP_MESSAGE
	ENDSELECT
	CALL メッセージ欄表示用関数(,,,0)
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

