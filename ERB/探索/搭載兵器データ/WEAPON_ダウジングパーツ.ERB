@搭載兵器関数_ダウジングパーツ(処理モード, 兵器id)
#DIMS 処理モード
#DIM 兵器id
#DIM 兵器_공격력補正
#DIM 兵器_명중률補正
#DIM 兵器_공격력変動
#DIM 兵器_명중률変動
#DIM 兵器_残弾種別
#DIM 兵器_残弾数
#DIMS 兵器_カテゴリ
#DIMS 兵器_距離適性
#DIMS 兵器名一時保存
LOCALS = 

SELECTCASE 処理モード
	CASE "兵器登録"
		兵器名一時保存 = ダウジングパーツ
		;공격력補正、명중률補正は「その兵器を使用した時に補正を掛ける数値」
		;공격력変動、명중률変動は「その兵器を装備しているだけで乗機のステータスを変える数値」
		兵器_공격력補正 = 0
		兵器_명중률補正 = 0
		兵器_공격력変動 = 0
		兵器_명중률変動 = 0
		;残弾種別 0:戦闘中回数制限 1:探索中回数制限 2:クールダウンターン発生
		兵器_残弾種別 = 2
		兵器_残弾数 = 2
		;カテゴリ 基本は「機械系」と「魔法系」の二択。自由文字列なので任意のカテゴリ名を突っ込んでも良い
		;距離適性 基本は「近距離」と「遠距離」と「味方支援」の三択。自由文字列なので任意のカテゴリ名を突っ込んでも良い
		;複数カテゴリをもたせる場合は「近距離_遠距離」のように記載
		兵器_カテゴリ = 魔法系
		兵器_距離適性 = 遠距離

		{
		DT_ROW_ADD "所持兵器データベース", "兵器名", 兵器名一時保存, 
			"공격력補正", 兵器_공격력補正, "공격력補正_強化後", 兵器_공격력補正, 
			"명중률補正", 兵器_명중률補正, "명중률補正_強化後", 兵器_명중률補正, 
			"공격력変動値", 兵器_공격력変動, "공격력変動値_強化後", 兵器_공격력変動, 
			"명중률変動値", 兵器_명중률変動, "명중률変動値_強化後", 兵器_명중률変動, 
			"残弾種別", 兵器_残弾種別, "残弾数", 兵器_残弾数, "残弾数_強化後", 兵器_残弾数, 
			"兵器カテゴリ", 兵器_カテゴリ, "兵器距離適性", 兵器_距離適性,
		}
	CASE "フレーバー"
		LOCALS += "마력파를 이용하여 지하에 묻혀 있는 물건을 발견하기 위한 부품.<br>"
		LOCALS += "상대에게 사용하면, 숨겨진 아이템을 발견할 수 있을지도 모른다.<br>"
		LOCALS += "재사용까지의 시간은 짧다.<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "作成素材"
		作成素材登録:0 = 星晶の破片_2
		作成素材登録:1 = 鉄鉱石_2
		作成素材登録:2 = 植物のツタ_1
	CASE "アビ名"
		TSTR:コマンド名受渡 = 다우징 파츠
	CASE "アビ説明文"
		;残弾などの説明文字列は機体側で記述
		LOCALS += "　효과: 적 단일 대상에게 드롭률 UP 효과를 부여한다.<br>"
		LOCALS += "　　　　시프의 증표를 장비한 서브 멤버 탑재 시, 그 인원수만큼 추가로 드롭률 UP 효과를 부여한다.<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		CALL アビテンプレート_不利効果_トレハン
		FOR LOCAL, 10, 14
			SIF BATTLE_STATE:LOCAL:キャラ登録番号 < 1
				CONTINUE
			SIF DT_CELL_GETS("所持ジョブ証データベース", 装備ステータス補正保存:(BATTLE_STATE:LOCAL:キャラ登録番号):装備証番号, "ジョブ証名", 1) == "シーフの証"
				CALL アビテンプレート_不利効果_トレハン
		NEXT
ENDSELECT

;兵装開発では出現しない（入手法：初期から所持）
;@兵器開発対象_ダウジングパーツ(処理モード)
