;-------------------------------------------------
;定住の誘い
;-------------------------------------------------
@COMNAME604
#FUNCTIONS
TSTR:コマンド名受渡 = 정주 권유

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_604
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_604
LOCALS = <br>■정주 권유<br>
LOCALS += "상대를 주거 구역에 정주하지 않겠냐고 권유하는 커맨드. <br>"
LOCALS += "성공 시, 주거 구역에 정주하게 된다. <br>"
LOCALS += "<br>COM 타입: 일상<br>획득 소스: 없음"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM604

;定住エリアが一杯
IF 定住エリア満杯()
	PRINTW 주거 구역은 현재 만원인 것 같다……
	RETURN -1
ENDIF

DRAWLINE
PRINTFORML %CALLNAME:TARGET%을(를) 주거 구역으로 정주하도록 권유하시겠습니까?
PRINTFORML 남은 방 수: {RFLAG:定住枠残りカウント, 3}개
DRAWLINE


PRINTBUTTON "[0]예", 0
PRINTL
PRINTBUTTON "[1]아니요", 1
PRINTL
$INPUT_LOOP
BINPUT

SELECTCASE RESULT
	CASE 0
		;好感度レベル80以上必要
		IF CFLAG:TARGET:好感度レベル < 関係閾値:4
			RFLAG:コマンド結果受渡し変数 = 0
		ELSE
			IF TALENT:TARGET:従業員
				FOR LOCAL, 0, 従業員部屋数
					IF 従業員部屋割り配列:LOCAL == TARGET
						従業員部屋割り配列:LOCAL = 0
						BREAK
					ENDIF
				NEXT
			ELSE
				CALL 滞在者部屋割り配列削除処理(TARGET)
			ENDIF
			RFLAG:コマンド結果受渡し変数 = 1
			TALENT:TARGET:定住者 = 1
			CFLAG:TARGET:長期雇用 = 0
			CFLAG:TARGET:滞在期間 = 999
			FOR LOCAL:1, 0, 住宅街広さ()
				IF 定住者部屋割り配列:(LOCAL:1) == ""
					定住者部屋割り配列:(LOCAL:1) = {TARGET}_
					BREAK
				ENDIF
			NEXT
		ENDIF
	CASE 1
		RETURN -1
ENDSELECT

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE604
#DIM ループ用
;移動実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(604)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;居住区エリアがまだないと出ない
SIF MATCH(開拓エリア開発番号, 2) < 1
	RETURN 0
;既に定住者だと無し
SIF TALENT:TARGET:定住者
	RETURN 0
;成功するのは80以上だが、話題に出すのに好感度レベル50以上
SIF CFLAG:TARGET:好感度レベル < 関係閾値:3
	RETURN 0
;eraは見ない
SIF NO:TARGET == 998
	RETURN 0
RETURN 1

;-------------------------------------------------
;メッセージ
;-------------------------------------------------

@MESSAGE_COM604

IF RFLAG:コマンド結果受渡し変数
	;成功
	PRINTFORML %CALLNAME:TARGET%은(는) %CALLNAME:PLAYER%의 권유에 응해, 주거 구역에 정주하게 되었다!
ELSE
	;失敗
	PRINTFORML %CALLNAME:TARGET%은(는) 아직 여기에 정주할 마음이 없는 것 같다……
ENDIF




@定住エリア満杯()
#FUNCTION

RFLAG:定住枠残りカウント = 0
FOR LOCAL, 0, 住宅街広さ()
	SIF 定住者部屋割り配列:LOCAL == ""
		RFLAG:定住枠残りカウント ++
NEXT

SIF RFLAG:定住枠残りカウント > 0
	RETURNF 0

RETURNF 1
