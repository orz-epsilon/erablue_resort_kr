@汎用固有アビ_1(ARGS, 対象キャラ)
#DIM 対象キャラ
SELECTCASE 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存
	CASE 1
		;攻撃
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ファイア(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_フリーズ(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_ブロウ(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_クエイク(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_シャイニング(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_ダークフラッド(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 2
		;防御
		JUMP 汎用アビリティ処理_フォーメーション(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
	CASE 3
		;回復
		JUMP 汎用アビリティ処理_ヒール(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
	CASE 4
		;サポート
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0, 3
				;火, 土
				JUMP 汎用アビリティ処理_フォーメーション(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
			CASE 1, 4
				;水, 光
				JUMP 汎用アビリティ処理_ヒール(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_クイックアップ(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_ダークミスト(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 5
		;連撃
		JUMP 汎用アビリティ処理_クイックアップ(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
	CASE 6
		;回避
		JUMP 汎用アビリティ処理_アトラクトダンス(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
	CASE 7
		;奥義
		JUMP 汎用アビリティ処理_士気向上(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
ENDSELECT
SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
	CASE 0
		;火
		JUMP 汎用アビリティ処理_ファイア(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
	CASE 1
		;水
		JUMP 汎用アビリティ処理_ヒール(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
	CASE 2
		;風
		JUMP 汎用アビリティ処理_クイックアップ(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
	CASE 3
		;土
		JUMP 汎用アビリティ処理_士気向上(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
	CASE 4
		;光
		JUMP 汎用アビリティ処理_シャイニング(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
	CASE 5
		;闇
		JUMP 汎用アビリティ処理_ダークミスト(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
ENDSELECT


@汎用固有アビ_2(ARGS, 対象キャラ)
#DIM 対象キャラ
SELECTCASE 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存
	CASE 1, 7
		;攻撃, 奥義
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_エクスプロージョン(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_アクアシュート(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_ダウンバースト(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_ロックブレイク(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_グリッターレイ(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_ディレイカース(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 5
		;連撃
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_エクスプロージョン(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_アクアシュート(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_フォローウィンド(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_ロックブレイク(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_グリッターレイ(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_ディレイカース(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 2, 3
		;防御, 回復
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ブレイズ(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_チルブレイン(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_フォローウィンド(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_フラッシュバン(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 3, 5
				;土, 闇
				JUMP 汎用アビリティ処理_リバースウェイブ(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 4
		;サポート
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_リバースウェイブ(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_チルブレイン(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_フォローウィンド(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_ロックブレイク(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_フラッシュバン(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_ディレイカース(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 6
		;回避
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 2
				;風
				JUMP 汎用アビリティ処理_フォローウィンド(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASEELSE
				;風以外
				JUMP 汎用アビリティ処理_ファストステップ(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
		ENDSELECT
ENDSELECT
SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
	CASE 0
		;火
		JUMP 汎用アビリティ処理_ブレイズ(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
	CASE 1
		;水
		JUMP 汎用アビリティ処理_アクアシュート(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
	CASE 2
		;風
		JUMP 汎用アビリティ処理_ダウンバースト(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
	CASE 3
		;土
		JUMP 汎用アビリティ処理_ロックブレイク(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
	CASE 4
		;光
		JUMP 汎用アビリティ処理_フラッシュバン(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
	CASE 5
		;闇
		JUMP 汎用アビリティ処理_ディレイカース(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
ENDSELECT


@汎用固有アビ_3(ARGS, 対象キャラ)
#DIM 対象キャラ
SELECTCASE 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存
	CASE 1, 7
		;攻撃, 奥義
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ヒートアップ(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_ブリザード(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_ポイズンクラウド(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_ランドスパイク(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_アスピルレイ(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_アベンジャー(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 5
		;連撃
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ヒートアップ(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_ブリザード(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_ファストレイド(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_ランドスパイク(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_フォーチュネイト(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_アベンジャー(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 2
		;防御
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ファイアウォール(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_マウントベール(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_ポイズンクラウド(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_リベンジウォール(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_アスピルレイ(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_チャーム(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 3
		;回復
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ファイアウォール(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_マウントベール(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_ファストレイド(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_リベンジウォール(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_アスピルレイ(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_チャーム(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 6
		;回避
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ヒートアップ(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_マウントベール(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_ファストレイド(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_ランドスパイク(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_アスピルレイ(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_デッドエンド(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 4
		;サポート
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ファイアウォール(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_チャーム(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASEELSE
				;火, 闇以外
				JUMP 汎用アビリティ処理_フォーチュネイト(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
		ENDSELECT
ENDSELECT
SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
	CASE 0
		;火
		JUMP 汎用アビリティ処理_ヒートアップ(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
	CASE 1
		;水
		JUMP 汎用アビリティ処理_マウントベール(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
	CASE 2
		;風
		JUMP 汎用アビリティ処理_ポイズンクラウド(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
	CASE 3
		;土
		JUMP 汎用アビリティ処理_リベンジウォール(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
	CASE 4
		;光
		JUMP 汎用アビリティ処理_アスピルレイ(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
	CASE 5
		;闇
		JUMP 汎用アビリティ処理_デッドエンド(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
ENDSELECT


@汎用固有アビ_4(ARGS, 対象キャラ)
#DIM 対象キャラ
SELECTCASE 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存
	CASE 1
		;攻撃
		JUMP 汎用アビリティ処理_アスピレーションバースト(ARGS, GET_SKILL_LEVEL(4, 対象キャラ), 対象キャラ)
	CASE 2
		;防御
		JUMP 汎用アビリティ処理_コンサベーションバースト(ARGS, GET_SKILL_LEVEL(4, 対象キャラ), 対象キャラ)
	CASE 3
		;回復
		JUMP 汎用アビリティ処理_サルベイションバースト(ARGS, GET_SKILL_LEVEL(4, 対象キャラ), 対象キャラ)
	CASE 4
		;サポート
		JUMP 汎用アビリティ処理_デディケーションバースト(ARGS, GET_SKILL_LEVEL(4, 対象キャラ), 対象キャラ)
	CASE 5
		;連撃
		JUMP 汎用アビリティ処理_コンセキューションバースト(ARGS, GET_SKILL_LEVEL(4, 対象キャラ), 対象キャラ)
	CASE 6
		;回避
		JUMP 汎用アビリティ処理_アダプテーションバースト(ARGS, GET_SKILL_LEVEL(4, 対象キャラ), 対象キャラ)
	CASE 7
		;奥義
		JUMP 汎用アビリティ処理_デタミネーションバースト(ARGS, GET_SKILL_LEVEL(4, 対象キャラ), 対象キャラ)
	CASEELSE
		;バランス
		JUMP 汎用アビリティ処理_パーフェクションバースト(ARGS, GET_SKILL_LEVEL(4, 対象キャラ), 対象キャラ)
ENDSELECT


@GET_SKILL_LEVEL(アビ番号, 対象キャラ)
#FUNCTION
#DIM アビ番号
#DIM 対象キャラ
SELECTCASE アビ番号
	CASE 1
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:Lv
			CASE IS >= 20
				RETURNF 3
			CASE IS >= 8
				RETURNF 2
			CASEELSE
				RETURNF 1
		ENDSELECT
	CASE 2
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:Lv
			CASE IS >= 28
				RETURNF 3
			CASE IS >= 16
				RETURNF 2
			CASE IS >= 4
				RETURNF 1
			CASEELSE
				RETURNF 0
		ENDSELECT
	CASE 3
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:Lv
			CASE IS >= 32
				RETURNF 3
			CASE IS >= 24
				RETURNF 2
			CASE IS >= 12
				RETURNF 1
			CASEELSE
				RETURNF 0
		ENDSELECT
	[SKIPSTART]
	CASE 4
		;Lvではなく、イベントで潜在能力を解放することで習得するイメージ
		;※この文は実装イメージです 実際の挙動とは異なる場合があります
		RETURNF LIMIT(CFLAG:対象キャラ:潜在能力解放レベル, 0, 3)
	[SKIPEND]
	CASEELSE
		RETURNF 0
ENDSELECT


@汎用アビリティ処理_ファイア(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ファイア＋＋
	消費ＭＰ = 25
	効果数値 = 300
ELSEIF LEVEL >= 2
	アビ名 = ファイア＋
	消費ＭＰ = 17
	効果数値 = 250
ELSEIF LEVEL >= 1
	アビ名 = ファイア
	消費ＭＰ = 15
	効果数値 = 200
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>적 횡렬에"
		ELSE
			詳細文文字列受け渡し変数 += "<br>적 단일 대상에게"
		ENDIF
		詳細文文字列受け渡し変数 += @"［공격력✕{効果数値}％］의 화속성 데미지를 준다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 3
			CALL アビ対象選択テンプレート_敵横列
		ELSE
			CALL アビ対象選択テンプレート_敵単体
		ENDIF
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_火属性
ENDSELECT


@汎用アビリティ処理_ブレイズ(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
IF LEVEL >= 3
	アビ名 = ブレイズ＋＋
	消費ＭＰ = 35
	効果数値１ = 20
	効果数値２ = 150
ELSEIF LEVEL >= 2
	アビ名 = ブレイズ＋
	消費ＭＰ = 25
	効果数値１ = 15
	効果数値２ = 100
ELSEIF LEVEL >= 1
	アビ名 = ブレイズ
	消費ＭＰ = 15
	効果数値１ = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>적 단일 대상에게［공격력✕100％］의 화속성 데미지를 준다."
		詳細文文字列受け渡し変数 += @"<br>［{効果数値１}％］（B 프레임・3턴）의 방어력 감소 효과를 준다. 성공률 80％."
		IF LEVEL >= 2
		詳細文文字列受け渡し変数 += @"<br>［회복력✕{効果数値２}％］（3턴）의 화상 효과를 준다. 성공률 70％."
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_ダメージ処理_火属性

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力B
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 効果数値１
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 80
		CALL アビテンプレート_不利効果_デバフ効果セット
		IF LEVEL >= 2
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 火傷
			アビ汎用文字列:バフ・デバフ対象能力 = 火傷
			CALL バフ込み数値算出("回復力")
			アビ汎用変数:効果量 = RESULT * 効果数値２ / 100
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 70
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_ヒートアップ(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
IF LEVEL >= 3
	アビ名 = ヒートアップ＋＋
	消費ＭＰ = 35
ELSEIF LEVEL >= 2
	アビ名 = ヒートアップ＋
	消費ＭＰ = 30
ELSEIF LEVEL >= 1
	アビ名 = ヒートアップ
	消費ＭＰ = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>아군 전체에게［30％］（A 프레임・3턴）의 공격력 상승 효과를 준다."
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += "<br>［20％］（Ａ枠・3턴）의 DA율 상승 효과를 부여한다."
		ENDIF
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>［100％］（Ａ枠・3턴）의 주는 피해 상승 효과를 부여한다."
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 공격력A
		アビ汎用文字列:バフ・デバフ対象能力 = 공격력
		アビ汎用変数:効果割合 = 30
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 2
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = DA율A
			アビ汎用文字列:バフ・デバフ対象能力 = DA율
			アビ汎用変数:効果量 = 20
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
		IF LEVEL >= 3
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 주는 피해 증가A
			アビ汎用文字列:バフ・デバフ対象能力 = 주는 피해
			アビ汎用変数:効果量 = 100
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_ヒール(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ヒール＋＋
	消費ＭＰ = 25
	効果数値 = 100
ELSEIF LEVEL >= 2
	アビ名 = ヒール＋
	消費ＭＰ = 15
	効果数値 = 75
ELSEIF LEVEL >= 1
	アビ名 = ヒール
	消費ＭＰ = 10
	効果数値 = 50
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>아군 한 명에게［회복력✕{効果数値}％］의 HP 회복을 부여한다."
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>［1］의 디버프 제거를 부여한다."
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方単体_生者のみ
		アビ汎用変数:効果割合 = 効果数値
		IF LEVEL >= 3
			CALL アビテンプレート_回復処理_MAXでも回復
			CALL アビ汎用変数文字列リセット
			CALL アビテンプレート_有利効果_デバフ消去
		ELSE
			CALL アビテンプレート_回復処理
		ENDIF
ENDSELECT


@汎用アビリティ処理_アクアシュート(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果回数
IF LEVEL >= 3
	アビ名 = アクアシュート＋＋
	消費ＭＰ = 30
	効果回数 = 4
ELSEIF LEVEL >= 2
	アビ名 = アクアシュート＋
	消費ＭＰ = 20
	効果回数 = 3
ELSEIF LEVEL >= 1
	アビ名 = アクアシュート
	消費ＭＰ = 15
	効果回数 = 2
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>적 한 명에게［공격력✕130％］의 수속성 대미지를{効果回数}회 부여한다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 130
		FOR LOCAL, 0, 効果回数
			CALL アビテンプレート_ダメージ処理_水属性
		NEXT
ENDSELECT


@汎用アビリティ処理_マウントベール(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
IF LEVEL >= 3
	アビ名 = マウントベール＋＋
	消費ＭＰ = 30
ELSEIF LEVEL >= 2
	アビ名 = マウントベール＋
	消費ＭＰ = 10
ELSEIF LEVEL >= 1
	アビ名 = マウントベール
	消費ＭＰ = 30
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>아군 전체에게（１회）의 약체 무효 효과를 부여한다."
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>［회복력✕30％］（A 프레임・3턴）의 재생 효과를 부여한다."
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 약체 무효
		アビ汎用文字列:バフ・デバフ対象能力 = 약체 무효
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 3
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 재생A
			アビ汎用文字列:バフ・デバフ対象能力 = 재생
			CALL バフ込み数値算出("回復力")
			アビ汎用変数:効果量 = RESULT * 30 / 100
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_クイックアップ(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
IF LEVEL >= 3
	アビ名 = クイックアップ＋＋
	消費ＭＰ = 25
	効果数値１ = 30
	効果数値２ = 20
ELSEIF LEVEL >= 2
	アビ名 = クイックアップ＋
	消費ＭＰ = 15
	効果数値１ = 30
	効果数値２ = 20
ELSEIF LEVEL >= 1
	アビ名 = クイックアップ
	消費ＭＰ = 10
	効果数値１ = 20
	効果数値２ = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>아군 전체에게 ［{効果数値１}％］（A 프레임・3턴）의 행동 속도 상승 효과를 부여한다."
		詳細文文字列受け渡し変数 += @"<br>［{効果数値２}％］（A 프레임・3턴）의 연속 공격률 상승 효과를 부여한다."
		IF LEVEL >= 3
		詳細文文字列受け渡し変数 += @"<br>［피해량✕10％］（A 프레임・3턴）의 추격 효과를 부여한다."
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 행동 속도A
		アビ汎用文字列:バフ・デバフ対象能力 = 행동 속도
		アビ汎用変数:効果割合 = 効果数値１
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 연속 공격률A
		アビ汎用文字列:バフ・デバフ対象能力 = 연속 공격률
		アビ汎用変数:効果量 = 効果数値２
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 3
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 추격A
			アビ汎用文字列:バフ・デバフ対象能力 = 추격
			アビ汎用変数:効果割合 = 10
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_フォローウィンド(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
IF LEVEL >= 3
	アビ名 = フォローウィンド＋＋
	消費ＭＰ = 30
	効果数値１ = 20
	効果数値２ = 250
ELSEIF LEVEL >= 2
	アビ名 = フォローウィンド＋
	消費ＭＰ = 20
	効果数値１ = 10
	効果数値２ = 150
ELSEIF LEVEL >= 1
	アビ名 = フォローウィンド
	消費ＭＰ = 15
	効果数値１ = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>아군 전체에게［+{効果数値１}％］（A 범위・3턴）의 회피율 상승 효과를 부여한다."
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += @"<br>자신에게［공격력✕{効果数値２}％］（일반 범위・7턴）의 회피 시 반격 효과를 부여한다."
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 회피율A
		アビ汎用文字列:バフ・デバフ対象能力 = 회피율
		アビ汎用変数:効果量 = 効果数値１
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 2
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ枠 = 회피 반격
			アビ汎用文字列:バフ・デバフ対象能力 = 회피 반격
			アビ汎用変数:効果割合 = 効果数値２
			アビ汎用変数:持続ターン = 7
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_ポイズンクラウド(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果ターン
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ポイズンクラウド＋＋
	消費ＭＰ = 50
	効果ターン = 5
	効果数値 = 200
ELSEIF LEVEL >= 2
	アビ名 = ポイズンクラウド＋
	消費ＭＰ = 40
	効果ターン = 3
	効果数値 = 200
ELSEIF LEVEL >= 1
	アビ名 = ポイズンクラウド
	消費ＭＰ = 25
	効果ターン = 3
	効果数値 = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>적 전체에게［공격력✕{効果数値}％］（일반 범위・{効果ターン}턴）의 독 효과를 부여한다. 성공률 60％."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 독
		アビ汎用文字列:バフ・デバフ対象能力 = 독
		CALL バフ込み数値算出("攻撃力")
		アビ汎用変数:効果量 = RESULT * 効果数値 / 100
		アビ汎用変数:持続ターン = 効果ターン
		アビ汎用変数:基礎成功確率 = 60
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


@汎用アビリティ処理_士気向上(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 士気向上＋＋
	消費ＭＰ = 15
	効果数値 = 25
ELSEIF LEVEL >= 2
	アビ名 = 士気向上＋
	消費ＭＰ = 10
	効果数値 = 20
ELSEIF LEVEL >= 1
	アビ名 = 士気向上
	消費ＭＰ = 5
	効果数値 = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>아군 전체에게［20％］（A 범위・3턴）의 게이지 상승 효율 상승 효과를 부여한다."
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>［{効果数値}％］의 오의 게이지 증가를 부여한다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		IF LEVEL >= 3
			アビ汎用文字列:バフ・デバフ枠 = 게이지 상승 효율A
			アビ汎用文字列:バフ・デバフ対象能力 = 게이지 상승 효율
			アビ汎用変数:効果割合 = 20
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット

			CALL アビ汎用変数文字列リセット
		ENDIF
		アビ汎用変数:効果量 = 効果数値
		CALL アビテンプレート_有利効果_奥義ゲージUP
ENDSELECT


@汎用アビリティ処理_ロックブレイク(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = ロックブレイク＋＋
	消費ＭＰ = 30
	効果数値１ = 200
	効果数値２ = 20
	効果成功率 = 100
ELSEIF LEVEL >= 2
	アビ名 = ロックブレイク＋
	消費ＭＰ = 20
	効果数値１ = 150
	効果数値２ = 15
	効果成功率 = 80
ELSEIF LEVEL >= 1
	アビ名 = ロックブレイク
	消費ＭＰ = 15
	効果数値１ = 100
	効果数値２ = 10
	効果成功率 = 80
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>적 하나에게 [공격력✕{効果数値１}％]의 흙 속성 데미지를 준다."
		詳細文文字列受け渡し変数 += @"<br>［{効果数値２}％］(B칸・3턴)의 방어력 감소 효과를 준다. 성공률{効果成功率}％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値１
		CALL アビテンプレート_ダメージ処理_土属性

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 방어력B
		アビ汎用文字列:バフ・デバフ対象能力 = 방어력
		アビ汎用変数:効果割合 = 効果数値２
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


@汎用アビリティ処理_リベンジウォール(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果回数
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = リベンジウォール＋＋
	消費ＭＰ = 30
	効果回数 = 4
	効果数値 = 200
ELSEIF LEVEL >= 2
	アビ名 = リベンジウォール＋
	消費ＭＰ = 25
	効果回数 = 3
	効果数値 = 100
ELSEIF LEVEL >= 1
	アビ名 = リベンジウォール
	消費ＭＰ = 20
	効果回数 = 2
	効果数値 = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>자신에게 [능력치✕{効果数値}％](고유 칸・{効果回数}회)의 특수 카운터 효과를 준다."
		詳細文文字列受け渡し変数 += "<br>이 카운터는 공격력・방어력・회복력 중 가장 높은 능력으로 데미지를 준다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 리벤지 월
		アビ汎用文字列:バフ・デバフ対象能力 = 특수 반응_피해 시
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続回数 = 効果回数
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@特殊反応_被ダメ時_リベンジウォール(バフ番号, 攻撃者隊列, 被ダメ記録順)
#DIMS CONST 能力候補 = "攻撃力", "防御力", "回復力"
#DIM 被ダメ記録順
#DIM 攻撃者隊列
#DIM バフ番号

CALL アビ対象選択テンプレート_指定(攻撃者隊列)
アビテンプレート用_アビ名 = カウンター

アビ汎用変数:効果割合 = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_割合")
アビ汎用変数:効果量 = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値")

LOCAL:1 = 0
FOR LOCAL, 0, 3
	CALL バフ込み数値算出(能力候補:LOCAL)
	IF LOCAL:1 < RESULT
		LOCAL:1 = RESULT
		アビ汎用文字列:攻撃側使用能力値 '= 능력 후보:LOCAL
	ENDIF
NEXT
CALL アビテンプレート_ダメージ処理_自属性

@バフ固有説明文_リベンジウォール(バフ・デバフ効果量, バフ・デバフ効果割合)
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
PRINTL 데미지를 받았을 때, 반격합니다.
PRINTL 이 반격은 공격력・방어력・회복력 중 가장 높은 능력으로 데미지를 줍니다.
RETURN 0


@汎用アビリティ処理_シャイニング(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = シャイニング＋＋
	消費ＭＰ = 25
	効果数値 = 350
ELSEIF LEVEL >= 2
	アビ名 = シャイニング＋
	消費ＭＰ = 15
	効果数値 = 200
ELSEIF LEVEL >= 1
	アビ名 = シャイニング
	消費ＭＰ = 10
	効果数値 = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>적 단일에게［공격력✕{効果数値}％］의 빛 속성 데미지를 준다."
		詳細文文字列受け渡し変数 += "<br>이 어빌리티는 상대의 방어력을 무시한다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:防御無視オプション_割合 = 100
		CALL アビテンプレート_ダメージ処理_光属性
ENDSELECT


@汎用アビリティ処理_フラッシュバン(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = フラッシュバン＋＋
	消費ＭＰ = 30
	効果数値 = 80
	効果成功率 = 80
ELSEIF LEVEL >= 2
	アビ名 = フラッシュバン＋
	消費ＭＰ = 20
	効果数値 = 50
	効果成功率 = 80
ELSEIF LEVEL >= 1
	アビ名 = フラッシュバン
	消費ＭＰ = 15
	効果数値 = 50
	効果成功率 = 60
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>적 세로열에［{効果数値}％］（일반 칸・3턴）의 암흑 효과를 준다. 성공률{効果成功率}％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵縦列
		アビ汎用文字列:バフ・デバフ枠 = 암흑
		アビ汎用文字列:バフ・デバフ対象能力 = 암흑
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


@汎用アビリティ処理_アスピルレイ(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 与ダメージ量
IF LEVEL >= 3
	アビ名 = アスピルレイ＋＋
	消費ＭＰ = 30
	効果数値 = 200
ELSEIF LEVEL >= 2
	アビ名 = アスピルレイ＋
	消費ＭＰ = 25
	効果数値 = 150
ELSEIF LEVEL >= 1
	アビ名 = アスピルレイ
	消費ＭＰ = 20
	効果数値 = 150
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>적 단일에게［회복력✕{効果数値}％］의 빛 속성 데미지를 준다."
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>［1］의 버프 제거를 준다."
		ENDIF
		詳細文文字列受け渡し変数 += "<br>자신에게［주는 데미지✕50％］의 HP 회복을 준다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用文字列:攻撃側使用能力値 = 회복력
		CALL アビテンプレート_ダメージ処理_光属性
		;RESULTに与えたダメージ量が入っている
		与ダメージ量 = RESULT
		IF LEVEL >= 3
			CALL アビ汎用変数文字列リセット
			CALL アビテンプレート_不利効果_バフ消去
		ENDIF
		IF 与ダメージ量 > 0
			;ダメージが発生した場合のみ処理
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:効果量 = 与ダメージ量 / 2
			CALL アビテンプレート_回復処理_MAXでも回復
		ENDIF
ENDSELECT


@汎用アビリティ処理_ダークミスト(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ダークミスト＋＋
	消費ＭＰ = 25
	効果数値 = 20
ELSEIF LEVEL >= 2
	アビ名 = ダークミスト＋
	消費ＭＰ = 15
	効果数値 = 20
ELSEIF LEVEL >= 1
	アビ名 = ダークミスト
	消費ＭＰ = 10
	効果数値 = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>적 세로열에"
		ELSE
			詳細文文字列受け渡し変数 += "<br>적 단일에게"
		ENDIF
		詳細文文字列受け渡し変数 += @"［{効果数値}％］（A 범위・3턴）공격력 감소 효과를 부여한다. 성공률 100%."
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>적 세로열에"
		ELSE
			詳細文文字列受け渡し変数 += "<br>적 단일에게"
		ENDIF
		詳細文文字列受け渡し変数 += @"［{効果数値}％］（A 범위・3턴）방어력 감소 효과를 부여한다. 성공률 100%."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 3
			CALL アビ対象選択テンプレート_敵縦列
		ELSE
			CALL アビ対象選択テンプレート_敵単体
		ENDIF
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 100
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 防御力A
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 100
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


@汎用アビリティ処理_ディレイカース(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = ディレイカース＋＋
	消費ＭＰ = 30
	効果数値 = 200
	効果成功率 = 100
ELSEIF LEVEL >= 2
	アビ名 = ディレイカース＋
	消費ＭＰ = 20
	効果数値 = 150
	効果成功率 = 100
ELSEIF LEVEL >= 1
	アビ名 = ディレイカース
	消費ＭＰ = 15
	効果数値 = 100
	効果成功率 = 80
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>적 단일에게［공격력✕{効果数値}％］의 어둠 속성 데미지를 준다."
		詳細文文字列受け渡し変数 += @"<br>［1］의 슬로우 효과를 부여한다. 성공률{効果成功率}％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_闇属性

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 1
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_スロウ
ENDSELECT


@汎用アビリティ処理_チャーム(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = チャーム＋＋
	消費ＭＰ = 30
	効果数値 = 65
	効果成功率 = 80
ELSEIF LEVEL >= 2
	アビ名 = チャーム＋
	消費ＭＰ = 25
	効果数値 = 35
	効果成功率 = 60
ELSEIF LEVEL >= 1
	アビ名 = チャーム
	消費ＭＰ = 20
	効果数値 = 35
	効果成功率 = 60
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}"
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += "<br>적 가로열에"
		ELSE
			詳細文文字列受け渡し変数 += "<br>적 단일에게"
		ENDIF
		詳細文文字列受け渡し変数 += @"［{効果数値}％］（일반 프레임・3턴）의 매료 효과를 부여한다. 성공률{効果成功率}％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 2
		CALL アビ対象選択テンプレート_敵横列
		ELSE
		CALL アビ対象選択テンプレート_敵単体
		ENDIF
		アビ汎用文字列:バフ・デバフ枠 = 매료
		アビ汎用文字列:バフ・デバフ対象能力 = 매료
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


@汎用アビリティ処理_フリーズ(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = フリーズ＋＋
	消費ＭＰ = 25
	効果数値１ = 300
	効果数値２ = 30
	効果成功率 = 100
ELSEIF LEVEL >= 2
	アビ名 = フリーズ＋
	消費ＭＰ = 15
	効果数値１ = 250
	効果数値２ = 20
	効果成功率 = 80
ELSEIF LEVEL >= 1
	アビ名 = フリーズ
	消費ＭＰ = 10
	効果数値１ = 200
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>적 단일에게［공격력✕{効果数値１}％］의 수속성 데미지를 준다."
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += @"<br>［{効果数値２}％］（B 프레임・3턴）의 행동 속도 감소 효과를 부여한다. 성공률{効果成功率}％。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値１
		CALL アビテンプレート_ダメージ処理_水属性
		IF LEVEL >= 2
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 행동 속도B
			アビ汎用文字列:バフ・デバフ対象能力 = 행동 속도
			アビ汎用変数:効果割合 = 効果数値２
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 効果成功率
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_ブロウ(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ブロウ＋＋
	消費ＭＰ = 25
	効果数値 = 200
ELSEIF LEVEL >= 2
	アビ名 = ブロウ＋
	消費ＭＰ = 15
	効果数値 = 160
ELSEIF LEVEL >= 1
	アビ名 = ブロウ
	消費ＭＰ = 10
	効果数値 = 120
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>적 전체에게"
		ELSE
			詳細文文字列受け渡し変数 += "<br>적 횡열에게"
		ENDIF
		詳細文文字列受け渡し変数 += @"［공격력✕{効果数値}％］의 풍속성 데미지를 준다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 3
			CALL アビ対象選択テンプレート_敵全体_生者のみ
		ELSE
			CALL アビ対象選択テンプレート_敵横列
		ENDIF
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_風属性
ENDSELECT


@汎用アビリティ処理_クエイク(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果回数
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = クエイク＋＋
	消費ＭＰ = 35
	効果数値 = 150
	効果回数 = 3
ELSEIF LEVEL >= 2
	アビ名 = クエイク＋
	消費ＭＰ = 25
	効果数値 = 140
	効果回数 = 2
ELSEIF LEVEL >= 1
	アビ名 = クエイク
	消費ＭＰ = 15
	効果数値 = 100
	効果回数 = 2
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP:{消費ＭＰ}<br>무작위 적 3명에게［공격력✕{効果数値}％］의 땅 속성 대미지를{効果回数}회 준다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		FOR LOCAL, 0, 効果回数
			アビ汎用変数:敵対心無視オプション = 1
			CALL アビ対象選択テンプレート_敵ランダムＸ体_重複無し(3)
			アビ汎用変数:効果割合 = 効果数値
			CALL アビテンプレート_ダメージ処理_土属性
		NEXT
ENDSELECT


@汎用アビリティ処理_ダークフラッド(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ダークフラッド＋＋
	消費ＭＰ = 25
	効果数値 = 280
ELSEIF LEVEL >= 2
	アビ名 = ダークフラッド＋
	消費ＭＰ = 15
	効果数値 = 170
ELSEIF LEVEL >= 1
	アビ名 = ダークフラッド
	消費ＭＰ = 10
	効果数値 = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP:{消費ＭＰ}<br>적 종열에게［공격력✕{効果数値}％］의 어둠 속성 대미지를 준다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵縦列
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_闇属性
ENDSELECT


@汎用アビリティ処理_フォーメーション(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果回数
IF LEVEL >= 3
	アビ名 = フォーメーション＋＋
	消費ＭＰ = 40
	効果回数 = 4
ELSEIF LEVEL >= 2
	アビ名 = フォーメーション＋
	消費ＭＰ = 30
	効果回数 = 2
ELSEIF LEVEL >= 1
	アビ名 = フォーメーション
	消費ＭＰ = 20
	効果回数 = 1
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP:{消費ＭＰ}<br>아군 전체에게［25％］（일반 프레임・{効果回数}회）의 피해 감소 효과를 준다."
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>［10％］（A 프레임・3턴）의 방어력 상승 효과를 준다."
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 피해 감소
		アビ汎用文字列:バフ・デバフ対象能力 = 피해
		アビ汎用変数:効果割合 = 25
		アビ汎用変数:持続回数 = 効果回数
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 3
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 방어력A
			アビ汎用文字列:バフ・デバフ対象能力 = 방어력
			アビ汎用変数:効果割合 = 10
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_アトラクトダンス(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = アトラクトダンス＋＋
	消費ＭＰ = 30
	効果数値 = 200
ELSEIF LEVEL >= 2
	アビ名 = アトラクトダンス＋
	消費ＭＰ = 10
	効果数値 = 100
ELSEIF LEVEL >= 1
	アビ名 = アトラクトダンス
	消費ＭＰ = 10
	効果数値 = 50
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP:{消費ＭＰ}<br>자신에게［{効果数値}％］（C 프레임・2행동）의 적대심 상승 효과를 준다."
		詳細文文字列受け渡し変数 += "<br>［60％］（C 프레임・2행동）의 회피율 상승 효과를 준다."
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += @"<br>이 어빌리티로는 행동 횟수를 소비하지 않는다."
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 적대심C
		アビ汎用文字列:バフ・デバフ対象能力 = 적대심
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続行動回数 = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 회피율C
		アビ汎用文字列:バフ・デバフ対象能力 = 회피율
		アビ汎用変数:効果量 = 60
		アビ汎用変数:持続行動回数 = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		IF LEVEL >= 3
			アビテンプレート用_行動消費無しフラグ = 1
		ENDIF
ENDSELECT


@汎用アビリティ処理_ファストステップ(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
IF LEVEL >= 3
	アビ名 = ファストステップ＋＋
	消費ＭＰ = 30
	効果数値１ = 300
	効果数値２ = 15
ELSEIF LEVEL >= 2
	アビ名 = ファストステップ＋
	消費ＭＰ = 20
	効果数値１ = 200
	効果数値２ = 10
ELSEIF LEVEL >= 1
	アビ名 = ファストステップ
	消費ＭＰ = 15
	効果数値１ = 150
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>자신에게［공격력✕{効果数値１}％］（일반 프레임・7턴）의 회피 시 반격 효과를 부여한다."
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += @"<br>［{効果数値２}％］（A 프레임・7턴）의 회피율 상승 효과를 부여한다."
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 회피 반격
		アビ汎用文字列:バフ・デバフ対象能力 = 회피 반격
		アビ汎用変数:効果割合 = 効果数値１
		アビ汎用変数:持続ターン = 7
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 2
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 회피율A
			アビ汎用文字列:バフ・デバフ対象能力 = 회피율
			アビ汎用変数:効果量 = 効果数値２
			アビ汎用変数:持続ターン = 7
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_エクスプロージョン(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 対象数
IF LEVEL >= 3
	アビ名 = エクスプロージョン＋＋
	消費ＭＰ = 45
	効果数値 = 50
ELSEIF LEVEL >= 2
	アビ名 = エクスプロージョン＋
	消費ＭＰ = 35
	効果数値 = 35
ELSEIF LEVEL >= 1
	アビ名 = エクスプロージョン
	消費ＭＰ = 30
	効果数値 = 25
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>적 전체에게［공격력✕적의 수✕{効果数値}％］의 화속성 대미지를 준다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		対象数 = 0
		FOR LOCAL, 10, 20
			SIF 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
				CONTINUE
			対象数 += 1
		NEXT
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値 * MAX(対象数, 1)
		CALL アビテンプレート_ダメージ処理_火属性
ENDSELECT


@汎用アビリティ処理_チルブレイン(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = チルブレイン＋＋
	消費ＭＰ = 50
	効果数値 = 200
	効果成功率 = 80
ELSEIF LEVEL >= 2
	アビ名 = チルブレイン＋
	消費ＭＰ = 35
	効果数値 = 100
	効果成功率 = 80
ELSEIF LEVEL >= 1
	アビ名 = チルブレイン
	消費ＭＰ = 25
	効果数値 = 100
	効果成功率 = 60
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>적 세로열에［회복력✕{効果数値}％］（일반 프레임・3턴）의 화상 효과를 부여한다. 성공률{効果成功率}％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵縦列
		アビ汎用文字列:バフ・デバフ枠 = 화상
		アビ汎用文字列:バフ・デバフ対象能力 = 화상
		CALL バフ込み数値算出("回復力")
		アビ汎用変数:効果量 = RESULT * 効果数値 / 100
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


@汎用アビリティ処理_ダウンバースト(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ダウンバースト＋＋
	消費ＭＰ = 30
	効果数値 = 400
ELSEIF LEVEL >= 2
	アビ名 = ダウンバースト＋
	消費ＭＰ = 20
	効果数値 = 300
ELSEIF LEVEL >= 1
	アビ名 = ダウンバースト
	消費ＭＰ = 15
	効果数値 = 220
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP:{消費ＭＰ}<br>적 하나에게 [공격력✕{効果数値}％]의 바람 속성 데미지를 준다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_風属性
ENDSELECT


@汎用アビリティ処理_グリッターレイ(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果回数_最小
#DIM 効果回数_最大
#DIM 効果回数
IF LEVEL >= 3
	アビ名 = グリッターレイ＋＋
	消費ＭＰ = 40
	効果回数_最小 = 4
	効果回数_最大 = 9
ELSEIF LEVEL >= 2
	アビ名 = グリッターレイ＋
	消費ＭＰ = 25
	効果回数_最小 = 2
	効果回数_最大 = 6
ELSEIF LEVEL >= 1
	アビ名 = グリッターレイ
	消費ＭＰ = 15
	効果回数_最小 = 1
	効果回数_最大 = 4
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP:{消費ＭＰ}<br>무작위 적에게 [공격력✕120％]의 빛 속성 데미지를 {効果回数_最小}～{効果回数_最大}회 준다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		効果回数 = RAND(効果回数_最小, 効果回数_最大 + 1)
		FOR LOCAL, 0, 効果回数
			アビ汎用変数:敵対心無視オプション = 1
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			アビ汎用変数:効果割合 = 120
			CALL アビテンプレート_ダメージ処理_光属性
		NEXT
ENDSELECT


@汎用アビリティ処理_リバースウェイブ(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = リバースウェイブ＋＋
	消費ＭＰ = 40
	効果数値 = 3
ELSEIF LEVEL >= 2
	アビ名 = リバースウェイブ＋
	消費ＭＰ = 20
	効果数値 = 2
ELSEIF LEVEL >= 1
	アビ名 = リバースウェイブ
	消費ＭＰ = 10
	効果数値 = 1
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP:{消費ＭＰ}"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>적 횡렬에"
		ELSE
			詳細文文字列受け渡し変数 += "<br>적 하나에게"
		ENDIF
		詳細文文字列受け渡し変数 += @"[{効果数値}]의 버프 제거를 준다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 3
			CALL アビ対象選択テンプレート_敵横列
		ELSE
			CALL アビ対象選択テンプレート_敵単体
		ENDIF
		FOR LOCAL, 0, 効果数値
			CALL アビテンプレート_不利効果_バフ消去
		NEXT
ENDSELECT


@汎用アビリティ処理_ファイアウォール(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果回数
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ファイアウォール＋＋
	消費ＭＰ = 30
	効果回数 = 2
	効果数値 = 170
ELSEIF LEVEL >= 2
	アビ名 = ファイアウォール＋
	消費ＭＰ = 25
	効果回数 = 1
	効果数値 = 170
ELSEIF LEVEL >= 1
	アビ名 = ファイアウォール
	消費ＭＰ = 20
	効果回数 = 1
	効果数値 = 120
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP:{消費ＭＰ}<br>아군 전체에게 [각자의 공격력✕{効果数値}％] (통상 범위・{効果回数}회)의 카운터 효과를 준다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 카운터
		アビ汎用文字列:バフ・デバフ対象能力 = 카운터
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続回数 = 効果回数
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT


@汎用アビリティ処理_ブリザード(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ブリザード＋＋
	消費ＭＰ = 30
	効果数値 = 150
ELSEIF LEVEL >= 2
	アビ名 = ブリザード＋
	消費ＭＰ = 20
	効果数値 = 100
ELSEIF LEVEL >= 1
	アビ名 = ブリザード
	消費ＭＰ = 15
	効果数値 = 75
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>적 전체에게［공격력✕{効果数値}％］의 물 속성 데미지를 준다."
		詳細文文字列受け渡し変数 += "<br>［20％］（B칸・3턴）의 방어력 감소 효과를 준다. 성공률 80％."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_水属性

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 방어력B
		アビ汎用文字列:バフ・デバフ対象能力 = 방어력
		アビ汎用変数:効果割合 = 20
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 80
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


@汎用アビリティ処理_ランドスパイク(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ランドスパイク＋＋
	消費ＭＰ = 30
	効果数値 = 240
ELSEIF LEVEL >= 2
	アビ名 = ランドスパイク＋
	消費ＭＰ = 25
	効果数値 = 200
ELSEIF LEVEL >= 1
	アビ名 = ランドスパイク
	消費ＭＰ = 20
	効果数値 = 130
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>적 가로열에［공격력✕{効果数値}％］의 흙 속성 데미지를 준다."
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>이 어빌리티는 상대의 방어력을 무시한다."
		ELSE
			詳細文文字列受け渡し変数 += "<br>이 어빌리티는 상대의 방어력을 50％ 무시한다."
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵横列
		アビ汎用変数:効果割合 = 効果数値
		IF LEVEL >= 3
			アビ汎用変数:防御無視オプション_割合 = 100
		ELSE
			アビ汎用変数:防御無視オプション_割合 = 50
		ENDIF
		CALL アビテンプレート_ダメージ処理_土属性
ENDSELECT


@汎用アビリティ処理_ファストレイド(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
IF LEVEL >= 3
	アビ名 = ファストレイド＋＋
	消費ＭＰ = 25
	効果数値１ = 200
	効果数値２ = 2300
ELSEIF LEVEL >= 2
	アビ名 = ファストレイド＋
	消費ＭＰ = 22
	効果数値１ = 150
	効果数値２ = 1400
ELSEIF LEVEL >= 1
	アビ名 = ファストレイド
	消費ＭＰ = 20
	効果数値１ = 100
	効果数値２ = 750
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>적 단일 대상에게［행동 속도✕{効果数値１}％］의 바람 속성 데미지를 준다."
		詳細文文字列受け渡し変数 += @"<br>전투 시작 직후 턴에 사용했을 경우,［행동 속도✕{効果数値２}％］로 데미지가 상승한다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		IF 現在ターン数 > 1
			アビ汎用変数:効果割合 = 効果数値１
		ELSE
			アビ汎用変数:効果割合 = 効果数値２
		ENDIF
		アビ汎用文字列:攻撃側使用能力値 = 행동 속도
		CALL アビテンプレート_ダメージ処理_風属性
ENDSELECT


@汎用アビリティ処理_アベンジャー(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果ターン
IF LEVEL >= 3
	アビ名 = アベンジャー＋＋
	消費ＭＰ = 30
	効果ターン = 7
ELSEIF LEVEL >= 2
	アビ名 = アベンジャー＋
	消費ＭＰ = 25
	効果ターン = 3
ELSEIF LEVEL >= 1
	アビ名 = アベンジャー
	消費ＭＰ = 20
	効果ターン = 1
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>자신에게［공격력✕200％］(일반 칸・{効果ターン}턴)의 카운터 효과를 준다."
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 카운터
		アビ汎用文字列:バフ・デバフ対象能力 = 카운터
		アビ汎用変数:効果割合 = 200
		アビ汎用変数:持続ターン = 効果ターン
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT


@汎用アビリティ処理_フォーチュネイト(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = フォーチュネイト＋＋
	消費ＭＰ = 30
	効果数値 = 50
ELSEIF LEVEL >= 2
	アビ名 = フォーチュネイト＋
	消費ＭＰ = 25
	効果数値 = 25
ELSEIF LEVEL >= 1
	アビ名 = フォーチュネイト
	消費ＭＰ = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}<br>아군 전체에게［40％］（A 프레임・7턴）의 치명타율 상승 효과를 부여한다."
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += @"<br>아군 전체에게［{効果数値}％］（A 프레임・7턴）의 치명타 피해율 상승 효과를 부여한다."
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 치명타율A
		アビ汎用文字列:バフ・デバフ対象能力 = 치명타율
		アビ汎用変数:効果量 = 40
		アビ汎用変数:持続ターン = 7
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 2
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 치명타 피해율A
			アビ汎用文字列:バフ・デバフ対象能力 = 치명타 피해율
			アビ汎用変数:効果量 = 効果数値
			アビ汎用変数:持続ターン = 7
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_デッドエンド(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
IF LEVEL >= 3
	アビ名 = デッドエンド＋＋
	消費ＭＰ = 30
ELSEIF LEVEL >= 2
	アビ名 = デッドエンド＋
	消費ＭＰ = 20
ELSEIF LEVEL >= 1
	アビ名 = デッドエンド
	消費ＭＰ = 15
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP：{消費ＭＰ}"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>적 전체에게"
		ELSEIF LEVEL >= 2
			詳細文文字列受け渡し変数 += "<br>적 세로열에"
		ELSE
			詳細文文字列受け渡し変数 += "<br>적 단일 대상에게"
		ENDIF
		詳細文文字列受け渡し変数 += "즉사 효과를 부여한다. 성공률 30%."
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += @"<br>［30％］（B 프레임・2턴）의 회피율 감소 효과를 부여한다. 성공률 90%."
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 3
			CALL アビ対象選択テンプレート_敵全体_生者のみ
		ELSEIF LEVEL >= 2
			CALL アビ対象選択テンプレート_敵縦列
		ELSE
			CALL アビ対象選択テンプレート_敵単体
		ENDIF
		アビ汎用変数:基礎成功確率 = 50
		CALL アビテンプレート_不利効果_即死
		IF LEVEL >= 3
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 回避率B
			アビ汎用文字列:バフ・デバフ対象能力 = 回避率
			アビ汎用変数:効果量 = 30
			アビ汎用変数:持続ターン = 2
			アビ汎用変数:基礎成功確率 = 90
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_パーフェクションバースト(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
#DIM 対象数
#DIM 与ダメージ量
IF LEVEL >= 3
	アビ名 = パーフェクションバースト＋＋
	消費ＭＰ = 50
	効果数値１ = 650
	効果数値２ = 30
ELSEIF LEVEL >= 2
	アビ名 = パーフェクションバースト＋
	消費ＭＰ = 50
	効果数値１ = 550
	効果数値２ = 25
ELSEIF LEVEL >= 1
	アビ名 = パーフェクションバースト
	消費ＭＰ = 50
	効果数値１ = 450
	効果数値２ = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP: 최대 MP의{消費ＭＰ}％<br>적 전체에게［공격력✕{効果数値１}÷대상 수％］의 무속성 데미지를 준다."
		詳細文文字列受け渡し変数 += @"<br>［{効果数値２}％］（고유 프레임・5턴）의 공격력 감소 효과를 준다. 성공률 100％。"
		詳細文文字列受け渡し変数 += @"<br>［{効果数値２}％］（고유 프레임・5턴）의 방어력 감소 효과를 준다. 성공률 100％。"
		詳細文文字列受け渡し変数 += "<br>아군 전체에게［주는 데미지✕50％］의 HP 회복을 준다."
		RETURN (基礎BATTLE_STATE:対象キャラ:最大ＭＰ * 消費ＭＰ + 99) / 100
	CASE "アビ効果"
		対象数 = 0
		FOR LOCAL, 10, 20
			SIF 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
				CONTINUE
			SIF 敵BATTLE_STATE:(LOCAL - 10):ＨＰ < 1
				CONTINUE
			対象数 += 1
		NEXT

		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値１ / MAX(対象数, 1)
		CALL アビテンプレート_ダメージ処理_無属性
		;RESULTに与えたダメージ量が入っている
		与ダメージ量 = RESULT

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力_パーフェクションバースト
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 効果数値２
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:基礎成功確率 = 100
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 防御力_パーフェクションバースト
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 効果数値２
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:基礎成功確率 = 100
		CALL アビテンプレート_不利効果_デバフ効果セット

		IF 与ダメージ量 > 0
			;ダメージが発生した場合のみ処理
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用変数:効果量 = 与ダメージ量 / 2
			CALL アビテンプレート_回復処理_MAXでも回復
		ENDIF
ENDSELECT


@汎用アビリティ処理_アスピレーションバースト(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
#DIM バフ番号
#DIM 対象数
IF LEVEL >= 3
	アビ名 = アスピレーションバースト＋＋
	消費ＭＰ = 50
	効果数値１ = 120
	効果数値２ = 100
ELSEIF LEVEL >= 2
	アビ名 = アスピレーションバースト＋
	消費ＭＰ = 50
	効果数値１ = 100
	効果数値２ = 75
ELSEIF LEVEL >= 1
	アビ名 = アスピレーションバースト
	消費ＭＰ = 50
	効果数値１ = 80
	効果数値２ = 50
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP: 최대 MP의{消費ＭＰ}％<br>적 전체에게［공격력✕적의 수✕{効果数値１}％］의 자속성 데미지를 준다."
		詳細文文字列受け渡し変数 += @"<br>자신에게［{効果数値２}％］（고유 프레임・3턴）의 어빌리티 데미지 상승 효과를 준다."
		詳細文文字列受け渡し変数 += "<br>이 어빌리티로 얻은 어빌리티 데미지 상승 효과는, 이 어빌리티에는 적용되지 않는다."
		RETURN (基礎BATTLE_STATE:対象キャラ:最大ＭＰ * 消費ＭＰ + 99) / 100
	CASE "アビ効果"
		バフ番号 = バフ番号取得_枠("アスピレーションバースト", 戦闘行動キャラ)
		IF バフ番号 > -1
			CALL 戦闘効果削除(バフ番号)
		ENDIF

		対象数 = 0
		FOR LOCAL, 10, 20
			SIF 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
				CONTINUE
			対象数 += 1
		NEXT

		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値１ * MAX(対象数, 1)
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 어스피레이션 버스트
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_アビリティダメージ
		アビ汎用変数:効果割合 = 効果数値２
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT


@汎用アビリティ処理_コンサベーションバースト(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = コンサベーションバースト＋＋
	消費ＭＰ = 50
	効果数値 = 95
ELSEIF LEVEL >= 2
	アビ名 = コンサベーションバースト＋
	消費ＭＰ = 50
	効果数値 = 85
ELSEIF LEVEL >= 1
	アビ名 = コンサベーションバースト
	消費ＭＰ = 50
	効果数値 = 75
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP: 최대 MP의{消費ＭＰ}%<br>자신에게 (2행동)의 전체 엄호 효과를 부여한다."
		詳細文文字列受け渡し変数 += @"<br>［{効果数値}%］ (2행동)의 대미지 컷 효과를 부여한다."
		RETURN (基礎BATTLE_STATE:対象キャラ:最大ＭＰ * 消費ＭＰ + 99) / 100
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 전체 엄호
		アビ汎用文字列:バフ・デバフ対象能力 = 전체 엄호
		;1にするとかばう使った直後に消えてしまうので2で設定
		アビ汎用変数:持続行動回数 = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 피해 감소
		アビ汎用文字列:バフ・デバフ対象能力 = 피해
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続行動回数 = 2
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT


@汎用アビリティ処理_サルベイションバースト(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
IF LEVEL >= 3
	アビ名 = サルベイションバースト＋＋
	消費ＭＰ = 50
	効果数値１ = 150
	効果数値２ = 600
ELSEIF LEVEL >= 2
	アビ名 = サルベイションバースト＋
	消費ＭＰ = 50
	効果数値１ = 110
	効果数値２ = 300
ELSEIF LEVEL >= 1
	アビ名 = サルベイションバースト
	消費ＭＰ = 50
	効果数値１ = 75
	効果数値２ = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP: 최대 MP의{消費ＭＰ}%<br>아군 전체에게 [회복력✕{効果数値１}%]의 HP 회복을 부여한다."
		詳細文文字列受け渡し変数 += @"<br>전투 불능의 아군 전체에게 [{効果数値２}] (소생 가능)의 HP 회복을 부여한다."
		RETURN (基礎BATTLE_STATE:対象キャラ:最大ＭＰ * 消費ＭＰ + 99) / 100
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値１
		CALL アビテンプレート_回復処理_MAXでも回復

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_死者のみ
		IF RESULT != -1
			アビ汎用変数:効果量 = 効果数値２
			アビ汎用変数:蘇生可能オプション = 1
			CALL アビテンプレート_回復処理_MAXでも回復
		ENDIF
ENDSELECT


@汎用アビリティ処理_デディケーションバースト(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
IF LEVEL >= 3
	アビ名 = デディケーションバースト＋＋
	消費ＭＰ = 50
	効果数値１ = 200
	効果数値２ = 50
ELSEIF LEVEL >= 2
	アビ名 = デディケーションバースト＋
	消費ＭＰ = 50
	効果数値１ = 170
	効果数値２ = 35
ELSEIF LEVEL >= 1
	アビ名 = デディケーションバースト
	消費ＭＰ = 50
	効果数値１ = 140
	効果数値２ = 25
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP: 최대 MP의{消費ＭＰ}%<br>아군 전체에게 [{効果数値１}%] (고유 프레임・1행동)의 공격력・방어력・회복력 상승 효과를 부여한다."
		詳細文文字列受け渡し変数 += @"<br>［{効果数値２}%］ (고유 프레임・1행동)의 약체 성공률 상승 효과를 부여한다."
		詳細文文字列受け渡し変数 += "<br>이 어빌리티는 행동을 소비하지 않는다."
		RETURN (基礎BATTLE_STATE:対象キャラ:最大ＭＰ * 消費ＭＰ + 99) / 100
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 '= @"소비 MP: 최대 MP의{消費ＭＰ}％<br>아군 전체에게［{効果数値１}％］(고유 슬롯・1행동)의 공격력 상승 효과를 부여한다."
		詳細文文字列受け渡し変数 += @"<br>［{効果数値１}％］(고유 슬롯・1행동)의 방어력 상승 효과를 부여한다."
		詳細文文字列受け渡し変数 += @"<br>［{効果数値１}％］(고유 슬롯・1행동)의 회복력 상승 효과를 부여한다."
		詳細文文字列受け渡し変数 += @"<br>［{効果数値２}％］(고유 슬롯・1행동)의 약체 성공률 상승 효과를 부여한다."
		詳細文文字列受け渡し変数 += @"<br>이 어빌리티로는 행동 횟수를 소비하지 않는다."
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力_デディケーションバースト
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 効果数値１
		アビ汎用変数:持続行動回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 防御力_デディケーションバースト
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 効果数値１
		アビ汎用変数:持続行動回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 回復力_デディケーションバースト
		アビ汎用文字列:バフ・デバフ対象能力 = 回復力
		アビ汎用変数:効果割合 = 効果数値１
		アビ汎用変数:持続行動回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 弱体成功率_デディケーションバースト
		アビ汎用文字列:バフ・デバフ対象能力 = 弱体成功率
		アビ汎用変数:効果量 = 効果数値２
		アビ汎用変数:持続行動回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT


@汎用アビリティ処理_コンセキューションバースト(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = コンセキューションバースト＋＋
	消費ＭＰ = 50
	効果数値 = 150
ELSEIF LEVEL >= 2
	アビ名 = コンセキューションバースト＋
	消費ＭＰ = 50
	効果数値 = 125
ELSEIF LEVEL >= 1
	アビ名 = コンセキューションバースト
	消費ＭＰ = 50
	効果数値 = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP: 최대 MP의{消費ＭＰ}％<br>자신에게 (일반 프레임・1회)의 재행동 효과를 부여한다."
		詳細文文字列受け渡し変数 += @"<br>［주는 피해✕{効果数値}％］（고유 프레임・1턴）의 추격 효과를 부여한다."
		詳細文文字列受け渡し変数 += @"<br>이 어빌리티로는 행동 횟수를 소비하지 않는다."
		RETURN (基礎BATTLE_STATE:対象キャラ:最大ＭＰ * 消費ＭＰ + 99) / 100
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 재행동
		アビ汎用文字列:バフ・デバフ対象能力 = 재행동
		アビ汎用変数:効果量 = 1
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 추격_コンセキューションバースト
		アビ汎用文字列:バフ・デバフ対象能力 = 추격
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT


@汎用アビリティ処理_アダプテーションバースト(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = アダプテーションバースト＋＋
	消費ＭＰ = 50
	効果数値 = 200
ELSEIF LEVEL >= 2
	アビ名 = アダプテーションバースト＋
	消費ＭＰ = 50
	効果数値 = 150
ELSEIF LEVEL >= 1
	アビ名 = アダプテーションバースト
	消費ＭＰ = 50
	効果数値 = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP: 최대 MP의{消費ＭＰ}％<br>자신에게 [1000%] (고유 프레임・3턴)의 회피율 상승 효과를 부여한다."
		詳細文文字列受け渡し変数 += @"<br>［공격력✕{効果数値}％］（고유 프레임・7턴）의 특수 회피 시 반격 효과를 부여한다."
		詳細文文字列受け渡し変数 += "이 회피 시 반격은, 자신 이외의 전투 가능한 아군도 참가한다."
		RETURN (基礎BATTLE_STATE:対象キャラ:最大ＭＰ * 消費ＭＰ + 99) / 100
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 회피율_アダプテーションバースト
		アビ汎用文字列:バフ・デバフ対象能力 = 회피율
		アビ汎用変数:効果量 = 1000
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = アダプテーションバースト
		アビ汎用文字列:バフ・デバフ対象能力 = 특수 반응_회피 시
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 7
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@特殊反応_回避時_アダプテーションバースト(バフ番号, 攻撃者隊列, 回避記録順)
#DIM 回避記録順
#DIM 攻撃者隊列
#DIM バフ番号
#DIM 戦闘行動キャラ一時保存
#DIM CONST 対象番号 = 0

CALL アビ対象選択テンプレート_指定(攻撃者隊列)
アビテンプレート用_アビ名 = 回避反撃

アビ汎用変数:効果割合 = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_割合")
アビ汎用変数:効果量 = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値")
CALL アビテンプレート_ダメージ処理_自属性

戦闘行動キャラ一時保存 = 戦闘行動キャラ
FOR LOCAL, 0, 4
	SIF LOCAL == 戦闘行動キャラ一時保存
		CONTINUE
	SIF BATTLE_STATE:LOCAL:キャラ登録番号 < 1
		CONTINUE
	SIF BATTLE_STATE:LOCAL:ＨＰ < 1
		CONTINUE
	戦闘行動キャラ = LOCAL
	アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号)%の追撃！
	CALL アビテンプレート_ダメージ処理_自属性
NEXT
戦闘行動キャラ = 戦闘行動キャラ一時保存

@バフ固有説明文_アダプテーションバースト(バフ・デバフ効果量, バフ・デバフ効果割合)
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
PRINTL 공격을 회피했을 때, 반격합니다.
PRINTL 이 반격은 자신 이외의 전투 가능한 아군도 참가합니다.
RETURN 0


@汎用アビリティ処理_デタミネーションバースト(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = デタミネーションバースト＋＋
	消費ＭＰ = 50
	効果数値 = 50
ELSEIF LEVEL >= 2
	アビ名 = デタミネーションバースト＋
	消費ＭＰ = 50
	効果数値 = 30
ELSEIF LEVEL >= 1
	アビ名 = デタミネーションバースト
	消費ＭＰ = 50
	効果数値 = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"소비 MP: 최대 MP의 {消費ＭＰ}%<br>아군 전체에 웨폰 버스트 효과를 부여한다."
		詳細文文字列受け渡し変数 += @"<br>［{効果数値}%］(고유 프레임・2턴)의 오의 대미지 상승 효과를 부여한다."
		RETURN (基礎BATTLE_STATE:対象キャラ:最大ＭＰ * 消費ＭＰ + 99) / 100
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		CALL アビテンプレート_有利効果_ウェポンバースト

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 디터미네이션 버스트
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_奥義ダメージ
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 2
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT
