@乗機関数_コロッサスレプリカ(処理モード)
#DIMS 処理モード
#DIMS 武装登録, 6
#DIM 乗機攻撃力
#DIM サブパイロット人数
#DIM 乗機命中率
#DIMS 乗機名一時保存
LOCALS = 

SELECTCASE 処理モード
	CASE "乗機登録"
		乗機名一時保存 = コロッサスレプリカ
		;武装が無い場合は「不可」を入れる
		;固有武装の場合は「固有」を入れる
		;装備付け替え可能枠は何入れてもいいけど、わかりやすいように「装備可能」を入れてる
		武装登録:0 = 固有
		武装登録:1 = 固有
		武装登録:2 = 装備可能
		武装登録:3 = 不可
		武装登録:4 = 不可
		武装登録:5 = 不可
		;攻撃系武装で使用する基礎공격력を設定する
		乗機攻撃力 = 700
		乗機命中率 = 95
		;何人サブメンバーを載せられるか？
		サブパイロット人数 = 2

		{
		DT_ROW_ADD "所持乗機データベース", "乗機名", 乗機名一時保存, "乗機攻撃力", 乗機攻撃力, "乗機攻撃力_強化後", 乗機攻撃力, 
			"乗機命中率", 乗機命中率, "乗機命中率_強化後", 乗機命中率, 
			"武装0", 武装登録:0, "武装1", 武装登録:1, "武装2", 武装登録:2, "武装3", 武装登録:3, "武装4", 武装登録:4, "武装5", 武装登録:5,
			"サブ搭乗数", サブパイロット人数, "サブ搭乗数_強化後", サブパイロット人数
		}
	CASE "フレーバー"
		LOCALS += "발츠의 지하 폐공장에서 발견한 도면으로 만든 기체.<br>"
		LOCALS += "콜로서스의 무장과 장갑만을 재현한 양산기.<br>"
		LOCALS += "성정수화는 되어있지 않으므로 성능은 하위 호환이다.<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "作成素材"
		作成素材登録:0 = 鉄鉱石_10
		作成素材登録:1 = 赤熱鉱_10
		作成素材登録:2 = プロミネンスリアクター_1
ENDSELECT

;兵装開発では出現しない（入手法：ダンジョン「バルツ地下廃工場」のイベントで入手）
;@乗機開発対象_コロッサスレプリカ(処理モード)


@乗機兵装関数_コロッサスレプリカ_0(処理モード)
;数字部分は武装登録の番号と対応させること
#DIMS 処理モード
#DIM Lv記録
#DIM 武装番号
;武装番号は武装登録の番号と対応させること
武装番号 = 0
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 = 셸터
	CASE "残弾セット"
		;残弾種別 0:戦闘中回数制限 1:探索中回数制限 2:クールダウンターン発生
		乗機残弾種別:武装番号 = 1
		乗機残弾数:武装番号 = 3
	CASE "アビ説明文"
		LOCALS += @"　%残弾種別名:(乗機残弾種別:武装番号)%제한: 남은{乗機残弾数:武装番号, 2}회<br>"
		LOCALS += "　효과: 아군 전체의 방어력을 3턴 동안 [50％/탑승기 칸] 상승시킨다.<br>"
		LOCALS += "　　　　아군 전체에게 3턴 동안 재생[아군 최대 레벨✕10/탑승기 칸] 효과를 부여한다.<br>"
		LOCALS += "　　　　적 전체에게 [아군 최대 레벨✕25]의 화속성 고정 대미지를 준다.<br>"
		LOCALS += "　　　　서브 멤버에 콜로서스, 혹은 콜로마그 쨩이 있을 경우, 재생 효과의 수치가 두 배가 된다."
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 50
		アビ汎用文字列:バフ・デバフ枠 = 防御力_乗機
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		Lv記録 = 0
		FOR LOCAL, 0, 14
			SIF LOCAL > 3 && LOCAL < 10
				CONTINUE
			SIF BATTLE_STATE:LOCAL:キャラ登録番号 < 1
				CONTINUE
			SIF Lv記録 < BATTLE_STATE:LOCAL:Lv
				Lv記録 = BATTLE_STATE:LOCAL:Lv
		NEXT

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果量 = Lv記録 * 10
		FOR LOCAL, 10, 14
			SIF BATTLE_STATE:LOCAL:キャラ登録番号 < 1
				CONTINUE
			IF キャラ一致チェック(BATTLE_STATE:LOCAL:キャラ登録番号, "[鋼鉄の巨人]コロッサス") || キャラ一致チェック(BATTLE_STATE:LOCAL:キャラ登録番号, "[こうてつのきょじん]コロマグちゃん")
				アビ汎用変数:効果量 = Lv記録 * 20
				BREAK
			ENDIF
		NEXT
		アビ汎用文字列:バフ・デバフ枠 = 再生_乗機
		アビ汎用文字列:バフ・デバフ対象能力 = 再生
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果量 = Lv記録 * 25
		CALL アビテンプレート_ダメージ処理_火属性
ENDSELECT

@乗機兵装関数_コロッサスレプリカ_1(処理モード)
;数字部分は武装登録の番号と対応させること
#DIMS 処理モード
#DIM 武装番号
;武装番号は武装登録の番号と対応させること
武装番号 = 1
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 = 프로미넌스 리액터
	CASE "残弾セット"
		;残弾種別 0:戦闘中回数制限 1:探索中回数制限 2:クールダウンターン発生
		乗機残弾種別:武装番号 = 1
		乗機残弾数:武装番号 = 3
	CASE "アビ説明文"
		LOCALS += @"　%残弾種別名:(乗機残弾種別:武装番号)%제한: 남은{乗機残弾数:武装番号, 2}회<br>"
		LOCALS += "　효과: 아군 전체의 공격력을 3턴 [20％/승기 칸] 상승시킨다.<br>"
		LOCALS += "　　　　적 전체에게 [공격력✕150％]의 화속성 데미지를 준다.<br>"
		LOCALS += "　　　　적 전체의 방어력을 3턴 [15％/乗機枠]하락시킨다. 기본 성공률 80％<br>"
		LOCALS += "　　　　서브 멤버에 콜로서스, 혹은 콜로마그 쨩이 있는 경우, 데미지 부분이 두 번 발생한다."
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 20
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力_乗機
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 150
		CALL アビテンプレート_ダメージ処理_火属性
		FOR LOCAL, 10, 14
			SIF BATTLE_STATE:LOCAL:キャラ登録番号 < 1
				CONTINUE
			IF キャラ一致チェック(BATTLE_STATE:LOCAL:キャラ登録番号, "[鋼鉄の巨人]コロッサス") || キャラ一致チェック(BATTLE_STATE:LOCAL:キャラ登録番号, "[こうてつのきょじん]コロマグちゃん")
				CALL アビテンプレート_ダメージ処理_火属性
				BREAK
			ENDIF
		NEXT

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 15
		アビ汎用文字列:バフ・デバフ枠 = 防御力_乗機
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 80
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@乗機兵装関数_コロッサスレプリカ_2(処理モード, 兵器id = 0)
;数字部分は武装登録の番号と対応させること
#DIMS 処理モード
#DIM 武装番号
#DIM 兵器id
;武装番号は武装登録の番号と対応させること
武装番号 = 2
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名", "アビ効果"
		;兵装処理系は装備してる兵装の関数に投げる
		TRYCCALLFORM 搭載兵器関数_%DT_CELL_GETS("所持兵器データベース", 兵器id, "兵器名", 1)%(処理モード, 兵器id)
		CATCH
			PRINTW 처리가 올바르지 않습니다
			アビテンプレート用_キャンセルフラグ = 1
			RETURN -1
		ENDCATCH
	CASE "アビ説明文"
		LOCALS += @"　%残弾種別名:(乗機残弾種別:武装番号)%제한: 남은{乗機残弾数:武装番号, 2}회<br>"
		TRYCCALLFORM 搭載兵器関数_%DT_CELL_GETS("所持兵器データベース", 兵器id, "兵器名", 1)%(処理モード, 兵器id)
			詳細文文字列受け渡し変数 = %LOCALS%%詳細文文字列受け渡し変数%
		CATCH
			PRINTW 처리가 올바르지 않습니다
			アビテンプレート用_キャンセルフラグ = 1
			RETURN -1
		ENDCATCH
	CASE "残弾セット"
		乗機残弾種別:武装番号 = DT_CELL_GET("所持兵器データベース", 兵器id, "残弾種別", 1)
		乗機残弾数:武装番号 = DT_CELL_GET("所持兵器データベース", 兵器id, "残弾数_強化後", 1)
;---------------------------------------------------------------------------------------------------------
;編集するのはここから
	CASE "装備条件説明文"
		LOCALS += @"　장비 가능 카테고리: 기계계or마법계<br>"
		LOCALS += "　장비 가능 거리: 근거리<br>"
		LOCALS += "　기타 조건: 없음<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "装備条件判定"
		;装備不可だと0、可能だと1を返す
		SIF 乗機装備条件判定(兵器id, "機械系_魔法系", "近距離")
			RETURN 1
		RETURN 0
;編集するのはここまで
;---------------------------------------------------------------------------------------------------------
ENDSELECT
