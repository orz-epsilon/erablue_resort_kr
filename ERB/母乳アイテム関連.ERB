
@所持ミルク一覧表示
#DIM ページ数
#DIM 表示数カウント
#DIM ミルク番号
#DIM ボタン記録
#DIM 一括削除モード
#DIM 一括削除件数
#DIM 次ページフラグ
#DIMS 削除番号
#DIMS 削除番号配列, 101
#DIMS ミルク名
#DIMS 消費期限

DRAWLINE
PRINTL 소지하고 있는 우유 목록입니다
IF 一括削除モード
	PRINTL 선택한 우유를 일괄 폐기할 수 있습니다 (100개까지)
ELSE
	PRINTL 선택하면 사용/폐기할 수 있습니다
ENDIF
DRAWLINE

表示数カウント = 0
FOR ミルク番号, 0, DT_ROW_LENGTH("ミルクデータベース") + (15 - DT_ROW_LENGTH("ミルクデータベース") % 15)
	IF 表示数カウント >= 15
		;表示スキップした中に表示するミルクがあるなら次ページフラグを立てとく
		SIF DT_ROW_LENGTH("ミルクデータベース") > ミルク番号 && DT_CELL_GETS("ミルクデータベース", ミルク番号, "状態") != "フタ"
			次ページフラグ = 1
		CONTINUE
	ENDIF
	IF DT_ROW_LENGTH("ミルクデータベース") > ミルク番号
		SIF DT_CELL_GETS("ミルクデータベース", ミルク番号, "状態") == "フタ"
			CONTINUE
		SIF ページ数 !=  表示数カウント / 15 
			CONTINUE
		SIF 一括削除モード && STRCOUNT(削除番号, @"_{DT_CELL_GET("ミルクデータベース", ミルク番号, "id")}_")
			SETCOLOR カラーパレット("黄")
		IF DT_CELL_GETS("ミルクデータベース", ミルク番号, "状態") == "通常"
			ミルク名 = %SUBSTRING(DT_CELL_GETS("ミルクデータベース", ミルク番号, "生産元名称"), STRFIND(DT_CELL_GETS("ミルクデータベース", ミルク番号, "生産元名称"), "]") + 1, -1)%のミルク
		ELSE
			ミルク名 = %SUBSTRING(DT_CELL_GETS("ミルクデータベース", ミルク番号, "生産元名称"), STRFIND(DT_CELL_GETS("ミルクデータベース", ミルク番号, "生産元名称"), "]") + 1, -1)%の%DT_CELL_GETS("ミルクデータベース", ミルク番号, "状態")%ミルク
		ENDIF
		SELECTCASE DAY - DT_CELL_GET("ミルクデータベース", ミルク番号, "生産日")
			CASE 0
				消費期限 = あと３日
			CASE 1
				消費期限 = あと２日
			CASE 2
				消費期限 = あと１日
			CASE 3
				消費期限 = 本日まで
			CASEELSE
				消費期限 = 期限切れ
		ENDSELECT
		PRINTBUTTON @"[{ミルク番号, 3}] - %ミルク名, 50, LEFT% - 유통기한:%消費期限%", 우유 번호
		RESETCOLOR
		PRINTL
	ELSE
		PRINTL
	ENDIF
	表示数カウント ++
NEXT

DRAWLINE
IF ページ数 > 0
	PRINTBUTTONLC "[9000] 이전 페이지", 9000
ELSE
	PRINTC  
ENDIF
IF 次ページフラグ
	PRINTBUTTONLC "[9001] 다음 페이지", 9001
ELSE
	PRINTC  
ENDIF
PRINTBUTTONLC "[9999] 돌아가기", 9999
IF 一括削除モード && 削除番号 != ""
	PRINTBUTTONLC "[9998] 폐기하기", 9998
ELSEIF 一括削除モード
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [9998] 폐기하기
	PRINT            
	RESETCOLOR
ELSE
	PRINTBUTTONLC "[9998] 일괄 삭제", 9998
ENDIF

BINPUT

ボタン記録 = RESULT
SELECTCASE ボタン記録
	CASE 9999
		IF 一括削除モード
			一括削除件数 = 0
			一括削除モード = 0
			削除番号 = 
			RESTART
		ENDIF
		ページ数 = 0
		RETURN 0
	CASE 9998
		;一括削除へ
		IF 一括削除モード
			VARSET 削除番号配列
			SPLIT 削除番号, "_", 削除番号配列
			FOR ミルク番号, 1, 一括削除件数 + 1
				DT_CELL_SET "ミルクデータベース", TOINT(削除番号配列:ミルク番号), "状態", "フタ", 1
			NEXT
			PRINTFORMW {一括削除件数}개의 우유를 삭제했습니다
			一括削除モード = 0
			一括削除件数 = 0
			削除番号 = 
		ELSE
			一括削除モード = 1
			一括削除件数 = 0
			削除番号 = 
		ENDIF
		RESTART
	CASE 9000
		ページ数 -= 1
		RESTART
	CASE 9001
		ページ数 += 1
		RESTART
	CASEELSE
		IF 一括削除モード
			SIF 一括削除件数 > 99
				RESTART
			IF STRCOUNT(削除番号, @"_{DT_CELL_GET("ミルクデータベース", ボタン記録, "id")}_") == 0
				IF 削除番号 != ""
					削除番号 += @"{DT_CELL_GET("ミルクデータベース", ボタン記録, "id")}_"
				ELSE
					削除番号 += @"_{DT_CELL_GET("ミルクデータベース", ボタン記録, "id")}_"
				ENDIF
				一括削除件数 += 1
			ELSE
				VARSET 削除番号配列
				SPLIT 削除番号, @"_{DT_CELL_GET("ミルクデータベース", ボタン記録, "id")}_", 削除番号配列
				削除番号 = %削除番号配列:0%_%削除番号配列:1%
				一括削除件数 -= 1
			ENDIF
		ELSE
			DO
			LOCAL = 0
			SIF ボタン記録 < DT_ROW_LENGTH("ミルクデータベース") - 1
				LOCAL = 1
			CALL ミルク詳細表示(DT_CELL_GET("ミルクデータベース", ボタン記録, "id"), ボタン記録, LOCAL)
			IF RESULT == 0
				RESTART
			ELSE
				ボタン記録 += RESULT
			ENDIF
			LOOP 1
		ENDIF
		RESTART
ENDSELECT

@ミルク詳細表示(ボタン記録, 前フラグ, 次フラグ)
#DIMS ミルク名
#DIM ボタン記録
#DIM 前フラグ
#DIM 次フラグ
#DIM DAY算出
#DIM TIME算出
#DIM CONST レイヤー番号 = 1500
#DIM size
#DIMS 画像リソース保存
#DIM 対象キャラ
#DIM 使用キャラ

IF DT_CELL_GETS("ミルクデータベース", ボタン記録, "状態", 1) == "通常"
	ミルク名 = %SUBSTRING(DT_CELL_GETS("ミルクデータベース", ボタン記録, "生産元名称", 1), STRFIND(DT_CELL_GETS("ミルクデータベース", ボタン記録, "生産元名称", 1), "]") + 1, -1)%のミルク
ELSE
	ミルク名 = %SUBSTRING(DT_CELL_GETS("ミルクデータベース", ボタン記録, "生産元名称", 1), STRFIND(DT_CELL_GETS("ミルクデータベース", ボタン記録, "生産元名称", 1), "]") + 1, -1)%の%DT_CELL_GETS("ミルクデータベース", ボタン記録, "状態", 1)%ミルク
ENDIF
対象キャラ = キャラ検索(DT_CELL_GETS("ミルクデータベース", ボタン記録, "生産元名称", 1))

;ミルク詳細表示
$ミルク表示
DRAWLINE
PRINTL
PRINTFORML ■%ミルク名%
PRINTL

;グラフィック表示
IF 対象キャラ >= 0
	;デフォ立ち絵を表示する
	画像リソース保存 '= DT_CELL_GETS("ミルクデータベース", ボタン記録, "グラフィック保存", 1)
	IF 画像リソース保存 == "" && 差分抽出(対象キャラ, "短冊_", "短冊_デフォルト", "表情")
		画像リソース保存 '= RESULTS
	ENDIF
	IF 画像リソース保存 != ""
		CALL PRINT_STRL(@"IMAGEPATH_%CSTR:対象キャラ:画像フォルダ%/%画像リソース保存%")
	ENDIF
ENDIF



;データベースに保存しといた文字列を引っ張ってくる
; 画像リソース保存 = %DT_CELL_GETS("ミルクデータベース", ボタン記録,  "撮影対象画像リソース", 1)%
; VARSET RESULTS
; ;エフェクト保存分を分割
; SPLIT 画像リソース保存, "+++", RESULTS
; IF RESULTS:0 != ""
; 	CALL GCREATE_拡張子(レイヤー番号, RESULTS:0)
; 	;画像生成に成功したら次へ
; 	IF RESULT
; 		LOCAL = 0
; 		IF STRFIND(RESULTS:0, "短冊_") > -1
; 			LOCAL = スプライト作成("ミルクグラ", レイヤー番号, 1)
; 			size = 2250
; 		ELSE
; 			LOCAL = スプライト作成("ミルクグラ", レイヤー番号, 0)
; 			size = 1125
; 		ENDIF
; 		;スプライト生成に成功したら表示へ
; 		IF LOCAL
; 			SIF RESULTS:1 != ""
; 				LOCAL = スプライト合成("ミルクグラ", レイヤー番号, RESULTS:1)
; 			SIF RESULTS:2 != ""
; 				LOCAL = スプライト合成("ミルクグラ", レイヤー番号, RESULTS:2)
; 			SIF RESULTS:3 != ""
; 				LOCAL = スプライト合成("ミルクグラ", レイヤー番号, RESULTS:3)
; 			HTML_PRINT @"　　<img src='ミルクグラ' width='1125' height='{size}'>"
; 			FOR LOCAL, 0, size / 100
; 				PRINTL
; 			NEXT
; 		ENDIF
; 	ENDIF
; ENDIF

DAY算出 = DT_CELL_GET("ミルクデータベース", ボタン記録, "生産日", 1)
PRINTFORML 　　생산 일시:{DAY算出 / 120 + 1}년째　%月登録:((DAY算出 / 30) % 4)%　{DAY算出 % 30 + 1}일(%曜日登録:(DAY算出 % 7)%)
PRINTFORML 　　생산원:%DT_CELL_GETS("ミルクデータベース", ボタン記録, "生産元名称", 1)%
PRINTL

PRINTBUTTONLC "[999] 돌아가기", 999
PRINTL
IF 射精可能(MASTER)
	PRINTBUTTONLC @"[1000] 이 우유를 %CALLNAME:MASTER%에 사용한다", 1000
	PRINTL 
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAINFORM [1000] 이 우유를 %CALLNAME:MASTER%에 사용한다
	RESETCOLOR
	PRINTL 
ENDIF
IF TARGET
	IF 射精可能(TARGET)
		PRINTBUTTONLC @"[1001] 이 우유를 %CALLNAME:TARGET%에 사용한다", 1001
		PRINTL 
	ELSE
		SETCOLOR カラーパレット("グレーアウト")
		PRINTPLAINFORM [1000] 이 우유를 %CALLNAME:TARGET%에 사용한다
		RESETCOLOR
		PRINTL 
	ENDIF
ELSE
	PRINTL 
ENDIF
PRINTBUTTONLC "[1010] 이 우유를 폐기한다", 1010
PRINTL 
IF 前フラグ
	PRINTBUTTONLC "[4] 이전 우유로", 4
ELSE
	PRINTC 　
ENDIF
IF 次フラグ
	PRINTBUTTONLC "[6] 다음 우유로", 6
ENDIF

BINPUT
SELECTCASE RESULT
	CASE 1000, 1001
		IF RESULT == 1000
			使用キャラ = MASTER
		ELSE
			使用キャラ = TARGET
		ENDIF
		PRINTFORML %CALLNAME:使用キャラ%은(는) %ミルク名%을(를) 다 마셨다.
		PRINTDATA
			DATA 부드럽고 깊은 맛이다.
			DATA 깔끔하고 잡미 없는 맛이다.
			DATA 진하고 달콤한 맛이다.
			DATA 뒷맛이 상쾌하고 깔끔한 맛이다.
		ENDDATA
		PRINTL 
		PRINTFORMW %CALLNAME:使用キャラ%의 정력이 회복되었다!
		SELECTCASE DT_CELL_GETS("ミルクデータベース", ボタン記録, "状態", 1)
			CASE "特濃"
				BASE:使用キャラ:精力 = MAXBASE:使用キャラ:精力
				;濃厚率補正は100％まで
				SIF TCVAR:使用キャラ:ミルク濃厚率補正 < 100
					TCVAR:使用キャラ:ミルク濃厚率補正 += 50
			CASE "濃厚"
				BASE:使用キャラ:精力 = MAXBASE:使用キャラ:精力
				;濃厚率補正は50％まで
				SIF TCVAR:使用キャラ:ミルク濃厚率補正 < 50
					TCVAR:使用キャラ:ミルク濃厚率補正 += 25
			CASEELSE
				BASE:使用キャラ:精力 = MIN(BASE:使用キャラ:精力 + MAXBASE:使用キャラ:精力 / 2, MAXBASE:使用キャラ:精力)
				;濃厚率補正は30％まで
				SIF TCVAR:使用キャラ:ミルク濃厚率補正 < 30
					TCVAR:使用キャラ:ミルク濃厚率補正 += 10
		ENDSELECT
		DT_CELL_SET "ミルクデータベース", ボタン記録, "状態", "フタ", 1
	CASE 1010
		DRAWLINE
		PRINTFORML 『%ミルク名%』을(를) 폐기해도 괜찮습니까?
		PRINTBUTTONLC "[0] 예", 0
		PRINTBUTTONLC "[1] 아니요", 1
		BINPUT
		IF RESULT == 0
			PRINTFORMW 『%ミルク名%』을(를) 폐기했습니다.
			DT_CELL_SET "ミルクデータベース", ボタン記録, "状態", "フタ", 1
		ELSE
			GOTO ミルク表示
		ENDIF
	CASE 4
		RETURN -1
	CASE 6
		RETURN 1
	CASE 999
		RETURN 0
ENDSELECT












