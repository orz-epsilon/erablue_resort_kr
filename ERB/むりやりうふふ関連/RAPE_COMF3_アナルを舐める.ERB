;-------------------------------------------------
;アナルを舐める
;愛撫系コマンド、レベル1
;-------------------------------------------------
@RAPE_COMNAME3
#FUNCTIONS
TSTR:コマンド名受渡 = 항문을 핥다



;===================================================
;コマンドタイプ
;===================================================
@RAPE_COMTYPE_3
#FUNCTION
TFLAG:コマンドタイプ受渡 = RAPE_COMTYPE("Ａ系")


@RAPE_COM_TOOLTIP_3
LOCALS = <br>■항문을 핥다<br>
LOCALS += "상대의 항문을 혀로 애무하는 커맨드。<br>실행자가[혀놀림][고양이 혀]를 가지고 있을 경우, 효과가 증감한다."
LOCALS += "<br>RAPE_COM 타입: A계<br>획득 소스: 쾌A・노출・접촉・액체"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@RAPE_COM3
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

LOCAL = 50
;調教者のTALENT:舌使いをみる
SIF TALENT:PLAYER:舌使い
	TIMES LOCAL , 1.50
;調教者のTALENT:猫舌をみる
SIF TALENT:PLAYER:猫舌
	TIMES LOCAL , 0.75
CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, LOCAL)
CALL SOURCE_CALC_液体(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 180)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 150)

;下タイツがある場合は破く。
IF TEQUIP:下タイツあり == 1
	TEQUIP:下タイツ破れ = 1
	RFLAG:コマンド結果受渡し変数 = 1
ENDIF
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 3
	EXP:PLAYER:同性愛経験 += 3
ENDIF

;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
SIF TALENT:PLAYER:猫舌
	EXP:PLAYER:嗜虐快楽経験 += 1

CALL 初体験日登録処理(PLAYER, TARGET, "ファーストキス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "アナル", "", 1)
IF RESULT
	CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:TARGET%의 [애널]에 첫 키스를 바쳤다.</font><img src='えっちハート'>","うふふ")
ENDIF


RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@RAPE_COM_ABLE3
;アナル舐め実行判定
;一括管理
SIF GLOBAL_RAPE_COMABLE(3)
	RETURN RESULT
;Aがない場合駄目
SIF TALENT:TARGET:Ａ感度 == -2
	RETURN 0

;猫舌の調教者の場合は技巧Lv2以上必要
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	RETURN 0

;下半身着衣
;後鎧、めくれない服があると無理
SIF TEQUIP:下腹部後鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0

;道具系はレイプからオミットしてるけど、一応残しておく。
;アナルバイブ使用中は不可
SIF TEQUIP:14
	RETURN 0
;アナルビーズ使用中は不可
SIF TEQUIP:15
	RETURN 0
;普通の浣腸使用中は不可
SIF TEQUIP:21
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:22
	RETURN 0
;電極使用中は不可
SIF TEQUIP:23
	RETURN 0
;口を何かに使ってる・届かないなら無理
SIF TEQUIP:PLAYER:口
	RETURN 0
;アナルを何かに使ってる・届かないなら無理
SIF TEQUIP:TARGET:アナル
	RETURN 0
;挿入系時は個別にOFF
SIF MODE_確認_完全一致("Ｖ挿入系", PLAYER, TARGET) > -1 || MODE_確認_完全一致("Ａ挿入系", PLAYER, TARGET) > -1
	RETURN 0
SIF MODE_確認_完全一致("Ｖ挿入系", TARGET, PLAYER) > -1 || MODE_確認_完全一致("Ａ挿入系", TARGET, PLAYER) > -1
	RETURN 0

;オートコマンド中の制限
IF FLAG:オートコマンド判定中フラグ
	SIF 初体験済みチェック("ファーストキス", PLAYER) == 0
		RETURN 0
	SIF EXP:PLAYER:Ａ経験 == 0 || EXP:TARGET:Ａ経験 == 0
		RETURN 0
ENDIF

RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@RAPE_MESSAGE_COM3
PRINTFORM %CALLNAME:PLAYER%은(는)
PRINTFORM %CALLNAME:TARGET%의
;下タイツがある場合は破く。
SIF RFLAG:コマンド結果受渡し変数 == 1
	PRINTFORM 입고 있던 타이츠를 강제로%TEXTR("찢어발기고/찢어")%、
;Ａ経験10以下
IF EXP:Ａ経験 <= 10
	PRINTFORM 굳게 닫힌
ENDIF
PRINTFORM 항문을 정성스럽게 핥아 풀고
;Ａ経験100以上＋欲望のPALAMLV3以上
SIF EXP:Ａ経験 >= 100 && PALAM:欲情 >= PALAMLV:3
	PRINTFORM , 혀끝을 찔러 넣어 구멍 안까지 휘저어 주었
PRINTFORML 다…

