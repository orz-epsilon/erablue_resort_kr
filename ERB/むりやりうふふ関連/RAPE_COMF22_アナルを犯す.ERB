;-------------------------------------------------
;セックス系コマンド
;-------------------------------------------------
@RAPE_COMNAME22
#FUNCTIONS
TSTR:コマンド名受渡 = 항문을 범하다
SIF MODE_確認_完全一致("Ａ挿入系", PLAYER, TARGET) > -1
	TSTR:コマンド名受渡 = 체위를 바꿔 A를 범하다

SIF 派生名フラグ
	TSTR:コマンド名受渡 = 항문을 범하다
;===================================================
;コマンドタイプ
;===================================================
@RAPE_COMTYPE_22
#FUNCTION
TFLAG:コマンドタイプ受渡 = RAPE_COMTYPE("Ａ系")


@RAPE_COM_TOOLTIP_22
LOCALS = <br>■항문을 범하다<br>
LOCALS += "상대의 항문에 삽입하여, 애널 섹스를 하는 커맨드. <br>A삽입계 모드 시, [체위를 바꿔 A를 범하다]로 변화한다. <br>A삽입계 모드가 되어, 모드를 변경하거나 성행위를 종료할 때까지 지속적으로 쾌락이 발생한다."
LOCALS += "<br>RAPE_COM 타입: A계<br>획득 소스: 쾌A・노출・접촉・고통"
LOCALS += "<br>고유 획득 경험: C성교 경험_A삽입(PLAYER)・A성교 경험(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@RAPE_COM22

	$体位_LOOP
	DRAWLINE
	PRINTL ■체위를 선택해 주세요
	PRINTL
	FOR LOCAL, 0, 20
		TRYCCALLFORM RAPE_COM_ABLE22_{LOCAL}
			IF RESULT
				SELECTCASE LOCAL
					CASE 0
						PRINTBUTTONLC "[0]A정상위", 0
					CASE 1
						PRINTBUTTONLC "[1]A후배위", 1
					CASE 2
						PRINTBUTTONLC "[2]A누운 후배위", 2
					CASE 3
						PRINTBUTTONLC "[3]A대면좌위", 3
					CASE 4
						PRINTBUTTONLC "[4]A배면좌위", 4
					CASE 5
						PRINTBUTTONLC "[5]A기승위", 5
					CASE 6
						PRINTBUTTONLC "[6]A측위", 6
					CASE 7
						PRINTBUTTONLC "[7]A 굴곡위", 7
					CASE 8
						PRINTBUTTONLC "[8]A 에키벤", 8
					CASE 9
						PRINTBUTTONLC "[9]A 배면 에키벤", 9
					CASE 10
						PRINTBUTTONLC "[10]A 대면 입위", 10
					CASE 11
						PRINTBUTTONLC "[11]A 스탠딩 백", 11
				ENDSELECT
			ENDIF
		CATCH
			CONTINUE
		ENDCATCH
	NEXT

	PRINTL
	PRINTL
	PRINTBUTTONLC "[999]돌아가기", 999

	$INPUT_LOOP
	INPUT

	LOCAL = RESULT
	SELECTCASE LOCAL
		CASE 0 TO 11
			TRYCALLFORM RAPE_COM_ABLE22_{LOCAL}
			IF RESULT
				IF TARGET == MASTER
					TARGET = PLAYER
					PLAYER = MASTER
					RFLAG:指示モード対象あなたフラグ = 1
					TRYCALLFORM CAN_RAPE_Ａ体位{LOCAL}(1)
					IF !RESULT
						TRYCALLFORM CAN_RAPE_Ａ体位{LOCAL}(0)
						PLAYER = TARGET
						TARGET = MASTER
						GOTO 体位_LOOP
					ENDIF
					RFLAG:指示モード対象あなたフラグ = 0
					PLAYER = TARGET
					TARGET = MASTER
				ELSE
					TRYCALLFORM CAN_RAPE_Ａ体位{LOCAL}(1)
					IF !RESULT
						TRYCALLFORM CAN_RAPE_Ａ体位{LOCAL}(0)
						GOTO 体位_LOOP
					ENDIF
				ENDIF
				TRYCALLFORM RAPE_COM22_{LOCAL}
				;メッセージ用に体位の種類を保存
				RFLAG:コマンド結果受渡し変数 = LOCAL
				RETURN 1
			ENDIF
			GOTO INPUT_LOOP
		CASE 999
			RETURN -1
		CASEELSE
			GOTO INPUT_LOOP
	ENDSELECT


@RAPE_COM22_0
;引数は前から快楽量、露出量、接触量
CALL Ａ体位RAPE_COM(400, 50, 150)
CALL MODE_CHANGE("Ａ正常位", PLAYER, TARGET)
RETURN RESULT

@RAPE_COM22_1
;後背位
CALL Ａ体位RAPE_COM(400, 70, 100)
CALL MODE_CHANGE("Ａ後背位", PLAYER, TARGET)
RETURN RESULT

@RAPE_COM22_2
;寝バック
CALL Ａ体位RAPE_COM(600, 50, 150)
CALL MODE_CHANGE("Ａ寝バック", PLAYER, TARGET)
RETURN RESULT

@RAPE_COM22_3
;対面座位
CALL Ａ体位RAPE_COM(550, 70, 200)
CALL MODE_CHANGE("Ａ対面座位", PLAYER, TARGET)
RETURN RESULT

@RAPE_COM22_4
;背面座位
CALL Ａ体位RAPE_COM(450, 90, 120)
CALL MODE_CHANGE("Ａ背面座位", PLAYER, TARGET)
RETURN RESULT

@RAPE_COM22_5
;騎乗位
CALL Ａ体位RAPE_COM(500, 120, 150)
CALL SOURCE_CALC_奉仕(TARGET, PLAYER, 250)
CALL MODE_CHANGE("Ａ騎乗位", PLAYER, TARGET)
RETURN RESULT

@RAPE_COM22_6
;側位
CALL Ａ体位RAPE_COM(500, 70, 100)
CALL MODE_CHANGE("Ａ側位", PLAYER, TARGET)
RETURN RESULT

@RAPE_COM22_7
;屈曲位
CALL Ａ体位RAPE_COM(550, 70, 100)
CALL MODE_CHANGE("Ａ屈曲位", PLAYER, TARGET)
RETURN RESULT

@RAPE_COM22_8
;駅弁
CALL Ａ体位RAPE_COM(450, 70, 200)
CALL MODE_CHANGE("Ａ駅弁", PLAYER, TARGET)
RETURN RESULT

@RAPE_COM22_9
;背面駅弁
CALL Ａ体位RAPE_COM(400, 110, 120)
CALL MODE_CHANGE("Ａ背面駅弁", PLAYER, TARGET)
RETURN RESULT

@RAPE_COM22_10
;対面立位
CALL Ａ体位RAPE_COM(400, 70, 150)
CALL MODE_CHANGE("Ａ対面立位", PLAYER, TARGET)
RETURN RESULT

@RAPE_COM22_11
;立ちバック
CALL Ａ体位RAPE_COM(450, 90, 100)
CALL MODE_CHANGE("Ａ立ちバック", PLAYER, TARGET)
RETURN RESULT




@Ａ体位RAPE_COM(ARG, ARG:1, ARG:2)
;-------------------------------------------------
;RAPE_COM別FLAGの設定
;-------------------------------------------------

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, ARG)
CALL SOURCE_CALC_露出(TARGET, PLAYER, ARG:1)
CALL SOURCE_CALC_接触(TARGET, PLAYER, ARG:2)

IF MODE_確認_ターゲット側("ペニスバンド", PLAYER) < 0
	CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, ARG)
ELSE
	CALL SOURCE_CALC_快Ｖ(PLAYER, TARGET, ARG / 3)
ENDIF

;苦痛
LOCAL = 800 * PAIN_CHECK_A(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
IF EXP:Ａ性交経験 == 0
	CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:TARGET%의 Ａ처녀를 빼앗았다.</font><img src='えっちハート'>","うふふ")
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:PLAYER%에게 Ａ처녀를 빼앗겼다.</font><img src='えっちハート'>","うふふ")
ENDIF
EXP:PLAYER:Ｃ性交経験_Ａ挿入 ++
EXP:Ａ性交経験 ++
CALL LOST_A_VIRGIN(TARGET, PLAYER, 1)
CALL LOST_MAN_A_VIRGIN(PLAYER, TARGET)
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 4
	EXP:PLAYER:同性愛経験 += 4
ENDIF


;愛情経験
EXP:愛情経験 += TALENT:恋慕

RETURN 1


@CAN_RAPE_Ａ体位(ARG)
#DIM 実行値
#DIM ＋表示フラグ

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;レイプなので素通し

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@RAPE_COM_ABLE22
;Ａ挿入共通実行判定
;一括管理
SIF GLOBAL_RAPE_COMABLE(22)
	RETURN RESULT
;Aがない場合駄目
SIF TALENT:TARGET:Ａ感度 == -2
	RETURN 0
;調教者がオトコ、半人半妖、ふたなりの場合はある程度勃起してないとダメ
SIF (TALENT:PLAYER:性別 & 2) && BASE:PLAYER:勃起 < 1000
	RETURN 0
;アナルバイブ挿入中はダメ
SIF TEQUIP:14
	RETURN 0
;アナルビーズ使用中は不可
SIF TEQUIP:15
	RETURN 0
;普通の浣腸使用中は不可
SIF TEQUIP:21
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:22
	RETURN 0
;電極使用中は不可
SIF TEQUIP:23
	RETURN 0
;一定のＡ経験が必要　無理やりなのでボーダーを下げた
SIF EXP:2 < EXPLV:2
	RETURN 0

;調教者がオトコ、半人半妖、ふたなり、もしくはペニスバンドがないとダメ
SIF !(TALENT:PLAYER:性別 & 2) && MODE_確認_ターゲット側("ペニスバンド", PLAYER) < 0
	RETURN 0

;下半身着衣
;前鎧、めくれない服、タイツがあると無理
SIF TEQUIP:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0

IF MODE_確認_完全一致("Ａ挿入系", PLAYER, TARGET) < 0 && MODE_確認_完全一致("Ａ挿入系", TARGET, PLAYER) < 0
	;ちんこを何かに使ってる・届かないなら無理
	SIF TEQUIP:PLAYER:ペニス
		RETURN 0
	SIF TEQUIP:TARGET:アナル
		RETURN 0
ENDIF

;オートコマンド中の制限
IF FLAG:オートコマンド判定中フラグ
	SIF EXP:PLAYER:Ａ経験 == 0 || EXP:TARGET:Ａ経験 == 0
		RETURN 0
	SIF MODE_確認_完全一致("Ａ挿入系", PLAYER, TARGET) > -1 && MODE_確認_完全一致("Ａ挿入系", TARGET, PLAYER) > -1
		RETURN 0
ENDIF

RETURN 1

;力ずくでモノにしている状況で、やれベッドがないとダメだのマットがないとダメだのなんて気にしない

@RAPE_COM_ABLE22_0
;正常位

RETURN 1

@RAPE_COM_ABLE22_1
;後背位
;だいたいいつでもOK

RETURN 1

@RAPE_COM_ABLE22_2
;寝バック
RETURN 1

@RAPE_COM_ABLE22_3
;対面座位

RETURN 1

@RAPE_COM_ABLE22_4
;背面座位

RETURN 1

@RAPE_COM_ABLE22_5
;騎乗位

;睡眠中ダメ
SIF CFLAG:TARGET:睡眠
	RETURN 0

RETURN 1

@RAPE_COM_ABLE22_6
;側位

RETURN 1

@RAPE_COM_ABLE22_7
;屈曲位

RETURN 1

@RAPE_COM_ABLE22_8
;駅弁
;男側がハーヴィンかつ相手がハーヴィン以外だとダメ
SIF TALENT:PLAYER:種族 == 4 && TALENT:TARGET:種族 != 4
	RETURN 0
;睡眠中ダメ
SIF CFLAG:TARGET:睡眠
	RETURN 0

RETURN 1

@RAPE_COM_ABLE22_9
;背面駅弁
;男側がハーヴィンかつ相手がハーヴィン以外だとダメ
SIF TALENT:PLAYER:種族 == 4 && TALENT:TARGET:種族 != 4
	RETURN 0
;睡眠中ダメ
SIF CFLAG:TARGET:睡眠
	RETURN 0

RETURN 1

@RAPE_COM_ABLE22_10
;対面立位
;男側がハーヴィンかつ相手がハーヴィン以外だとダメ
SIF TALENT:PLAYER:種族 == 4 && TALENT:TARGET:種族 != 4
	RETURN 0
;睡眠中ダメ
SIF CFLAG:TARGET:睡眠
	RETURN 0

RETURN 1

@RAPE_COM_ABLE22_11
;立ちバック
;男側がハーヴィンかつ相手がハーヴィン以外だとダメ
SIF TALENT:PLAYER:種族 == 4 && TALENT:TARGET:種族 != 4
	RETURN 0
;睡眠中ダメ
SIF CFLAG:TARGET:睡眠
	RETURN 0

RETURN 1



;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@RAPE_MESSAGE_COM22
#DIM 同時キャラ番号


IF MODE_確認_ターゲット側("ペニスバンド", PLAYER) < 0
	LOCALS = 단단하게 솟아오른 음경
ELSE
	LOCALS = 페니스 밴드
ENDIF

SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE 0
		;正常位
		PRINTFORM %CALLNAME:PLAYER%은(는) 똑바로 눕혀 쓰러뜨린 %CALLNAME:TARGET%의,
	CASE 1
		;後背位
		PRINTFORM %CALLNAME:PLAYER%은(는) %CALLNAME:TARGET%을(를) 엎드리게 하여 억누르고,
	CASE 2
		;寝バック
		PRINTFORM %CALLNAME:PLAYER%은(는) 엎드리게 하여 억누른 %CALLNAME:TARGET%의,
	CASE 3
		;対面座位
		PRINTFORM %CALLNAME:PLAYER%은(는) %CALLNAME:TARGET%을(를) 마주 보게 안고,
	CASE 4
		;背面座位
		PRINTFORM %CALLNAME:PLAYER%은(는) %CALLNAME:TARGET%을(를) 뒤에서 안고,
	CASE 5
		;騎乗位
		PRINTFORM %CALLNAME:PLAYER%은(는) %CALLNAME:TARGET%을(를) 위에 태우고, 그대로 아래에서,
	CASE 6
		;側位
		PRINTFORM %CALLNAME:PLAYER%은(는) 옆으로 눕힌 %CALLNAME:TARGET%의,
	CASE 7
		;屈曲位
PRINTFORM %CALLNAME:PLAYER%은(는) 눕힌 %CALLNAME:TARGET%의 다리를 들어 올리고,
	CASE 8
		;駅弁
PRINTFORM %CALLNAME:PLAYER%은(는) %CALLNAME:TARGET%을(를) 마주 본 채 들어 올리고,
	CASE 9
		;背面駅弁
PRINTFORM %CALLNAME:PLAYER%은(는) %CALLNAME:TARGET%을(를) 뒤돌아 안아 들어 올리고,
	CASE 10
		;対面立位
PRINTFORM %CALLNAME:PLAYER%은(는) 선 채로 %CALLNAME:TARGET%의 한쪽 다리를 들어 올리고,
	CASE 11
		;立ちバック
PRINTFORM %CALLNAME:PLAYER%은(는) %CALLNAME:TARGET%에게 뒤돌아 엉덩이를 내밀게 하고,
ENDSELECT

PRINTFORM 꽉 조여져%TEXTR("약간 저항하/눈물겨운 저항을 계속하/쓸데없는 저항을 계속하")%는%TEXTR("엉덩이 구멍/애널/항문/엉덩이 구멍/엉덩이 보지")%으로


IF MODE_確認_ターゲット側("ペニスバンド", PLAYER) < 0
	PRINTFORM %TEXTR("딱딱하게 발기하/단단하게 솟아올랐")%은%TEXTR("음경/자지/페니스/육봉")%
ELSE
	PRINTFORM 페니스 밴드
ENDIF

PRINTFORML 을(를)%TEXTR("삽입해 나갔다/밀어 넣었다/쑤셔 넣었다/쑤셔 넣어 나갔다/채워 나갔다")%…

;アナルに関しては、シコらせられそうなパワーワードごり押しがそんなに強そうに見えないのでクソ長派生はとりあえずなし。
