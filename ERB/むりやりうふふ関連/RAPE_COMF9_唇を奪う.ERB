;-------------------------------------------------
;唇を奪う
;コミュニケーション系コマンド、レベル1
;-------------------------------------------------
@RAPE_COMNAME9
#FUNCTIONS
TSTR:コマンド名受渡 = 입술을 빼앗다



;===================================================
;コマンドタイプ
;===================================================
@RAPE_COMTYPE_9
#FUNCTION
TFLAG:コマンドタイプ受渡 = RAPE_COMTYPE("うふふ_被挿入中")



@RAPE_COM_TOOLTIP_9
LOCALS = <br>■입술을 빼앗다<br>
LOCALS += "상대의 입술을 억지로 빼앗는다. 키스 모드로 이행. <br>[혀놀림][고양이혀]에 따라 효과가 증감."
LOCALS += "<br>RAPE_COM타입: 우후후or피삽입중or성희롱<br>획득 소스: 노출・접촉・쾌S・불결・고통(일정 조건 시)"
LOCALS += "<br>고유 획득 경험: 키스 경험(당신・TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@RAPE_COM9
;-------------------------------------------------
;モードの設定
;-------------------------------------------------
CALL MODE_CHANGE("キス", PLAYER, TARGET)
;-------------------------------------------------
;RAPE_COM別FLAGの設定
;-------------------------------------------------
;奉仕コマンドフラグ
RCVAR:TARGET:奉仕コマンドフラグ = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

IF !CFLAG:睡眠
	CALL SOURCE_CALC_露出(TARGET, PLAYER, 30)
	CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)
	CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 100)
	CALL SOURCE_CALC_不潔(TARGET, PLAYER, 50)

	;舌を使う調教では、調教者の舌使いによって、快感のソース増加
	IF TALENT:PLAYER:舌使い
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
	ENDIF

	IF TALENT:TARGET:舌使い
		CALL SOURCE_CALC_快Ｓ(PLAYER, TARGET, 150)
	ENDIF

	;猫舌では減少
	SIF TALENT:PLAYER:猫舌
		CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 20)

	;同性愛　レズなら戻り値1ゲイなら戻り値2
	IF HETEROSEX(TARGET,PLAYER)
		EXP:同性愛経験 += 3
		EXP:PLAYER:同性愛経験 += 3
	ENDIF

	SIF TALENT:恋慕
		EXP:愛情経験 += 1

	SIF TALENT:TARGET:猫舌
		EXP:嗜虐快楽経験 += 1
ENDIF
CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 150)
CALL SOURCE_CALC_快Ｓ(PLAYER, TARGET, 150)
IF TALENT:PLAYER:舌使い
	CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 150)
ENDIF


;-------------------------------------------------
;経験の処理
;-------------------------------------------------

EXP:キス経験 ++
EXP:PLAYER:キス経験 ++

CALL 初体験日登録処理(PLAYER, TARGET, "ファーストキス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 1)
IF RESULT
	RCVAR:PLAYER:ファーストキス中フラグ = 1
	CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:TARGET%의 [입술]에 첫 키스를 해버렸다.</font><img src='えっちハート'>","うふふ")
ENDIF
CALL 初体験日登録処理(PLAYER, TARGET, "キス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 0)

CALL 初体験日登録処理(TARGET, PLAYER, "ファーストキス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 1)
IF RESULT
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:PLAYER%의 [입술]에 첫 키스를 빼앗겼다.</font><img src='えっちハート'>","うふふ")
	RFLAG:コマンド結果受渡し変数 = 1
	RCVAR:TARGET:ファーストキス中フラグ = 1
ENDIF
CALL 初体験日登録処理(TARGET, PLAYER, "キス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 0)


RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@RAPE_COM_ABLE9
;キスする実行判定
;一括管理
SIF GLOBAL_RAPE_COMABLE(9)
	RETURN RESULT
;ボールギャグ使用中は不可
; SIF TEQUIP:ボールギャグ
; 	RETURN 0
;口を何かに使ってるなら無理
IF TEQUIP:PLAYER:口
	RETURN 0
ENDIF
SIF TEQUIP:TARGET:口
	RETURN 0
;キス魔は猫舌無視
SIF TALENT:TARGET:キス嗜好 == 2
	RETURN 1
;猫舌の調教者の場合は技巧Lv2以上必要
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	RETURN 0

;オートコマンド中の制限
IF FLAG:オートコマンド判定中フラグ
	SIF 初体験済みチェック("ファーストキス", PLAYER) == 0
		RETURN 0
ENDIF

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@RAPE_MESSAGE_COM9

PRINTFORM %CALLNAME:PLAYER%은(는)

IF CFLAG:TARGET:睡眠
	IF TCVAR:TARGET:泥酔眠姦
		PRINTFORM 술에 취해 쓰러진
	ELSE
		PRINTFORM 잠든
	ENDIF
ENDIF
IF MODE_確認_完全一致("Ｖ挿入系", PLAYER, TARGET) > -1 || MODE_確認_完全一致("Ａ挿入系", PLAYER, TARGET) > -1 || MODE_確認_完全一致("Ｖ挿入系", TARGET, PLAYER) > -1 || MODE_確認_完全一致("Ａ挿入系", TARGET, PLAYER) > -1
	PRINTFORML %CALLNAME:TARGET%을(를)%TEXTR("마음대로/욕망대로")%으로%TEXTR("범하면서/범하며 허리를 부딪치면서/범하며 허리를 흔들어대면서/")%,
ELSE
	PRINTFORM %CALLNAME:TARGET%의,
ENDIF

IF TALENT:TARGET:恋人持ち == 1
	IF RFLAG:コマンド結果受渡し変数 == 1
		PRINTFORM 아직 연인에게도 바치지 않았던 순결한
	ELSE
		PRINTFORM 연인의 것이었을 터인
	ENDIF
ELSEIF TALENT:TARGET:恋人持ち == 2
	IF RFLAG:コマンド結果受渡し変数 == 1
		PRINTFORM 반려에게도 바치지 않았던 순결한
	ELSE
		PRINTFORM 반려의 것이었을 터인
	ENDIF
ELSEIF TALENT:TARGET:恋人持ち == 3
	IF RFLAG:コマンド結果受渡し変数 == 1
		PRINTFORM 죽은 반려에게도 바치지 않았던 순결한
	ELSE
		PRINTFORM 과거에는 반려에게 바쳤을
	ENDIF
ELSE
	IF RFLAG:コマンド結果受渡し変数 == 1
		PRINTFORM 아직 누구와도 키스한 적 없었던 순수한
	ELSE
		PRINTFORM %TEXTR("부드러운/싱그러운/맛있어 보이는/사랑스러운")%
	ENDIF
ENDIF

PRINTFORML 입술을%TEXTR("억지로 빼앗았다/억지로 빼앗아 맛보았다/억지로 빼앗아 만끽했다")%…

