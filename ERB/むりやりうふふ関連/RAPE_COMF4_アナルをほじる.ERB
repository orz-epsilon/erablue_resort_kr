;-------------------------------------------------
;アナルをほじる
;愛撫系コマンド、レベル2
;-------------------------------------------------
@RAPE_COMNAME4
#FUNCTIONS
TSTR:コマンド名受渡 = 항문을 후비다



;===================================================
;コマンドタイプ
;===================================================
@RAPE_COMTYPE_4
#FUNCTION
TFLAG:コマンドタイプ受渡 = RAPE_COMTYPE("Ａ系")


@RAPE_COM_TOOLTIP_4
LOCALS = <br>■항문을 후비다<br>
LOCALS += "상대의 항문을 손가락으로 후빈다."
LOCALS += "A 경험이 적을 때는 바깥쪽부터, 익숙해지면 손가락을 삽입하여 후빈다"
LOCALS += "<br>RAPE_COM 타입: A계<br>획득 소스: 쾌A・노출・접촉・고통"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@RAPE_COM4
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, 100)
SIF TALENT:PLAYER:器用な指
	CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, 50)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 150)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)

;苦痛
LOCAL = 120 * PAIN_CHECK_A(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)

;下タイツがある場合は破く。
IF TEQUIP:下タイツあり == 1
	TEQUIP:下タイツ破れ = 1
	RFLAG:コマンド結果受渡し変数 = 1
ENDIF
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 2
	EXP:PLAYER:同性愛経験 += 2
ENDIF

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@RAPE_COM_ABLE4
;アナル愛撫実行判定
;一括管理
SIF GLOBAL_RAPE_COMABLE(4)
	RETURN RESULT
;Aがない場合駄目
SIF TALENT:TARGET:Ａ感度 == -2
	RETURN 0

;下半身着衣
;後鎧、めくれない服があると無理
SIF TEQUIP:下腹部後鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
;指を何かに使ってる・届かないなら無理
SIF TEQUIP:PLAYER:指
	RETURN 0
;アナルを何かに使ってる・届かないなら無理
SIF TEQUIP:TARGET:アナル
	RETURN 0
	
;オートコマンド中の制限
IF FLAG:オートコマンド判定中フラグ
	SIF EXP:PLAYER:Ａ経験 == 0 || EXP:TARGET:Ａ経験 == 0
		RETURN 0
ENDIF
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@RAPE_MESSAGE_COM4

PRINTFORM %CALLNAME:PLAYER%은(는)%CALLNAME:TARGET%의

SIF MODE_確認_完全一致("Ｖ挿入系", PLAYER, TARGET) > -1
	PRINTFORM %TEXTR("보지를 범하면서/질을 범하며 허리를 흔들면서/질의 조임을 페니스로 만끽하면서")%、

;下タイツがある場合は破く。挿入できてるってことは破れてるから場所はここでよさそう。
SIF RFLAG:コマンド結果受渡し変数 == 1
	PRINTFORM 입고 있던 타이츠를 강제로%TEXTR("찢어발기고/찢어")%、

;Ａ経験10以下
IF EXP:Ａ経験 <= 10
	PRINTFORM 굳게 닫힌 항문을 손가락으로 후비듯이 자극하고
;Ａ経験100以上＋欲望のPALAMLV3以上
ELSEIF EXP:Ａ経験 >= 100 && PALAM:欲情 >= PALAMLV:3
	PRINTFORM 항문에 손가락을 찔러 넣어 직장을 휘저어
ELSE
	PRINTFORM 항문을 손가락으로 후비듯이 자극하고
ENDIF

PRINTFORML 했다…

