;-------------------------------------------------
;胸でちんぽをしごく
;奉仕系コマンド、レベル3
;-------------------------------------------------
@RAPE_COMNAME15
#FUNCTIONS

TSTR:コマンド名受渡 = 가슴으로 자지를 문지르다


;===================================================
;コマンドタイプ
;===================================================
@RAPE_COMTYPE_15
#FUNCTION
TFLAG:コマンドタイプ受渡 = RAPE_COMTYPE("うふふ")



@RAPE_COM_TOOLTIP_15
LOCALS = <br>■가슴으로 자지를 문지르다<br>
LOCALS += "상대의 가슴을 사용하여 자신의 페니스를 기분 좋게 한다. <br>가슴 애무 모드가 되어, 모드를 변경하거나 성행위를 종료할 때까지 지속적으로 특정 소스가 발생한다. <br>강간이므로 [가슴 애무 펠라]로는 변화하지 않는다."
LOCALS += "<br>RAPE_COM 타입: 우후후<br>획득 소스: 쾌B・노출・접촉・봉사・불결"
LOCALS += "<br>고유 획득 경험: 가슴 애무 경험(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@RAPE_COM15
;-------------------------------------------------
;モードの設定
;-------------------------------------------------

CALL MODE_CHANGE("パイズリ", TARGET, PLAYER)

;-------------------------------------------------
;RAPE_COM別FLAGの設定
;-------------------------------------------------


RCVAR:TARGET:汎用喘ぎ_文節低減フラグ = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------




CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 80)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)
CALL SOURCE_CALC_奉仕(TARGET, PLAYER, 400)

CALL SOURCE_CALC_不潔(TARGET, PLAYER, 200)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:パイズリ経験 ++

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 7
	EXP:PLAYER:同性愛経験 += 7
ENDIF

;愛情経験
SIF TALENT:恋慕
	EXP:愛情経験 ++

RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------

@RAPE_COM_ABLE15
;パイズリ実行判定
SIF GLOBAL_RAPE_COMABLE(15)
	RETURN RESULT
;ちんこがない場合駄目
SIF TALENT:PLAYER:Ｃ感度 == -2
	RETURN 0
;調教者がオトコか半人半妖かふたなりじゃないとダメ
SIF !(TALENT:PLAYER:性別 & 2)
	RETURN 0
;相手に胸がないならダメ
SIF TALENT:TARGET:性別 == 2
	RETURN 0
;縄使用中は不可
; SIF TEQUIP:縄
; 	RETURN 0
;上半身着衣
;鎧、差し込めないorはだけない服、タイツがあると無理
SIF TEQUIP:上半身鎧あり
	RETURN 0
SIF TEQUIP:上半身服あり && (!TEQUIP:はだけ可 && !TEQUIP:パイズリ可)
	RETURN 0
SIF TEQUIP:上タイツあり && !TEQUIP:上タイツ破れ
	RETURN 0
;胸、ペニスを何かに使ってる・届かないなら無理
IF MODE_確認_完全一致("フェラチオ", TARGET, PLAYER) < 0
	SIF TEQUIP:PLAYER:ペニス
		RETURN 0
	SIF TEQUIP:TARGET:胸
		RETURN 0
ENDIF
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@RAPE_MESSAGE_COM15

PRINTFORM %CALLNAME:PLAYER%은(는)
IF CFLAG:TARGET:睡眠
	IF TCVAR:TARGET:泥酔眠姦
		PRINTFORM 술에 취해 쓰러진
	ELSE
		PRINTFORM 잠든
	ENDIF
ENDIF
PRINTFORM %CALLNAME:TARGET% 위에 올라타서,
	;貧乳
	IF TALENT:バストサイズ <= -1
		PRINTFORM 얇은 가슴에 페니스를 밀어붙여
	;巨乳
	ELSEIF TALENT:バストサイズ == 1
		PRINTFORM 풍만한 가슴을 움켜쥐고 페니스를 끼워
	;爆乳以上
	ELSEIF TALENT:バストサイズ > 1
		PRINTFORM 너무나 풍만한 가슴을 움켜쥐고 페니스를 끼워
	ELSE
		PRINTFORM 부드러운 가슴골에 페니스를 끼워
	ENDIF
	PRINTFORML 자지 애무 도구로 사용했다…


