[IF_DEBUG]
;---------------------------------------------------------------------
;【デバッグ用】画像ファイル名のチェック処理＠キャラ指定版
;---------------------------------------------------------------------
@デバッグ用_画像ファイル名チェック_キャラ指定()
#DIM  CSV_NO
#DIM  L_CNT
#DIM  L_CNT_F
#DIMS FOLDER_NAME
#DIMS DYNAMIC FOLDER_LIST, VARSIZE("RESULTS") ; 取得したフォルダリストの格納先
#DIM  FOLDER_CNT
#DIM  DYNAMIC FOLDER_CNT_ALL
#DIM  CSV_NO_F
#DIMS CHARA_NAME_F

;- 対象入力
PRINTL
PRINTL 【디버그용】이미지 파일명 체크＠캐릭터 지정판
PRINTL 　※ png 파일은 체크되지 않습니다.
PRINTL
CALL デバッグ用_画像ファイル名チェック_キャラ指定_CSV番号入力()
CSV_NO = RESULT
SIF CSV_NO == -1
	RETURN
PRINTL
REDRAW 0

;- ファイル名チェック実行
; チェック前に情報をリセット
CALL デバッグ用_エラー画像情報管理("リセット")

; フォルダ名のチェック
CALL デバッグ用_画像フォルダ名チェック(CSV_NO)

; CSV番号＋CSV記載のCALLNAMEからフォルダ名を生成
FOLDER_NAME '= TOSTR(CSV_NO) + CSVCALLNAME(CSV_NO)
FOLDER_CNT = デバッグ用_画像ファイル名チェック_フォルダ取得(FOLDER_NAME, FOLDER_LIST)
FOLDER_CNT_ALL += FOLDER_CNT

FOR L_CNT_F, 0, FOLDER_CNT
	CALL デバッグ用_画像ファイル名チェック_フォルダ(FOLDER_LIST:L_CNT_F)
NEXT

; 命名規則から外れたリソースフォルダのチェック(0あなた等)
FOR L_CNT, 0, VARSIZE("デバッグ用_特殊リソースフォルダ")
	FOLDER_NAME '= デバッグ用_特殊リソースフォルダ:L_CNT
	;CSV番号が一致するものだけチェック
	CALL デバッグ用_画像ファイル名チェック_画像フォルダ名分解(FOLDER_NAME, CSV_NO_F, CHARA_NAME_F)
	SIF CSV_NO_F != CSV_NO
		CONTINUE
	FOLDER_CNT= デバッグ用_画像ファイル名チェック_フォルダ取得(FOLDER_NAME, FOLDER_LIST)
	FOLDER_CNT_ALL += FOLDER_CNT
	FOR L_CNT_F, 0, FOLDER_CNT
		CALL デバッグ用_画像ファイル名チェック_フォルダ(FOLDER_LIST:L_CNT_F)
	NEXT
NEXT

;- 結果の表示
REDRAW 1
AWAIT
;画像フォルダがない場合は結果表示なしで終了
IF FOLDER_CNT_ALL == 0
	PRINTW 지정된 캐릭터의 이미지 폴더를 찾을 수 없었습니다.
	RETURN
ENDIF

CALL デバッグ用_エラー画像情報管理("結果出力")

;- 結果のファイル出力
PRINTL
PRINTL 결과를 파일로 출력하시겠습니까?
PRINTL
PRINTBUTTONLC "[0] 예", 0
PRINTBUTTONLC "[1] 아니요", 1
BINPUT
SIF RESULT == 1
	RETURN

; ファイル保存実行
; RESULT:保存成否 RESULTS:保存したファイルのパス
FOLDER_NAME '= TOSTR(CSV_NO) + CSVCALLNAME(CSV_NO)
CALL デバッグ用_エラー画像情報管理("ファイル保存", FOLDER_NAME)
IF RESULT
	; ファイル保存成功
	PRINTFORML 결과를 「%RESULTS%」에 저장했습니다
ELSE
	; ファイル保存失敗
	PRINTL 파일 저장에 실패했습니다
ENDIF
PRINTW

;---------------------------------------------------------------------
;【デバッグ用】チェック対象のCSV番号入力処理
;---------------------------------------------------------------------
@デバッグ用_画像ファイル名チェック_キャラ指定_CSV番号入力()
#DIM CSV_NO

;- チェック対象の指定
PRINTL
PRINTL
PRINTL 체크하고 싶은 캐릭터의 CSV 번호를 입력해주세요(0으로 취소)

;入力処理
INPUT
CSV_NO = RESULT
;0でキャンセル
SIF CSV_NO == 0
	RETURN 0
;対応するキャラが居ない場合は再入力
IF !EXISTCSV(CSV_NO)
	PRINTW 입력된 CSV 번호의 캐릭터는 존재하지 않습니다.
	RESTART
ENDIF

;- チェック対象の確認
PRINTL
PRINTL
PRINTFORML 체크할 캐릭터는 아래와 같습니까?
DRAWLINE
PRINTFORML <CSV 번호>
PRINTFORML {CSV_NO}
PRINTL
PRINTFORML <이름>
PRINTFORML %CSVNAME(CSV_NO)%
PRINTL
PRINTFORML <호칭>
PRINTFORML %CSVCALLNAME(CSV_NO)%
PRINTL
PRINTFORML <이미지 폴더명>
PRINTFORML {CSV_NO}%CSVCALLNAME(CSV_NO)%
DRAWLINE
PRINTBUTTONLC "[0] 예", 0
PRINTBUTTONLC "[1] 아니오", 1
PRINTBUTTONLC "[2] 체크를 그만두다", 2

;入力処理
BINPUT
SELECTCASE RESULT
	CASE 0
		; はい
		RETURN CSV_NO
	CASE 1
		; いいえ
		RESTART
	CASE 2
		; やめる
		RETURN -1
ENDSELECT
[ENDIF]
