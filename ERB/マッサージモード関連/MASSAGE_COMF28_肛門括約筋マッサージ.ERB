;-------------------------------------------------
;肛門括約筋マッサージ
;-------------------------------------------------
@MASSAGE_COMNAME28
#FUNCTIONS
TSTR:コマンド名受渡 = 항문 괄약근 마사지


;===================================================
;コマンドタイプ
;===================================================
@MASSAGE_COMTYPE_28
#FUNCTION
TFLAG:コマンドタイプ受渡 = MASSAGE_COMTYPE("エロマッサージ")

@MASSAGE_COM_TOOLTIP_28
LOCALS = <br>■항문 괄약근 마사지<br>
LOCALS += "엉덩이를 벌리고, 오일을 바른 손가락으로 항문 괄약근을 바깥쪽에서 주물러 풀어준다."
LOCALS += "<br>사용 빈도에 비해 관리되지 않는 근육이므로, 전문가의 시술이 필요하다…는 것으로 되어 있다."
LOCALS += "<br>COM 타입: 에로 마사지<br>획득 소스: 쾌A・접촉・환락・액체"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@MASSAGE_COM28
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

;基盤ができたあとで理性を削る処理を入れる

;外側から揉みほぐすだけなので苦痛はない。
CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, 120)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 150)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)

;ローションは別のコマンドになるっぽいので気持ち程度
CALL SOURCE_CALC_液体(TARGET, PLAYER, 1000)

;下半身下着がある場合は脱がす。
IF TEQUIP:下半身下着あり > 0
	TEQUIP:下半身下着あり = 0
	TEQUIP:下半身服あり = 0
	SIF TEQUIP:上下一体服
		TEQUIP:上半身服あり = 0
	RFLAG:コマンド結果受渡し変数 = 1
ENDIF
IF BASE:TARGET:理性 <= 1
;理性がおしまいになってしまった場合、快楽に流されて警戒が下がっていく。
	BASE:TARGET:性欲 += 120
	BASE:TARGET:警戒 += 警戒変動値算出(TARGET, -40)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
ELSEIF 知識素質:TARGET:性知識 <= -1
;性知識がない場合でも「流石にこれはダメなやつなのでは？」と思いはじめる。が、時既に遅し。
;警戒90で理性が45上がり、そこから理性が80下がるイメージでこの数値。ここにきて未だに警戒90なのが無知シチュの『味』。
;尻穴で性感に？？？となっているので性欲の上昇量はちょっとだけ抑えめ。
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -35)
	BASE:TARGET:性欲 += 80
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 90)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
ELSEIF (BASE:TARGET:理性 + BASE:TARGET:警戒) < 650
;実行に際し十分な理性・警戒削りがなされている場合。
;警戒160で理性が80回復するも、煽られる性欲と与えられる刺激で理性が120削られて理性変動は-40。
;……というのが平常時における挙動だが、ここまで削られていると与えられる快感に警戒心が塗りつぶされてしまう。
;よって警戒64で理性32回復、そこから理性120低下でこの数字となる。理性と警戒の合計値は少しだけ下がり、そのまま次のコマンドへ。
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -88)
	BASE:TARGET:性欲 += 120
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 64)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
ELSE
;実行に際し十分な理性・警戒削りがなされていない場合。
;性欲は煽れるが、警戒と理性が跳ねる。警戒心がビンっ！ってなるので、煽られる性欲の理性低下を撥ね退けてしまう。
	BASE:TARGET:理性 +=理性変動値算出(TARGET, 80)
	BASE:TARGET:性欲 += 100
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 160)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
ENDIF

BASE:TARGET:ちんイラ = MAX(0, BASE:TARGET:ちんイラ - 200)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@MASSAGE_COM_ABLE28
SIF SAVESTR:ゲームフェイズ管理 != "マッサージモード"
	RETURN 0

SIF GLOBAL_MASSAGE_COMABLE(28)
	RETURN RESULT

;Aがない場合駄目
SIF TALENT:TARGET:Ａ感度 == -2
	RETURN 0

;仰向けだとダメ
SIF TCVAR:TARGET:マッサージ_うつ伏せフラグ == 0
	RETURN 0


;理性警戒の合計が1000以下じゃないとダメ
SIF (BASE:TARGET:理性 + BASE:TARGET:警戒) > 1000
	RETURN 0


RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MASSAGE_MESSAGE_COM28

PRINTFORM %CALLNAME:PLAYER%은(는),

	IF RFLAG:コマンド結果受渡し変数 == 1
		PRINTFORM %CALLNAME:TARGET%의
			;Ｖがあり欲情している場合
			SIF TALENT:TARGET:Ｖ感度 >= -1 && PALAM:欲情  >= PALAMLV:3
				PRINTFORM 흠뻑 애액을 머금은
		PRINTFORM 시술복을 벗기고,
	ENDIF
	PRINTFORML 오일을 듬뿍 바른 손가락으로%CALLNAME:TARGET%의%TEXTR("항문/엉덩이 구멍/아누스")%을(를) 부드럽게 주물러 풀어주었다.

	
;削りが十分な時は追加のメッセージ
IF (BASE:TARGET:理性 + BASE:TARGET:警戒) < 650
	PRINTW 
	SELECTCASE RAND:2
	CASE 0
		PRINTFORM %CALLNAME:TARGET%은(는)%TEXTR("황홀하게 눈가를 녹이며/녹아내린 얼굴로 헤프게 입을 벌리고/법열에 눈가를 녹이며")%,
	CASE 1
		PRINTFORM %CALLNAME:TARGET%은(는)헤프게 풀린 입에서%TEXTR("망측한 소리/쾌락에 물든 교성/음란한 소리")%을(를)%TEXTR("내며/흘리며/흘리면서")%,
	ENDSELECT
	PRINTFORML %CALLNAME:PLAYER%%TEXTR("의 『마사지』/의 수기/가 주는 쾌락")%%TEXTR("에 항문을 내주고 있다/에 취해 있다/에 당하는 대로 되어 있다/을 받아들이고 있다/을 감수하고 있다")%…
ENDIF
