;-------------------------------------------------
;乳首のコリほぐし
;-------------------------------------------------
@MASSAGE_COMNAME23
#FUNCTIONS
TSTR:コマンド名受渡 = 유두 뭉침 풀기
SIF TALENT:TARGET:陥没乳首 && !TCVAR:TARGET:陥没乳首露出フラグ
	TSTR:コマンド名受渡 = 함몰 유두 파내기


;===================================================
;コマンドタイプ
;===================================================
@MASSAGE_COMTYPE_23
#FUNCTION
TFLAG:コマンドタイプ受渡 = MASSAGE_COMTYPE("エロマッサージ")

@MASSAGE_COM_TOOLTIP_23
IF TALENT:TARGET:陥没乳首 && !TCVAR:TARGET:陥没乳首露出フラグ
	LOCALS = <br>■함몰 유두 파내기<br>
	LOCALS += "유방 속에서 ‘뭉쳐’ 있는 수줍은 함몰 유두를 파낸다."
	LOCALS += "<br>COM 타입: 에로 마사지<br>획득 소스: 쾌B・노출・접촉・환락"
	LOCALS += "<br>고유 획득 경험: 없음<br>"
	TSTR:ツールチップ受渡 = %LOCALS%
ELSE
	LOCALS = <br>■유두 뭉침 풀기<br>
	LOCALS += "마사지 중 쾌락으로 ‘뭉쳐’ 버린 유두를 주물러 풀어준다."
	LOCALS += "<br>주물러 풀어주고 있는데도 팽팽하게 단단해져만 가는 것은 애교."
	LOCALS += "<br>COM 타입: 에로 마사지<br>획득 소스: 쾌B・노출・접촉・환락"
	LOCALS += "<br>고유 획득 경험: 없음<br>"
	TSTR:ツールチップ受渡 = %LOCALS%
ENDIF
@MASSAGE_COM23
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
RCVAR:乳首開発フラグ = 1

;Ｂ愛撫より全体的に強め。勿論です、プロですから。
;性交ではなくマッサージの中での行為のため露出強め。エロマッサージなので歓楽は通常より弱め
CALL SOURCE_CALC_露出(TARGET, PLAYER, 100)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 80)
CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 175)
CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 50)

;陥没乳首をほじり出すときは刺激が強い。
IF TALENT:TARGET:陥没乳首 && !TCVAR:TARGET:陥没乳首露出フラグ
	CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 50)
ENDIF

;せっかくだから男性相手にはさらに効果が増すように。
;男への性感マッサージコマンドは数が少なめだから……
SIF TALENT:TARGET:性別 == 2
	CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 50)

;陥没乳首をほじり出す場合のみ、上半身下着を脱がす。
IF TALENT:TARGET:陥没乳首 && !TCVAR:TARGET:陥没乳首露出フラグ && TEQUIP:上半身下着あり
	TEQUIP:上半身下着あり = 0
	TEQUIP:上半身服あり = 0
	SIF TEQUIP:上下一体服
		TEQUIP:下半身服あり = 0
	RFLAG:コマンド結果受渡し変数 = 1
ENDIF


;乳首をいじるため性欲を煽る。
BASE:TARGET:性欲 += 80

;警戒は理性の倍回復する。

IF BASE:TARGET:理性 <= 1
;理性がおしまいになってしまった場合、快楽に流されて警戒が下がっていく。
	BASE:TARGET:警戒 += 警戒変動値算出(TARGET, -40)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)

ELSEIF 知識素質:TARGET:性知識 <= -1
;性知識がない場合、そもそも警戒することができない。そういうもんかって受け入れるため理性は下がり続けるし、快感を伴うから警戒も下がり続ける。
;……シヴァお前……
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -50)
	BASE:TARGET:警戒 += 警戒変動値算出(TARGET, -50)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)

ELSEIF (BASE:TARGET:理性 + BASE:TARGET:警戒) < 700
;実行に際し十分な理性・警戒削りがなされている場合。
;警戒140で理性が70回復するも、煽られる性欲と与えられる刺激で理性が80削られて理性変動は-10。
;……というのが平常時における挙動だが、ここまで削られていると与えられる快感に警戒心が塗りつぶされてしまう。
;よって警戒56で理性28回復、そこから理性80低下でこの数字となる。理性と警戒の合計値はほんの少しだけ上がり、そのまま次のコマンドへ。
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -52)
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 56)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)

ELSEIF BASE:TARGET:理性 <= 200
;理性がグズグズに煮詰まっちゃってると、警戒及び理性の回復は半分になる。理性200警戒800とかの場合これ。
	BASE:TARGET:理性 +=理性変動値算出(TARGET, 35)
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 70)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)

ELSEIF TALENT:TARGET:バストサイズ <= -1
;相手が貧乳以下の場合、実行可能であるならば「これもおっぱい大きくするため……？」ってしぶしぶ受け入れるイメージ。
	BASE:TARGET:理性 +=理性変動値算出(TARGET, 50)
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 100)
	CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 40)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)

ELSE
;基本値
	BASE:TARGET:理性 +=理性変動値算出(TARGET, 70)
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 140)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
ENDIF

BASE:TARGET:ちんイラ = MAX(0, BASE:TARGET:ちんイラ - 100)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@MASSAGE_COM_ABLE23
SIF SAVESTR:ゲームフェイズ管理 != "マッサージモード"
	RETURN 0

SIF GLOBAL_MASSAGE_COMABLE(23)
	RETURN RESULT

;Bがない場合駄目
SIF TALENT:TARGET:Ｂ感度 == -2
	RETURN 0

;乳首が勃起するくらいの欲情が必要
SIF PALAM:欲情 <= PALAMLV:3
	RETURN 0

;うつ伏せだとダメ
SIF TCVAR:TARGET:マッサージ_うつ伏せフラグ == 1
	RETURN 0

;理性警戒の合計が1000以下じゃないとダメ
SIF (BASE:TARGET:理性 + BASE:TARGET:警戒) > 1000
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MASSAGE_MESSAGE_COM23

IF TALENT:TARGET:陥没乳首 && !TCVAR:TARGET:陥没乳首露出フラグ
	PRINTFORM %CALLNAME:PLAYER%은(는), %CALLNAME:TARGET%의
		;胸の施術着があった場合
		SIF RFLAG:コマンド結果受渡し変数 == 1
			PRINTFORM 가슴의 시술복을 벗기고,
	PRINTFORML 유방 속에서 단단해져 있던 함몰 유두를 손가락으로 파냈다.
ELSE
	PRINTFORM %CALLNAME:PLAYER%은(는),
		;胸の施術着がある場合
		SIF TEQUIP:上半身下着あり > 0
			PRINTFORM 시술복 위에서도 알 수 있을 정도로
	PRINTFORML %TEXTR("팽팽하게 솟아오른/단단하게 뭉친")%%CALLNAME:TARGET%의 유두를 손가락으로 정성껏 주물러 풀었다.
ENDIF

;削りが十分な時は追加のメッセージ
IF (BASE:TARGET:理性 + BASE:TARGET:警戒) < 700
	PRINTW 
	SELECTCASE RAND:2
	CASE 0
		PRINTFORM %CALLNAME:TARGET%은(는)%TEXTR("황홀하게 눈가를 녹이며/녹아내린 얼굴로 헤프게 입을 벌리고/법열에 눈가를 녹이며")%,
	CASE 1
		PRINTFORM %CALLNAME:TARGET%은(는) 헤프게 풀린 입에서%TEXTR("부끄러운 목소리/쾌락에 물든 교성/음란한 목소리")%을(를)%TEXTR("내며/흘리며/흘리면서")%,
	ENDSELECT
	PRINTFORML %CALLNAME:PLAYER%%TEXTR("의 '마사지'/의 손기술/이 주는 쾌락")%%TEXTR("에 유두를 내주고 있다/에 취해 있다/에 당하는 대로 되어 있다/을 받아들이고 있다/을 감수하고 있다")%…
ENDIF
