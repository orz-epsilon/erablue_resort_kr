;-------------------------------------------------
;恥丘揉みほぐし
;-------------------------------------------------
@MASSAGE_COMNAME25
#FUNCTIONS
TSTR:コマンド名受渡 = 치구 주무르기


;===================================================
;コマンドタイプ
;===================================================
@MASSAGE_COMTYPE_25
#FUNCTION
TFLAG:コマンドタイプ受渡 = MASSAGE_COMTYPE("エロマッサージ")

@MASSAGE_COM_TOOLTIP_25
LOCALS = <br>■치구 주무르기<br>
LOCALS += "손바닥 전체를 사용하여 감싸듯이 부드럽게 치구를 주무른다."
LOCALS += "<br>애액이 나오는 것은 해독이 되니까요. 많이 나오게 하자."
LOCALS += "<br>COM 타입: 에로 마사지<br>획득 소스: 쾌C・접촉・환락・액체"
LOCALS += "<br>고유 획득 경험: 없음<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@MASSAGE_COM25
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

;快Ｃ強め。でもローターより強いのはあれだから……200くらい？
CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER, 200)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 75)
CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 100)

;ローションは別のコマンドになるっぽいので気持ち程度
CALL SOURCE_CALC_液体(TARGET, PLAYER, 1000)

IF BASE:TARGET:理性 <= 1
;理性がおしまいになってしまった場合、快楽に流されて警戒が下がっていく。
	BASE:TARGET:性欲 += 150
	BASE:TARGET:警戒 += 警戒変動値算出(TARGET, -40)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
ELSEIF 知識素質:TARGET:性知識 <= -1
;性知識がない場合でも「ちょっとこれはダメなやつなのでは？」と思いはじめる。が、時既に遅し。
;警戒60で理性が30上がり、そこから理性が150下がるイメージでこの数値。
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -120)
	BASE:TARGET:性欲 += 150
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 60)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 150)
ELSEIF (BASE:TARGET:理性 + BASE:TARGET:警戒) < 500
;実行に際し十分な理性・警戒削りがなされている場合。
;警戒200で理性が100回復するも、煽られる性欲と与えられる刺激で理性が150削られて理性変動は-50。
;……というのが平常時における挙動だが、ここまで削られていると与えられる快感に警戒心が塗りつぶされてしまう。
;よって警戒80で理性40回復、そこから理性150低下でこの数字となる。理性と警戒の合計値は少しだけ下がり、そのまま次のコマンドへ。
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -110)
	BASE:TARGET:性欲 += 150
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 80)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 150)
ELSE
;実行に際し十分な理性・警戒削りがなされていない場合。
;一応性欲は煽れるが、警戒と理性が跳ねる。警戒心がビンっ！ってなるので、煽られる性欲の理性低下を撥ね退けてしまう。
	BASE:TARGET:理性 +=理性変動値算出(TARGET, 100)
	BASE:TARGET:性欲 += 100
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 200)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@MASSAGE_COM_ABLE25
SIF SAVESTR:ゲームフェイズ管理 != "マッサージモード"
	RETURN 0

SIF GLOBAL_MASSAGE_COMABLE(25)
	RETURN RESULT

;対象者がオトコ、半人半妖、ふたなりの場合はダメ。ちんこがあると揉めない
SIF GETBIT(TALENT:TARGET:性別, 1)
	RETURN 0

;Ｖがないとダメ
SIF GETBIT(TALENT:TARGET:性別, 0) == 0
	RETURN 0
SIF TALENT:TARGET:Ｖ感度 == -2
	RETURN 0

;うつ伏せだとダメ
SIF TCVAR:TARGET:マッサージ_うつ伏せフラグ == 1
	RETURN 0

;理性警戒の合計が1000以下じゃないとダメ
;実行自体はできるが、この時点だと警戒と理性が跳ねる。されるがままになっちゃうのは合計値500以下くらいを想定。
SIF (BASE:TARGET:理性 + BASE:TARGET:警戒) > 1000
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MASSAGE_MESSAGE_COM25

PRINTFORML %CALLNAME:PLAYER%은(는) 마사지 오일을 바른 손바닥으로, %CALLNAME:TARGET%의 치구를 감싸듯이%TEXTR("부드럽게/상냥하게")% 주물렀다.

;削りが十分な時は追加のメッセージ
IF (BASE:TARGET:理性 + BASE:TARGET:警戒) < 500
	PRINTW 
	SELECTCASE RAND:2
	CASE 0
		PRINTFORM %CALLNAME:TARGET%은(는) %TEXTR("황홀하게 눈가를 녹이며/녹아내린 얼굴로 헤프게 입을 벌리고/법열에 눈가를 녹이며")%、
	CASE 1
		PRINTFORM %CALLNAME:TARGET%은(는) 헤프게 풀린 입에서%TEXTR("상스러운 목소리/쾌락에 물든 교성/음란한 목소리")%을(를)%TEXTR("내며/흘리며/흘리면서")%、
	ENDSELECT
	PRINTFORML %CALLNAME:PLAYER%%TEXTR("의 『마사지』/의 손기술/이 주는 쾌락")%%TEXTR("에 보지를 내주고 있다/에 취해 있다/에 당하는 대로 되어 있다/을 받아들이고 있다/을 감수하고 있다")%…
ENDIF
