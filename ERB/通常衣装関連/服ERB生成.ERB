@服ERB生成処理
#DIM 服構造保存, 30
#DIM 表示番号, 20
#DIMS 服名保存
#DIM 服種別設定保存
#DIM 入手手段設定保存
#DIM 編集モード
#DIMS 説明文章, 10
#DIMS 出典元
#DIM 販売価格 = 10000
#DIMS 連動衣装
#DIMS CONST 服種別 = "汎用衣装", "エロ衣装", "条件付き汎用", "条件付きエロ"
#DIMS CONST 入手手段 = "別途設定", "初期制服", "購入", "他の衣装に連動"

LOCALS = 
;左ウインドウ表示
LOCALS += "<div rect='81,375,1937,2812' padding='75' border='31' bcolor='#C0C0C0'>"
LOCALS += "<button value='100'>[100] 이름을 설정한다</button><br>"
LOCALS += "<button value='101'>[101] 옷 종류를 설정한다</button><br>"
LOCALS += "<button value='102'>[102] 입수 수단을 설정한다</button><br>"
IF 入手手段設定保存 == 2
	LOCALS += "<button value='103'>[103] 판매 가격을 설정한다</button><br>"
ELSE
	LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[103] 판매 가격을 설정한다</font><br>"
ENDIF
IF 入手手段設定保存 >= 2
	LOCALS += "<button value='104'>[104] 연동 의상을 설정한다</button><br>"
ELSE
	LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[104] 연동 의상을 설정한다</font><br>"
ENDIF
LOCALS += "<button value='105'>[105] 출전 원본을 설정한다</button><br>"
LOCALS += "<button value='20'>[ 20] 설명문을 편집한다</button><br>"
LOCALS += "<br>"
IF 服名保存 == ""
	LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[998] 생성 불가능 (명칭 미설정)</font><br>"
ELSEIF MAXARRAY(服構造保存) < 1
	LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[998] 생성 불가능 (전라 상태)</font><br>"
ELSEIF ENUMFUNCENDSWITH(@"CLOTHES_CHANGE_%服名保存%") > 0
	LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[998] 생성 불가능 (동명의 옷이 존재)</font><br>"
ELSE
	LOCALS += "<button value='998'>[998] 이 설정으로 생성한다</button><br>"
ENDIF
LOCALS += "<button value='999'>[999] 생성하지 않고 돌아간다</button><br>"
LOCALS += "</div>"

;右ウインドウ表示
LOCALS += "<div rect='2050,375,3875,2812' padding='75' border='31' bcolor='#C0C0C0'>"
LOCALS += @"%"■服名称", 24, LEFT%："
IF 服名保存 == ""
	LOCALS += "（미설정）　　　　　　　　　　"
ELSE
	LOCALS += @"%服名保存, 30, LEFT%"
ENDIF
LOCALS += "　　<button value='100'>[변경]</button><br>"
LOCALS += @"%"■服種別設定", 24, LEFT%：%服種別:服種別設定保存, 30, LEFT%"
LOCALS += "　　<button value='101'>[변경]</button><br>"

LOCALS += @"%"■入手手段", 24, LEFT%：%入手手段:入手手段設定保存, 30, LEFT%"
LOCALS += "　　<button value='102'>[변경]</button><br>"
LOCALS += "-------------------------------------------------------------------------<br>"

SELECTCASE 編集モード
	CASE 0
		LOCALS += "■상반신 설정<br>"
		VARSET 表示番号
		表示番号 = 1,2,3,4,5,6,7,8,9,10,19,20,21
	CASE 1
		LOCALS += "■하반신 설정<br>"
		VARSET 表示番号
		表示番号 = 11,12,13,14,15,16,17,18,19,20,22,23
	CASE 2
		LOCALS += "■판매·해설 설정<br>"
		VARSET 表示番号
		LOCALS += @"<button value='103'>%"　販売価格", 24, LEFT%：<font color='#\@ 入手手段設定保存 != 2 ? 808080 # C0C0C0 \@'> {販売価格, 8, LEFT}</font>루피</button><br>"
		LOCALS += @"<button value='104'>%"　連動衣装", 24, LEFT%：<font color='#\@ !STRLENS(連動衣装) ? 808080'>（없음）# C0C0C0'>%連動衣装% \@</font></button><br>"
		LOCALS += @"<button value='105'>%"　出典元", 24, LEFT%：<font color='#\@ !STRLENS(出典元) ? 808080'>（없음）# C0C0C0'>%出典元% \@</font></button><br>"
		LOCALS += @"%"　説明文", 24, LEFT%<br>"
		FOR LOCAL, 0, VARSIZE("説明文章")
			LOCALS += @"<button value='{20 + LOCAL}'>[{20 + LOCAL}] "
			IF 説明文章:LOCAL == ""
				LOCALS += @"<font color='#808080'>\@ LOCAL ? （행을 늘리다） # （미설정） \@</font></button><br>"
				BREAK
			ELSE
				LOCALS += @"%説明文章:LOCAL%</button><br>"
			ENDIF
		NEXT
ENDSELECT

FOR LOCAL, 1, 24
	SIF !MATCH(表示番号, LOCAL)
		CONTINUE
	SIF GROUPMATCH(EQUIPNAME:LOCAL, "上タイツ破れ", "下タイツ破れ")
		CONTINUE
	IF EQUIPNAME_TIPS(LOCAL) != ""
		LOCALS += @"<nonbutton title='%EQUIPNAME_TIPS(LOCAL)%'>%EQUIPNAME:LOCAL%"
		LOCALS += @"<font color='#808080'>%"(?)", 24 - STRLENS(EQUIPNAME:LOCAL), LEFT%</font>"
		LOCALS += "：</nonbutton>"
	ELSE
		LOCALS += @"%EQUIPNAME:LOCAL, 24, LEFT%："
	ENDIF
	IF LOCAL == 9
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 0 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 0}'>[노브라]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 1 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 1}'>[브라 착용]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 2 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 2}'>[니플 패치]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 3 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 3}'>[가슴 붕대]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 4 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 4}'>[리본]</button></font>"
		LOCALS += "<br>" + " " * 26
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == -1 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 5}'>[오픈컵 브라]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == -2 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 6}'>[니플 피어싱]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == -3 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 7}'>[유두에 명찰]</button></font>"
	ELSEIF LOCAL == 17
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 0 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 0}'>[노팬티]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 1 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 1}'>[속옷 착용]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 2 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 2}'>[앞가리개]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 3 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 3}'>[훈도시]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 4 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 4}'>[리본]</button></font>"
		LOCALS += "<br>" + " " * 26
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == -1 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 5}'>[오픈 팬티]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == -2 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 6}'>[성기 피어싱]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == -3 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 7}'>[클리에 명찰]</button></font>"
	ELSE
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 0 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 0}'>[없음]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 1 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 1}'>[있음]</button></font>"
	ENDIF
	IF EQUIPNAME:LOCAL == "上タイツあり"
		LOCAL:1 = GETNUM(EQUIP, "上タイツ破れ")
		LOCALS += @"　　%EQUIPNAME:(LOCAL:1)%："
		LOCALS += @"<font color='#\@ 服構造保存:(LOCAL:1)  == 0 ? C0C0C0 # 333333\@'><button value='{1000 + (LOCAL:1) * 10 + 0}'>[없음]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:(LOCAL:1)  == 1 ? C0C0C0 # 333333\@'><button value='{1000 + (LOCAL:1) * 10 + 1}'>[있음]</button></font>"
	ELSEIF EQUIPNAME:LOCAL == "下タイツあり"
		LOCAL:1 = GETNUM(EQUIP, "下タイツ破れ")
		LOCALS += @"　　%EQUIPNAME:(LOCAL:1)%："
		LOCALS += @"<font color='#\@ 服構造保存:(LOCAL:1)  == 0 ? C0C0C0 # 333333\@'><button value='{1000 + (LOCAL:1) * 10 + 0}'>[없음]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:(LOCAL:1)  == 1 ? C0C0C0 # 333333\@'><button value='{1000 + (LOCAL:1) * 10 + 1}'>[있음]</button></font>"
	ENDIF
	LOCALS += "<br>"
NEXT
LOCALS += "</div>"
LOCALS += "<div rect='2250,2975,3875,2812'>"
FOR LOCAL, 0, 3
	LOCALS += @"<font color='#\@ 編集モード == LOCAL ? 666666'> # C0C0C0'><button value='{300 + LOCAL}'> \@[{300 + LOCAL}] "
	SELECTCASE LOCAL
		CASE 0
			LOCALS += "상반신 설정으로"
		CASE 1
			LOCALS += "하반신 설정으로"
		CASE 2
			LOCALS += "판매·해설 설정으로"
	ENDSELECT
	LOCALS += @"</font>\@ 編集モード == LOCAL ? # </button> \@　　"
NEXT
LOCALS += "</div>"

HTML_PRINT LOCALS
FOR LOCAL, 0, 28
	PRINTL
NEXT

BINPUT
SELECTCASE RESULT
	CASE 100
		DRAWLINE
		PRINTL 옷의 명칭을 입력해 주세요.
		PRINTBUTTON "[변경하지 않고 돌아가기]", "変更せず戻る"
		PRINTL
		DRAWLINE
		INPUTS
		IF RESULTS != "変更せず戻る"
			服名保存 = %RESULTS%
			PRINTFORMW 의상 이름을 「%服名保存%」으로 변경했습니다.
		ENDIF
	CASE 101
		DRAWLINE
		PRINTL 의상 종류를 설정해 주세요.
		DRAWLINE
		FOR LOCAL, 0, VARSIZE("服種別")
			IF 服種別設定保存 == LOCAL
				SETCOLOR カラーパレット("黄")
				PRINTPLAINFORM [{LOCAL}] %服種別:LOCAL%
				RESETCOLOR
				PRINTL
			ELSE
				PRINTBUTTON @"[{LOCAL}] %服種別:LOCAL%", LOCAL
				PRINTL
			ENDIF
		NEXT
		PRINTL
		PRINTBUTTON @"[999] 돌아가기", 999
		PRINTL
		BINPUT
		IF RESULT != 999
			服種別設定保存 = RESULT
			PRINTFORMW 의상 종류를 「%服種別:RESULT%」으로 변경했습니다.
			SIF RESULT >= 2
				PRINTFORMW ERB 출력 후, 다시 착용 조건을 기술해 주세요
		ENDIF
	CASE 102
		DRAWLINE
		PRINTL 의상 입수 방법을 설정해 주세요.
		DRAWLINE
		FOR LOCAL, 0, VARSIZE("入手手段")
			IF 入手手段設定保存 == LOCAL
				SETCOLOR カラーパレット("黄")
				PRINTPLAINFORM [{LOCAL}] %入手手段:LOCAL%
				RESETCOLOR
				PRINTL
			ELSE
				PRINTBUTTON @"[{LOCAL}] %入手手段:LOCAL%", LOCAL
				PRINTL
			ENDIF
		NEXT
		PRINTL
		PRINTBUTTON @"[999] 돌아가기", 999
		PRINTL
		BINPUT
		IF RESULT != 999
			入手手段設定保存 = RESULT
			PRINTFORMW 의상 종류를 「%入手手段:RESULT%」으로 변경했습니다.
			SIF !入手手段設定保存
				PRINTFORMW 어떤 형태로든 입수 방법을 설정하지 않는 한, 게임 중에 등장하지 않으니 주의해 주세요.
		ENDIF
	CASE 103
		SETTEXTBOX TOSTR(販売価格)
		PRINTL 판매 가격을 입력해 주세요
		INPUT
		販売価格 = RESULT
	CASE 104
		SETTEXTBOX 連動衣装
		PRINTL 입수를 연동시킬 의상 이름을 입력해 주세요
		IF 入手手段設定保存 == 2
			PRINTL （해당 의상을 소지하고 있는 경우, 구매 가능해집니다）
		ELSE
			PRINTL （해당 의상이 초기 교복으로 설정되어 있는 경우, 자동으로 사용 가능해집니다
			PRINTL 　해당 의상 자체에 입수 방법이 있는 경우, 그쪽에 다시
			PRINTFORML 　『CALL \@ 의상 종류 설정 저장 % 2 ? エロ衣装 # 制服 \@登録処理("%의상 이름 저장%")』이라고 추가해 주세요）
			PRINTL 　
		ENDIF
		INPUTS
		LOCALS:1 '= RESULTS
		IF RESULTS != "" && ENUMFUNCENDSWITH(@"CLOTHES_CHANGE_%RESULTS%") < 1
			CALL CHOICE("その名前の衣装はまだ存在しませんが、よろしいですか？", "はい", "いいえ")
			SIF RESULT
				RESTART
		ENDIF
		連動衣装 '= LOCALS:1
	CASE 105
		SETTEXTBOX 出典元
		PRINTL 의상의 출처를 입력해주세요
		INPUTS
		出典元 '= RESULTS
	CASE 20 TO 29
		編集モード = 2
		LOCAL = RESULT - 20
		SETTEXTBOX 説明文章:LOCAL
		PRINTL 설명문을 입력해주세요
		INPUTS
		説明文章:LOCAL '= RESULTS
	CASE 300 TO 302
		編集モード = RESULT - 300
	CASE 999
		CALL CHOICE("服ERBエディタを終了しますか？（作成中のデータは失われます）", "はい", "いいえ")
		SIF RESULT
			RESTART
		GOTO 終了処理
	CASE 998
		CALL CHOICE("服ERBを出力し、終了しますか？", "はい", "いいえ")
		SIF RESULT
			RESTART
		;ERBファイル生成
		CLEARLINE LINECOUNT
		;記述データPRINT

		IF 入手手段設定保存 == 1 || 入手手段設定保存 == 3
			PRINTFORML @CLOTHES_初期所持関数_%服名保存%()
			IF 入手手段設定保存 == 3 && 連動衣装 != ""
				PRINTFORML SIF STRCOUNT(所持制服文字列, @"汎用/%連動衣装%_") || STRCOUNT(所持エロ衣装文字列, @"エロ/%連動衣装%_")
			ENDIF
			PRINTFORML CALL \@ 服種別設定保存 % 2 ? エロ衣装 # 制服 \@登録処理("%服名保存%")
			HTML_PRINT "<br>"
		ELSEIF 入手手段設定保存 == 2 || 説明文章:0 != ""
			PRINTFORML @CLOTHES_衣装説明_%服名保存%()
			PRINTL #DIMS 説明文章
			PRINTL #DIMS 出典元
			PRINTL #DIM 販売価格
			PRINTL #DIMS 販売分類
			HTML_PRINT "<br>"
			PRINTL ;단위는 루피
			PRINTFORML 販売価格 = { 入手手段設定保存 == 2 ? 販売価格 # 0 }
			PRINTL ;일반 의상, 혹은 에로 의상 중 하나
			PRINTFORML 販売分類 = \@ 服種別設定保存 % 2 ? 에로 의상 # 일반 의상 \@
			PRINTL ;판매하지 않을 옷은 여기에 1을 넣으세요
			IF 連動衣装 != ""
				PRINTFORML ;%連動衣装%을(를) 입수한 후에 판매 개시
				PRINTFORML 販売禁止フラグ = !(STRCOUNT(所持制服文字列, @"汎用/%連動衣装%_") + STRCOUNT(所持エロ衣装文字列, @"エロ/%連動衣装%_"))
			ELSE
				PRINTL 販売禁止フラグ = 0
			ENDIF
			HTML_PRINT "<br>"
			PRINTL 説明文章 = 
			FOR LOCAL, 0, VARSIZE("説明文章")
				IF 説明文章:LOCAL == ""
					BREAK
				ELSE
					PRINTFORML 説明文章 += "%説明文章:LOCAL%<br>"
				ENDIF
			NEXT
			HTML_PRINT "<br>"
			IF 出典元 != ""
				PRINTL ;출처가 있는 경우, 그것을 기재
				PRINTFORML 出典元 = %出典元%
				HTML_PRINT "<br>"
			ENDIF
			PRINTL RESULTS = %説明文章%
			PRINTL RESULTS:1 = %出典元%
			PRINTL RESULTS:2 = %販売分類%
			HTML_PRINT "<br>"
			PRINTL RETURN 판매 가격
			HTML_PRINT "<br>"
		ENDIF
		PRINTFORML @CLOTHES_CHANGE_%服名保存%(대상 캐릭터)
		PRINTL #DIM 대상 캐릭터
		HTML_PRINT "<br>"
		PRINTL ;이전 의상을 유지
		PRINTFORML SIF CSTR:対象キャラ:着せ替え服 != "%服名保存%"
		PRINTL 	CSTR:対象キャラ:脱ぐ前の服 = %CSTR:対象キャラ:着せ替え服%
		HTML_PRINT "<br>"

		PRINTFORML CSTR:対象キャラ:着せ替え服 = %服名保存%
		PRINTFORML CSTR:対象キャラ:着せ替え服追加名 =
		FOR LOCAL, 1, 24
			PRINTFORML TEQUIP:対象キャラ:%EQUIPNAME:LOCAL% = {服構造保存:LOCAL}
		NEXT
		HTML_PRINT "<br>"
		IF 服種別設定保存 >= 2
			PRINTFORML @CLOTHES_착용조건_%服名保存%(대상캐릭터)
			PRINTL #DIM 대상캐릭터
			HTML_PRINT "<br>"
			PRINTL ;조건을 만족하지 못하는 경우는 NG
			PRINTL ;SIF NG조건
			PRINTL ;	RETURN 0
			HTML_PRINT "<br>"
			PRINTL ;조건을 만족하는 경우는 OK
			PRINTL ;SIF OK조건
			PRINTL ;	RETURN 1
			HTML_PRINT "<br>"
			PRINTL ;조건을 통과한 캐릭터는 OK
			PRINTL RETURN 1
			HTML_PRINT "<br>"
		ENDIF
		HTML_PRINT "<br>"

		OUTPUTLOG @"ERB_EDITFILE/CLOTHES_%服名保存%.ERB", 1
		CLEARLINE LINECOUNT
		PRINTL ERB_EDITFILE 폴더에 출력했습니다. ERB 폴더로 이동하여 재시작해 주세요
		WAIT
		GOTO 終了処理
	CASEELSE
		服構造保存:((RESULT - 1000) / 10) = RESULT % 10
		SIF INRANGE(RESULT, 1095, 1097)
			服構造保存:((RESULT - 1000) / 10) = 1094 - RESULT
		SIF INRANGE(RESULT, 1175, 1177)
			服構造保存:((RESULT - 1000) / 10) = 1174 - RESULT
		CALL 服構造連動処理(服構造保存, (RESULT - 1000) / 10, RESULT % 10)
ENDSELECT

RESTART

$終了処理
VARSET 服構造保存
VARSET 説明文章
服名保存 = 
服種別設定保存 = 0
入手手段設定保存 = 0
編集モード = 0
出典元 = 
販売価格 = 10000
連動衣装 = 
RETURN 0

@服構造連動処理(服構造, 変更部位, 変更値)
#DIM REF 服構造
#DIM 変更部位
#DIM 変更値

;「上半身鎧あり」「上半身服あり」が両方OFFになると「脇出し」「へそ出し」「はだけ可」「手差し込み可」「パイズリ可」もOFFになる
IF (変更部位 == 2 && 変更値 == 0 && 服構造:1 == 0) || (変更部位 == 1 && 変更値 == 0 && 服構造:2 == 0)
	服構造:3 = 0
	服構造:4 = 0
	服構造:5 = 0
	服構造:6 = 0
	服構造:7 = 0
ENDIF
;「「脇出し」「へそ出し」「はだけ可」「手差し込み可」「パイズリ可」のどれかがONになると、上半身鎧あり」がOFFの場合「上半身服あり」がONになる
SIF MAX(服構造:3, 服構造:4, 服構造:5, 服構造:6, 服構造:7) > 0 && 服構造:1 == 0
	服構造:2 = 1

;「下半身前鎧あり」「下半身服あり」が両方OFFになると「スカートめくり可」「ずらし可」もOFFになる
IF (変更部位 == 13 && 変更値 == 0 && 服構造:12 == 0) || (変更部位 == 12 && 変更値 == 0 && 服構造:13 == 0)
	服構造:14 = 0
	服構造:15 = 0
ENDIF
;「スカートめくり可」「ずらし可」のどれかがONになると、「下半身前鎧あり」がOFFの場合「下半身服あり」がONになる
SIF MAX(服構造:14, 服構造:15) > 0  && 服構造:12 == 0
	服構造:13 = 1

;「上下一体服」がONになると「上半身服あり」「下半身服あり」がONになる
IF 変更部位 == 19 && 変更値 == 1
	服構造:2 = 1
	服構造:13 = 1
ENDIF
;逆方向処理
SIF (変更部位 == 2 && 変更値 == 0) || (変更部位 == 13 && 変更値 == 0)
	服構造:19 = 0

;「上タイツあり」がOFFになると「上タイツ破れ」「上下一体タイツあり」もOFFになる
IF 変更部位 == 8 && 変更値 == 0
	服構造:21 = 0
	服構造:20 = 0
ENDIF
;逆方向処理
SIF 服構造:21 > 0
	服構造:8 = 1
;「下タイツあり」がOFFになると「下タイツ破れ」「上下一体タイツあり」もOFFになる
IF 変更部位 == 16 && 変更値 == 0
	服構造:22 = 0
	服構造:20 = 0
ENDIF
;逆方向処理
SIF 服構造:22 > 0
	服構造:16 = 1

;「上下一体タイツあり」がONになると「上タイツあり」「下タイツあり」もONになる
IF 服構造:20 > 0
	服構造:8 = 1
	服構造:16 = 1
;逆方向処理
ELSEIF 変更部位 == 20 && 変更値 == 0
	服構造:8 = 0
	服構造:16 = 0
	服構造:21 = 0
	服構造:22 = 0
ENDIF

