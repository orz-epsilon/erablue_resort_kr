
@イベント発生条件_突発イベント_ならず者の来訪
;開放条件を満たしたら1を返すこと

イベント消滅日数渡し = 3
イベント消滅確率渡し = 30
イベント再発生日数渡し = 20
イベント再発生確率渡し = 10

;開放条件を満たしたら1を返すこと
IF 空艇港初来訪イベントフラグ
	RETURN 1
ENDIF
RETURN 0

@イベント関数_突発イベント_ならず者の来訪(ARG)
LOCAL = DT_CELL_GET("各イベント用変数配列", ARG, "イベント再発生CT")
IF LOCAL == 0
	;初回
	DRAWLINE
	PRINTL ［무법자의 방문］
	DRAWLINE
	PRINTW
	PRINTFORMW 아무래도 산속의 작은 공터에 무법자들의 배가 내려온 것 같다.
	PRINTFORMW 방치하면 도둑질이나 자원 남획을 하고 도망칠 것이다.
	PRINTFORMW 그렇게 되면 리조트의 인기가 떨어지는 것은 불 보듯 뻔한 일이다.
	PRINTFORMW 피해가 발생하기 전에 쓰러뜨리자.
	PRINTL
	PRINTL 산속에 내려온 무법자들과 싸웁니다. 준비되셨습니까? (2연전 있습니다!)
	PRINTBUTTON "[0]예", 0
	PRINTL
	PRINTBUTTON "[1]아니요", 1
	PRINTL
	BINPUT
	SELECTCASE RESULT
		CASE 0
			PRINTFORMW %CALLNAME:PLAYER%들은 산속에 내려온 무법자들을 습격했다!

			CALL ならず者の来訪イベント_戦闘部分
		CASE 1
			PRINTW 아니, 방심은 좋지 않다. 여기는 제대로 준비를 하고 나서 하자.
	ENDSELECT
ELSE
	;二回目以降
	DRAWLINE
	PRINTL ［무법자의 방문］
	DRAWLINE
	PRINTW
	PRINTFORMW 또 무법자들의 배가 내려온 것 같다.
	PRINTFORMW 피해가 발생하기 전에 얼른 쓰러뜨리자.
	PRINTL
	PRINTL 산속에 내려온 무법자들과 싸웁니다. 준비되셨습니까? (2연전 있습니다!)
	PRINTBUTTON "[0]예", 0
	PRINTL
	PRINTBUTTON "[1]아니요", 1
	PRINTL
	BINPUT
	SELECTCASE RESULT
		CASE 0
			PRINTFORMW %CALLNAME:PLAYER%들은 산중에 내려온 불량배들을 습격했다!

			CALL ならず者の来訪イベント_戦闘部分
		CASE 1
			PRINTW 아니, 방심은 좋지 않다. 여기는 확실히 준비를 하고 나서 하자.
	ENDSELECT
ENDIF


@ならず者の来訪イベント_戦闘部分
CALL イベント戦闘初期処理()
敵BATTLE_STATE_STR:0:エネミー名 = Lv10_ならず者の構成員
敵BATTLE_STATE_STR:2:エネミー名 = Lv10_ならず者の構成員
敵BATTLE_STATE_STR:4:エネミー名 = Lv10_ならず者の構成員
CALL イベント中戦闘用処理()
IF RESULT == 0
	敵BATTLE_STATE_STR:0:エネミー名 = Lv10_ならず者の構成員
	敵BATTLE_STATE_STR:1:エネミー名 = Lv10_ならず者の構成員
	敵BATTLE_STATE_STR:2:エネミー名 = Lv15_ならず者の頭領
	敵BATTLE_STATE_STR:3:エネミー名 = Lv10_ならず者の構成員
	敵BATTLE_STATE_STR:4:エネミー名 = Lv10_ならず者の構成員
	CALL イベント中戦闘用処理()
	IF RESULT == 0
		;勝利
		PRINTFORMW %CALLNAME:PLAYER%들은 무사히 불량배들을 소탕했다.
		IF RAND:100 < 5
			;5%だけ起こる処理
			PRINTW 남겨진 배의 잔해에서 꽤 괜찮은 물건이 있었지만, 불량배들에게는 가치를 몰랐던 것 같아 무심하게 놓여 있었다.
			PRINTW 감사히 유효하게 활용하도록 하자.
			STRDATA LOCALS
				DATA 낡은 반지・은
				DATA 백은의 고문
				DATA 낡은 귀걸이・은
				DATA 보정 원석
			ENDDATA
			SIF !FLAG:クリア済み視聴フラグ
				CALL 素材入手処理(LOCALS, 1)
			PRINTFORMW 「%LOCALS%」을(를) 손에 넣었다.
		ELSEIF !乗機所持チェック("試作大型走艇") && !RAND:4
			;設計図未所持時、25％で発見
			PRINTW 남겨진 배의 잔해에 희귀한 설계의 주정 도면이 있었지만, 불량배들은 가치를 몰랐는지 아무렇게나 놓여 있었다.
			PRINTW 감사히 유효 활용하도록 하자.
			PRINTW 「시작 대형 주정 설계도」를 손에 넣었다.
			SIF !FLAG:クリア済み視聴フラグ
				CALL 乗機取得処理_設計図("試作大型走艇")
		ELSE
			SELECTCASE RAND(10)
				CASE 0
					LOCAL = RAND(2500, 7501)
					SIF !FLAG:クリア済み視聴フラグ
						MONEY += LOCAL
					PRINTFORMW 남겨진 배의 잔해에서 {LOCAL}루피가 나왔으므로 압수했다.
				CASE 1
					PRINTW 남겨진 배의 잔해는 텅 비어 있었고, 불량배 두목이 남긴 것으로 보이는 휘갈겨 쓴 메모만이 남아 있었다.
					IF FLAG:クリア済み視聴フラグ
						PRINTW 아무래도 노리던 보물의 정보인 듯하지만, 글씨가 매우 더러워 읽을 수 없었다……
					ELSE
						PRINTW 아무래도 노리던 보물의 정보인 듯하지만, 글씨가 매우 더러워 읽을 수 있는 것은 일부분뿐이다……
						SELECTCASE RAND(4)
							CASE 0
								PRINTL 「와이번은 체내에서────을(를) 결정화────
								PRINTW 　희귀한────────더스트로서─────가치로 거래───」
							CASE 1
								PRINTL 「덴킨나기가 만들어낸──────규사에─────
								PRINTW 　삼킨───운 좋게──────,────고가로─────」
							CASE 2
								PRINTL 「프로미넌스 리액터─────불꽃────높이다────
								PRINTW 　강철의 거인─────성정─────」
							CASE 3
								PRINTL 「구르는 짐승은───공격으로 스스로의────잃어버린다
								PRINTW 　한 번도──────사냥하다───────,───고가가 붙는다」
						ENDSELECT
					ENDIF
				CASE 2
					LOCALS = %ランダム素材名("ダンジョン素材", 0)%
					SIF !FLAG:クリア済み視聴フラグ
						CALL 素材入手処理(LOCALS, 1)
					PRINTFORMW 남겨진 배의 잔해에서 「%LOCALS%」이(가) 있었으므로 압수했다.
				CASE 3
					LOCALS = %ランダム素材名("食物素材", 1)%
					SIF !FLAG:クリア済み視聴フラグ
						CALL 素材入手処理(LOCALS, 1)
					PRINTFORMW 남겨진 배의 잔해에서 손대지 않은 「%LOCALS%」이(가) 남아있었으므로 압수했다.
				CASE 4
					LOCAL = RAND(1000, 5001)
					SIF !FLAG:クリア済み視聴フラグ
						MONEY += LOCAL
					PRINTFORMW 남겨진 배는 텅 비어 있었지만, 그 잔해에서 {LOCAL}루피가 되는 부품이 나왔으므로 매각했다.
				CASEELSE
					LOCAL = RAND(500, 2501)
					SIF !FLAG:クリア済み視聴フラグ
						MONEY += LOCAL
					PRINTFORMW 남겨진 배의 잔해에서 {LOCAL}루피가 나왔으므로 압수했다.
			ENDSELECT
		ENDIF
		CALL イベント完了フラグ汎用処理("ならず者の来訪")
	ELSEIF RESULT == 1
		;全滅
		PRINTW 방심하고 말았다. 여기는 일단 철수하자.
	ELSEIF RESULT == -1
		;逃走
		PRINTW 생각보다 강한 것 같다. 여기는 일단 철수하자.
	ENDIF
ELSEIF RESULT == 1
	;全滅
	PRINTW 방심하고 말았다. 여기는 일단 철수하자.
ELSEIF RESULT == -1
	;逃走
	PRINTW 생각보다 강한 것 같다. 여기는 일단 철수하자.
ENDIF
CALL ダンジョン終了時処理
